#Использовать cmdline
#Использовать oint

#Область СлужебныйПрограммныйИнтерфейс

#Область СвязьОПИ

Функция ПолучитьИмяМодуля(Знач ИмяКоманды = "") Экспорт

	СоответствиеКомандМодулей = Новый Соответствие();
	СоответствиеКомандМодулей.Вставить("telegram", "OPI_Telegram");

	Если ЗначениеЗаполнено(ИмяКоманды) Тогда
		Результат = СоответствиеКомандМодулей.Получить(ИмяКоманды);
	Иначе
		Результат = СоответствиеКомандМодулей;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Результат) Тогда
		ВызватьИсключение "Неизвестная команда: " + ИмяКоманды;
	Иначе
		Возврат Результат;
	КонецЕсли;

КонецФункции

Функция ПолучитьТаблицуПараметров(Знач ИмяКоманды) Экспорт

	ТПМ = Новый ТаблицаЗначений();
	
	ТПМ.Колонки.Добавить("Метод");
	ТПМ.Колонки.Добавить("МетодПоиска");
	ТПМ.Колонки.Добавить("Параметр");
	ТПМ.Колонки.Добавить("Имя");
	ТПМ.Колонки.Добавить("Описание");
	ТПМ.Колонки.Добавить("ВариантОбработки");

	Попытка
		Выполнить("ЗаполнитьТаблицуПараметров" + ИмяКоманды + "(ТПМ);");
	Исключение
		ВызватьИсключение "Неизвестная команда: " + ИмяКоманды;
	КонецПопытки;

	Возврат ТПМ;

КонецФункции

Процедура ЗаполнитьТаблицуПараметровTelegram(ТПМ) Экспорт
	
	ДобавитьПараметрМетода(ТПМ, "ПолучитьИнформациюБота"	 , "--token"   , "Токен"     , "Токен бота");

	ДобавитьПараметрМетода(ТПМ, "ПолучитьОбновления"		 , "--token"   , "Токен"     , "Токен бота");

	ДобавитьПараметрМетода(ТПМ, "УстановитьWebhook" 		 , "--token"   , "Токен"     , "Токен бота");
	ДобавитьПараметрМетода(ТПМ, "УстановитьWebhook" 		 , "--url"     , "URL"       , "Адрес обработки запросов (с https://)");

	ДобавитьПараметрМетода(ТПМ, "УдалитьWebHook" 		     , "--token"   , "Токен"     , "Токен бота");
	ДобавитьПараметрМетода(ТПМ, "УдалитьWebHook" 	      	 , "--url"     , "URL"       , "Адрес обработки запросов (с https://)");

	ДобавитьПараметрМетода(ТПМ, "ОтправитьТекстовоеСообщение", "--token"   , "Токен"     , "Токен бота");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьТекстовоеСообщение", "--id"      , "IDЧата"    , "ID чата / ID чата*ID темы");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьТекстовоеСообщение", "--text"    , "Текст"     , "Текст сообщения");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьТекстовоеСообщение", "--keyboard", "Клавиатура", "JSON клавиатуры / файл .json клавиатуры (необяз.)"	    , "ОбработатьПараметрТекст");

	ДобавитьПараметрМетода(ТПМ, "ОтправитьКартинку"			 , "--token"   , "Токен"     , "Токен бота");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьКартинку"			 , "--id"      , "IDЧата"    , "ID чата / ID чата*ID темы");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьКартинку"			 , "--text"    , "Текст"     , "Текст сообщения");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьКартинку"			 , "--path"    , "Картинка"  , "Путь к файлу");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьКартинку"			 , "--keyboard", "Клавиатура", "JSON клавиатуры / файл .json клавиатуры (необяз.)"	    , "ОбработатьПараметрТекст");

	ДобавитьПараметрМетода(ТПМ, "ОтправитьВидео"			 , "--token"   , "Токен"     , "Токен бота");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьВидео"			 , "--id"      , "IDЧата"    , "ID чата / ID чата*ID темы");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьВидео"			 , "--text"    , "Текст"     , "Текст сообщения");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьВидео"			 , "--keyboard", "Клавиатура", "JSON клавиатуры / файл .json клавиатуры (необяз.)"	    , "ОбработатьПараметрТекст");

	ДобавитьПараметрМетода(ТПМ, "ОтправитьГифку"			 , "--token"   , "Токен"     , "Токен бота");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьГифку"			 , "--id"      , "IDЧата"    , "ID чата / ID чата*ID темы");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьГифку"			 , "--text"    , "Текст"     , "Текст сообщения");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьГифку"			 , "--path"    , "Гифка"     , "Путь к файлу");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьГифку"			 , "--keyboard", "Клавиатура", "JSON клавиатуры / файл .json клавиатуры (необяз.)"	    , "ОбработатьПараметрТекст");

	ДобавитьПараметрМетода(ТПМ, "ОтправитьАудио"			 , "--token"   , "Токен"     , "Токен бота");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьАудио"			 , "--id"      , "IDЧата"    , "ID чата / ID чата*ID темы");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьАудио"			 , "--text"    , "Текст"     , "Текст сообщения");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьАудио"			 , "--path"    , "Аудио"     , "Путь к файлу");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьАудио"			 , "--keyboard", "Клавиатура", "JSON клавиатуры / файл .json клавиатуры (необяз.)"	    , "ОбработатьПараметрТекст");

	ДобавитьПараметрМетода(ТПМ, "ОтправитьДокумент"			 , "--token"   , "Токен"     , "Токен бота");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьДокумент"			 , "--id"      , "IDЧата"    , "ID чата / ID чата*ID темы");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьДокумент"			 , "--text"    , "Текст"     , "Текст сообщения");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьДокумент"			 , "--path"    , "Документ"  , "Путь к файлу");
	ДобавитьПараметрМетода(ТПМ, "ОтправитьДокумент"			 , "--keyboard", "Клавиатура", "JSON клавиатуры / файл .json клавиатуры (необяз.)"	    , "ОбработатьПараметрТекст");

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Основные

Процедура ОсновнойОбработчик()
	
	Парсер = Новый ПарсерАргументовКоманднойСтроки();
	
	Для Каждого Команда Из ПолучитьИмяМодуля() Цикл
		СформироватьКоманду(Команда.Ключ, Парсер);
	КонецЦикла;
	
	Результат      = Парсер.Разобрать(АргументыКоманднойСтроки);
	ТекущаяКоманда = Результат["Команда"];
	
	Если ТекущаяКоманда = Неопределено Тогда
		ВывестиНачальнуюСтраницу();
	Иначе
		ВыполнитьОбработкуКоманды(Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьКоманду(Знач Имя, Парсер)
	
	Команда           = Парсер.ОписаниеКоманды(Имя);
	ТаблицаПараметров = ПолучитьТаблицуПараметров(Имя);
	Парсер.ДобавитьПозиционныйПараметрКоманды(Команда, "Метод");
	
	ДобавитьПараметрыКоманды(Парсер, Команда, ТаблицаПараметров);
	
	Парсер.ДобавитьКоманду(Команда);
	
КонецПроцедуры

Процедура ВыполнитьОбработкуКоманды(Знач Данные)
	
	ТекущаяКоманда = Данные["Команда"];
	Вывод		   = "";
	
	Попытка
			
		Вывод = ПолучитьРезультатОбработки(ТекущаяКоманда, Данные);
		
		Если ТипЗнч(Вывод) = Тип("Структура")
			Или ТипЗнч(Вывод) = Тип("Соответствие")
			Или ТипЗнч(Вывод) = Тип("Массив") Тогда
		
			Вывод = OPI_Инструменты.JSONСтрокой(Вывод);

		КонецЕсли;

		Если ЗначениеЗаполнено(Вывод) Тогда
			Сообщить(Символы.ПС + Вывод + Символы.ПС, СтатусСообщения.Внимание);
		КонецЕсли;

	Исключение

		Если ЗначениеЗаполнено(Вывод) Тогда
			Сообщить(Вывод);
		Иначе
			Сообщить(ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
			Сообщить(Символы.ПС);
		КонецЕсли;
		
	КонецПопытки;
	
КонецПроцедуры

Функция ПолучитьРезультатОбработки(Знач Команда, Знач Данные)
	
	Модуль    = ПолучитьИмяМодуля(Команда);
	Параметры = Данные["ЗначенияПараметров"];
	Метод     = Параметры["Метод"];
	Ответ     = "<пустой возврат>";

	ТаблицаПараметров = ПолучитьТаблицуПараметров(Команда);

	Если Не ЗначениеЗаполнено(Метод) Тогда
		Сообщить(Символы.ПС + " ## Команда - " + Команда, СтатусСообщения.Информация);
		ВывестиСправкуПоМетодам(ТаблицаПараметров);
		Возврат "";
	КонецЕсли;

	ОтборКоманды      = Новый Структура("МетодПоиска", вРег(Метод));
	ПараметрыМетода   = ТаблицаПараметров.НайтиСтроки(ОтборКоманды);
	ТекстВыполнения   = "";

	Если Параметры.Количество() = 1 Тогда
		ВывестиСправкуПоПараметрам(ПараметрыМетода);
		Возврат "";
	КонецЕсли;

	СтрокаВызова = Модуль + "." + Метод + "(";
	Для Каждого НеобходимыйПараметр Из ПараметрыМетода Цикл

		ЗначениеПараметра = Параметры.Получить(НеобходимыйПараметр.Параметр);

		Если ЗначениеЗаполнено(ЗначениеПараметра) Тогда

			ТекстВыполнения = ТекстВыполнения 
				+ Символы.ПС 
				+ НеобходимыйПараметр.Имя 
				+ " = ОбработатьВходящийПараметр(""" 
				+ ЗначениеПараметра
				+ """, """
				+ НеобходимыйПараметр.ВариантОбработки
				+ """);";

			СтрокаВызова = СтрокаВызова + НеобходимыйПараметр.Имя + ", ";

		Иначе
			СтрокаВызова = СтрокаВызова + " , ";
		КонецЕсли;

	КонецЦикла;

	СтрокаВызова = Лев(СтрокаВызова, СтрДлина(СтрокаВызова) - 2);
	СтрокаВызова = СтрокаВызова + ");";
	СтрокаВызова = "Ответ = " + СтрокаВызова;

	ТекстВыполнения = ТекстВыполнения + Символы.ПС + СтрокаВызова;

	Выполнить(ТекстВыполнения);

	Возврат Ответ;
	
КонецФункции

#КонецОбласти

#Область Вспомогательные

Процедура ДобавитьПараметрыКоманды(Парсер, Команда, Знач ТаблицаПараметров);
	
	ТаблицаПараметров.Свернуть("Параметр");

	МассивПараметров = ТаблицаПараметров.ВыгрузитьКолонку("Параметр");
	
	Для Каждого Параметр Из МассивПараметров Цикл
		Парсер.ДобавитьИменованныйПараметрКоманды(Команда, Параметр);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьПараметрМетода(Таблица
	, Знач Метод
	, Знач Параметр
	, Знач Имя
	, Знач Описание
	, Знач ВариантОбработки = "Строка")
	
	НовыйПараметр = Таблица.Добавить();
	НовыйПараметр.МетодПоиска 		= вРег(Метод);
	НовыйПараметр.Метод 			= Метод;
	НовыйПараметр.Параметр 			= Параметр;
	НовыйПараметр.Имя 				= Имя;
	НовыйПараметр.Описание 			= Описание;
	НовыйПараметр.ВариантОбработки 	= ВариантОбработки;
	
КонецПроцедуры

Функция ОбработатьВходящийПараметр(Знач Значение, Знач ВидОбработки)

	Если ВидОбработки = "СоответствиеJSON" Тогда
		ОбработатьПараметрJSON(Значение);
	ИначеЕсли ВидОбработки = "ТекстовыеДанные" Тогда
		ОбработатьПараметрТекст(Значение);
	Иначе
		Значение = Строка(Значение);
	КонецЕсли;

	Возврат Значение;
	
КонецФункции

Процедура ОбработатьПараметрJSON(Значение)

	Файл 		= Новый Файл(Значение);
	ЧтениеJSON  = Новый ЧтениеJSON;

	Если Файл.Существует() Тогда
		ЧтениеJSON.ОткрытьФайл(Значение);
		ЧтениеJSON.Прочитать();
	Иначе
		ЧтениеJSON.УстановитьСтроку(СокрЛП(Значение));
	КонецЕсли;	
	
	Значение = ПрочитатьJSON(ЧтениеJSON, Истина, Неопределено, ФорматДатыJSON.ISO);
	ЧтениеJSON.Закрыть();

КонецПроцедуры

Процедура ОбработатьПараметрТекст(Значение)

	Файл 		 = Новый Файл(Значение);

	Если Файл.Существует() Тогда
		ЧтениеТекста = Новый ЧтениеТекста(Значение);
		Значение     = ЧтениеТекста.Прочитать();
		ЧтениеТекста.Закрыть();
	Иначе
		Значение = Строка(Значение);
	КонецЕсли;

	Значение = СокрЛП(Значение);

КонецПроцедуры

#КонецОбласти

#Область Справки

Процедура ВывестиНачальнуюСтраницу()
	
	СписокКоманд = "";

	Для Каждого Команда Из ПолучитьИмяМодуля() Цикл
		СписокКоманд = СписокКоманд + Команда.Ключ + ", ";
	КонецЦикла;

	СписокКоманд = Лев(СписокКоманд, СтрДлина(СписокКоманд) - 2);

	Сообщить("-----------------------------------------------------", СтатусСообщения.Информация);
	Сообщить("
		|
		|    _______ _____________  ___  _______
		|    __  __ ___/__  _/_ /  |  / /___  __/
		|    _  / / / __  /  __      / __  /   
		|    / /_/ / __/ /   _  /|  /  _  /    
		|    \____/  /___/   /_/ |_/   /_/     
		|                          
		|
		| Добро пожаловать в OInt!
		|
		| Структура команд:
		| OInt БИБЛИОТЕКА ""МЕТОД"" [Параметры]
		|
		| Вызов библиотеки без метода или метода без параметров возвращает справку
		| Список доступных библиотек: " 
		+ СписокКоманд 
		+ "
		|", СтатусСообщения.Внимание);
	
	Сообщить("Полную документацию можно найти по адресу: https://opi.neocities.org" + Символы.ПС, СтатусСообщения.Информация);
	
КонецПроцедуры

Процедура ВывестиСправкуПоМетодам(Знач ТаблицаПараметров)

	ТаблицаПараметров.Свернуть("Метод");
	МассивМетодов = ТаблицаПараметров.ВыгрузитьКолонку("Метод");

	Сообщить(" ## Доступные методы: " + Символы.ПС, СтатусСообщения.Информация);

	Для каждого Метод Из МассивМетодов Цикл
		Сообщить("    - " + Метод, СтатусСообщения.Внимание);
	КонецЦикла;

	Сообщить(Символы.ПС);

КонецПроцедуры

Процедура ВывестиСправкуПоПараметрам(Знач ТаблицаПараметров)

	Если ТаблицаПараметров.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ИмяМетода    = ТаблицаПараметров[0].Метод;
	ТекстСправки = "";

	Сообщить("
	| ## Метод " + ИмяМетода , СтатусСообщения.Информация);

	Для Каждого ПараметрМетода Из ТаблицаПараметров Цикл

		ТекстСправки = ТекстСправки 
			+ Символы.ПС
			+ "    "
			+ ПараметрМетода["Параметр"]
			+ " - "
			+ ПараметрМетода["Описание"];

	КонецЦикла;

	Сообщить(ТекстСправки + Символы.ПС, СтатусСообщения.Внимание);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

Попытка
	ОсновнойОбработчик();	
Исключение
	Сообщить(Символы.ПС + "!!! ОШИБКА: Проверьте корректность введенных данных" + Символы.ПС, СтатусСообщения.ОченьВажное);
	Сообщить(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()) + Символы.ПС, СтатусСообщения.Важное);
КонецПопытки;

