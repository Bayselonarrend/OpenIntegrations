// OneScript: ./OInt/core/Modules/OPI_GRPC.os
// Lib: GRPC
// CLI: grpc
// Keywords: grpc

// MIT License

// Copyright (c) 2023-2025 Anton Tsitavets

// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:

// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.

// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

// https://github.com/Bayselonarrend/OpenIntegrations

// BSLLS:Typo-off
// BSLLS:LatinAndCyrillicSymbolInWord-off
// BSLLS:IncorrectLineBreak-off
// BSLLS:NumberOfOptionalParams-off
// BSLLS:UsingServiceTag-off
// BSLLS:LineLength-off
// BSLLS:UsingSynchronousCalls-off
// BSLLS:DuplicateStringLiteral-off
// BSLLS:MagicNumber-off

//@skip-check module-structure-top-region
//@skip-check module-structure-method-in-regions
//@skip-check wrong-string-literal-content
//@skip-check method-too-many-params
//@skip-check constructor-function-return-section
//@skip-check doc-comment-collection-item-type

#Область ПрограммныйИнтерфейс

#Область ОсновныеМетоды

// Открыть соединение !NOCLI
// Открывает gRPC соединение
//
// Параметры:
//  Параметры - Строка                     - Параметры соединения. См. ПолучитьПараметрыСоединения    - params
//  Tls       - Структура Из КлючИЗначение - Настройки TLS, если необходимо. См. ПолучитьНастройкиTls - tls
//
// Возвращаемое значение:
//  Произвольный - Объект коннектора или соответствие с информацией об ошибке
Функция ОткрытьСоединение(Знач Параметры, Знач Tls = Неопределено) Экспорт

    Если ЭтоКоннектор(Параметры) Тогда
        Возврат Параметры;
    КонецЕсли;

    OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Параметры);

    Адрес = OPI_Инструменты.ПолучитьИли(Параметры, "address", Неопределено);

    Если Адрес = Неопределено Тогда
        Ошибка = Новый Соответствие;
        Ошибка.Вставить("result", Ложь);
        Ошибка.Вставить("error" , "The parameter structure doesn't contain an address");
    КонецЕсли;

    Коннектор               = OPI_Компоненты.ПолучитьКомпоненту("GRPC");
    Коннектор.ServerAddress = Строка(Адрес);

    СохранитьНастройкиВКомпоненте(Коннектор, Параметры);

    Tls = OPI_Компоненты.УстановитьTls(Коннектор, Tls);

    Если Не OPI_Инструменты.ПолучитьИли(Tls, "result", Ложь) Тогда
        Возврат Tls;
    КонецЕсли;

    Результат = Коннектор.Connect();
    Результат = OPI_Инструменты.JSONВСтруктуру(Результат);

    Если Не OPI_Инструменты.ПолучитьИли(Результат, "result", Ложь) Тогда
        Возврат Результат;
    КонецЕсли;

    Результат = УстановитьProto(Коннектор, Параметры);

    Если Не OPI_Инструменты.ПолучитьИли(Результат, "result", Ложь) Тогда
        Возврат Результат;
    КонецЕсли;

    МетаданныеСоединения = OPI_Инструменты.ПолучитьИли(Параметры, "metadata", Неопределено);

    Если МетаданныеСоединения <> Неопределено Тогда

        Результат = УстановитьМетаданные(Коннектор, МетаданныеСоединения);

        Если Не OPI_Инструменты.ПолучитьИли(Результат, "result", Ложь) Тогда
            Возврат Результат;
        КонецЕсли;

    КонецЕсли;

    Возврат Коннектор;

КонецФункции

// Закрыть соединение !NOCLI
// Явно закрывает переданное соединение
//
// Параметры:
//  Соединение - Произвольный - Объект компоненты с открытым соединением - conn
//
// Возвращаемое значение:
//  Структура Из КлючИЗначение - Результат закрытия соединения
Функция ЗакрытьСоединение(Знач Соединение) Экспорт

    Если ЭтоКоннектор(Соединение) Тогда

        Результат = Соединение.Disconnect();
        Результат = OPI_Инструменты.JsonВСтруктуру(Результат, Ложь);

    Иначе

        Результат = Новый Структура("result,error", Ложь, "It's not a connection");

    КонецЕсли;

    Возврат Результат;

КонецФункции

// Установить метаданные !NOCLI
// Устанавливает новые метаданные соединения
//
// Примечание:
// Заменяет метаданные, установленные при создании соединения
//
// Параметры:
//  Соединение - Произвольный               - Существующее соединение - conn
//  Метаданные - Структура Из КлючИЗначение - Список метаданных       - meta
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция УстановитьМетаданные(Знач Соединение, Знач Метаданные) Экспорт

    Если Не ЭтоКоннектор(Соединение) Тогда

        СоответствиеОшибки = Новый Соответствие;
        СоответствиеОшибки.Вставить("result", Ложь);
        СоответствиеОшибки.Вставить("error" , "Implicit connection opening is senseless in this case");

        Возврат СоответствиеОшибки;

    КонецЕсли;

    OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Метаданные);

    МетаданныеСтрокой = OPI_Инструменты.JSONСтрокой(Метаданные);

    Результат = Соединение.SetMetadata(МетаданныеСтрокой);
    Результат = OPI_Инструменты.JsonВСтруктуру(Результат);

    Возврат Результат;

КонецФункции

// Вызвать метод
// Выполняет унарный запрос к выбранному сервису
//
// Параметры:
//  Соединение - Произвольный               - Существующее соединение или параметры соединения         - conn
//  Сервис     - Строка                     - Имя сервиса                                              - service
//  Метод      - Строка                     - Имя метода                                               - method
//  Запрос     - Структура Из КлючИЗначение - Данные запроса                                           - data
//  Таймаут    - Число                      - Таймаут (в мс)                                           - tout
//  Tls        - Структура Из КлючИЗначение - Настройки TLS, если необходимо. См. ПолучитьНастройкиTls - tls
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ВызватьМетод(Знач Соединение
    , Знач Сервис
    , Знач Метод
    , Знач Запрос = Неопределено
    , Знач Таймаут = 10000
    , Знач Tls = Неопределено) Экспорт

    Если ЭтоКоннектор(Соединение) Тогда
        ЗакрыватьСоединение = Ложь;
        Коннектор           = Соединение;
    Иначе
        ЗакрыватьСоединение = Истина;
        Коннектор           = ОткрытьСоединение(Соединение, Tls);
    КонецЕсли;

    Если Не ЭтоКоннектор(Коннектор) Тогда
        Возврат Коннектор;
    КонецЕсли;

    СтруктураВызоваСтрокой = СформироватьСтрокуВызова(Коннектор, Сервис, Метод, Таймаут, Запрос);

    Результат = Коннектор.Call(СтруктураВызоваСтрокой);
    Результат = OPI_Инструменты.JsonВСтруктуру(Результат);

    Если ЗакрыватьСоединение Тогда
        ЗакрытьСоединение(Коннектор);
    КонецЕсли;

    Возврат Результат;

КонецФункции

// Это коннектор !NOCLI
// Проверяет, что значение является объектом внешней компоненты для работы с gRPC
//
// Параметры:
//  Значение - Произвольный - Значение для проверки - value
//
// Возвращаемое значение:
//  Булево -  Это коннектор
Функция ЭтоКоннектор(Знач Значение) Экспорт

    Возврат Строка(ТипЗнч(Значение)) = "AddIn.OPI_GRPC.Main";

КонецФункции

// Получить параметры соединения
// Формирует структуру параметров для создания соединения
//
// Примечание:
// Имя файла Proto используется для возможности дальнейшего указания этого Proto в import другого Proto^^
// Оно не обязано являтся настоящим путем к файлу.^^
// Proto, не учавствующие в import других Proto, могут иметь произвольное имя файла
// Если значение Proto не является объектом (коллекцией ключ и значение), то оно будет интерпретировано как^^
// `{"main.proto": переданное значение}`
//
// Параметры:
//  Адрес      - Строка                     - Адрес для подключения (с протоколом)                                                               - addr
//  Proto      - Произвольный               - Схема или структура схем: Ключ > имя файла (для import), Значение > схема строкой или путь к файлу - proto
//  Метаданные - Структура Из КлючИЗначение - Структура метаданных, если необходимо                                                              - meta
//
// Возвращаемое значение:
//  Структура Из КлючИЗначение - Параметры соединения
Функция ПолучитьПараметрыСоединения(Знач Адрес, Знач Proto = Неопределено, Знач Метаданные = Неопределено) Экспорт

    ПараметрыСоединения = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("address", Адрес, "Строка", ПараметрыСоединения);

    Если ЗначениеЗаполнено(Метаданные) Тогда
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Метаданные);
        ПараметрыСоединения.Вставить("metadata", Метаданные);
    КонецЕсли;

    Если ЗначениеЗаполнено(Proto) Тогда

        Попытка
            Proto_ = Proto;
            OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Proto_);
        Исключение
            Proto_ = Новый Соответствие;
            Proto_.Вставить("main.proto", Proto);
        КонецПопытки;

        ПараметрыСоединения.Вставить("proto", Proto_);

    КонецЕсли;

    Возврат ПараметрыСоединения;

КонецФункции

// Получить настройки TLS
// Формирует настройки для использования TLS при выполнении запросов
//
// Примечание:
// Настройки Tls могут быть установлены только в момент создания соединения: явного, при использовании функции `ОткрытьСоединение`^^
// или неявного, при передаче настроек
//
// Параметры:
//  ОтключитьПроверкуСертификатов - Булево - Позволяет работать с некорретными сертификатами, в т.ч. самоподписанными   - trust
//  ПутьКСертификату              - Строка - Путь к корневому PEM файлу сертификата, если его нет в системном хранилище - cert
//
// Возвращаемое значение:
//  Структура Из КлючИЗначение - Структура настроек TLS соединения
Функция ПолучитьНастройкиTls(Знач ОтключитьПроверкуСертификатов, Знач ПутьКСертификату = "") Экспорт

    Возврат OPI_Компоненты.ПолучитьНастройкиTls(ОтключитьПроверкуСертификатов, ПутьКСертификату);

КонецФункции

#КонецОбласти

#Область Интроспекция

// Получить список сервисов
// Получает список доступных сервисов
//
// Параметры:
//  Соединение - Произвольный               - Существующее соединение или параметры соединения         - conn
//  Tls        - Структура Из КлючИЗначение - Настройки TLS, если необходимо. См. ПолучитьНастройкиTls - tls
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ПолучитьСписокСервисов(Знач Соединение, Знач Tls = Неопределено) Экспорт

    Если ЭтоКоннектор(Соединение) Тогда
        ЗакрыватьСоединение = Ложь;
        Коннектор           = Соединение;
    Иначе
        ЗакрыватьСоединение = Истина;
        Коннектор           = ОткрытьСоединение(Соединение, Tls);
    КонецЕсли;

    Если Не ЭтоКоннектор(Коннектор) Тогда
        Возврат Коннектор;
    КонецЕсли;

    Результат = Коннектор.ListServices();
    Результат = OPI_Инструменты.JsonВСтруктуру(Результат);

    Если ЗакрыватьСоединение Тогда
        ЗакрытьСоединение(Коннектор);
    КонецЕсли;

    Возврат Результат;

КонецФункции

// Получить список методов
// Получает список доступных методов указанного сервиса
//
// Параметры:
//  Соединение - Произвольный               - Существующее соединение или параметры соединения         - conn
//  Сервис     - Строка                     - Имя сервиса                                              - service
//  Tls        - Структура Из КлючИЗначение - Настройки TLS, если необходимо. См. ПолучитьНастройкиTls - tls
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ПолучитьСписокМетодов(Знач Соединение, Знач Сервис, Знач Tls = Неопределено) Экспорт

    Если ЭтоКоннектор(Соединение) Тогда
        ЗакрыватьСоединение = Ложь;
        Коннектор           = Соединение;
    Иначе
        ЗакрыватьСоединение = Истина;
        Коннектор           = ОткрытьСоединение(Соединение, Tls);
    КонецЕсли;

    Если Не ЭтоКоннектор(Коннектор) Тогда
        Возврат Коннектор;
    КонецЕсли;

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Сервис);

    Результат = Коннектор.ListMethods(Сервис);
    Результат = OPI_Инструменты.JsonВСтруктуру(Результат);

    Если ЗакрыватьСоединение Тогда
        ЗакрытьСоединение(Коннектор);
    КонецЕсли;

    Возврат Результат;

КонецФункции

// Получить метод
// Получает информацию о методе
//
// Параметры:
//  Соединение - Произвольный               - Существующее соединение или параметры соединения         - conn
//  Сервис     - Строка                     - Имя сервиса                                              - service
//  Метод      - Строка                     - Имя метода                                               - method
//  Tls        - Структура Из КлючИЗначение - Настройки TLS, если необходимо. См. ПолучитьНастройкиTls - tls
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ПолучитьМетод(Знач Соединение, Знач Сервис, Знач Метод, Знач Tls = Неопределено) Экспорт

    Если ЭтоКоннектор(Соединение) Тогда
        ЗакрыватьСоединение = Ложь;
        Коннектор           = Соединение;
    Иначе
        ЗакрыватьСоединение = Истина;
        Коннектор           = ОткрытьСоединение(Соединение, Tls);
    КонецЕсли;

    Если Не ЭтоКоннектор(Коннектор) Тогда
        Возврат Коннектор;
    КонецЕсли;

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Сервис);
    OPI_ПреобразованиеТипов.ПолучитьСтроку(Метод);

    Результат = Коннектор.GetMethodInfo(Сервис, Метод);
    Результат = OPI_Инструменты.JsonВСтруктуру(Результат);

    Если ЗакрыватьСоединение Тогда
        ЗакрытьСоединение(Коннектор);
    КонецЕсли;

    Возврат Результат;

КонецФункции

#КонецОбласти

#Область Стриминг

// Инициализировать серверный поток !NOCLI
// Инициализирует новый серверный поток
//
// Параметры:
//  Соединение - Произвольный               - Объект соединения           - conn
//  Сервис     - Строка                     - Имя сервиса                 - service
//  Метод      - Строка                     - Имя метода                  - method
//  Запрос     - Структура Из КлючИЗначение - Данные клиентского запроса  - data
//  Таймаут    - Число                      - Таймаут (в мс)              - tout
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ИнициализироватьСерверныйПоток(Знач Соединение
    , Знач Сервис
    , Знач Метод
    , Знач Запрос
    , Знач Таймаут = 10000) Экспорт

    Возврат ИнициализироватьПоток("server", Соединение, Сервис, Метод, Запрос, Таймаут);

КонецФункции

// Инициализировать клиентский поток !NOCLI
// Инициализирует новый клиентский поток
//
// Параметры:
//  Соединение - Произвольный - Объект соединения - conn
//  Сервис     - Строка       - Имя сервиса       - service
//  Метод      - Строка       - Имя метода        - method
//  Таймаут    - Число        - Таймаут (в мс)    - tout
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ИнициализироватьКлиентскийПоток(Знач Соединение
    , Знач Сервис
    , Знач Метод
    , Знач Таймаут = 10000) Экспорт

    Возврат ИнициализироватьПоток("client", Соединение, Сервис, Метод, Неопределено, Таймаут);

КонецФункции

// Инициализировать двунаправленный поток !NOCLI
// Инициализирует новый двунаправленный поток
//
// Параметры:
//  Соединение - Произвольный - Объект соединения  - conn
//  Сервис     - Строка       - Имя сервиса        - service
//  Метод      - Строка       - Имя метода         - method
//  Таймаут    - Число        - Таймаут (в мс)     - tout
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ИнициализироватьДвунаправленныйПоток(Знач Соединение
    , Знач Сервис
    , Знач Метод
    , Знач Таймаут = 10000) Экспорт

    Возврат ИнициализироватьПоток("bidi", Соединение, Сервис, Метод, Неопределено, Таймаут);

КонецФункции

// Отправить сообщение !NOCLI
// Отправляет очередное сообщение в клиентский или двунаправленный поток
//
// Параметры:
//  Соединение - Произвольный               - Объект соединения           - conn
//  IDПотока   - Строка                     - Идентификатор потока        - stream
//  Запрос     - Структура Из КлючИЗначение - Данные запроса для отправки - data
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ОтправитьСообщение(Знач Соединение, Знач IDПотока, Знач Запрос) Экспорт

    Если Не ЭтоКоннектор(Соединение) Тогда

        СоответствиеОшибки = Новый Соответствие;
        СоответствиеОшибки.Вставить("result", Ложь);
        СоответствиеОшибки.Вставить("error" , "Implicit connection opening is senseless in this case");

        Возврат СоответствиеОшибки;

    КонецЕсли;

    OPI_ПреобразованиеТипов.ПолучитьСтроку(IDПотока);

    ОбработанныйЗапрос = ОбработатьЗапросПередОтправкой(Соединение, Запрос);
    ОбработанныйЗапрос = OPI_Инструменты.JSONСтрокой(ОбработанныйЗапрос);

    Результат = Соединение.SendMessage(IDПотока, ОбработанныйЗапрос);
    Результат = OPI_Инструменты.JsonВСтруктуру(Результат);

    Возврат Результат;

КонецФункции

// Получить сообщение !NOCLI
// Получает очередное сообщение из потока
//
// Параметры:
//  Соединение - Произвольный - Объект соединения    - conn
//  IDПотока   - Строка       - Идентификатор потока - stream
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ПолучитьСообщение(Знач Соединение, Знач IDПотока) Экспорт

    Если Не ЭтоКоннектор(Соединение) Тогда

        СоответствиеОшибки = Новый Соответствие;
        СоответствиеОшибки.Вставить("result", Ложь);
        СоответствиеОшибки.Вставить("error" , "Implicit connection opening is senseless in this case");

        Возврат СоответствиеОшибки;

    КонецЕсли;

    OPI_ПреобразованиеТипов.ПолучитьСтроку(IDПотока);

    Результат = Соединение.GetNextMessage(IDПотока);
    Результат = OPI_Инструменты.JsonВСтруктуру(Результат);

    Возврат Результат;

КонецФункции

// Завершить отправку !NOCLI
// Закрывает канал отправки в клиентском или двунаправленном потоке
//
// Параметры:
//  Соединение - Произвольный - Объект соединения    - conn
//  IDПотока   - Строка       - Идентификатор потока - stream
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ЗавершитьОтправку(Знач Соединение, Знач IDПотока) Экспорт

    Если Не ЭтоКоннектор(Соединение) Тогда

        СоответствиеОшибки = Новый Соответствие;
        СоответствиеОшибки.Вставить("result", Ложь);
        СоответствиеОшибки.Вставить("error" , "Implicit connection opening is senseless in this case");

        Возврат СоответствиеОшибки;

    КонецЕсли;

    OPI_ПреобразованиеТипов.ПолучитьСтроку(IDПотока);

    Результат = Соединение.FinishSending(IDПотока);
    Результат = OPI_Инструменты.JsonВСтруктуру(Результат);

    Возврат Результат;

КонецФункции

// Закрыть поток !NOCLI
// Закрывает поток по ID
//
// Параметры:
//  Соединение - Произвольный - Объект соединения    - conn
//  IDПотока   - Строка       - Идентификатор потока - stream
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ЗакрытьПоток(Знач Соединение, Знач IDПотока) Экспорт

    Если Не ЭтоКоннектор(Соединение) Тогда

        СоответствиеОшибки = Новый Соответствие;
        СоответствиеОшибки.Вставить("result", Ложь);
        СоответствиеОшибки.Вставить("error" , "Implicit connection opening is senseless in this case");

        Возврат СоответствиеОшибки;

    КонецЕсли;

    OPI_ПреобразованиеТипов.ПолучитьСтроку(IDПотока);

    Результат = Соединение.CloseStream(IDПотока);
    Результат = OPI_Инструменты.JsonВСтруктуру(Результат);

    Возврат Результат;

КонецФункции

// Обработать серверный поток
// Инициализирует серверный поток и возвращает массив полученных сообщений
//
// Параметры:
//  Соединение     - Произвольный               - Существующее соединение или параметры соединения               - conn
//  Сервис         - Строка                     - Имя сервиса                                                    - service
//  Метод          - Строка                     - Имя метода                                                     - method
//  Запрос         - Структура Из КлючИЗначение - Данные первого запроса                                         - data
//  Таймаут        - Число                      - Таймаут выполнения отдельной операции (в мс)                   - tout
//  Tls            - Структура Из КлючИЗначение - Настройки TLS, если необходимо. См. ПолучитьНастройкиTls       - tls
//  ЧислоСообщений - Число                      - Максимум сообщений для получения. Без ограничений по умолчанию - count
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ОбработатьСерверныйПоток(Знач Соединение
    , Знач Сервис
    , Знач Метод
    , Знач Запрос
    , Знач Таймаут = 10000
    , Знач Tls = Неопределено
    , Знач ЧислоСообщений = Неопределено) Экспорт

    Если ЭтоКоннектор(Соединение) Тогда
        ЗакрыватьСоединение = Ложь;
        Коннектор           = Соединение;
    Иначе
        ЗакрыватьСоединение = Истина;
        Коннектор           = ОткрытьСоединение(Соединение, Tls);
    КонецЕсли;

    Если Не ЭтоКоннектор(Коннектор) Тогда
        Возврат Коннектор;
    КонецЕсли;

    ЕстьЧислоСообщений = ЧислоСообщений <> Неопределено;

    Если ЕстьЧислоСообщений Тогда
        OPI_ПреобразованиеТипов.ПолучитьЧисло(ЧислоСообщений);
    КонецЕсли;

    Инициализация = ИнициализироватьПоток("server", Коннектор, Сервис, Метод, Запрос, Таймаут);

    Если Не Инициализация["result"] Тогда
        Возврат Инициализация;
    КонецЕсли;

    IDПотока        = Инициализация["streamId"];
    МассивСообщений = Новый Массив;

    Счетчик = 0;
    Пока Истина Цикл

        Если ЕстьЧислоСообщений Тогда
            Если Счетчик >= ЧислоСообщений Тогда
                Прервать;
            КонецЕсли;
        КонецЕсли;

        ТекущееСообщение = ПолучитьСообщение(Коннектор, IDПотока);

        Если Не ТекущееСообщение["result"] Тогда

            Ошибка = ТекущееСообщение["error"];

            СоответствиеОшибки = Новый Соответствие;
            СоответствиеОшибки.Вставить("result", Ложь);
            СоответствиеОшибки.Вставить("error" , Ошибка);
            СоответствиеОшибки.Вставить("data"  , МассивСообщений);

        Иначе

            ДанныеСообщения = ТекущееСообщение["message"];
            МассивСообщений.Добавить(ДанныеСообщения);

        КонецЕсли;

        Счетчик = Счетчик + 1;

    КонецЦикла;

    ЗакрытьПоток(Коннектор, IDПотока);

    Если ЗакрыватьСоединение Тогда
        ЗакрытьСоединение(Коннектор);
    КонецЕсли;

    СоответствиеРезультата = Новый Соответствие;
    СоответствиеРезультата.Вставить("result", Истина);
    СоответствиеРезультата.Вставить("data"  , МассивСообщений);

    Возврат СоответствиеРезультата;

КонецФункции

// Обработать клиентский поток
// Инициализирует клиентский поток, передает массив сообщений и получает финальный ответ
//
// Параметры:
//  Соединение     - Произвольный               - Существующее соединение или параметры соединения               - conn
//  Сервис         - Строка                     - Имя сервиса                                                    - service
//  Метод          - Строка                     - Имя метода                                                     - method
//  Запросы        - Массив Из Структура        - Массив запросов или одиночный запрос                           - data
//  Таймаут        - Число                      - Таймаут выполнения отдельной операции (в мс)                   - tout
//  Tls            - Структура Из КлючИЗначение - Настройки TLS, если необходимо. См. ПолучитьНастройкиTls       - tls
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ОбработатьКлиентскийПоток(Знач Соединение
    , Знач Сервис
    , Знач Метод
    , Знач Запросы
    , Знач Таймаут = 10000
    , Знач Tls = Неопределено) Экспорт

    Если ЭтоКоннектор(Соединение) Тогда
        ЗакрыватьСоединение = Ложь;
        Коннектор           = Соединение;
    Иначе
        ЗакрыватьСоединение = Истина;
        Коннектор           = ОткрытьСоединение(Соединение, Tls);
    КонецЕсли;

    Если Не ЭтоКоннектор(Коннектор) Тогда
        Возврат Коннектор;
    КонецЕсли;

    Инициализация = ИнициализироватьПоток("client", Коннектор, Сервис, Метод, Неопределено, Таймаут);

    Если Не Инициализация["result"] Тогда
        Возврат Инициализация;
    КонецЕсли;

    OPI_ПреобразованиеТипов.ПолучитьМассив(Запросы);

    IDПотока = Инициализация["streamId"];
    Счетчик  = 0;

    Для Каждого Запрос Из Запросы Цикл

        ТекущаяОтправка = ОтправитьСообщение(Коннектор, IDПотока, Запрос);

        Счетчик = Счетчик + 1;

        Если Не ТекущаяОтправка["result"] Тогда

            Ошибка    = ТекущаяОтправка["error"];
            Получение = ПолучитьСообщение(Коннектор, IDПотока);

            СоответствиеОшибки = Новый Соответствие;
            СоответствиеОшибки.Вставить("result"         , Ложь);
            СоответствиеОшибки.Вставить("error"          , Ошибка);
            СоответствиеОшибки.Вставить("messages_sent"  , Счетчик);
            СоответствиеОшибки.Вставить("final_retrieval", Получение);

        КонецЕсли;

    КонецЦикла;

    ЗакрытиеКанала = ЗавершитьОтправку(Коннектор, IDПотока);

    Результат = ПолучитьСообщение(Коннектор, IDПотока);
    Результат.Вставить("messages_sent" , Счетчик);
    Результат.Вставить("finish_sending", ЗакрытиеКанала);

    ЗакрытьПоток(Коннектор, IDПотока);

    Если ЗакрыватьСоединение Тогда
        ЗакрытьСоединение(Коннектор);
    КонецЕсли;

    Возврат Результат;

КонецФункции

// Обработать двунаправленный поток
// Инициализирует двунаправленный поток, после чего передает и отправляет сообщения в указанном порядке
//
// Примечание:
// Пример: Получение сообщения после каждыйх двух отправленных и еще 3 в конце (null в 1С = Неопределено)^
// ПорядокОбмена: `[{Сообщение1}, {Сообщение2}, null, {Сообщение3}, ... {СообщениеN}, null, null, null, null, null]`
//
// Параметры:
//  Соединение          - Произвольный               - Существующее соединение или параметры соединения                         - conn
//  Сервис              - Строка                     - Имя сервиса                                                              - service
//  Метод               - Строка                     - Имя метода                                                               - method
//  ПорядокОбмена       - Массив Из Произвольный     - Массив из данных запросов и Неопределено там, где необходимо получение   - exch
//  Таймаут             - Число                      - Таймаут выполнения отдельной операции (в мс)                             - tout
//  Tls                 - Структура Из КлючИЗначение - Настройки TLS, если необходимо. См. ПолучитьНастройкиTls                 - tls
//  ПродолжатьПолучение - Булево                     - Продолжать получение после обработки порядка обмена                      - cnt
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ОбработатьДвунаправленныйПоток(Знач Соединение
    , Знач Сервис
    , Знач Метод
    , Знач ПорядокОбмена
    , Знач Таймаут = 10000
    , Знач Tls = Неопределено
    , Знач ПродолжатьПолучение = Истина) Экспорт

    Если ЭтоКоннектор(Соединение) Тогда
        ЗакрыватьСоединение = Ложь;
        Коннектор           = Соединение;
    Иначе
        ЗакрыватьСоединение = Истина;
        Коннектор           = ОткрытьСоединение(Соединение, Tls);
    КонецЕсли;

    Если Не ЭтоКоннектор(Коннектор) Тогда
        Возврат Коннектор;
    КонецЕсли;

    Инициализация = ИнициализироватьПоток("bidi", Коннектор, Сервис, Метод, Неопределено, Таймаут);

    Если Не Инициализация["result"] Тогда
        Возврат Инициализация;
    КонецЕсли;

    OPI_ПреобразованиеТипов.ПолучитьМассив(ПорядокОбмена);
    OPI_ПреобразованиеТипов.ПолучитьБулево(ПродолжатьПолучение);

    IDПотока         = Инициализация["streamId"];
    МассивПолученных = Новый Массив;

    Для Каждого Действие Из ПорядокОбмена Цикл

        ЭтоПолучение = Действие = Неопределено;

        Если ЭтоПолучение Тогда
            ТекущаяОперация = ПолучитьСообщение(Коннектор, IDПотока);
        Иначе
            ТекущаяОперация = ОтправитьСообщение(Коннектор, IDПотока, Действие);
        КонецЕсли;

        Если Не ТекущаяОперация["result"] Тогда

            Ошибка = ТекущаяОперация["error"];

            СоответствиеОшибки = Новый Соответствие;
            СоответствиеОшибки.Вставить("result", Ложь);
            СоответствиеОшибки.Вставить("error" , Ошибка);
            СоответствиеОшибки.Вставить("data"  , МассивПолученных);

        Иначе

            Если ЭтоПолучение Тогда
                МассивПолученных.Добавить(ТекущаяОперация["message"]);
            КонецЕсли;

        КонецЕсли;

    КонецЦикла;

    ЗакрытиеКанала = ЗавершитьОтправку(Коннектор, IDПотока);

    Результат = Новый Соответствие;
    Результат.Вставить("result"        , Истина);
    Результат.Вставить("finish_sending", ЗакрытиеКанала);

    Если ПродолжатьПолучение Тогда

        Пока Истина Цикл

            ТекущаяОперация = ПолучитьСообщение(Коннектор, IDПотока);

            Если ТекущаяОперация["result"] Тогда
                МассивПолученных.Добавить(ТекущаяОперация["message"]);
            Иначе
                Результат.Вставить("final_retrieval", ТекущаяОперация);
                Прервать;
            КонецЕсли;

        КонецЦикла;

    КонецЕсли;

    Результат.Вставить("data", МассивПолученных);

    ЗакрытьПоток(Коннектор, IDПотока);

    Если ЗакрыватьСоединение Тогда
        ЗакрытьСоединение(Коннектор);
    КонецЕсли;

    Возврат Результат;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция УстановитьProto(Коннектор, Знач Параметры)

    НаборProto = OPI_Инструменты.ПолучитьИли(Параметры, "proto", Неопределено);

    Если НаборProto <> Неопределено Тогда

        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(НаборProto);

        //@skip-check bsl-legacy-check-for-each-statetement-collection
        Для Каждого Proto Из НаборProto Цикл

            ТекущийКонтент = Proto.Значение;

            OPI_ПреобразованиеТипов.ПолучитьСтроку(ТекущийКонтент, Истина);

            Результат = Коннектор.LoadProto(Proto.Ключ, ТекущийКонтент);
            Результат = OPI_Инструменты.JSONВСтруктуру(Результат);

            Если Не Результат["result"] Тогда
                Возврат Результат;
            КонецЕсли;

        КонецЦикла;

        Результат = Коннектор.CompileProtos();
        Результат = OPI_Инструменты.JSONВСтруктуру(Результат);

    Иначе
        Результат = Новый Структура("result", Истина);
    КонецЕсли;

    Возврат Результат;

КонецФункции

Функция СформироватьСтрокуВызова(Знач Компонента
    , Знач Сервис
    , Знач Метод
    , Знач Таймаут = Неопределено
    , Знач Запрос  = Неопределено)

    СтруктураВызова = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("service"   , Сервис , "Строка", СтруктураВызова);
    OPI_Инструменты.ДобавитьПоле("method"    , Метод  , "Строка", СтруктураВызова);
    OPI_Инструменты.ДобавитьПоле("timeout_ms", Таймаут, "Число" , СтруктураВызова);

    ОбработанныйЗапрос = ОбработатьЗапросПередОтправкой(Компонента, Запрос);
    OPI_Инструменты.ДобавитьПоле("request", ОбработанныйЗапрос, "Текущий", СтруктураВызова);

    СтруктураВызоваСтрокой = OPI_Инструменты.JSONСтрокой(СтруктураВызова);

    Возврат СтруктураВызоваСтрокой;

КонецФункции

Функция ОбработатьЗапросПередОтправкой(Знач Компонента, Знач Запрос)

    Если Не ЗначениеЗаполнено(Запрос) Тогда
        Возврат Запрос;
    КонецЕсли;

    OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Запрос);

    ОбработанныйЗапрос = Новый(ТипЗнч(Запрос));

    Для Каждого ТекущийЭлемент Из Запрос Цикл

        ТекущийКлюч     = ТекущийЭлемент.Ключ;
        ТекущееЗначение = ОбработатьЗначениеДляЗапроса(Компонента, ТекущийЭлемент.Значение);

        ОбработанныйЗапрос.Вставить(ТекущийКлюч, ТекущееЗначение);

    КонецЦикла;

    Возврат ОбработанныйЗапрос;

КонецФункции

Функция ИнициализироватьПоток(Знач Вид
    , Знач Соединение
    , Знач Сервис
    , Знач Метод
    , Знач Запрос
    , Знач Таймаут = 10000)

    Если Не ЭтоКоннектор(Соединение) Тогда

        СоответствиеОшибки = Новый Соответствие;
        СоответствиеОшибки.Вставить("result", Ложь);
        СоответствиеОшибки.Вставить("error" , "Implicit connection opening is senseless in this case");

        Возврат СоответствиеОшибки;

    КонецЕсли;

    СтруктураВызоваСтрокой = СформироватьСтрокуВызова(Соединение, Сервис, Метод, Таймаут, Запрос);

    Если Вид = "client" Тогда
        Результат = Соединение.StartClientStream(СтруктураВызоваСтрокой);
    ИначеЕсли Вид = "server" Тогда
        Результат = Соединение.StartServerStream(СтруктураВызоваСтрокой);
    Иначе
        Результат = Соединение.StartBidiStream(СтруктураВызоваСтрокой);
    КонецЕсли;

    Результат = OPI_Инструменты.JsonВСтруктуру(Результат);

    Возврат Результат;

КонецФункции

Функция ОбработатьЗначениеДляЗапроса(Знач Компонента, Значение)

    Если OPI_Инструменты.ЭтоКоллекция(Значение, Истина) Тогда

        ОбработанноеЗначение = Новый(ТипЗнч(Значение));

        Для Каждого ЭлементКоллекции Из Значение Цикл

            ТекущийКлюч     = ЭлементКоллекции.Ключ;
            ТекущееЗначение = ОбработатьЗначениеДляЗапроса(Компонента, ЭлементКоллекции.Значение);

            ОбработанноеЗначение.Вставить(ТекущийКлюч, ТекущееЗначение);

        КонецЦикла;

    ИначеЕсли OPI_Инструменты.ЭтоКоллекция(Значение) Тогда

        ОбработанноеЗначение = Новый Массив;

        Для Каждого ЭлементКоллекции Из Значение Цикл

            ТекущееЗначение = ОбработатьЗначениеДляЗапроса(Компонента, ЭлементКоллекции);
            ОбработанноеЗначение.Добавить(ТекущееЗначение);

        КонецЦикла;

    ИначеЕсли ТипЗнч(Значение) = Тип("ДвоичныеДанные") Тогда

        Результат = OPI_Компоненты.ПоместитьДанные(Компонента, Значение);

        Если Не Результат["result"] Тогда
            ВызватьИсключение СтрШаблон("Ошибка при передаче двоичного значения: %1", Результат["error"]);
        КонецЕсли;

        ОбработанноеЗначение = Результат["key"];

    Иначе

        ОбработанноеЗначение = Значение;

    КонецЕсли;

    Возврат ОбработанноеЗначение;

КонецФункции

Функция СохранитьНастройкиВКомпоненте(Коннектор, Знач Параметры)

    ПараметрыСтрокой   = OPI_Инструменты.JSONСтрокой(Параметры);
    СохранениеНастроек = Коннектор.StoreSettings(ПараметрыСтрокой);

    Возврат СохранениеНастроек;

КонецФункции

#КонецОбласти
