// OneScript: ./OInt/core/Modules/OPI_ClickHouse.os
// Lib: ClickHouse
// CLI: clickhouse
// Keywords: clickhouse
// Depends: OPI_GRPC

// DocsCategory: Database
// DocsNameRU: ClickHouse
// DocsNameEN: ClickHouse

// MIT License

// Copyright (c) 2023-2026 Anton Tsitavets

// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:

// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.

// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

// https://github.com/Bayselonarrend/OpenIntegrations

// BSLLS:Typo-off
// BSLLS:LatinAndCyrillicSymbolInWord-off
// BSLLS:IncorrectLineBreak-off
// BSLLS:NumberOfOptionalParams-off
// BSLLS:UsingServiceTag-off
// BSLLS:LineLength-off
// BSLLS:UsingSynchronousCalls-off

//@skip-check module-structure-top-region
//@skip-check module-structure-method-in-regions
//@skip-check wrong-string-literal-content
//@skip-check method-too-many-params
//@skip-check constructor-function-return-section
//@skip-check doc-comment-collection-item-type

//#Использовать "../../tools/main"
//#Использовать "../../tools/http"

#Область ПрограммныйИнтерфейс

#Область ОсновныеМетоды

// Выполнить запрос
// Выполняет запрос с указанными параметрами
//
// Параметры:
//  Соединение - Произвольный               - Настройки или объект соединения (для gRPC)    - conn
//  Запрос     - Структура Из КлючИЗначение - Данные запроса. См. ПолучитьНастройкиЗапроса  - req
//  Сессия     - Структура Из КлючИЗначение - Настройки сессии. См. ПолучитьНастройкиСессии - session
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения
Функция ВыполнитьЗапрос(Знач Соединение, Знач Запрос, Знач Сессия = Неопределено) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Запрос, "Некорректная структура запроса");

    Если Сессия <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Сессия, "Некорректная структура сессии");
    КонецЕсли;

    ПереданКоннекторGrpc = OPI_GRPC.ЭтоКоннектор(Соединение);
    ТранспортПоУмолчанию = ?(ПереданКоннекторGrpc, "grpc", "http");

    Если Не ПереданКоннекторGrpc Тогда

        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Соединение, "Некорректная структура соединения");
        ОтсутствующиеПоля = OPI_Инструменты.НайтиОтсутствующиеПоляКоллекции(Соединение, "address");

        Если ЗначениеЗаполнено(ОтсутствующиеПоля) Тогда
            ПоляСтрокой = СтрСоединить(ОтсутствующиеПоля, ", ");
            ТекстОшибки = СтрШаблон("Отсутствуют обязательные поля настроек соединения: %1", ПоляСтрокой);
            ВызватьИсключение ТекстОшибки;
        КонецЕсли;

    КонецЕсли;

    Транспорт = OPI_Инструменты.ПолучитьИли(Соединение, "transport", ТранспортПоУмолчанию);

    Если Транспорт = "http" Тогда

        Результат = ВыполнитьЗапросЧерезHTTP(Соединение, Запрос, Сессия);

    ИначеЕсли Транспорт = "grpc" Тогда

        Результат = ВыполнитьЗапросЧерезGRPC(Соединение, Запрос, Сессия);

    КонецЕсли;

    Возврат Результат;

КонецФункции

// Получить настройки соединения HTTP
// Получает структуру настроек для HTTP соединения
//
// Параметры:
//  Адрес        - Строка                             - Адрес подключения с протоколом и портом          - url
//  Авторизация  - Строка, Структура Из КлючИЗначение - Авторизация: строка для JWT, структура для basic - auth
//  ДопЗаголовки - Соответствие Из КлючИЗначение      - Дополнительные заголовки запроса                 - headers
//
// Возвращаемое значение:
//  Структура Из КлючИЗначение - Структура настроек соединения
Функция ПолучитьНастройкиСоединенияHTTP(Знач Адрес
    , Знач Авторизация = Неопределено
    , Знач ДопЗаголовки = Неопределено) Экспорт

    НастройкиСоединения = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("address"  , Адрес       , "Строка"       , НастройкиСоединения);
    OPI_Инструменты.ДобавитьПоле("headers"  , ДопЗаголовки, "КлючИЗначение", НастройкиСоединения);
    OPI_Инструменты.ДобавитьПоле("transport", "http"      , "Строка"       , НастройкиСоединения);

    Если Авторизация = Неопределено Тогда
        Возврат НастройкиСоединения;
    КонецЕсли;

    ДополнитьНастройкиАвторизацией(Авторизация, НастройкиСоединения);

    Возврат НастройкиСоединения;

КонецФункции

// Получить настройки запроса
// Формирует структуру описания запроса
//
// Примечание:
// При использовании транспорта `http` нельзя одновременно использовать `Данные` и `Внешние таблицы`
// Тело ответа может быть возвращено в виде коллекции (для JSON) или в виде двоичных данных (для остальных форматов).^^
// В CLI версии двоичные данные будут представлены в виде Base64 строки
//
// Параметры:
//  Текст          - Строка                        - Текст запроса                                                      - query
//  БазаДанных     - Строка                        - База данных                                                        - db
//  IDЗапроса      - Строка                        - Уникальный ID запроса, если необходимо                             - id
//  Данные         - Произвольный                  - Строка, файл или двоичные данные запроса                           - data
//  ФорматОтвета   - Строка                        - Формат получения ответа: JSON, CSV, TSV и пр.                      - format
//  ВнешниеТаблицы - Массив Из Структура           - Информация о внешних таблицах. См. ПолучитьСтруктуруВнешнейТаблицы - ext
//  Настройки      - Соответствие Из КлючИЗначение - Дополнительные query параметры запроса                             - settings
//
// Возвращаемое значение:
//  Структура Из КлючИЗначение - Структура запроса
Функция ПолучитьНастройкиЗапроса(Знач Текст
    , Знач БазаДанных = Неопределено
    , Знач IDЗапроса = Неопределено
    , Знач Данные = Неопределено
    , Знач ФорматОтвета = "JSON"
    , Знач ВнешниеТаблицы = Неопределено
    , Знач Настройки = Неопределено) Экспорт

    ПараметрыЗапроса = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("query"          , Текст         , "Строка"       , ПараметрыЗапроса);
    OPI_Инструменты.ДобавитьПоле("database"       , БазаДанных    , "Строка"       , ПараметрыЗапроса);
    OPI_Инструменты.ДобавитьПоле("id"             , IDЗапроса     , "Строка"       , ПараметрыЗапроса);
    OPI_Инструменты.ДобавитьПоле("data"           , Данные        , "Текущий"      , ПараметрыЗапроса);
    OPI_Инструменты.ДобавитьПоле("format"         , ФорматОтвета  , "Строка"       , ПараметрыЗапроса);
    OPI_Инструменты.ДобавитьПоле("external_tables", ВнешниеТаблицы, "Массив"       , ПараметрыЗапроса);
    OPI_Инструменты.ДобавитьПоле("settings"       , Настройки     , "КлючИЗначение", ПараметрыЗапроса);

    Возврат ПараметрыЗапроса;

КонецФункции

// Получить структуру внешней таблицы
// Получает структуру описания внешней таблицы запроса
//
// Параметры:
//  Имя              - Строка                     - Имя таблицы                                                  - name
//  СтруктураКолонок - Структура Из КлючИЗначение - Структура колонок таблицы: Ключ > имя, Значение > тип данных - cols
//  Данные           - Произвольный               - Строка, файл или двоичные данные таблицы                     - data
//  ФорматДанных     - Строка                     - Формат данных: CVS, TVS, JSON и др.                          - format
//
// Возвращаемое значение:
//  Структура Из КлючИЗначение - Структура описания внешней таблицы
Функция ПолучитьСтруктуруВнешнейТаблицы(Знач Имя
    , Знач СтруктураКолонок
    , Знач Данные = Неопределено
    , Знач ФорматДанных = Неопределено) Экспорт

    ПараметрыТаблицы = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("name"  , Имя             , "Строка"       , ПараметрыТаблицы);
    OPI_Инструменты.ДобавитьПоле("cols"  , СтруктураКолонок, "КлючИЗначение", ПараметрыТаблицы);
    OPI_Инструменты.ДобавитьПоле("data"  , Данные          , "Текущий"      , ПараметрыТаблицы);
    OPI_Инструменты.ДобавитьПоле("format", ФорматДанных    , "Строка"       , ПараметрыТаблицы);

    Возврат ПараметрыТаблицы;

КонецФункции

// Получить настройки сессии
// Формирует структуру настроек сессии выполнения запроса
//
// Параметры:
//  IDСессии        - Строка - ID сессии. Новый уникальный идентификатор, если не указано (будет создана новая сессия)         - id
//  ПроверятьСессию - Булево - Проверять существование сессии. Истина, когда ID указан и ложь, когда нет, если не указано иное - check
//  Таймаут         - Число  - Время жизни сессии в секундах                                                                   - timeout
//
// Возвращаемое значение:
//  Структура Из КлючИЗначение - Структура настроек сессии
Функция ПолучитьНастройкиСессии(Знач IDСессии = Неопределено
    , Знач ПроверятьСессию = Неопределено
    , Знач Таймаут = 60) Экспорт

    НастройкиСессии         = Новый Структура;
    ЕстьИдентификаторСессии = IDСессии <> Неопределено;

    Если ПроверятьСессию = Неопределено Тогда
        ПроверятьСессию = ЕстьИдентификаторСессии;
    КонецЕсли;

    OPI_Инструменты.ДобавитьПоле("id"     , IDСессии       , "Строка", НастройкиСессии);
    OPI_Инструменты.ДобавитьПоле("check"  , ПроверятьСессию, "Булево", НастройкиСессии);
    OPI_Инструменты.ДобавитьПоле("timeout", Таймаут        , "Число" , НастройкиСессии);

    Возврат НастройкиСессии;

КонецФункции

#КонецОбласти

#Область GRPC

// Получить настройки соединения gRPC
// Получает структуру настроек для gRPC соединения
//
// Параметры:
//  Адрес       - Строка                             - Адрес подключения с протоколом и портом          - url
//  Авторизация - Строка, Структура Из КлючИЗначение - Авторизация: строка для JWT, структура для basic - auth
//  Meta        - Структура Из КлючИЗначение         - Структура метаданных gRPC, если необходимо       - meta
//  Tls         - Структура Из КлючИЗначение         - Настройки TLS. См. ПолучитьНастройкиTls          - tls
//
// Возвращаемое значение:
//  Структура Из КлючИЗначение - Структура настроек соединения
Функция ПолучитьНастройкиСоединенияGRPC(Знач Адрес
    , Знач Авторизация = Неопределено
    , Знач Meta = Неопределено
    , Знач Tls = Неопределено) Экспорт

    НастройкиСоединения = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("address"  , Адрес , "Строка"       , НастройкиСоединения);
    OPI_Инструменты.ДобавитьПоле("tls"      , Tls   , "КлючИЗначение", НастройкиСоединения);
    OPI_Инструменты.ДобавитьПоле("metadata" , Meta  , "КлючИЗначение", НастройкиСоединения);
    OPI_Инструменты.ДобавитьПоле("transport", "grpc", "Строка"       , НастройкиСоединения);

    Если Авторизация = Неопределено Тогда
        Возврат НастройкиСоединения;
    КонецЕсли;

    ДополнитьНастройкиАвторизацией(Авторизация, НастройкиСоединения);

    Возврат НастройкиСоединения;

КонецФункции

// Открыть соединение GRPC !NOCLI
// Открывает GRPC соединение для работы с ClickHouse
//
// Параметры:
//  НастройкиСоединения - Структура Из КлючИЗначение - Параметры соединения. См. ПолучитьНастройкиСоединенияGRPC - set
//
// Возвращаемое значение:
//  Произвольный - Объект коннектора или соответствие с информацией об ошибке
Функция ОткрытьСоединениеGRPC(Знач НастройкиСоединения) Экспорт

    Если OPI_GRPC.ЭтоКоннектор(НастройкиСоединения) Тогда
        Возврат НастройкиСоединения;
    КонецЕсли;

    ТекстОшибки = "Переданы некорректные параметры соединения";
    OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(НастройкиСоединения, ТекстОшибки);

    Адрес = OPI_Инструменты.ПолучитьИли(НастройкиСоединения, "address", Неопределено);

    Если Адрес = Неопределено Тогда
        ВызватьИсключение "Не заполнен параметр address в параметрах соединения";
    КонецЕсли;

    Meta = OPI_Инструменты.ПолучитьИли(НастройкиСоединения, "metadata", Неопределено);

    Если ЗначениеЗаполнено(Meta) Тогда
        ТекстОшибки = "Передана некорректная структура метаданных соединения";
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Meta, ТекстОшибки);
    КонецЕсли;

    Tls        = OPI_Инструменты.ПолучитьИли(НастройкиСоединения, "tls", Неопределено);
    Proto      = OPI_Инструменты.ПолучитьТекстовыйМакет("OPI_Text_ClickHouseProto");
    Параметры  = OPI_GRPC.ПолучитьПараметрыСоединения(Адрес, Proto, Meta);
    Соединение = OPI_GRPC.ОткрытьСоединение(Параметры, Tls);

    Если OPI_GRPC.ЭтоКоннектор(Соединение) Тогда
        ПараметрыСтрокой = OPI_Инструменты.JSONСтрокой(НастройкиСоединения);
        //@skip-check module-unused-local-variable
        Результат        = Соединение.StoreSettings(ПараметрыСтрокой);
    КонецЕсли;

    Возврат Соединение;

КонецФункции

// Открыть поток GRPC !NOCLI
// Инициализирует I/O поток для обмена
//
// Параметры:
//  Соединение - Произвольный - Объект GRPC соединения - conn
//  Таймаут    - Число        - Таймаут (в мс)         - tout
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ОткрытьПотокGRPC(Знач Соединение, Знач Таймаут = 10000) Экспорт

    Сервис    = ПолучитьИмяСервисаGRPC();
    Метод     = "ExecuteQueryWithStreamIO";
    Результат = OPI_GRPC.ИнициализироватьДвунаправленныйПоток(Соединение, Сервис, Метод, Таймаут);

    Возврат Результат;

КонецФункции

// Отправить сообщение GRPC !NOCLI
// Отправляет очередное сообщение в клиентский или двунаправленный поток
//
// Параметры:
//  Соединение       - Произвольный               - Объект соединения GRPC                           - conn
//  IDПотока         - Строка                     - Идентификатор потока                             - stream
//  Запрос           - Структура Из КлючИЗначение - Данные запроса. См. ПолучитьНастройкиЗапроса     - req
//  Сессия           - Структура Из КлючИЗначение - Настройки сессии. См. ПолучитьНастройкиСессии    - session
//  ОжидатьСледующее - Булево                     - Флаг ожидания следующих сообщений после текущего - next
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ОтправитьСообщениеGRPC(Знач Соединение
    , Знач IDПотока
    , Знач Запрос
    , Знач Сессия = Неопределено
    , Знач ОжидатьСледующее = Ложь) Экспорт

    ЗапросGRPC = СформироватьЗапросGRPC(Соединение, Запрос, Сессия);
    OPI_Инструменты.ДобавитьПоле("next_query_info", ОжидатьСледующее, "Булево", ЗапросGRPC);

    Результат = OPI_GRPC.ОтправитьСообщение(Соединение, IDПотока, ЗапросGRPC);

    Возврат Результат;

КонецФункции

// Отправить данные GRPC !NOCLI
// Отправляет часть данных (дополнительный QueryInfo) в поток
//
// Параметры:
//  Соединение       - Произвольный - Объект соединения GRPC                           - conn
//  IDПотока         - Строка       - Идентификатор потока                             - stream
//  Данные           - Произвольный - Данные для отправки                              - data
//  ОжидатьСледующее - Булево       - Флаг ожидания следующих сообщений после текущего - next
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ОтправитьДанныеGRPC(Знач Соединение, Знач IDПотока, Знач Данные, Знач ОжидатьСледующее = Ложь) Экспорт

    ЗапросGRPC = Новый Структура;

    OPI_ПреобразованиеТипов.ПолучитьДвоичныеДанные(Данные, Истина);
    OPI_Инструменты.ДобавитьПоле("input_data"     , Данные          , "ДвоичныеДанные", ЗапросGRPC);
    OPI_Инструменты.ДобавитьПоле("next_query_info", ОжидатьСледующее, "Булево"        , ЗапросGRPC);

    Результат = OPI_GRPC.ОтправитьСообщение(Соединение, IDПотока, ЗапросGRPC);

    Возврат Результат;

КонецФункции

// Получить сообщение GRPC !NOCLI
// Получает очередное сообщение из потока
//
// Параметры:
//  Соединение - Произвольный - Объект соединения    - conn
//  IDПотока   - Строка       - Идентификатор потока - stream
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ПолучитьСообщениеGRPC(Знач Соединение, Знач IDПотока) Экспорт

    Результат = OPI_GRPC.ПолучитьСообщение(Соединение, IDПотока);

    Возврат Результат;

КонецФункции

// Завершить отправку GRPC !NOCLI
// Закрывает канал отправки в клиентском или двунаправленном потоке
//
// Параметры:
//  Соединение - Произвольный - Объект соединения    - conn
//  IDПотока   - Строка       - Идентификатор потока - stream
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ЗавершитьОтправкуGRPC(Знач Соединение, Знач IDПотока) Экспорт

    Результат = OPI_GRPC.ЗавершитьОтправку(Соединение, IDПотока);

    Возврат Результат;

КонецФункции

// Закрыть поток GRPC !NOCLI
// Закрывает поток по ID
//
// Параметры:
//  Соединение - Произвольный - Объект соединения    - conn
//  IDПотока   - Строка       - Идентификатор потока - stream
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат обработки
Функция ЗакрытьПотокGRPC(Знач Соединение, Знач IDПотока) Экспорт

    Результат = OPI_GRPC.ЗакрытьПоток(Соединение, IDПотока);

    Возврат Результат;

КонецФункции

// Получить настройки TLS
// Формирует настройки для использования TLS при выполнении запросов
//
// Примечание:
// Настройки Tls могут быть установлены только в момент создания соединения: явного, при использовании функции `ОткрытьСоединение`^^
// или неявного, при передаче настроек
//
// Параметры:
//  ОтключитьПроверкуСертификатов - Булево - Позволяет работать с некорректными сертификатами, в т.ч. самоподписанными  - trust
//  ПутьКСертификату              - Строка - Путь к корневому PEM файлу сертификата, если его нет в системном хранилище - cert
//
// Возвращаемое значение:
//  Структура Из КлючИЗначение - Структура настроек TLS соединения
Функция ПолучитьНастройкиTls(Знач ОтключитьПроверкуСертификатов, Знач ПутьКСертификату = "") Экспорт

    Возврат OPI_Компоненты.ПолучитьНастройкиTls(ОтключитьПроверкуСертификатов, ПутьКСертификату);

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Общие

Процедура ДополнитьНастройкиАвторизацией(Знач Авторизация, НастройкиСоединения)

    OPI_ПреобразованиеТипов.ПолучитьКоллекцию(Авторизация);

    Если ТипЗнч(Авторизация) = Тип("Массив") Тогда

        JWT = Авторизация[0];
        OPI_Инструменты.ДобавитьПоле("auth_type" , "jwt", "Строка", НастройкиСоединения);
        OPI_Инструменты.ДобавитьПоле("token"     , JWT  , "Строка", НастройкиСоединения);

    Иначе

        OPI_Инструменты.ДобавитьПоле("auth_type" , "basic", "Строка", НастройкиСоединения);

        Для Каждого КлючЗначение Из Авторизация Цикл
            OPI_Инструменты.ДобавитьПоле("user"     , КлючЗначение.Ключ    , "Строка", НастройкиСоединения);
            OPI_Инструменты.ДобавитьПоле("password" , КлючЗначение.Значение, "Строка", НастройкиСоединения);
            Прервать;
        КонецЦикла;

    КонецЕсли;

КонецПроцедуры

Функция ЭтоФорматВалидногоJSON(Знач Формат)

    Возврат Формат = "JSON"
        Или Формат = "JSONCompact"
        Или Формат = "JSONColumns"
        Или Формат = "JSONColumnsWithMetadata";

КонецФункции

Функция ЭтоСтроковыйФормат(Знач Формат)

    Возврат СтрНайти(Формат, "CSV") > 0
        Или СтрНайти(Формат, "TSV") > 0
        Или СтрНайти(Формат, "Pretty") > 0;

КонецФункции

#КонецОбласти

#Область HTTP

Функция ВыполнитьЗапросЧерезHTTP(Знач Соединение, Знач Запрос, Знач Сессия)

    Данные         = OPI_Инструменты.ПолучитьИли(Запрос, "data"           , Неопределено);
    ВнешниеТаблицы = OPI_Инструменты.ПолучитьИли(Запрос, "external_tables", Неопределено);

    Если Данные <> Неопределено И ВнешниеТаблицы <> Неопределено Тогда
        ВызватьИсключение "Нельзя использовать Данные и Внешние таблицы одновременно при передаче через http";
    КонецЕсли;

    URL          = Соединение["address"];
    IDЗапроса    = OPI_Инструменты.ПолучитьИли(Запрос, "id"         , Неопределено);
    ТекстЗапроса = OPI_Инструменты.ПолучитьИли(Запрос, "query"      , Неопределено);
    ФорматДанных = OPI_Инструменты.ПолучитьИли(Запрос, "format"     , "JSON");
    БазаДанных   = OPI_Инструменты.ПолучитьИли(Запрос, "database"   , Неопределено);

    Если ТекстЗапроса <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(ТекстЗапроса, Истина);
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса, Символы.ПС, "");
    КонецЕсли;

    HTTPКлиент = OPI_ЗапросыHTTP
        .НовыйЗапрос()
        .Инициализировать(URL)
        .ДобавитьПараметрURL("query"         , ТекстЗапроса, Истина)
        .ДобавитьПараметрURL("query_id"      , IDЗапроса   , Истина)
        .ДобавитьПараметрURL("default_format", ФорматДанных, Истина)

        .ДобавитьЗаголовок("X-ClickHouse-Database", БазаДанных, Истина)
        .УстановитьДвоичноеТело(Данные);

    УстановитьАвторизациюHTTP(HTTPКлиент, Соединение);
    УстановитьВнешниеТаблицыHTTP(HTTPКлиент, ВнешниеТаблицы);
    УстановитьСессиюHTTP(HTTPКлиент, Сессия);
    УстановитьДополнительныеПараметрыHTTP(HTTPКлиент, Запрос);
    УстановитьДополнительныеЗаголовкиHTTP(HTTPКлиент, Соединение);


    HTTPКлиент.ОбработатьЗапрос("POST", Истина);

    Если HTTPКлиент.Ошибка Тогда
        Ответ = ОформитьОшибочныйОтветHTTP(HTTPКлиент);
    Иначе

        Попытка

            ОтветОбъект = HTTPКлиент.ВернутьОтвет(, Истина);
            ОтветКод    = ОтветОбъект.КодСостояния;

            Ответ = Новый Соответствие();
            Ответ.Вставить("status", ОтветОбъект.КодСостояния);

            Если ОтветКод < 300 Тогда

                Результат = Истина;

                Если ЭтоФорматВалидногоJSON(ФорматДанных) Тогда
                    ТелоОтвета = HTTPКлиент.ВернутьОтветКакJSONКоллекцию();
                ИначеЕсли ЭтоСтроковыйФормат(ФорматДанных) Тогда
                    ТелоОтвета = HTTPКлиент.ВернутьОтветКакСтроку();
                Иначе
                    ТелоОтвета = HTTPКлиент.ВернутьОтветКакДвоичныеДанные();
                КонецЕсли;

                Если OPI_Инструменты.ЭтоCLI() Тогда

                    ТелоОтвета = ?(ТипЗнч(ТелоОтвета) = Тип("ДвоичныеДанные")
                        , ПолучитьBase64СтрокуИзДвоичныхДанных(ТелоОтвета)
                        , ТелоОтвета);

                КонецЕсли;

            Иначе

                Результат  = Ложь;
                ТелоОтвета = HTTPКлиент.ВернутьОтветКакСтроку();

            КонецЕсли;


            Ответ.Вставить("result" , Результат);
            Ответ.Вставить("body"   , ТелоОтвета);
            Ответ.Вставить("headers", ОтветОбъект.Заголовки);

        Исключение
            Ответ = ОформитьОшибочныйОтветHTTP(HTTPКлиент);
        КонецПопытки;

    КонецЕсли;

    Возврат Ответ;

КонецФункции

Функция ОформитьОшибочныйОтветHTTP(HTTPКлиент)

    Лог   = HTTPКлиент.ПолучитьЛог();
    Ответ = Новый Соответствие;
    Ответ.Вставить("result", Ложь);
    Ответ.Вставить("error" , Лог[Лог.ВГраница()]);
    Ответ.Вставить("log"   , Лог);

    Возврат Ответ;

КонецФункции

Процедура УстановитьДополнительныеПараметрыHTTP(HTTPКлиент, Знач Запрос)

    Настройки = OPI_Инструменты.ПолучитьИли(Запрос, "settings" , Новый Структура);
    OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Настройки, "Передана некорректная структура доп. настроек");

    Для Каждого Настройка Из Настройки Цикл
        HTTPКлиент.ДобавитьПараметрURL(Настройка.Ключ, Настройка.Значение);
    КонецЦикла;

КонецПроцедуры

Процедура УстановитьДополнительныеЗаголовкиHTTP(HTTPКлиент, Знач Соединение)

    Заголовки = OPI_Инструменты.ПолучитьИли(Соединение, "headers" , Новый Соответствие);

    OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Заголовки
        , "Передана некорректная структура доп. заголовков");

    Для Каждого Заголовок Из Заголовки Цикл
        HTTPКлиент.ДобавитьЗаголовок(Заголовок.Ключ, Заголовок.Значение);
    КонецЦикла;

КонецПроцедуры

Процедура УстановитьАвторизациюHTTP(HTTPКлиент, Знач Соединение)

    Авторизация = OPI_Инструменты.ПолучитьИли(Соединение, "auth_type", "none");
    OPI_ПреобразованиеТипов.ПолучитьСтроку(Авторизация);

    Если Авторизация = "jwt" Тогда

        JWT = OPI_Инструменты.ПолучитьИли(Соединение, "token", "");
        HTTPКлиент.ДобавитьBearerАвторизацию(JWT);

    ИначеЕсли Авторизация = "basic" Тогда

        Логин  = OPI_Инструменты.ПолучитьИли(Соединение, "user"    , Неопределено);
        Пароль = OPI_Инструменты.ПолучитьИли(Соединение, "password", Неопределено);

        HTTPКлиент
            .ДобавитьЗаголовок("X-ClickHouse-User", Логин , Истина)
            .ДобавитьЗаголовок("X-ClickHouse-Key" , Пароль, Истина);

    Иначе
        Возврат;
    КонецЕсли;

КонецПроцедуры

Процедура УстановитьВнешниеТаблицыHTTP(HTTPКлиент, Знач ВнешниеТаблицы)

    Если Не ЗначениеЗаполнено(ВнешниеТаблицы) Тогда
        Возврат;
    КонецЕсли;

    ТипДанныхФайла = "application/octet-stream";
    HTTPКлиент.НачатьЗаписьТелаMultipart(Ложь);

    OPI_ПреобразованиеТипов.ПолучитьМассив(ВнешниеТаблицы);

    Для Каждого Таблица Из ВнешниеТаблицы Цикл

        ТекстОшибки = "Передана некорректная структура внешней таблицы";
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Таблица, ТекстОшибки);

        ИмяТаблицы     = OPI_Инструменты.ПолучитьИли(Таблица, "name"  , "");
        ФорматТаблицы  = OPI_Инструменты.ПолучитьИли(Таблица, "format", Неопределено);
        ДанныеТаблицы  = OPI_Инструменты.ПолучитьИли(Таблица, "data"  , Неопределено);
        КолонкиТаблицы = OPI_Инструменты.ПолучитьИли(Таблица, "cols"  , Неопределено);

        ТекстОшибки = СтрШаблон("Некорректная структура колонок для таблицы ""%1""", ИмяТаблицы);
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(КолонкиТаблицы, ТекстОшибки);

        МассивКолонок = Новый Массив;

        Для Каждого КолонкаТаблицы Из КолонкиТаблицы Цикл

            КолонкаСтрокой = СтрШаблон("%1 %2", КолонкаТаблицы.Ключ, КолонкаТаблицы.Значение);
            МассивКолонок.Добавить(КолонкаСтрокой);

        КонецЦикла;

        КолонкиТаблицыСтрокой = СтрСоединить(МассивКолонок, ", ");
        ИмяПараметраФормат    = СтрШаблон("%1_%2" , ИмяТаблицы, "format");
        ИмяПараметраСтруктура = СтрШаблон("%1_%2" , ИмяТаблицы, "structure");
        ИмяФайла              = СтрШаблон("%1.bin", ИмяТаблицы);

        HTTPКлиент
            .ДобавитьПараметрURL(ИмяПараметраФормат   , ФорматТаблицы        , Истина)
            .ДобавитьПараметрURL(ИмяПараметраСтруктура, КолонкиТаблицыСтрокой, Истина)

            .ДобавитьФайлMultipartFormData(ИмяТаблицы, ИмяФайла, ДанныеТаблицы, ТипДанныхФайла, Истина);

    КонецЦикла;

КонецПроцедуры

Процедура УстановитьСессиюHTTP(HTTPКлиент, Знач Сессия)

    Если Не ЗначениеЗаполнено(Сессия) Тогда
        Возврат;
    КонецЕсли;

    ТекстОшибки = "Передана некорректная структура информации о сессии";
    OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Сессия, ТекстОшибки);

    IDСессии = OPI_Инструменты.ПолучитьИли(Сессия, "id"     , Неопределено);
    Проверка = OPI_Инструменты.ПолучитьИли(Сессия, "check"  , Неопределено);
    Таймаут  = OPI_Инструменты.ПолучитьИли(Сессия, "timeout", Неопределено);

    HTTPКлиент
        .ДобавитьПараметрURL("session_id"     , IDСессии, Истина)
        .ДобавитьПараметрURL("session_check"  , Проверка, Истина)
        .ДобавитьПараметрURL("session_timeout", Таймаут , Истина);

КонецПроцедуры

#КонецОбласти

#Область GRPC

Функция ВыполнитьЗапросЧерезGRPC(Знач Соединение, Знач Запрос, Знач Сессия)

    Если OPI_GRPC.ЭтоКоннектор(Соединение) Тогда
        ЗакрыватьСоединение = Ложь;
        Коннектор           = Соединение;
        НастройкиСоединения = OPI_Инструменты.JsonВСтруктуру(Коннектор.GetSettings());
    Иначе
        ЗакрыватьСоединение = Истина;
        Коннектор           = ОткрытьСоединениеGRPC(Соединение);
        НастройкиСоединения = Соединение;
    КонецЕсли;

    Если Не OPI_GRPC.ЭтоКоннектор(Коннектор) Тогда
        Возврат Коннектор;
    КонецЕсли;

    ЗапросGRPC = СформироватьЗапросGRPC(НастройкиСоединения, Запрос, Сессия);

    Сервис    = ПолучитьИмяСервисаGRPC();
    Метод     = "ExecuteQuery";
    Результат = OPI_GRPC.ВызватьМетод(Коннектор, Сервис, Метод, ЗапросGRPC);

    ОбработатьОтветGRPC(Результат);

    Если ЗакрыватьСоединение Тогда
        OPI_GRPC.ЗакрытьСоединение(Коннектор);
    КонецЕсли;

    Возврат Результат;

КонецФункции

Функция СформироватьЗапросGRPC(Знач Соединение, Знач Запрос, Знач Сессия)

    Если Запрос = Неопределено Тогда
        Возврат Запрос;
    КонецЕсли;

    Если OPI_GRPC.ЭтоКоннектор(Соединение) Тогда
        НастройкиСоединения = OPI_Инструменты.JsonВСтруктуру(Соединение.GetSettings());
    Иначе
        НастройкиСоединения = Соединение;
    КонецЕсли;

    Данные         = OPI_Инструменты.ПолучитьИли(Запрос, "data"           , Неопределено);
    IDЗапроса      = OPI_Инструменты.ПолучитьИли(Запрос, "id"             , Неопределено);
    ТекстЗапроса   = OPI_Инструменты.ПолучитьИли(Запрос, "query"          , Неопределено);
    ФорматДанных   = OPI_Инструменты.ПолучитьИли(Запрос, "format"         , Неопределено);
    БазаДанных     = OPI_Инструменты.ПолучитьИли(Запрос, "database"       , Неопределено);
    ДопНастройки   = OPI_Инструменты.ПолучитьИли(Запрос, "settings"       , Новый Структура);
    ВнешниеТаблицы = OPI_Инструменты.ПолучитьИли(Запрос, "external_tables", Неопределено);

    OPI_ПреобразованиеТипов.ПолучитьДвоичныеДанные(Данные, Истина);

    ЗапросGRPC = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("query"        , ТекстЗапроса, "Строка"        , ЗапросGRPC);
    OPI_Инструменты.ДобавитьПоле("query_id"     , IDЗапроса   , "Строка"        , ЗапросGRPC);
    OPI_Инструменты.ДобавитьПоле("settings"     , ДопНастройки, "КлючИЗначение" , ЗапросGRPC);
    OPI_Инструменты.ДобавитьПоле("database"     , БазаДанных  , "Строка"        , ЗапросGRPC);
    OPI_Инструменты.ДобавитьПоле("input_data"   , Данные      , "ДвоичныеДанные", ЗапросGRPC);
    OPI_Инструменты.ДобавитьПоле("output_format", ФорматДанных, "Строка"        , ЗапросGRPC);

    УстановитьАвторизациюGRPC(ЗапросGRPC, НастройкиСоединения);
    УстановитьВнешниеТаблицыGRPC(ЗапросGRPC, ВнешниеТаблицы);
    УстановитьСессиюGRPC(ЗапросGRPC, Сессия);

    Возврат ЗапросGRPC;

КонецФункции

Функция ПолучитьИмяСервисаGRPC()

    Возврат "clickhouse.grpc.ClickHouse";

КонецФункции

Процедура УстановитьАвторизациюGRPC(ЗапросGRPC, Знач НастройкиСоединения)

    Авторизация = OPI_Инструменты.ПолучитьИли(НастройкиСоединения, "auth_type", "none");
    OPI_ПреобразованиеТипов.ПолучитьСтроку(Авторизация);

    Если Авторизация = "jwt" Тогда

        JWT = OPI_Инструменты.ПолучитьИли(НастройкиСоединения, "token", "");
        OPI_Инструменты.ДобавитьПоле("jwt", JWT, "Строка", ЗапросGRPC);

    ИначеЕсли Авторизация = "basic" Тогда

        Логин  = OPI_Инструменты.ПолучитьИли(НастройкиСоединения, "user", Неопределено);
        Пароль = OPI_Инструменты.ПолучитьИли(НастройкиСоединения, "password", Неопределено);

        OPI_Инструменты.ДобавитьПоле("user_name", Логин , "Строка", ЗапросGRPC);
        OPI_Инструменты.ДобавитьПоле("password" , Пароль, "Строка", ЗапросGRPC);

    Иначе
        Возврат;
    КонецЕсли;

КонецПроцедуры

Процедура УстановитьВнешниеТаблицыGRPC(ЗапросGRPC, Знач ВнешниеТаблицы)

    Если Не ЗначениеЗаполнено(ВнешниеТаблицы) Тогда
        Возврат;
    КонецЕсли;

    OPI_ПреобразованиеТипов.ПолучитьМассив(ВнешниеТаблицы);

    МассивТаблиц = Новый Массив;

    Для Каждого Таблица Из ВнешниеТаблицы Цикл

        ТекстОшибки = "Передана некорректная структура внешней таблицы";
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Таблица, ТекстОшибки);

        ИмяТаблицы     = OPI_Инструменты.ПолучитьИли(Таблица, "name"  , "");
        ФорматТаблицы  = OPI_Инструменты.ПолучитьИли(Таблица, "format", Неопределено);
        ДанныеТаблицы  = OPI_Инструменты.ПолучитьИли(Таблица, "data"  , Неопределено);
        КолонкиТаблицы = OPI_Инструменты.ПолучитьИли(Таблица, "cols"  , Неопределено);

        Если ДанныеТаблицы <> Неопределено Тогда
            OPI_ПреобразованиеТипов.ПолучитьДвоичныеДанные(ДанныеТаблицы, Истина);
        КонецЕсли;

        ТекстОшибки = СтрШаблон("Некорректная структура колонок для таблицы ""%1""", ИмяТаблицы);
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(КолонкиТаблицы, ТекстОшибки);

        СтруктураТаблицы = Новый Структура;

        OPI_Инструменты.ДобавитьПоле("name"            , ИмяТаблицы   , "Строка"        , СтруктураТаблицы);
        OPI_Инструменты.ДобавитьПоле("data"            , ДанныеТаблицы, "ДвоичныеДанные", СтруктураТаблицы);
        OPI_Инструменты.ДобавитьПоле("format"          , ФорматТаблицы, "Строка"        , СтруктураТаблицы);
        OPI_Инструменты.ДобавитьПоле("compression_type", "none"       , "Строка"        , СтруктураТаблицы);

        МассивКолонок = Новый Массив;

        Для Каждого КолонкаТаблицы Из КолонкиТаблицы Цикл

            ТекущаяКолонка = Новый Структура;
            OPI_Инструменты.ДобавитьПоле("name", КолонкаТаблицы.Ключ    , "Строка", ТекущаяКолонка);
            OPI_Инструменты.ДобавитьПоле("type", КолонкаТаблицы.Значение, "Строка", ТекущаяКолонка);
            МассивКолонок.Добавить(ТекущаяКолонка);

        КонецЦикла;

        СтруктураТаблицы.Вставить("columns", МассивКолонок);

        МассивТаблиц.Добавить(СтруктураТаблицы);

    КонецЦикла;

    ЗапросGRPC.Вставить("external_tables", МассивТаблиц);

КонецПроцедуры

Процедура УстановитьСессиюGRPC(ЗапросGRPC, Знач Сессия)

    Если Не ЗначениеЗаполнено(Сессия) Тогда
        Возврат;
    КонецЕсли;

    ТекстОшибки = "Передана некорректная структура информации о сессии";
    OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Сессия, ТекстОшибки);

    IDСессии = OPI_Инструменты.ПолучитьИли(Сессия, "id"     , Неопределено);
    Проверка = OPI_Инструменты.ПолучитьИли(Сессия, "check"  , Неопределено);
    Таймаут  = OPI_Инструменты.ПолучитьИли(Сессия, "timeout", Неопределено);

    OPI_Инструменты.ДобавитьПоле("session_id"     , IDСессии, "Строка", ЗапросGRPC);
    OPI_Инструменты.ДобавитьПоле("session_check"  , Проверка, "Булево", ЗапросGRPC);
    OPI_Инструменты.ДобавитьПоле("session_timeout", Таймаут , "Число" , ЗапросGRPC);

КонецПроцедуры

Процедура ОбработатьОтветGRPC(Ответ)

    Если Не OPI_Инструменты.ПолучитьИли(Ответ, "result", Ложь) Тогда
        Возврат;
    КонецЕсли;

    Формат = OPI_Инструменты.ПолучитьИли(Ответ, "data.output_format", Неопределено);

    Если Не ЗначениеЗаполнено(Формат) Тогда
        Возврат;
    КонецЕсли;

    B64Строка = "";

    Если  OPI_Инструменты.ПолеКоллекцииСуществует(Ответ, "data.output.BYTES", B64Строка) Тогда

        Если ЭтоФорматВалидногоJSON(Формат) Тогда

            Значение = ПолучитьДвоичныеДанныеИзBase64Строки(B64Строка);
            Успех    = Ложь;
            OPI_ПреобразованиеТипов.ПолучитьКоллекцию(Значение, , Успех);

            Если Не Успех Тогда
                Значение = ПолучитьСтрокуИзДвоичныхДанных(Значение);
            КонецЕсли;

        ИначеЕсли ЭтоСтроковыйФормат(Формат) Тогда

            Значение = ПолучитьДвоичныеДанныеИзBase64Строки(B64Строка);
            Значение = ПолучитьСтрокуИзДвоичныхДанных(Значение);

        Иначе

            Возврат;

        КонецЕсли;

        Ответ["data"]["output"] = Значение;

    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти
