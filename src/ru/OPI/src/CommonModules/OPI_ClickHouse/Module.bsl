// OneScript: ./OInt/core/Modules/OPI_ClickHouse.os
// Lib: ClickHouse
// CLI: clickhouse
// Keywords: clickhouse

// MIT License

// Copyright (c) 2023-2026 Anton Tsitavets

// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:

// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.

// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

// https://github.com/Bayselonarrend/OpenIntegrations

// BSLLS:Typo-off
// BSLLS:LatinAndCyrillicSymbolInWord-off
// BSLLS:IncorrectLineBreak-off
// BSLLS:NumberOfOptionalParams-off
// BSLLS:UsingServiceTag-off
// BSLLS:LineLength-off
// BSLLS:UsingSynchronousCalls-off

//@skip-check module-structure-top-region
//@skip-check module-structure-method-in-regions
//@skip-check wrong-string-literal-content
//@skip-check method-too-many-params
//@skip-check constructor-function-return-section
//@skip-check doc-comment-collection-item-type

#Область ПрограммныйИнтерфейс

#Область ОсновныеМетоды

// Выполнить запрос
// Выполняет запрос с указанными параметрами
// 
// Параметры:
//  Соединение - Произвольный               - Настройки или объект соединения. См. ПолучитьНастройкиСоединения - conn
//  Запрос     - Структура Из КлючИЗначение - Данные запроса. См. ПолучитьНастройкиЗапроса                     - req
//  Сессия     - Структура Из КлючИЗначение - Настройки сессии. См. ПолучитьНастройкиСессии                    - session
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения
Функция ВыполнитьЗапрос(Знач Соединение, Знач Запрос, Знач Сессия = Неопределено) Экспорт
    
    OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Запрос, "Некорректная структура запроса");
        
    Если Сессия <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Сессия, "Некорректная структура сессии");
    КонецЕсли;
    
    ПереданКоннекторGrpc = OPI_GRPC.ЭтоКоннектор(Соединение);
    ТранспортПоУмолчанию = ?(ПереданКоннекторGrpc, "grpc", "http");
    
    Если Не ПереданКоннекторGrpc Тогда
        
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Соединение, "Некорректная структура соединения");
        ОтсутствующиеПоля = OPI_Инструменты.НайтиОтсутствующиеПоляКоллекции(Соединение, "address");
        
        Если ЗначениеЗаполнено(ОтсутствующиеПоля) Тогда      
            ПоляСтрокой = СтрСоединить(ОтсутствующиеПоля, ", ");
            ТекстОшибки = СтрШаблон("Отсутствуют обязательные поля настроек соединения: %1", ПоляСтрокой);
            ВызватьИсключение ТекстОшибки;    
        КонецЕсли;
        
    КонецЕсли;
    
    Транспорт = OPI_Инструменты.ПолучитьИли(Соединение, "transport", ТранспортПоУмолчанию);
    
    Если Транспорт = "http" Тогда
              
        Результат = ВыполнитьЗапросЧерезHttp(Соединение, Запрос, Сессия);
        
    ИначеЕсли Транспорт = "grpc" Тогда
            
        Результат = ВыполнитьЗапросЧерезGrpc(Соединение, Запрос, Сессия);
        
    КонецЕсли;
    
    Возврат Результат;
    
КонецФункции

// Получить настройки соединения
// Получает структуру настроек соединения
// 
// Параметры:
//  Адрес        - Строка                             - Адрес подключения с протоколом и портом            - url 
//  Авторизация  - Строка, Структура Из КлючИЗначение - Авторизация: строка для JWT, структура для basic   - auth
//  ДопЗаголовки - Соответствие Из КлючИЗначение      - Дополнительные заголовки запроса (только для http) - headers 
//  Транспорт    - Строка                             - Вид транспорта для подключения: http, grpc         - trns
// 
// Возвращаемое значение:
//  Структура Из КлючИЗначение - Структура настроек соединения
Функция ПолучитьНастройкиСоединения(Знач Адрес
    , Знач Авторизация = Неопределено
    , Знач ДопЗаголовки = Неопределено
    , Знач Транспорт = "http") Экспорт
    
    НастройкиСоединения = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("address", Адрес       , "Строка"       , НастройкиСоединения);
    OPI_Инструменты.ДобавитьПоле("headers", ДопЗаголовки, "КлючИЗначение", НастройкиСоединения);
    
    OPI_ПреобразованиеТипов.ПолучитьСтроку(Транспорт);
    Транспорт = нРег(Транспорт);
    
    Если Транспорт <> "http" И Транспорт <> "grpc" Тогда
        ВызватьИсключение "Некорректный вид транспорта. Доступные виды: http, grpc";
    КонецЕсли;
    
    OPI_Инструменты.ДобавитьПоле("transport", Транспорт, "Строка", НастройкиСоединения);
    
    Если Авторизация = Неопределено Тогда
        Возврат НастройкиСоединения;
    КонецЕсли;
    
    OPI_ПреобразованиеТипов.ПолучитьКоллекцию(Авторизация);
    
    Если ТипЗнч(Авторизация) = Тип("Массив") Тогда
        
        JWT = Авторизация[0];
        OPI_Инструменты.ДобавитьПоле("auth_type" , "jwt", "Строка", НастройкиСоединения);
        OPI_Инструменты.ДобавитьПоле("token"     , JWT  , "Строка", НастройкиСоединения);
        
    Иначе
        
        OPI_Инструменты.ДобавитьПоле("auth_type" , "basic", "Строка", НастройкиСоединения);
        
        Для Каждого КлючЗначение Из Авторизация Цикл
            OPI_Инструменты.ДобавитьПоле("user"     , КлючЗначение.Ключ    , "Строка", НастройкиСоединения);
            OPI_Инструменты.ДобавитьПоле("password" , КлючЗначение.Значение, "Строка", НастройкиСоединения);
            Прервать;    
        КонецЦикла;
        
    КонецЕсли;
        
    Возврат НастройкиСоединения;
    
КонецФункции

// Получить настройки запроса
// Формирует структуру описания запроса
// 
// При использовании транспорта `http` нельзя одновременно использовать Данные и Внешние таблицы
// 
// Параметры:
//  Текст          - Строка                        - Текст запроса                                                     - query
//  БазаДанных     - Строка                        - База данных                                                       - db
//  IDЗапроса      - Строка                        - Уникальный ID запроса, если необъодимо                            - id
//  Данные         - Произвольный                  - Строка, файл или двоичные данные запроса                          - data
//  ФорматДанных   - Строка                        - Формат данных: CVS, TVS, JSON и др.                               - format
//  ВнешниеТаблицы - Массив Из Структура           - Информация о внешних таблицах. См. ПолучитьСтруктуруВнешнихТаблиц - ext
//  Настройки      - Соответствие Из КлючИЗначение - Дополнительные query параметры запроса                            - settings
// 
// Возвращаемое значение:
//  Структура Из КлючИЗначение - Структура запроса
Функция ПолучитьНастройкиЗапроса(Знач Текст
    , Знач БазаДанных = Неопределено
    , Знач IDЗапроса = Неопределено
    , Знач Данные = Неопределено
    , Знач ФорматДанных = Неопределено
    , Знач ВнешниеТаблицы = Неопределено
    , Знач Настройки = Неопределено) Экспорт
    
    ПараметрыЗапроса = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("query"          , Текст         , "Строка"       , ПараметрыЗапроса);
    OPI_Инструменты.ДобавитьПоле("database"       , БазаДанных    , "Строка"       , ПараметрыЗапроса);
    OPI_Инструменты.ДобавитьПоле("id"             , IDЗапроса     , "Строка"       , ПараметрыЗапроса);
    OPI_Инструменты.ДобавитьПоле("data"           , Данные        , "Текущий"      , ПараметрыЗапроса);
    OPI_Инструменты.ДобавитьПоле("format"         , ФорматДанных  , "Строка"       , ПараметрыЗапроса);
    OPI_Инструменты.ДобавитьПоле("external_tables", ВнешниеТаблицы, "Массив"       , ПараметрыЗапроса);
    OPI_Инструменты.ДобавитьПоле("settings"       , Настройки     , "КлючИЗначение", ПараметрыЗапроса);
    
    Возврат ПараметрыЗапроса;
    
КонецФункции

// Получить структуру внешней таблицы
// Получает структуру описания внешней таблицы запроса
// 
// Параметры:
//  Имя              - Строка                     - Имя таблицы                                                  - name
//  СтруктураКолонок - Структура Из КлючИЗначение - Структура колонок таблицы: Ключ > имя, Значение > тип данных - cols
//  Данные           - Произвольный               - Строка, файл или двоичные данные таблицы                     - data
//  ФорматДанных     - Строка                     - Формат данных: CVS, TVS, JSON и др.                          - format
// 
// Возвращаемое значение:
//  Структура Из КлючИЗначение - Структура описания внешней таблицы
Функция ПолучитьСтруктуруВнешнейТаблицы(Знач Имя
    , Знач СтруктураКолонок
    , Знач Данные = Неопределено
    , Знач ФорматДанных = Неопределено) Экспорт

    ПараметрыТаблицы = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("name"  , Имя             , "Строка"       , ПараметрыТаблицы);
    OPI_Инструменты.ДобавитьПоле("cols"  , СтруктураКолонок, "КлючИЗначение", ПараметрыТаблицы);
    OPI_Инструменты.ДобавитьПоле("data"  , Данные          , "Текущий"      , ПараметрыТаблицы);
    OPI_Инструменты.ДобавитьПоле("format", ФорматДанных    , "Строка"       , ПараметрыТаблицы);
    
    Возврат ПараметрыТаблицы;
        
КонецФункции

// Получить настройки сессии
// Формирует структуру настроек сессии выполнения запроса
// 
// Параметры:
//  IDСессии        - Строка - ID сессии. Новый уникальный идентификатор, если не указано (будет создана новая сессия)         - id
//  ПроверятьСессию - Булево - Проверять существование сессии. Истина, когда ID указан и ложь, когда нет, если не указано иное - check
//  Таймаут         - Число  - Время жизни сессии в секундах                                                                   - timeout                                      
// 
// Возвращаемое значение:
//  Структура Из КлючИЗначение - Структура настроек сессии
Функция ПолучитьНастройкиСессии(Знач IDСессии = Неопределено
    , Знач ПроверятьСессию = Неопределено
    , Знач Таймаут = 60) Экспорт
    
    НастройкиСессии         = Новый Структура;
    ЕстьИдентификаторСессии = IDСессии <> Неопределено;
    
    Если ЕстьИдентификаторСессии Тогда
        IDСессии = Строка(Новый УникальныйИдентификатор());
    КонецЕсли;
    
    Если ПроверятьСессию = Неопределено Тогда
        ПроверятьСессию = ЕстьИдентификаторСессии;
    КонецЕсли;
    
    OPI_Инструменты.ДобавитьПоле("id"     , IDСессии       , "Строка", НастройкиСессии);
    OPI_Инструменты.ДобавитьПоле("check"  , ПроверятьСессию, "Булево", НастройкиСессии);
    OPI_Инструменты.ДобавитьПоле("timeout", Таймаут        , "Число" , НастройкиСессии);
    
    Возврат НастройкиСессии;
    
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВыполнитьЗапросЧерезHttp(Знач Соединение, Знач Запрос, Знач Сессия)
    
    Данные         = OPI_Инструменты.ПолучитьИли(Запрос, "data"           , Неопределено);
    ВнешниеТаблицы = OPI_Инструменты.ПолучитьИли(Запрос, "external_tables", Неопределено);
    
    Если Данные <> Неопределено И ВнешниеТаблицы <> Неопределено Тогда
        ВызватьИсключение "Нельзя использовать Данные и Внешние таблицы одновременно при передаче через http";
    КонецЕсли;
    
    URL           = Соединение["address"];
    IDЗапроса     = OPI_Инструменты.ПолучитьИли(Запрос, "id"       , Строка(Новый УникальныйИдентификатор));   
    ТекстЗапроса  = OPI_Инструменты.ПолучитьИли(Запрос, "query"    , Неопределено);
    ФорматДанных  = OPI_Инструменты.ПолучитьИли(Запрос, "format"   , Неопределено);
    БазаДанных    = OPI_Инструменты.ПолучитьИли(Запрос, "database" , Неопределено);
    
    HTTPКлиент = OPI_ЗапросыHTTP
        .НовыйЗапрос()
        .Инициализировать(URL)
        .ДобавитьПараметрURL("query"         , ТекстЗапроса, Истина)
        .ДобавитьПараметрURL("format"        , ФорматДанных, Истина)
        .ДобавитьПараметрURL("query_id"      , IDЗапроса   , Истина)
        .ДобавитьПараметрURL("default_format", "JSON"      , Истина)

        .ДобавитьЗаголовок("X-ClickHouse-Database", БазаДанных, Истина)
        .УстановитьДвоичноеТело(Данные);
    
    УстановитьДополнительныеПараметры(HTTPКлиент, Запрос);
    УстановитьДополнительныеЗаголовки(HTTPКлиент, Соединение);
    УстановитьАвторизацию(HTTPКлиент, Запрос);
    УстановитьВнешниеТаблицы(HTTPКлиент, ВнешниеТаблицы);    
       
    HTTPКлиент.ОбработатьЗапрос("POST", Истина);
    
    Если HTTPКлиент.Ошибка Тогда
        
        Ответ = ОформитьОшибочныйОтвет(HTTPКлиент);
        
    Иначе
        
        Попытка
            
            ОтветОбъект = HTTPКлиент.ВернутьОтвет(, Истина);
            ОтветJSON   = HTTPКлиент.ВернутьОтветКакJSONКоллекцию();
        
            Ответ = Новый Соответствие();
            Ответ.Вставить("result", Истина);
            Ответ.Вставить("status", ОтветОбъект.КодСостояния);
            
            Если OPI_Инструменты.ЭтоCLI() Тогда
                
                ОтветJSON = ?(ТипЗнч(ОтветJSON) = Тип("ДвоичныеДанные")
                    , ПолучитьBase64СтрокуИзДвоичныхДанных(ОтветJSON)
                    , ОтветJSON);
                    
            КонецЕсли;
            
            Ответ.Вставить("body", ОтветJSON);
            Ответ.Вставить("headers", ОтветОбъект.Заголовки);
        
        Исключение
            Ответ = ОформитьОшибочныйОтвет(HTTPКлиент);
        КонецПопытки;
        
    КонецЕсли;
    
    Возврат Ответ;
    
КонецФункции

Функция ВыполнитьЗапросЧерезGrpc(Знач Соединение, Знач Запрос, Знач Сессия)
    
КонецФункции

Функция ОформитьОшибочныйОтвет(HTTPКлиент)
    
    Лог   = HTTPКлиент.ПолучитьЛог();
    Ответ = Новый Соответствие;
    Ответ.Вставить("result", Ложь);
    Ответ.Вставить("error" , Лог[Лог.ВГраница()]);
    Ответ.Вставить("log"   , Лог);
    
    Возврат Ответ;
    
КонецФункции

Процедура УстановитьДополнительныеПараметры(HTTPКлиент, Знач Запрос)

    Настройки = OPI_Инструменты.ПолучитьИли(Запрос, "settings" , Новый Структура); 
    OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Настройки, "Передана некорректная структура доп. настроек");
    
    Для Каждого Настройка Из Настройки Цикл
        HTTPКлиент.ДобавитьПараметрURL(Настройка.Ключ, Настройка.Значение);    
    КонецЦикла;
       
КонецПроцедуры

Процедура УстановитьДополнительныеЗаголовки(HTTPКлиент, Знач Соединение)

    Заголовки = OPI_Инструменты.ПолучитьИли(Соединение, "headers" , Новый Соответствие); 
    
    OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Заголовки
        , "Передана некорректная структура доп. заголовков");
    
    Для Каждого Заголовок Из Заголовки Цикл
        HTTPКлиент.ДобавитьЗаголовок(Заголовок.Ключ, Заголовок.Значение);    
    КонецЦикла;
       
КонецПроцедуры

Процедура УстановитьАвторизацию(HTTPКлиент, Знач Запрос)

    Авторизация   = OPI_Инструменты.ПолучитьИли(Запрос, "auth_type", "none");
    OPI_ПреобразованиеТипов.ПолучитьСтроку(Авторизация);
    
    Если Авторизация = "jwt" Тогда
        
        JWT = OPI_Инструменты.ПолучитьИли(Запрос, "token", "");
        HTTPКлиент.ДобавитьBearerАвторизацию(JWT);
        
    ИначеЕсли Авторизация = "basic" Тогда
        
        Логин  = OPI_Инструменты.ПолучитьИли(Запрос, "user", Неопределено);
        Пароль = OPI_Инструменты.ПолучитьИли(Запрос, "password", Неопределено);
        
        HTTPКлиент
            .ДобавитьЗаголовок("X-ClickHouse-User", Логин , Истина)
            .ДобавитьЗаголовок("X-ClickHouse-Key" , Пароль, Истина);
            
    Иначе
        Возврат;            
    КонецЕсли;
        
КонецПроцедуры

Процедура УстановитьВнешниеТаблицы(HTTPКлиент, Знач ВнешниеТаблицы)

    Если Не ЗначениеЗаполнено(ВнешниеТаблицы) Тогда
        Возврат;
    КонецЕсли;
        
    ТипДанныхФайла = "application/octet-stream";
    HTTPКлиент.НачатьЗаписьТелаMultipart(Ложь);
    
    OPI_ПреобразованиеТипов.ПолучитьМассив(ВнешниеТаблицы);
    
    Для Каждого Таблица Из ВнешниеТаблицы Цикл
 
        ИмяТаблицы     = OPI_Инструменты.ПолучитьИли(Таблица, "name"  , "");
                
        ТекстОшибки = СтрШаблон("Передана некорректная структура внешней таблицы", ИмяТаблицы);
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Таблица, ТекстОшибки);
        
        ФорматТаблицы  = OPI_Инструменты.ПолучитьИли(Таблица, "format", Неопределено);
        ДанныеТаблицы  = OPI_Инструменты.ПолучитьИли(Таблица, "data"  , Неопределено);
        КолонкиТаблицы = OPI_Инструменты.ПолучитьИли(Таблица, "cols"  , Неопределено);
        
        ТекстОшибки = СтрШаблон("Некорректная структура колонок для таблицы ""%1""", ИмяТаблицы);
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(КолонкиТаблицы, ТекстОшибки);
        
        МассивКолонок = Новый Массив;
        
        Для Каждого КолонкаТаблицы Из КолонкиТаблицы Цикл
            
            КолонкаСтрокой = СтрШаблон("%1 %2", КолонкаТаблицы.Ключ, КолонкаТаблицы.Значение);
            МассивКолонок.Добавить(КолонкаСтрокой);
            
        КонецЦикла;
        
        КолонкиТаблицыСтрокой = СтрСоединить(МассивКолонок, ", ");
        ИмяПараметраФормат    = СтрШаблон("%1_%2" , ИмяТаблицы, "format");
        ИмяПараметраСтруктура = СтрШаблон("%1_%2" , ИмяТаблицы, "structure");
        ИмяФайла              = СтрШаблон("%1.bin", ИмяТаблицы);
        
        HTTPКлиент
            .ДобавитьПараметрURL(ИмяПараметраФормат   , ФорматТаблицы        , Истина)
            .ДобавитьПараметрURL(ИмяПараметраСтруктура, КолонкиТаблицыСтрокой, Истина)
            .ДобавитьФайлMultipartFormData(ИмяТаблицы, ИмяФайла, ДанныеТаблицы, ТипДанныхФайла, Истина);
        
    КонецЦикла;
                      
КонецПроцедуры

#КонецОбласти