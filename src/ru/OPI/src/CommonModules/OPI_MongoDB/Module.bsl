// OneScript: ./OInt/core/Modules/OPI_MongoDB.os
// Lib: MongoDB
// CLI: mongodb
// Keywords: mongodb, mongo, nosql

// MIT License

// Copyright (c) 2023-2025 Anton Tsitavets

// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:

// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.

// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

// https://github.com/Bayselonarrend/OpenIntegrations

// BSLLS:Typo-off
// BSLLS:LatinAndCyrillicSymbolInWord-off
// BSLLS:IncorrectLineBreak-off
// BSLLS:NumberOfOptionalParams-off
// BSLLS:UsingServiceTag-off
// BSLLS:LineLength-off
// BSLLS:UsingSynchronousCalls-off
// BSLLS:DuplicateStringLiteral-off

//@skip-check module-structure-top-region
//@skip-check module-structure-method-in-regions
//@skip-check wrong-string-literal-content
//@skip-check method-too-many-params
//@skip-check constructor-function-return-section
//@skip-check doc-comment-collection-item-type

#Область ПрограммныйИнтерфейс

#Область ОсновныеМетоды

// Открыть соединение !NOCLI
// Создает подключение к указанной базе
//
// Параметры:
//  СтрокаПодключения - Строка - Строка подключения - string
//
// Возвращаемое значение:
//  Произвольный - Объект коннектора или структура с информацией об ошибке
Функция ОткрытьСоединение(Знач СтрокаПодключения) Экспорт

    Если ЭтоКоннектор(СтрокаПодключения) Тогда
        Возврат СтрокаПодключения;
    КонецЕсли;

    OPI_ПреобразованиеТипов.ПолучитьСтроку(СтрокаПодключения);
    OPI_Инструменты.ВернутьУправляющиеПоследовательности(СтрокаПодключения);

    Коннектор = OPI_Компоненты.ПолучитьКомпоненту("MongoDB");

    Коннектор.ConnectionString = СтрокаПодключения;

    Результат = Коннектор.Connect();
    Результат = OPI_Инструменты.JsonВСтруктуру(Результат, Ложь);

    Возврат ?(Результат["result"], Коннектор, Результат);

КонецФункции

// Закрыть соединение !NOCLI
// Явно закрывает переданное соединение
//
// Параметры:
//  Соединение - Произвольный - Объект компоненты с открытым соединением - dbc
//
// Возвращаемое значение:
//  Структура Из КлючИЗначение - Результат закрытия соединения
Функция ЗакрытьСоединение(Знач Соединение) Экспорт

    Если ЭтоКоннектор(Соединение) Тогда

        Результат = Соединение.Disconnect();
        Результат = OPI_Инструменты.JsonВСтруктуру(Результат, Ложь);

    Иначе

        Результат = Новый Структура("result,error", Ложь, "It's not a connection");

    КонецЕсли;

    Возврат Результат;

КонецФункции

// Это коннектор !NOCLI
// Проверяет, что значение является объектом внешней компоненты MSSQL
//
// Параметры:
//  Значение - Произвольный - Значение для проверки - value
//
// Возвращаемое значение:
//  Булево -  Это коннектор
Функция ЭтоКоннектор(Знач Значение) Экспорт

    Возврат Строка(ТипЗнч(Значение)) = "AddIn.OPI_MongoDB.Main";

КонецФункции

// Сформировать строку подключения
// Формирует строку для подключения к серверу MongoDB
//
// Параметры:
//  Адрес     - Строка                                   - IP адрес с портом или доменное имя для подключения - addr
//  База      - Строка                                   - База для подключения, если необходимо              - db
//  Логин     - Строка                                   - Имя пользователя авторизации                       - usr
//  Пароль    - Строка                                   - Пароль пользователя для авторизации                - pwd
//  Параметры - Структура Из КлючИЗначение, Неопределено - Дополнительные параметры подключения               - params
//
// Возвращаемое значение:
//  Строка - Строка подключения MongoDB
Функция СформироватьСтрокуПодключения(Знач Адрес
    , Знач База = ""
    , Знач Логин = ""
    , Знач Пароль = ""
    , Знач Параметры = Неопределено) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Адрес);
    OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    OPI_ПреобразованиеТипов.ПолучитьСтроку(Логин);
    OPI_ПреобразованиеТипов.ПолучитьСтроку(Пароль);

    Если ЗначениеЗаполнено(Параметры) Тогда
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Параметры);
    КонецЕсли;

    ШаблонПодключения = "mongodb://%1";

    Если ЗначениеЗаполнено(Логин) Тогда
        Авторизация = СтрШаблон("%1:%2", Логин, Пароль);
    Иначе
        Авторизация = "";
    КонецЕсли;

    Если ЗначениеЗаполнено(Авторизация) Тогда
        ОсновнаяЧасть = СтрШаблон("%1@%2", Авторизация, Адрес);
    Иначе
        ОсновнаяЧасть = Адрес;
    КонецЕсли;

    СтрокаПодключения = СтрШаблон(ШаблонПодключения, ОсновнаяЧасть);

    Если ЗначениеЗаполнено(База) Или ЗначениеЗаполнено(Параметры) Тогда

        СтрокаПодключения = СтрокаПодключения + "/";

        Если ЗначениеЗаполнено(База) Тогда
            СтрокаПодключения = СтрокаПодключения + База;
        КонецЕсли;

        Если ЗначениеЗаполнено(Параметры) Тогда

            СтрокаПодключения = СтрокаПодключения + "?";
            МассивПараметров  = Новый Массив;

            Для Каждого Параметр Из Параметры Цикл
                МассивПараметров.Добавить(СтрШаблон("%1=%2", Параметр.Ключ, Параметр.Значение));
            КонецЦикла;

            СтрокаПодключения = СтрокаПодключения + СтрСоединить(МассивПараметров, "&");

        КонецЕсли;

    КонецЕсли;

    Возврат СтрокаПодключения;

КонецФункции

// Выполнить команду
// Выполняет команду по ее описанию
//
// Параметры:
//  Соединение    - Строка, Произвольный       - Соединение или строка подключения                    - dbc
//  Комманда      - Строка                     - Имя команды для выполнения                           - comm
//  Аргумент      - Произвольный               - Аргумент команды                                     - arg
//  База          - Строка                     - База данных, в которой необходимо провести операцию  - db
//  Данные        - Структура Из КлючИЗначение - Основные поля данных для выполнения операции         - data
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция ВыполнитьКоманду(Знач Соединение
    , Знач Комманда
    , Знач Аргумент = 1
    , Знач База = Неопределено
    , Знач Данные = Неопределено) Экспорт

    Если ЭтоКоннектор(Соединение) Тогда
        ЗакрыватьСоединение = Ложь;
        Коннектор           = Соединение;
    Иначе
        ЗакрыватьСоединение = Истина;
        Коннектор           = ОткрытьСоединение(Соединение);
    КонецЕсли;

    Если Не ЭтоКоннектор(Коннектор) Тогда
        Возврат Коннектор;
    КонецЕсли;

    СтруктураОперации = Новый Структура();

    OPI_Инструменты.ДобавитьПоле("operation", Комманда, "Строка", СтруктураОперации);

    Если База <> Неопределено Тогда
        OPI_Инструменты.ДобавитьПоле("database", База, "Строка", СтруктураОперации);
    КонецЕсли;

    Если Аргумент <> Неопределено Тогда
        ОбработанныйАргумент = ОбработатьДанныеДляОперации(Аргумент);
        СтруктураОперации.Вставить("argument", ОбработанныйАргумент);
    КонецЕсли;

    Если Данные <> Неопределено Тогда
        ОбработанныеДанные = ОбработатьДанныеДляОперации(Данные);
        СтруктураОперации.Вставить("data", ОбработанныеДанные);
    КонецЕсли;

    ОперацияJSON = OPI_Инструменты.JSONСтрокой(СтруктураОперации);

    Результат = Коннектор.Execute(ОперацияJSON);
    Результат = OPI_Инструменты.JsonВСтруктуру(Результат);

    Если ЗакрыватьСоединение Тогда
        ЗакрытьСоединение(Коннектор);
    КонецЕсли;

    Возврат Результат;

КонецФункции

#КонецОбласти

#Область РаботаСБазами

// Получить список баз
// Получает список баз данных
//
// Параметры:
//  Соединение - Строка, Произвольный - Соединение или строка подключения - dbc
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция ПолучитьСписокБаз(Знач Соединение) Экспорт

    Результат = ВыполнитьКоманду(Соединение, "listDatabases");
    Возврат Результат;

КонецФункции

// Получить базу данных
// Получает информацию о базе данных
//
// Параметры:
//  Соединение - Строка, Произвольный - Соединение или строка подключения       - dbc
//  База       - Строка               - Имя базы. Текущая база, если не указано - db
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция ПолучитьБазуДанных(Знач Соединение, Знач База = Неопределено) Экспорт

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Результат = ВыполнитьКоманду(Соединение, "dbStats", , База);
    Возврат Результат;

КонецФункции

// Удалить базу данных
// Удаляет базу данных
//
// Параметры:
//  Соединение - Строка, Произвольный - Соединение или строка подключения       - dbc
//  База       - Строка               - Имя базы. Текущая база, если не указано - db
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция УдалитьБазуДанных(Знач Соединение, Знач База = Неопределено) Экспорт

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Результат = ВыполнитьКоманду(Соединение, "dropDatabase", , База);
    Возврат Результат;

КонецФункции

#КонецОбласти

#Область РаботаСКоллекциями

// Получить список коллекций
// Получает список коллекций базы данных
//
// Параметры:
//  Соединение - Строка, Произвольный - Соединение или строка подключения       - dbc
//  База       - Строка               - Имя базы. Текущая база, если не указано - db
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция ПолучитьСписокКоллекций(Знач Соединение, Знач База = Неопределено) Экспорт

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Результат = ВыполнитьКоманду(Соединение, "listCollections", , База);
    Возврат Результат;

КонецФункции

// Создать коллекцию
// Создает новую коллекцию с указанными параметрами
//
// Параметры:
//  Соединение   - Строка, Произвольный       - Соединение или строка подключения       - dbc
//  Наименование - Строка                     - Имя новой коллекции                     - name
//  База         - Строка                     - Имя базы. Текущая база, если не указано - db
//  Параметры    - Структура Из КлючИЗначение - Дополнительные параметры создания       - params
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция СоздатьКоллекцию(Знач Соединение
    , Знач Наименование
    , Знач База = Неопределено
    , Знач Параметры = Неопределено) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Наименование);

    Если Параметры <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Параметры);
    КонецЕсли;

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Результат = ВыполнитьКоманду(Соединение, "create", Наименование, База, Параметры);
    Возврат Результат;

КонецФункции

// Удалить коллекцию
// Удаляет выбранную коллекцию базы
//
// Параметры:
//  Соединение   - Строка, Произвольный - Соединение или строка подключения       - dbc
//  Коллекция    - Строка               - Имя коллекции                           - coll
//  База         - Строка               - Имя базы. Текущая база, если не указано - db
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция УдалитьКоллекцию(Знач Соединение, Знач Коллекция, Знач База = Неопределено) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Коллекция);

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Результат = ВыполнитьКоманду(Соединение, "drop", Коллекция, База);
    Возврат Результат;

КонецФункции

#КонецОбласти

#Область РаботаСДокументами

// Вставить документы
// Вставляет новые документы в коллекцию
//
// Параметры:
//  Соединение - Строка, Произвольный       - Соединение или строка подключения       - dbc
//  Коллекция  - Строка                     - Имя коллекции                           - coll
//  Значения   - Массив Из Произвольный     - Массив объектов документов              - docs
//  База       - Строка                     - Имя базы. Текущая база, если не указано - db
//  Параметры  - Структура Из КлючИЗначение - Дополнительные параметры вставки        - params
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция ВставитьДокументы(Знач Соединение
    , Знач Коллекция
    , Знач Значения
    , Знач База = Неопределено
    , Знач Параметры = Неопределено) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Коллекция);
    OPI_ПреобразованиеТипов.ПолучитьМассив(Значения);

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Если Параметры <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Параметры);
    Иначе
        Параметры = Новый Структура;
    КонецЕсли;

    МассивДокументов = Новый Массив;

    Для Каждого Элемент Из Значения Цикл

        ТекущийЭлемент = Элемент;
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(ТекущийЭлемент);
        МассивДокументов.Добавить(ТекущийЭлемент);

    КонецЦикла;

    Параметры.Вставить("documents", МассивДокументов);

    Результат = ВыполнитьКоманду(Соединение, "insert", Коллекция, База, Параметры);

    Возврат Результат;

КонецФункции

// Обновить документы
// Обновляет данные документов в коллекции
//
// Параметры:
//  Соединение - Строка, Произвольный       - Соединение или строка подключения                            - dbc
//  Коллекция  - Строка                     - Имя коллекции                                                - coll
//  Обновления - Массив Из Структура        - Массив обновлений. См. ПолучитьСтруктуруОбновленияДокументов - updates
//  База       - Строка                     - Имя базы. Текущая база, если не указано                      - db
//  Параметры  - Структура Из КлючИЗначение - Дополнительные параметры вставки                             - params
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция ОбновитьДокументы(Знач Соединение
    , Знач Коллекция
    , Знач Обновления
    , Знач База = Неопределено
    , Знач Параметры = Неопределено) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Коллекция);
    OPI_ПреобразованиеТипов.ПолучитьМассив(Обновления);

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Если Параметры <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Параметры);
    Иначе
        Параметры = Новый Структура;
    КонецЕсли;

    МассивОбновлений = Новый Массив;

    Для Каждого Элемент Из Обновления Цикл

        ТекущийЭлемент = Элемент;
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(ТекущийЭлемент);
        МассивОбновлений.Добавить(ТекущийЭлемент);

    КонецЦикла;

    Параметры.Вставить("updates", МассивОбновлений);

    Результат = ВыполнитьКоманду(Соединение, "update", Коллекция, База, Параметры);

    Возврат Результат;

КонецФункции

// Получить документы
// Получает документы коллекции
//
// Параметры:
//  Соединение - Строка, Произвольный       - Соединение или строка подключения       - dbc
//  Коллекция  - Строка                     - Имя коллекции                           - coll
//  База       - Строка                     - Имя базы. Текущая база, если не указано - db
//  Фильтр     - Структура Из КлючИЗначение - Фильтр документов                       - query
//  Сортировка - Структура Из КлючИЗначение - Сортировка выборки                      - sort
//  Параметры  - Структура Из КлючИЗначение - Дополнительные параметры получения      - params
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция ПолучитьДокументы(Знач Соединение
    , Знач Коллекция
    , Знач База = Неопределено
    , Знач Фильтр = Неопределено
    , Знач Сортировка = Неопределено
    , Знач Параметры = Неопределено) Экспорт

    Если ЭтоКоннектор(Соединение) Тогда
        ЗакрыватьСоединение = Ложь;
        Коннектор           = Соединение;
    Иначе
        ЗакрыватьСоединение = Истина;
        Коннектор           = ОткрытьСоединение(Соединение);
    КонецЕсли;

    Если Не ЭтоКоннектор(Коннектор) Тогда
        Возврат Коннектор;
    КонецЕсли;

    Курсор = ПолучитьКурсор(Коннектор, Коллекция, База, Фильтр, Сортировка, Параметры);

    Если Не Курсор["result"] Тогда
        Возврат Курсор;
    КонецЕсли;

    Курсор            = Курсор["data"]["cursor"];
    МассивДокументов  = Курсор["firstBatch"];
    IDКурсора         = Курсор["id"];
    ПродолжатьВыборку = IDКурсора > 0;

    Пока ПродолжатьВыборку Цикл

        Пакет  = ПолучитьПакетДокументов(Коннектор, Коллекция, IDКурсора, База);
        Успех  = Пакет["result"];
        Данные = Пакет["data"];

        Если Не Успех Тогда
            Возврат Пакет;
        КонецЕсли;

        Курсор = Данные["cursor"];

        Если Курсор = Неопределено Тогда
            Возврат Пакет;
        КонецЕсли;

        ПродолжатьВыборку = Курсор["id"] > 0;

        Для Каждого Запись Из Курсор["nextBatch"] Цикл
            МассивДокументов.Добавить(Запись);
        КонецЦикла;

    КонецЦикла;

    Если ЗакрыватьСоединение Тогда
        ЗакрытьСоединение(Коннектор);
    КонецЕсли;

    Результат = Новый Соответствие;
    Результат.Вставить("result", Истина);
    Результат.Вставить("data"  , МассивДокументов);

    Возврат Результат;

КонецФункции

// Удалить документы
// Удаляет документы коллекции
//
// Параметры:
//  Соединение - Строка, Произвольный       - Соединение или строка подключения                            - dbc
//  Коллекция  - Строка                     - Имя коллекции                                                - coll
//  Удаления   - Массив Из Структура        - Массив удаления. См. ПолучитьСтруктуруУдаленияДокументов     - deletes
//  База       - Строка                     - Имя базы. Текущая база, если не указано                      - db
//  Параметры  - Структура Из КлючИЗначение - Дополнительные параметры вставки                             - params
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция УдалитьДокументы(Знач Соединение
    , Знач Коллекция
    , Знач Удаления
    , Знач База = Неопределено
    , Знач Параметры = Неопределено) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Коллекция);
    OPI_ПреобразованиеТипов.ПолучитьМассив(Удаления);

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Если Параметры <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Параметры);
    Иначе
        Параметры = Новый Структура;
    КонецЕсли;

    МассивОбновлений = Новый Массив;

    Для Каждого Элемент Из Удаления Цикл

        ТекущийЭлемент = Элемент;
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(ТекущийЭлемент);
        МассивОбновлений.Добавить(ТекущийЭлемент);

    КонецЦикла;

    Параметры.Вставить("deletes", МассивОбновлений);

    Результат = ВыполнитьКоманду(Соединение, "delete", Коллекция, База, Параметры);

    Возврат Результат;

КонецФункции

// Получить курсор !NOCLI
// Получает курсор для порционного получения документов коллекции
//
// Параметры:
//  Соединение - Строка, Произвольный       - Соединение или строка подключения       - dbc
//  Коллекция  - Строка                     - Имя коллекции                           - coll
//  База       - Строка                     - Имя базы. Текущая база, если не указано - db
//  Фильтр     - Структура Из КлючИЗначение - Фильтр документов                       - query
//  Сортировка - Структура Из КлючИЗначение - Сортировка выборки                      - sort
//  Параметры  - Структура Из КлючИЗначение - Дополнительные параметры получения      - params
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция ПолучитьКурсор(Знач Соединение
    , Знач Коллекция
    , Знач База = Неопределено
    , Знач Фильтр = Неопределено
    , Знач Сортировка = Неопределено
    , Знач Параметры = Неопределено) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Коллекция);

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Если Параметры <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Параметры);
    Иначе
        Параметры = Новый Структура;
    КонецЕсли;

    OPI_Инструменты.ДобавитьПоле("filter", Фильтр    , "КлючИЗначение", Параметры);
    OPI_Инструменты.ДобавитьПоле("sort"  , Сортировка, "КлючИЗначение", Параметры);

    Результат = ВыполнитьКоманду(Соединение, "find", Коллекция, База, Параметры);

    Возврат Результат;

КонецФункции

// Получить пакет документов !NOCLI
// Получает очередной пакет документов курсора
//
// Параметры:
//  Соединение   - Строка, Произвольный - Соединение или строка подключения                   - dbc
//  Коллекция    - Строка               - Имя коллекции                                       - coll
//  IDКурсора    - Число                - ID курсора                                          - cursor
//  База         - Строка               - Имя базы. Текущая база, если не указано             - db
//  РазмерПакета - Число                - Число записей в пакете                              - limit
//  Ожидание     - Число                - Максимальное время блокировки при awaitData (в мс.) - time
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция ПолучитьПакетДокументов(Знач Соединение
    , Знач Коллекция
    , Знач IDКурсора
    , Знач База = Неопределено
    , Знач РазмерПакета = Неопределено
    , Знач Ожидание = Неопределено) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьЧисло(IDКурсора);

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("collection", Коллекция   , "Строка", Параметры);
    OPI_Инструменты.ДобавитьПоле("batchSize" , РазмерПакета, "Число" , Параметры);
    OPI_Инструменты.ДобавитьПоле("maxTimeMS" , РазмерПакета, "Число" , Параметры);

    Результат = ВыполнитьКоманду(Соединение, "getMore", IDКурсора, База, Параметры);

    Возврат Результат;

КонецФункции

// Получить структуру обновления документов
// Формирует структуру данных для обновления документов
//
// Параметры:
//  Фильтр        - Структура Из КлючИЗначение - Фильтр документов                                               - query
//  Данные        - Структура Из КлючИЗначение - Новые данные документов для обновления                          - data
//  ОбновитьВсе   - Булево                     - Истина > обновляет все документы по фильтру, Ложь > только один - multi
//  Вставлять     - Булево                     - Вставить новый документ, если ни один документ не был найден    - upsert
//  Сопоставление - Структура Из КлючИЗначение - Особые настройки правил сравнения полей при поиске              - cltn
//
// Возвращаемое значение:
//  Структура Из КлючИЗначение - Результат закрытия соединения
Функция ПолучитьСтруктуруОбновленияДокументов(Знач Фильтр
    , Знач Данные
    , Знач ОбновитьВсе = Истина
    , Знач Вставлять = Ложь
    , Знач Сопоставление = Неопределено) Экспорт

    СтруктураОбновления = Новый Структура;

    OPI_Инструменты.ДобавитьПоле("q"        , Фильтр       , "КлючИЗначение", СтруктураОбновления);
    OPI_Инструменты.ДобавитьПоле("u"        , Данные       , "КлючИЗначение", СтруктураОбновления);
    OPI_Инструменты.ДобавитьПоле("multi"    , ОбновитьВсе  , "Булево"       , СтруктураОбновления);
    OPI_Инструменты.ДобавитьПоле("upsert"   , Вставлять    , "Булево"       , СтруктураОбновления);
    OPI_Инструменты.ДобавитьПоле("collation", Сопоставление, "КлючИЗначение", СтруктураОбновления);

    Возврат СтруктураОбновления;

КонецФункции

// Получить структуру удаления документов
// Формирует структу данных для обновления
//
// Параметры:
//  Фильтр        - Структура Из КлючИЗначение - Фильтр документов                                      - query
//  Количество    - Число                      - Лимит удаления: 1 > первый документ, 0 > все документы - limit
//  Сопоставление - Структура Из КлючИЗначение - Особые настройки правил сравнения полей при поиске     - cltn
//
// Возвращаемое значение:
//  Структура - Получить структуру удаления документов
Функция ПолучитьСтруктуруУдаленияДокументов(Знач Фильтр, Знач Количество = 0, Сопоставление = Неопределено) Экспорт

    СтруктураУдаления = Новый Структура;

    OPI_Инструменты.ДобавитьПоле("q"        , Фильтр       , "КлючИЗначение", СтруктураУдаления);
    OPI_Инструменты.ДобавитьПоле("collation", Сопоставление, "КлючИЗначение", СтруктураУдаления);

    OPI_ПреобразованиеТипов.ПолучитьЧисло(Количество);
    СтруктураУдаления.Вставить("limit", Количество);

    Возврат СтруктураУдаления;

КонецФункции

#КонецОбласти

#Область УправлениеПользователями

// Создать пользователя
// Создает нового пользователя
//
// Примечание:
// Роль может быть определена в одном из следующих форматов:^
// Структура: `{ role: "имя", db: "база данных" }`^
// Строка:    `имя роли`
//
// Параметры:
//  Соединение       - Строка, Произвольный       - Соединение или строка подключения                     - dbc
//  Имя              - Строка                     - Имя пользователя                                      - name
//  РолиПользователя - Массив Из Произвольный     - Массив ролей в виде строк или структур с указанием БД - roles
//  База             - Строка                     - Имя базы. Текущая база, если не указано               - db
//  Пароль           - Строка                     - Пароль пользователя                                   - pwd
//  Параметры        - Структура Из КлючИЗначение - Дополнительные параметры создания                     - params
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция СоздатьПользователя(Знач Соединение
    , Знач Имя
    , Знач РолиПользователя
    , Знач База = Неопределено
    , Знач Пароль = Неопределено
    , Знач Параметры = Неопределено) Экспорт

    Возврат СоздатьИзменитьПользователя("createUser", Соединение, Имя, РолиПользователя, База, Пароль, Параметры);

КонецФункции

// Изменить пользователя
// Изменяет данные существуюего пользователя
//
// Примечание:
// Переданные роли целиком заменяют список ролей пользователя
// Роль может быть определена в одном из следующих форматов:^
// Структура: `{ role: "имя", db: "база данных" }`^
// Строка:    `имя роли`
//
// Параметры:
//  Соединение       - Строка, Произвольный       - Соединение или строка подключения                     - dbc
//  Имя              - Строка                     - Имя пользователя                                      - name
//  РолиПользователя - Массив Из Произвольный     - Массив ролей в виде строк или структур с указанием БД - roles
//  База             - Строка                     - Имя базы. Текущая база, если не указано               - db
//  Пароль           - Строка                     - Пароль пользователя                                   - pwd
//  Параметры        - Структура Из КлючИЗначение - Дополнительные параметры                              - params
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция ИзменитьПользователя(Знач Соединение
    , Знач Имя
    , Знач РолиПользователя = Неопределено
    , Знач База = Неопределено
    , Знач Пароль = Неопределено
    , Знач Параметры = Неопределено) Экспорт

    Возврат СоздатьИзменитьПользователя("updateUser", Соединение, Имя, РолиПользователя, База, Пароль, Параметры);

КонецФункции

// Получить пользователей
// Получает данные одного или нескольких пользователей
//
// Примечание:
// Пользователь может быть указан в одном из сдедующих форматов:^
// Строка:    `имя пользователя`^
// Структура: `{ user: <имя>, db: <база данных> }`^
// Массив:    `[ { user: <имя>, db: <база данных> },  { user: <имя>, db: <база данных> }, ... ]`
//
// Параметры:
//  Соединение         - Строка, Произвольный - Соединение или строка подключения                              - dbc
//  Пользователи       - Произвольный         - Имя пользователя, структура или массив структур пользователей  - usrs
//  База               - Строка               - Имя базы. Текущая база, если не указано                        - db
//  ПолучатьПароль     - Булево               - Добавляет хеш пароля пользователя в возвращаемые данные        - spwd
//  ПолучатьПривелегии - Булево               - Добавляет список привелегий пользователя в возвращаемые данные - sprv
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция ПолучитьПользователей(Знач Соединение
    , Знач Пользователи
    , Знач База = Неопределено
    , Знач ПолучатьПароль = Ложь
    , Знач ПолучатьПривелегии = Ложь) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьМассив(Пользователи);

    Если Пользователи.Количество() = 1 Тогда
        Пользователи = Пользователи[0];
    КонецЕсли;

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("showCredentials", ПолучатьПароль    , "Булево", Параметры);
    OPI_Инструменты.ДобавитьПоле("showPrivileges" , ПолучатьПривелегии, "Булево", Параметры);

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Результат = ВыполнитьКоманду(Соединение, "usersInfo", Пользователи, База, Параметры);
    Возврат Результат;

КонецФункции

// Получить пользователей базы
// Получает информацию о всех пользователях базы
//
// Параметры:
//  Соединение         - Строка, Произвольный - Соединение или строка подключения                              - dbc
//  База               - Строка               - Имя базы. Текущая база, если не указано                        - db
//  ПолучатьПароль     - Булево               - Добавляет хеш пароля пользователя в возвращаемые данные        - spwd
//  ПолучатьПривелегии - Булево               - Добавляет список привелегий пользователя в возвращаемые данные - sprv
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция ПолучитьПользователейБазы(Знач Соединение
    , Знач База = Неопределено
    , Знач ПолучатьПароль = Ложь
    , Знач ПолучатьПривелегии = Ложь) Экспорт

    Возврат ПолучитьПользователей(Соединение, 1, База, ПолучатьПароль, ПолучатьПривелегии);

КонецФункции

// Удалить пользователя
// Удаляет пользователя по имени
//
// Параметры:
//  Соединение       - Строка, Произвольный       - Соединение или строка подключения       - dbc
//  Имя              - Строка                     - Имя пользователя                        - name
//  База             - Строка                     - Имя базы. Текущая база, если не указано - db
//  Параметры        - Структура Из КлючИЗначение - Дополнительные параметры удаления       - params
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция УдалитьПользователя(Знач Соединение, Знач Имя, Знач База = Неопределено, Знач Параметры = Неопределено) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Имя);

    Если Параметры <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Параметры);
    Иначе
        Параметры = Новый Структура;
    КонецЕсли;

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Результат = ВыполнитьКоманду(Соединение, "dropUser", Имя, База, Параметры);
    Возврат Результат;

КонецФункции

#КонецОбласти

#Область УправлениеРолями

// Создать роль
// Создает новую роль
//
// Примечание:
// Роль может быть определена в одном из следующих форматов:^
// Структура: `{ role: "имя", db: "база данных" }`^
// Строка:    `имя роли`
//
// Параметры:
//  Соединение       - Строка, Произвольный       - Соединение или строка подключения                               - dbc
//  Имя              - Строка                     - Название роли                                                   - name
//  База             - Строка                     - Имя базы. Текущая база, если не указано                         - db
//  МассивПривелегий - Массив Из Структура        - Массив привелегий. См. ПолучитьСтруктуруПривелегииРоли          - prvl                                          - prvl
//  МассивРолей      - Массив Из Произвольный     - Массив ролей оснований в виде строк или структур с указанием БД - roles
//  Параметры        - Структура Из КлючИЗначение - Дополнительные параметры удаления                               - params
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция СоздатьРоль(Знач Соединение
    , Знач Имя
    , Знач База = Неопределено
    , Знач МассивПривелегий = Неопределено
    , Знач МассивРолей = Неопределено
    , Знач Параметры = Неопределено) Экспорт

    Возврат СоздатьИзменитьРоль("createRole", Соединение, Имя, База, МассивПривелегий, МассивРолей, Параметры);

КонецФункции

// Изменить роль
// Изменяет данные существующей роли
//
// Примечание:
// Переданные роли и привелегии целиком заменяют текущие данные роли
// Роль может быть определена в одном из следующих форматов:^
// Структура: `{ role: "имя", db: "база данных" }`^
// Строка:    `имя роли`
//
// Параметры:
//  Соединение       - Строка, Произвольный       - Соединение или строка подключения                               - dbc
//  Имя              - Строка                     - Название роли                                                   - name
//  База             - Строка                     - Имя базы. Текущая база, если не указано                         - db
//  МассивПривелегий - Массив Из Структура        - Массив привелегий. См. ПолучитьСтруктуруПривелегииРоли          - prvl
//  МассивРолей      - Массив Из Произвольный     - Массив ролей оснований в виде строк или структур с указанием БД - roles
//  Параметры        - Структура Из КлючИЗначение - Дополнительные параметры удаления                               - params
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция ИзменитьРоль(Знач Соединение
    , Знач Имя
    , Знач База = Неопределено
    , Знач МассивПривелегий = Неопределено
    , Знач МассивРолей = Неопределено
    , Знач Параметры = Неопределено) Экспорт

    Возврат СоздатьИзменитьРоль("updateRole", Соединение, Имя, База, МассивПривелегий, МассивРолей, Параметры);

КонецФункции

// Получить роли
// Получает информацию об указанных полях
//
// Параметры:
//  Соединение         - Строка, Произвольный - Соединение или строка подключения                      - dbc
//  МассивРолей        - Произвольный         - Роль или список ролей                                  - roles
//  База               - Строка               - Имя базы. Текущая база, если не указано                - db
//  ПолучатьПривелегии - Булево               - Добавляет список привелегий ролт в возвращаемые данные - sprv
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция ПолучитьРоли(Знач Соединение
    , Знач МассивРолей
    , Знач База = Неопределено
    , Знач ПолучатьПривелегии = Ложь) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьМассив(МассивРолей);
    OPI_ПреобразованиеТипов.ПолучитьБулево(ПолучатьПривелегии);

    Если МассивРолей.Количество() = 1 Тогда
        МассивРолей = МассивРолей[0];
    КонецЕсли;

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Параметры = Новый Структура("showPrivileges", ПолучатьПривелегии);

    Результат = ВыполнитьКоманду(Соединение, "rolesInfo", МассивРолей, База, Параметры);
    Возврат Результат;

КонецФункции

// Удалить роль
// Удаляет существующую роль
//
// Параметры:
//  Соединение  - Строка, Произвольный       - Соединение или строка подключения       - dbc
//  Имя         - Строка                     - Название роли                           - name
//  База        - Строка                     - Имя базы. Текущая база, если не указано - db
//  Параметры   - Структура Из КлючИЗначение - Дополнительные параметры удаления       - params
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция УдалитьРоль(Знач Соединение, Знач Имя, Знач База = Неопределено, Знач Параметры = Неопределено) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Имя);

    Если Параметры <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Параметры);
    Иначе
        Параметры = Новый Структура;
    КонецЕсли;

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Результат = ВыполнитьКоманду(Соединение, "dropRole", Имя, База, Параметры);
    Возврат Результат;

КонецФункции

// Назначить роли
// Назначает выбранные роли пользователю
//
// Примечание:
// Роль может быть определена в одном из следующих форматов:^
// Структура: `{ role: "имя", db: "база данных" }`^
// Строка:    `имя роли`
//
// Параметры:
//  Соединение   - Строка, Произвольный       - Соединение или строка подключения       - dbc
//  Пользователь - Строка                     - Имя пользователя                        - user
//  МассивРолей  - Массив Из Произвольный     - Роль или массив ролей                   - roles
//  База         - Строка                     - Имя базы. Текущая база, если не указано - db
//  Параметры    - Структура Из КлючИЗначение - Дополнительные параметры удаления       - params
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция НазначитьРоли(Знач Соединение
    , Знач Пользователь
    , Знач МассивРолей
    , Знач База = Неопределено
    , Знач Параметры = Неопределено) Экспорт

    Возврат НазначитьСнятьРоль("grantRolesToUser", Соединение, Пользователь, МассивРолей, База, Параметры);

КонецФункции

// Снять роли
// Снимает выбранные роли пользователя
//
// Примечание:
// Роль может быть определена в одном из следующих форматов:^
// Структура: `{ role: "имя", db: "база данных" }`^
// Строка:    `имя роли`
//
// Параметры:
//  Соединение   - Строка, Произвольный       - Соединение или строка подключения       - dbc
//  Пользователь - Строка                     - Имя пользователя                        - user
//  МассивРолей  - Массив Из Произвольный     - Роль или массив ролей                   - roles
//  База         - Строка                     - Имя базы. Текущая база, если не указано - db
//  Параметры    - Структура Из КлючИЗначение - Дополнительные параметры удаления       - params
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция СнятьРоли(Знач Соединение
    , Знач Пользователь
    , Знач МассивРолей
    , Знач База = Неопределено
    , Знач Параметры = Неопределено) Экспорт

    Возврат НазначитьСнятьРоль("revokeRolesFromUser", Соединение, Пользователь, МассивРолей, База, Параметры);

КонецФункции

// Получить структуру привелегии роли
// Формирует структуру информации о привелегии для использования при работе с ролями
//
// Примечание:
// Ресурс может быть определен в одном из следующих форматов:^
// Структура для коллекции: `{ db: "база данных", collection: "имя коллекции" }`
// Структура для кластера:  `{ cluster : true }`
//
// Параметры:
//  Ресурс         - Структура Из КлючИЗначение - Ресурс привелегии                    - res
//  МассивДействий - Массив Из Строка           - Массив доступных действий привелегии - act
//
// Возвращаемое значение:
//  Структура - Структура привелегии
Функция ПолучитьСтруктуруПривелегииРоли(Знач Ресурс, Знач МассивДействий) Экспорт

    СтруктураПривелегии = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("resource", Ресурс        , "КлючИЗначение", СтруктураПривелегии);
    OPI_Инструменты.ДобавитьПоле("actions" , МассивДействий, "Массив"       , СтруктураПривелегии);

    Возврат СтруктураПривелегии;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбработатьДанныеДляОперации(Знач Данные, Обернут = Ложь)

    Попытка

        ОбработанныеДанные = Данные;
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(ОбработанныеДанные);
        ОбработанныеДанные = ОбработатьКоллекциюДляОперации(ОбработанныеДанные);

    Исключение

        ОбработанныеДанные = Данные;
        ТекущийКлюч        = Неопределено;

        Если OPI_Инструменты.ЭтоКоллекция(ОбработанныеДанные) Тогда

            ОбработанныеДанные = ОбработатьКоллекциюДляОперации(ОбработанныеДанные);

        ИначеЕсли ТипЗнч(ОбработанныеДанные) = Тип("ДвоичныеДанные") Тогда

            ОбработанныеДанные = ПолучитьBase64СтрокуИзДвоичныхДанных(ОбработанныеДанные);
            ТекущийКлюч        = "__OPI_BINARY__";

        ИначеЕсли ТипЗнч(ОбработанныеДанные) = Тип("Дата") Тогда

            ОбработанныеДанные = OPI_Инструменты.ДатаRFC3339(ОбработанныеДанные);
            ТекущийКлюч        = "__OPI_DATETIME__";

        ИначеЕсли ОбработанныеДанные = Неопределено Тогда

            ТекущийКлюч = "__OPI_NULL__";

        Иначе

            Если Не ТипЗнч(ОбработанныеДанные) = Тип("Число") И Не ТипЗнч(ОбработанныеДанные) = Тип("Булево") Тогда
                OPI_ПреобразованиеТипов.ПолучитьСтроку(ОбработанныеДанные);
            КонецЕсли;

        КонецЕсли;

        Если ЗначениеЗаполнено(ТекущийКлюч) И Не Обернут Тогда
            ОбработанныеДанные = Новый Структура(ТекущийКлюч, ОбработанныеДанные);
        КонецЕсли;

    КонецПопытки;

    Возврат ОбработанныеДанные;

КонецФункции

Функция ОбработатьКоллекциюДляОперации(Знач Данные)

    ОбработанныеДанные = Новый(ТипЗнч(Данные));

    Если OPI_Инструменты.ЭтоКоллекция(ОбработанныеДанные, Истина) Тогда

        Для Каждого ЧастьДанных Из Данные Цикл

            ТекущийКлюч = Строка(ЧастьДанных.Ключ);
            Обернут     = СтрНачинаетсяС(ТекущийКлюч, "__OPI_");

            ТекущееЗначение = ЧастьДанных.Значение;

            Если ТекущийКлюч = "__OPI_BINARY__" Тогда
                OPI_ПреобразованиеТипов.ПолучитьДвоичныеДанные(ТекущееЗначение);
            КонецЕсли;

            ОбработанноеЗначение = ОбработатьДанныеДляОперации(ТекущееЗначение, Обернут);

            ОбработанныеДанные.Вставить(ТекущийКлюч, ОбработанноеЗначение);

        КонецЦикла;

    Иначе

        Для Каждого Элемент Из Данные Цикл
            ОбработанныеДанные.Добавить(ОбработатьДанныеДляОперации(Элемент));
        КонецЦикла;

    КонецЕсли;

    Возврат ОбработанныеДанные;

КонецФункции

Функция СоздатьИзменитьПользователя(Знач КомандаБД
    , Знач Соединение
    , Знач Имя
    , Знач РолиПользователя = Неопределено
    , Знач База             = Неопределено
    , Знач Пароль           = Неопределено
    , Знач Параметры        = Неопределено)

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Имя);

    Если Параметры <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Параметры);
    Иначе
        Параметры = Новый Структура;
    КонецЕсли;

    Если РолиПользователя <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьМассив(РолиПользователя);
        Параметры.Вставить("roles", РолиПользователя);
    КонецЕсли;

    Если Пароль <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(Пароль);
        Параметры.Вставить("pwd", Пароль);
    КонецЕсли;

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Результат = ВыполнитьКоманду(Соединение, КомандаБД, Имя, База, Параметры);
    Возврат Результат;

КонецФункции

Функция СоздатьИзменитьРоль(Знач КомандаБД
    , Знач Соединение
    , Знач Имя
    , Знач База             = Неопределено
    , Знач МассивПривелегий = Неопределено
    , Знач МассивРолей      = Неопределено
    , Знач Параметры        = Неопределено)

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Имя);

    Если Параметры <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Параметры);
    Иначе
        Параметры = Новый Структура;
    КонецЕсли;

    Если МассивРолей <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьМассив(МассивРолей);
        Параметры.Вставить("roles", МассивРолей);
    КонецЕсли;

    Если МассивПривелегий <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьМассив(МассивПривелегий);
        Параметры.Вставить("privileges", МассивПривелегий);
    КонецЕсли;

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Результат = ВыполнитьКоманду(Соединение, КомандаБД, Имя, База, Параметры);
    Возврат Результат;

КонецФункции

Функция НазначитьСнятьРоль(Знач КомандаБД
    , Знач Соединение
    , Знач Пользователь
    , Знач МассивРолей
    , Знач База      = Неопределено
    , Знач Параметры = Неопределено)

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Пользователь);

    Если Параметры <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Параметры);
    Иначе
        Параметры = Новый Структура;
    КонецЕсли;

    OPI_Инструменты.ДобавитьПоле("roles", МассивРолей, "Массив", Параметры);

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Результат = ВыполнитьКоманду(Соединение, КомандаБД, Пользователь, База, Параметры);
    Возврат Результат;

КонецФункции

#КонецОбласти
