
#Область ОбъявлениеТестов

Процедура ИсполняемыеСценарии() Экспорт
	
	ЮТТесты
		.ДобавитьТестовыйНабор("Redis: Инфраструктура")
			.ДобавитьТест("НедоступностьСервера")
		.ДобавитьТестовыйНабор("Redis: Общие")
			.ДобавитьТест("УстановитьИПолучитьЗначение_Строку")
			.ДобавитьТест("УстановитьИПолучитьЗначение_Массив")
			.ДобавитьТест("УстановитьИПолучитьЗначение_МассивСМногострочнымиЗначениями")
			.ДобавитьТест("УстановитьИПолучитьЗначение_Структуру")
			.ДобавитьТест("УстановитьИПолучитьЗначение_Соответствие")
			.ДобавитьТест("ПолучитьЗначениеОтсутствующегоКлюча")
			.ДобавитьТест("ПроверитьСуществованиеКлюча_КлючСуществует")
			.ДобавитьТест("ПроверитьСуществованиеКлюча_КлючОтсутствует")
			.ДобавитьТест("НайтиКлючиПоШаблону")
			.ДобавитьТест("УдалитьЗначение")
			.ДобавитьТест("УдалитьЗначениеОтсутствующегоКлюча")
			.ДобавитьТест("ТипДанных_Строка")
			.ДобавитьТест("ТипДанных_Массив")
			.ДобавитьТест("ТипДанных_ХэшТаблица")
			.ДобавитьТест("ТипДанныхОтсутствующегоКлюча")
			.ДобавитьТест("ОчиститьВсе_Синхронно")
			.ДобавитьТест("ОчиститьВсе_Асинхронно")
			.ДобавитьТест("УстановитьВремяЖизни")
			.ДобавитьТест("ПолучитьВремяЖизни")
				.Перед("Перед_ПолучитьВремяЖизни")
			.ДобавитьТест("ПолучитьНеЗаданноеВремяЖизни")
			.ДобавитьТест("УстановитьВремяЖизниОтсутствующегоКлюча")
			.ДобавитьТест("ПолучитьВремяЖизниОтсутствующегоКлюча")
		.ДобавитьТестовыйНабор("Redis: Строка")
			.ДобавитьТест("УстановитьСтроку")
			.ДобавитьТест("ПолучитьСтроку")
		.ДобавитьТестовыйНабор("Redis: Массив")
			.ДобавитьТест("ДобавитьЭлементыВМассив_ВКонец")
			.ДобавитьТест("ДобавитьЭлементыВМассив_ВНачало")
			.ДобавитьТест("ПолучитьМассив")
			.ДобавитьТест("КоличествоСтрокМассива_НормальныйВызов")
			.ДобавитьТест("КоличествоСтрокМассива_КлючНеЗадан")
			.ДобавитьТест("КоличествоСтрокМассива_ЗначениеНеМассив")
			.ДобавитьТест("ПолучитьСтрокуМассива_НормальныйВызов")
			.ДобавитьТест("ПолучитьСтрокуМассива_КлючНеЗадан")
			.ДобавитьТест("ПолучитьСтрокуМассива_ЗначениеНеМассив")
			.ДобавитьТест("ПолучитьСтрокуМассива_НекорретныйИндекс")
			.ДобавитьТест("УдалитьСтрокиМассива_ОднаСтрока")
			.ДобавитьТест("УдалитьСтрокиМассива_НесколькоСтрок")
			.ДобавитьТест("УдалитьСтрокиМассива_НечегоУдалять")
		.ДобавитьТестовыйНабор("Redis: Хэш-таблица")
			.ДобавитьТест("ПолучитьХэшТаблицу")
			.ДобавитьТест("КоличествоПолейХэшТаблицы")
			.ДобавитьТест("ПоляХэшТаблицы")
			.ДобавитьТест("ЗначениеПоляХэшТаблицы")
			.ДобавитьТест("УдалитьПоляХэшТаблицы")
		.ДобавитьТестовыйНабор("Redis: Json")
			.ДобавитьТест("УстановитьИПолучитьJson")
			.ДобавитьТест("УстановитьИПолучитьМногострочныйJson")
			.ДобавитьТест("УстановитьJsonДляПути_Число")
			.ДобавитьТест("УстановитьJsonДляПути_Структура")
			.ДобавитьТест("УстановитьJsonДляПути_КореньОтсутствует_Число")
			.ДобавитьТест("УстановитьJsonДляПути_КореньОтсутствует_Структура")
			.ДобавитьТест("УстановитьJsonДляПути_ГлубокаяВложенность_Число")
			.ДобавитьТест("УстановитьJsonДляПути_ГлубокаяВложенностьОтсутствует_Число")
			.ДобавитьТест("ПолучитьJsonДляПути")
			.ДобавитьТест("ПолучитьJsonДляПути_КореньОтсутствует")
			.ДобавитьТест("ПолучитьJsonДляПути_ГлубокаяВложенностьОтсутствует")
	;
	
КонецПроцедуры

#КонецОбласти

#Область События

Процедура ПередКаждымТестом() Экспорт
	
	OPI_Redis.ОчиститьВсе(АдресСервераRedis());
	
КонецПроцедуры

// Выделил в отдельную процедуру чтобы ожидание в 5 сек. не попало в оценку времени самого теста
Процедура Перед_ПолучитьВремяЖизни() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтрока";
	ТестоваяСтрока = "ТестовоеЗначение";
	ВремяЖизниСек = 10;
	
	OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестоваяСтрока);
	OPI_Redis.УстановитьВремяЖизни(АдресСервераRedis, Ключ, ВремяЖизниСек);
	
	ЮТест.Пауза(3);
	
КонецПроцедуры

#КонецОбласти

#Область Тесты

#Область Инфраструктура

Процедура НедоступностьСервера() Экспорт
	
	ЮТест.ОжидаетЧто(OPI_Redis)
		.Метод("УстановитьЗначение")
			.Параметр("localhost:6380")
			.Параметр("ТестоваяСтрока")
			.Параметр("ТестовоеЗначение")
		.ВыбрасываетИсключение("Direct connection error: Подключение не установлено, т.к. конечный компьютер отверг запрос на подключение. (os error 10061)")
	;
	
КонецПроцедуры

#КонецОбласти

#Область Общие

Процедура УстановитьИПолучитьЗначение_Строку() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтрока";
	ТестоваяСтрока = "ТестовоеЗначение";
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестоваяСтрока);
	
	РезультатПолучения = OPI_Redis.ПолучитьЗначение(АдресСервераRedis, Ключ);
	
	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Строка")
		.Равно(ТестоваяСтрока)
	;
	
КонецПроцедуры

Процедура УстановитьИПолучитьЗначение_Массив() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйМассив";

	ТестовыйМассив = Новый Массив;
	ТестовыйМассив.Добавить("ЗначениеМассива1");
	ТестовыйМассив.Добавить("ЗначениеМассива2");
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестовыйМассив);

	РезультатПолучения = OPI_Redis.ПолучитьЗначение(АдресСервераRedis, Ключ);

	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Массив")
		.ИмеетДлину(2)
	;
	
КонецПроцедуры

Процедура УстановитьИПолучитьЗначение_МассивСМногострочнымиЗначениями() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйМассив";

	ТестовыйМассив = Новый Массив;
	ТестовыйМассив.Добавить("ЗначенияМассива1:
	|	Строка1_1,
	|	Строка1_2");
	ТестовыйМассив.Добавить("ЗначениеМассива2:
	|	Строка2_1,
	|	Строка2_2,
	|	Строка2_3");
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестовыйМассив);

	РезультатПолучения = OPI_Redis.ПолучитьЗначение(АдресСервераRedis, Ключ);

	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Массив")
		.ИмеетДлину(2)
		.Содержит(ТестовыйМассив[0])
		.Содержит(ТестовыйМассив[1])
	;
	
КонецПроцедуры

Процедура УстановитьИПолучитьЗначение_Структуру() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтруктура";

	ТестоваяСтруктура = Новый Структура;
	ТестоваяСтруктура.Вставить("КлючСтруктуры1", "ЗначениеСтруктуры1");
	ТестоваяСтруктура.Вставить("КлючСтруктуры2", "ЗначениеСтруктуры2");
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестоваяСтруктура);

	РезультатПолучения = OPI_Redis.ПолучитьЗначение(АдресСервераRedis, Ключ);

	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;
	
	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Структура")
		.ИмеетДлину(2)
	;
	
КонецПроцедуры

Процедура УстановитьИПолучитьЗначение_Соответствие() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовоеСоответствие";

	ТестовоеСоответствие = Новый Соответствие;
	ТестовоеСоответствие.Вставить("1-йКлюч", "ЗначениеСоответствия1");
	ТестовоеСоответствие.Вставить("2-йКлюч", "ЗначениеСоответствия2");
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестовоеСоответствие);

	РезультатПолучения = OPI_Redis.ПолучитьЗначение(АдресСервераRedis, Ключ);

	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;
	
	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Соответствие")
		.ИмеетДлину(2)
	;
	
КонецПроцедуры

Процедура ПолучитьЗначениеОтсутствующегоКлюча() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтрока";
	
	// Специально ничего не записываю в Redis
	
	РезультатПолучения = OPI_Redis.ПолучитьЗначение(АдресСервераRedis, Ключ);
	
	ЮТест.ОжидаетЧто(РезультатПолучения)
		.Равно(Неопределено)
	;
	
КонецПроцедуры

Процедура ПроверитьСуществованиеКлюча_КлючСуществует() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтрока";
	ТестоваяСтрока = "ТестовоеЗначение";
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестоваяСтрока);
	
	РезультатПроверки = OPI_Redis.ПроверитьСуществованиеКлюча(АдресСервераRedis, Ключ);
	
	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПроверки)
		.ЭтоИстина()
	;
	
КонецПроцедуры

Процедура ПроверитьСуществованиеКлюча_КлючОтсутствует() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтрока";
	
	// Специально ничего не записываю в Redis
	
	РезультатПроверки = OPI_Redis.ПроверитьСуществованиеКлюча(АдресСервераRedis, Ключ);
	
	ЮТест.ОжидаетЧто(РезультатПроверки)
		.ЭтоЛожь()
	;
	
КонецПроцедуры

Процедура НайтиКлючиПоШаблону() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	
	РезультатУстановки1 = OPI_Redis.УстановитьЗначение(АдресСервераRedis, "ТестовыйКлюч:1", "ТестовоеЗначение1");
	РезультатУстановки2 = OPI_Redis.УстановитьЗначение(АдресСервераRedis, "ТестовыйКлюч:2", "ТестовоеЗначение2");
	РезультатУстановки3 = OPI_Redis.УстановитьЗначение(АдресСервераRedis, "НеподходящийКлюч", "ЗначениеНеподходящегоКлюча");
	
	РезультатПроверки = OPI_Redis.НайтиКлючиПоШаблону(АдресСервераRedis, "ТестовыйКлюч:*");
	
	ЮТест.ОжидаетЧто(РезультатУстановки1)
		.ЭтоИстина()
	;
	ЮТест.ОжидаетЧто(РезультатУстановки2)
		.ЭтоИстина()
	;
	ЮТест.ОжидаетЧто(РезультатУстановки3)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПроверки)
		.ИмеетТип("Массив")
		.ИмеетДлину(2)
	;
	
КонецПроцедуры

Процедура УдалитьЗначение() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтрока";
	ТестоваяСтрока = "ТестовоеЗначение";
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестоваяСтрока);
	
	РезультатКонтроляДоУдаления = OPI_Redis.ПроверитьСуществованиеКлюча(АдресСервераRedis, Ключ);

	РезультатУдаления = OPI_Redis.УдалитьЗначение(АдресСервераRedis, Ключ);

	РезультатКонтроляПослеУдаления = OPI_Redis.ПроверитьСуществованиеКлюча(АдресСервераRedis, Ключ);
	
	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатКонтроляДоУдаления)
		.ЭтоИстина()
	;
	
	ЮТест.ОжидаетЧто(РезультатУдаления)
		.ЭтоИстина()
	;
	
	ЮТест.ОжидаетЧто(РезультатКонтроляПослеУдаления)
		.ЭтоЛожь()
	;
	
КонецПроцедуры

Процедура УдалитьЗначениеОтсутствующегоКлюча() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтрока";
	ТестоваяСтрока = "ТестовоеЗначение";

	// Специально ничего не записываю в Redis
	
	РезультатКонтроляДоУдаления = OPI_Redis.ПроверитьСуществованиеКлюча(АдресСервераRedis, Ключ);

	РезультатУдаления = OPI_Redis.УдалитьЗначение(АдресСервераRedis, Ключ);

	ЮТест.ОжидаетЧто(РезультатКонтроляДоУдаления)
		.ЭтоЛожь()
	;

	ЮТест.ОжидаетЧто(РезультатУдаления)
		.ЭтоЛожь()
	;
	
КонецПроцедуры

Процедура ТипДанных_Строка() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтрока";
	ТестоваяСтрока = "ТестовоеЗначение";
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестоваяСтрока);
	
	РезультатПолучения = OPI_Redis.ТипДанных(АдресСервераRedis, Ключ);
	
	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Строка")
		.Равно("string")
	;
	
КонецПроцедуры

Процедура ТипДанных_Массив() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйМассив";

	ТестовыйМассив = Новый Массив;
	ТестовыйМассив.Добавить("ЗначениеМассива1");
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестовыйМассив);
	
	РезультатПолучения = OPI_Redis.ТипДанных(АдресСервераRedis, Ключ);
	
	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Строка")
		.Равно("list")
	;
	
КонецПроцедуры

Процедура ТипДанных_ХэшТаблица() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтруктура";

	ТестоваяСтруктура = Новый Структура;
	ТестоваяСтруктура.Вставить("КлючСтруктуры1", "ЗначениеСтруктуры1");
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестоваяСтруктура);
	
	РезультатПолучения = OPI_Redis.ТипДанных(АдресСервераRedis, Ключ);
	
	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Строка")
		.Равно("hash")
	;
	
КонецПроцедуры

Процедура ТипДанныхОтсутствующегоКлюча() Экспорт
	
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтрока";
	
	// Специально ничего не записываю в Redis
	
	РезультатПолучения = OPI_Redis.ТипДанных(АдресСервераRedis, Ключ);
	
	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Строка")
		.Равно("none")
	;

КонецПроцедуры

Процедура ОчиститьВсе_Синхронно() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтрока";
	ТестоваяСтрока = "ТестовоеЗначение";
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестоваяСтрока);
	
	РезультатПолученияДоОчистки = OPI_Redis.ПолучитьЗначение(АдресСервераRedis, Ключ);
	
	РезультатОчистки = OPI_Redis.ОчиститьВсе(АдресСервераRedis, Ложь);

	РезультатПолученияПослеОчистки = OPI_Redis.ПолучитьЗначение(АдресСервераRedis, Ключ);
	
	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолученияДоОчистки)
		.ИмеетТип("Строка")
		.Равно(ТестоваяСтрока)
	;

	ЮТест.ОжидаетЧто(РезультатОчистки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолученияПослеОчистки)
		.Равно(Неопределено)
	;
	
КонецПроцедуры

Процедура ОчиститьВсе_Асинхронно() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтрока";
	ТестоваяСтрока = "ТестовоеЗначение";
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестоваяСтрока);
	
	РезультатПолученияДоОчистки = OPI_Redis.ПолучитьЗначение(АдресСервераRedis, Ключ);
	
	РезультатОчистки = OPI_Redis.ОчиститьВсе(АдресСервераRedis);

	РезультатПолученияПослеОчистки = OPI_Redis.ПолучитьЗначение(АдресСервераRedis, Ключ);
	
	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолученияДоОчистки)
		.ИмеетТип("Строка")
		.Равно(ТестоваяСтрока)
	;

	ЮТест.ОжидаетЧто(РезультатОчистки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолученияПослеОчистки)
		.Равно(Неопределено)
	;
	
КонецПроцедуры

Процедура УстановитьВремяЖизни() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтрока";
	ТестоваяСтрока = "ТестовоеЗначение";
	ВремяЖизниСек = 10;
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестоваяСтрока);
	
	РезультатУстановкиВремениЖизни = OPI_Redis.УстановитьВремяЖизни(АдресСервераRedis, Ключ, ВремяЖизниСек);

	РезультатПолученияВремениЖизни = OPI_Redis.ПолучитьВремяЖизни(АдресСервераRedis, Ключ);
	
	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатУстановкиВремениЖизни)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолученияВремениЖизни)
		.ИмеетТип("Число")
		.Равно(ВремяЖизниСек)
	;
	
КонецПроцедуры

Процедура ПолучитьВремяЖизни() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтрока";

	РезультатПолученияВремениЖизни = OPI_Redis.ПолучитьВремяЖизни(АдресСервераRedis, Ключ);
	
	ЮТест.ОжидаетЧто(РезультатПолученияВремениЖизни)
		.ИмеетТип("Число")
		.Равно(7) // см.процедуру Перед_ПолучитьВремяЖизни 10 (установил) - 3 (ждать) = 7 сек.
	;
	
КонецПроцедуры

Процедура ПолучитьНеЗаданноеВремяЖизни() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтрока";
	ТестоваяСтрока = "ТестовоеЗначение";
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестоваяСтрока);
	
	// Пропускаю процедуру установки времени жизни
	
	РезультатПолученияВремениЖизни = OPI_Redis.ПолучитьВремяЖизни(АдресСервераRedis, Ключ);
	
	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолученияВремениЖизни)
		.ИмеетТип("Число")
		.Равно(0)
	;
	
КонецПроцедуры

Процедура УстановитьВремяЖизниОтсутствующегоКлюча() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтрока";
	
	// Специально ничего не записываю в Redis
	
	РезультатУстановкиВремениЖизни = OPI_Redis.УстановитьВремяЖизни(АдресСервераRedis, Ключ, 10);

	ЮТест.ОжидаетЧто(РезультатУстановкиВремениЖизни)
		.ЭтоЛожь()
	;
	
КонецПроцедуры

Процедура ПолучитьВремяЖизниОтсутствующегоКлюча() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтрока";
	
	// Специально ничего не записываю в Redis
	
	РезультатПолученияВремениЖизни = OPI_Redis.ПолучитьВремяЖизни(АдресСервераRedis, Ключ);

	ЮТест.ОжидаетЧто(РезультатПолученияВремениЖизни)
		.Равно(Неопределено)
	;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСоСтроками

Процедура УстановитьСтроку() Экспорт

	// TODO:

КонецПроцедуры

Процедура ПолучитьСтроку() Экспорт

	// TODO:

КонецПроцедуры

#КонецОбласти

#Область РаботаСМассивами

Процедура ДобавитьЭлементыВМассив_ВКонец() Экспорт

	// TODO:

КонецПроцедуры

Процедура ДобавитьЭлементыВМассив_ВНачало() Экспорт

	// TODO:

КонецПроцедуры

Процедура ПолучитьМассив() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйМассив";

	ТестовыйМассив = Новый Массив;
	ТестовыйМассив.Добавить("ЗначениеМассива1");
	ТестовыйМассив.Добавить("ЗначениеМассива2");
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестовыйМассив);

	РезультатПолучения = OPI_Redis.ПолучитьМассив(АдресСервераRedis, Ключ);

	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Массив")
		.ИмеетДлину(2)
	;
	
КонецПроцедуры

Процедура КоличествоСтрокМассива_НормальныйВызов() Экспорт

	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйМассив";

	ТестовыйМассив = Новый Массив;
	ТестовыйМассив.Добавить("ЗначениеМассива1");
	ТестовыйМассив.Добавить("ЗначениеМассива2");
	ТестовыйМассив.Добавить("ЗначениеМассива3");
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестовыйМассив);

	РезультатПолучения = OPI_Redis.КоличествоСтрокМассива(АдресСервераRedis, Ключ);

	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Число")
		.Равно(3)
	;

КонецПроцедуры

Процедура КоличествоСтрокМассива_КлючНеЗадан() Экспорт

	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйМассив";

	// Специально ничего не записываю в Redis
	
	РезультатПолучения = OPI_Redis.КоличествоСтрокМассива(АдресСервераRedis, Ключ);

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.Равно(Неопределено)
	;

КонецПроцедуры

Процедура КоличествоСтрокМассива_ЗначениеНеМассив() Экспорт

	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйМассив";
	Ключ = "ТестоваяСтрока";
	ТестоваяСтрока = "ТестовоеЗначение";
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестоваяСтрока);

	// Получение данных из Redis не описываю т.к. для данных условий предусмотрено вызов исключения

	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(OPI_Redis)
		.Метод("КоличествоСтрокМассива")
			.Параметр(АдресСервераRedis)
			.Параметр(Ключ)
		.ВыбрасываетИсключение(СтрШаблон("Значение в БД Redis для ключа ""%1"" не являеся массивом", Ключ))
	;

КонецПроцедуры

Процедура ПолучитьСтрокуМассива_НормальныйВызов() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйМассив";

	ТестовыйМассив = Новый Массив;
	ТестовыйМассив.Добавить("ЗначениеМассива1");
	ТестовыйМассив.Добавить("ЗначениеМассива2");
	ТестовыйМассив.Добавить("ЗначениеМассива3");
	
	НомерСтрокиДляПолучения = 2;
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестовыйМассив);

	РезультатПолучения = OPI_Redis.ПолучитьСтрокуМассива(АдресСервераRedis, Ключ, НомерСтрокиДляПолучения);

	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Строка")
		.Равно(ТестовыйМассив[НомерСтрокиДляПолучения])
	;
	
КонецПроцедуры

Процедура ПолучитьСтрокуМассива_КлючНеЗадан() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйМассив";

	НомерСтрокиДляПолучения = 1;

	// Специально ничего не записываю в Redis
	
	РезультатПолучения = OPI_Redis.ПолучитьСтрокуМассива(АдресСервераRedis, Ключ, НомерСтрокиДляПолучения);

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.Равно(Неопределено)
	;
	
КонецПроцедуры

Процедура ПолучитьСтрокуМассива_ЗначениеНеМассив() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоваяСтрока";
	ТестоваяСтрока = "ТестовоеЗначение";
	
	НомерСтрокиДляПолучения = 1;

	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестоваяСтрока);

	// Получение данных из Redis не описываю т.к. для данных условий предусмотрено вызов исключения

	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(OPI_Redis)
		.Метод("ПолучитьСтрокуМассива")
			.Параметр(АдресСервераRedis)
			.Параметр(Ключ)
			.Параметр(НомерСтрокиДляПолучения)
		.ВыбрасываетИсключение(СтрШаблон("Значение в БД Redis для ключа ""%1"" не являеся массивом", Ключ))
	;
	
КонецПроцедуры

Процедура ПолучитьСтрокуМассива_НекорретныйИндекс() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйМассив";

	ТестовыйМассив = Новый Массив;
	ТестовыйМассив.Добавить("ЗначениеМассива1");
	ТестовыйМассив.Добавить("ЗначениеМассива2");
	ТестовыйМассив.Добавить("ЗначениеМассива3");
	
	НомерСтрокиДляПолучения = 5;
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестовыйМассив);

	РезультатПолучения = OPI_Redis.ПолучитьСтрокуМассива(АдресСервераRedis, Ключ, НомерСтрокиДляПолучения);

	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.Равно(Неопределено)
	;
	
КонецПроцедуры

Процедура УдалитьСтрокиМассива_ОднаСтрока() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйМассив";

	ТестовыйМассив = Новый Массив;
	ТестовыйМассив.Добавить("ЗначениеМассива1");
	ТестовыйМассив.Добавить("ЗначениеМассива2");
	ТестовыйМассив.Добавить("ЗначениеМассива3");
	
	НомерСтрокиДляУдаления = 2;
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестовыйМассив);

	РезультатУдаления = OPI_Redis.УдалитьСтрокиМассива(АдресСервераRedis, Ключ, 0, ТестовыйМассив[1]);

	РезультатПолучения = OPI_Redis.ПолучитьМассив(АдресСервераRedis, Ключ);
	
	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатУдаления)
		.ИмеетТип("Число")
		.Равно(1)
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Массив")
		.ИмеетДлину(2)
		.Содержит(ТестовыйМассив[0])
		.Содержит(ТестовыйМассив[2])
	;
	
КонецПроцедуры

Процедура УдалитьСтрокиМассива_НесколькоСтрок() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйМассив";

	ТестовыйМассив = Новый Массив;
	ТестовыйМассив.Добавить("ЗначениеМассива1");
	ТестовыйМассив.Добавить("ЗначениеДляУдаления");
	ТестовыйМассив.Добавить("ЗначениеМассива3");
	ТестовыйМассив.Добавить("ЗначениеДляУдаления");
	ТестовыйМассив.Добавить("ЗначениеДляУдаления");
	
	НомерСтрокиДляУдаления = 2;
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестовыйМассив);

	РезультатУдаления = OPI_Redis.УдалитьСтрокиМассива(АдресСервераRedis, Ключ, 0, ТестовыйМассив[1]);

	РезультатПолучения = OPI_Redis.ПолучитьМассив(АдресСервераRedis, Ключ);
	
	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатУдаления)
		.ИмеетТип("Число")
		.Равно(3)
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Массив")
		.ИмеетДлину(2)
		.Содержит(ТестовыйМассив[0])
		.Содержит(ТестовыйМассив[2])
	;
	
КонецПроцедуры

Процедура УдалитьСтрокиМассива_НечегоУдалять() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйМассив";

	ТестовыйМассив = Новый Массив;
	ТестовыйМассив.Добавить("ЗначениеМассива1");
	ТестовыйМассив.Добавить("ЗначениеМассива2");
	ТестовыйМассив.Добавить("ЗначениеМассива3");
	
	НомерСтрокиДляУдаления = 2;
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестовыйМассив);

	РезультатУдаления = OPI_Redis.УдалитьСтрокиМассива(АдресСервераRedis, Ключ, 0, "ЗначениеДляУдаления");

	РезультатПолучения = OPI_Redis.ПолучитьМассив(АдресСервераRedis, Ключ);
	
	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатУдаления)
		.ИмеетТип("Число")
		.Равно(0)
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Массив")
		.ИмеетДлину(3)
		.Содержит(ТестовыйМассив[0])
		.Содержит(ТестовыйМассив[1])
		.Содержит(ТестовыйМассив[2])
	;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСХэшТаблицами

Процедура ПолучитьХэшТаблицу() Экспорт

	// TODO:

КонецПроцедуры

Процедура КоличествоПолейХэшТаблицы() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоввяСтруктура";

	ТестоввяСтруктура = Новый Структура;
	ТестоввяСтруктура.Вставить("Ключ1", "ЗначениеСтруктуры1");
	ТестоввяСтруктура.Вставить("Ключ2", "ЗначениеСтруктуры2");
	ТестоввяСтруктура.Вставить("Ключ3", "ЗначениеСтруктуры3");
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестоввяСтруктура);

	РезультатПолучения = OPI_Redis.КоличествоПолейХэшТаблицы(АдресСервераRedis, Ключ);

	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Число")
		.Равно(3)
	;
	
КонецПроцедуры

Процедура ПоляХэшТаблицы() Экспорт

	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоввяСтруктура";

	ТестоввяСтруктура = Новый Структура;
	ТестоввяСтруктура.Вставить("Ключ1", "ЗначениеСтруктуры1");
	ТестоввяСтруктура.Вставить("Ключ2", "ЗначениеСтруктуры2");
	ТестоввяСтруктура.Вставить("Ключ3", "ЗначениеСтруктуры3");
	
	МассивПолейДляСравнения = ПолучитьМассивПолейСтруктуры(ТестоввяСтруктура);
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестоввяСтруктура);

	РезультатПолучения = OPI_Redis.ПоляХэшТаблицы(АдресСервераRedis, Ключ);

	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Массив")
		.ИмеетДлину(3)
		.Содержит(МассивПолейДляСравнения[0])
		.Содержит(МассивПолейДляСравнения[1])
		.Содержит(МассивПолейДляСравнения[2])
	;
	
КонецПроцедуры

Процедура ЗначениеПоляХэшТаблицы() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоввяСтруктура";

	ТестоввяСтруктура = Новый Структура;
	ТестоввяСтруктура.Вставить("Ключ1", "ЗначениеСтруктуры1");
	ТестоввяСтруктура.Вставить("Ключ2", "ЗначениеСтруктуры2");
	ТестоввяСтруктура.Вставить("Ключ3", "ЗначениеСтруктуры3");

	МассивПолейДляСравнения = ПолучитьМассивПолейСтруктуры(ТестоввяСтруктура);
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестоввяСтруктура);

	РезультатПолучения = OPI_Redis.ЗначениеПоляХэшТаблицы(АдресСервераRedis, Ключ, МассивПолейДляСравнения[2]);

	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Строка")
		.Равно(ПолучитьЗначениеПоКлючу(ТестоввяСтруктура, МассивПолейДляСравнения[2]))
	;
	
КонецПроцедуры

Процедура УдалитьПоляХэшТаблицы() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестоввяСтруктура";

	ТестоввяСтруктура = Новый Структура;
	ТестоввяСтруктура.Вставить("Ключ1", "ЗначениеСтруктуры1");
	ТестоввяСтруктура.Вставить("Ключ2", "ЗначениеСтруктуры2");
	ТестоввяСтруктура.Вставить("Ключ3", "ЗначениеСтруктуры3");
	ТестоввяСтруктура.Вставить("Ключ4", "ЗначениеСтруктуры4");
	ТестоввяСтруктура.Вставить("Ключ5", "ЗначениеСтруктуры5");

	МассивПолейДляСравнения = ПолучитьМассивПолейСтруктуры(ТестоввяСтруктура);
	
	МассивПолейДляУдаления = Новый Массив;
	МассивПолейДляУдаления.Добавить(МассивПолейДляСравнения[2]);
	МассивПолейДляУдаления.Добавить(МассивПолейДляСравнения[3]);
	
	РезультатУстановки = OPI_Redis.УстановитьЗначение(АдресСервераRedis, Ключ, ТестоввяСтруктура);

	РезультатПолучения = OPI_Redis.УдалитьПоляХэшТаблицы(АдресСервераRedis, Ключ, МассивПолейДляУдаления);

	РезультатПроверки = OPI_Redis.ПоляХэшТаблицы(АдресСервераRedis, Ключ);
	
	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("Число")
		.Равно(МассивПолейДляУдаления.Количество())
	;
	
	ЮТест.ОжидаетЧто(РезультатПроверки)
		.ИмеетТип("Массив")
		.ИмеетДлину(ТестоввяСтруктура.Количество() - МассивПолейДляУдаления.Количество())
		.Содержит(МассивПолейДляСравнения[0])
		.Содержит(МассивПолейДляСравнения[1])
		.НеСодержит(МассивПолейДляСравнения[2])
		.НеСодержит(МассивПолейДляСравнения[3])
		.Содержит(МассивПолейДляСравнения[4])
	;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСJson

Процедура УстановитьИПолучитьJson() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйJson";
	
	ТестоваяСтруктура = Новый Структура("Тест1, Тест2",
		"ТестовоеЗначение1", "ТестовоеЗначение2");
	
	ЗаписьJson = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку(
		Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет));
	ЗаписатьJSON(ЗаписьJson, ТестоваяСтруктура);
	
	РезультатУстановки = OPI_Redis.УстановитьJson(АдресСервераRedis, Ключ, ЗаписьJson);
	
	РезультатПолучения = OPI_Redis.ПолучитьJson(АдресСервераRedis, Ключ);

	СтруктураИзRedis = ПрочитатьJSON(РезультатПолучения);
	СтруктурыРавны = СтруктурыРавны(ТестоваяСтруктура, СтруктураИзRedis);

	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("ЧтениеJSON")
	;

	ЮТест.ОжидаетЧто(СтруктурыРавны)
		.ЭтоИстина()
	;
	
КонецПроцедуры

Процедура УстановитьИПолучитьМногострочныйJson() Экспорт

	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйJson";
	
	ТестоваяСтруктура = Новый Структура("Тест1, Тест2",
		"ТестовоеЗначение1", "ТестовоеЗначение2");
	
	ЗаписьJson = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON());
	ЗаписатьJSON(ЗаписьJson, ТестоваяСтруктура);
	
	РезультатУстановки = OPI_Redis.УстановитьJson(АдресСервераRedis, Ключ, ЗаписьJson);
	
	РезультатПолучения = OPI_Redis.ПолучитьJson(АдресСервераRedis, Ключ);

	СтруктураИзRedis = ПрочитатьJSON(РезультатПолучения);
	СтруктурыРавны = СтруктурыРавны(ТестоваяСтруктура, СтруктураИзRedis);

	ЮТест.ОжидаетЧто(РезультатУстановки)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатПолучения)
		.ИмеетТип("ЧтениеJSON")
	;

	ЮТест.ОжидаетЧто(СтруктурыРавны)
		.ЭтоИстина()
	;

КонецПроцедуры

Процедура УстановитьJsonДляПути_Число() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйJson";
	
	ТестоваяСтруктура = Новый Структура("Тест1, Тест2",
		"ТестовоеЗначение1", "ТестовоеЗначение2");
	
	ЗаписьJson = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку(
		Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет));
	ЗаписатьJSON(ЗаписьJson, ТестоваяСтруктура);
	
	РезультатУстановкиКорня = OPI_Redis.УстановитьJson(АдресСервераRedis, Ключ, ЗаписьJson);

	РезультатУстановкиДляПути = OPI_Redis.УстановитьJsonДляПути(АдресСервераRedis, Ключ, 3, "Тест2");

	РезультатПолучения = OPI_Redis.ПолучитьJson(АдресСервераRedis, Ключ);

	СтруктураИзRedis = ПрочитатьJSON(РезультатПолучения);

	ЮТест.ОжидаетЧто(РезультатУстановкиКорня)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатУстановкиДляПути)
		.ЭтоИстина()
	;
	
	ЮТест.ОжидаетЧто(СтруктураИзRedis)
		.ИмеетТип("Структура")
		.ИмеетСвойство("Тест2")
		.Свойство("Тест2")
			.ИмеетТип("Число")
			.Равно(3)
	;
	
КонецПроцедуры

Процедура УстановитьJsonДляПути_Структура() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйJson";
	
	ТестоваяСтруктура = Новый Структура("Тест1, Тест2",
		"ТестовоеЗначение1", "ТестовоеЗначение2");
	
	ЗаписьJson_ТестоваяСтруктура = Новый ЗаписьJSON();
	ЗаписьJson_ТестоваяСтруктура.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJson_ТестоваяСтруктура, ТестоваяСтруктура);
	
	КорневаяСтруктура = Новый Структура("Поле1, Поле2",
		"ЗначениеДляПоля1", "ЗначениеДляПоля2");
	
	ЗаписьJson_КорневаяСтруктура = Новый ЗаписьJSON();
	ЗаписьJson_КорневаяСтруктура.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJson_КорневаяСтруктура, КорневаяСтруктура);
	
	РезультатУстановкиКорня = OPI_Redis.УстановитьJson(АдресСервераRedis, Ключ, ЗаписьJson_КорневаяСтруктура);

	РезультатУстановкиДляПути = OPI_Redis.УстановитьJsonДляПути(АдресСервераRedis, Ключ, ЗаписьJson_ТестоваяСтруктура, "Поле2");
	
	РезультатПолучения = OPI_Redis.ПолучитьJson(АдресСервераRedis, Ключ);

	СтруктураИзRedis = ПрочитатьJSON(РезультатПолучения);

	ЮТест.ОжидаетЧто(РезультатУстановкиКорня)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатУстановкиДляПути)
		.ЭтоИстина()
	;
	
	ЮТест.ОжидаетЧто(СтруктураИзRedis)
		.ИмеетТип("Структура")
		.ИмеетСвойство("Поле2")
		.Свойство("Поле2")
			.ИмеетТип("Структура")
			.ИмеетСвойство("Тест1")
			.ИмеетСвойство("Тест2")
		.Свойство("Поле2.Тест1").Равно("ТестовоеЗначение1")
		.Свойство("Поле2.Тест2").Равно("ТестовоеЗначение2")
	;
	
КонецПроцедуры

Процедура УстановитьJsonДляПути_КореньОтсутствует_Число() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйJson";
	Путь = "Поле1";
	
	// Путь "Поле1" отсутствует
	
	ЮТест.ОжидаетЧто(OPI_Redis)
		.Метод("УстановитьJsonДляПути")
			.Параметр(АдресСервераRedis)
			.Параметр(Ключ)
			.Параметр(3)
			.Параметр(Путь)
		.ВыбрасываетИсключение(СтрШаблон("Для значения %1 не определен путь %2", Ключ, Путь))
	;
	
КонецПроцедуры

Процедура УстановитьJsonДляПути_КореньОтсутствует_Структура() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйJson";
	Путь = "Поле1";
	
	ТестоваяСтруктура = Новый Структура("Тест1", "ТестовоеЗначение1");
	
	ЗаписьJson_ТестоваяСтруктура = Новый ЗаписьJSON();
	ЗаписьJson_ТестоваяСтруктура.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJson_ТестоваяСтруктура, ТестоваяСтруктура);

	// Путь "Поле1" отсутствует
	
	ЮТест.ОжидаетЧто(OPI_Redis)
		.Метод("УстановитьJsonДляПути")
			.Параметр(АдресСервераRedis)
			.Параметр(Ключ)
			.Параметр(ЗаписьJson_ТестоваяСтруктура)
			.Параметр(Путь)
		.ВыбрасываетИсключение(СтрШаблон("Для значения %1 не определен путь %2", Ключ, Путь))
	;
		
КонецПроцедуры

Процедура УстановитьJsonДляПути_ГлубокаяВложенность_Число() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйJson";
	
	ТестоваяСтруктура = Новый Структура("Тест1, Тест2",
		Новый Структура("Поле1_1", "ТестовоеЗначение1_1"), "ТестовоеЗначение2");
	
	ЗаписьJson = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку(
		Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет));
	ЗаписатьJSON(ЗаписьJson, ТестоваяСтруктура);
	
	РезультатУстановкиКорня = OPI_Redis.УстановитьJson(АдресСервераRedis, Ключ, ЗаписьJson);
	
	РезультатУстановкиДляПути = OPI_Redis.УстановитьJsonДляПути(АдресСервераRedis, Ключ, 3, "Тест1.Поле1_1");
	
	РезультатПолучения = OPI_Redis.ПолучитьJson(АдресСервераRedis, Ключ);

	СтруктураИзRedis = ПрочитатьJSON(РезультатПолучения);

	ЮТест.ОжидаетЧто(РезультатУстановкиКорня)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(РезультатУстановкиДляПути)
		.ЭтоИстина()
	;

	ЮТест.ОжидаетЧто(СтруктураИзRedis)
		.ИмеетТип("Структура")
		.ИмеетСвойство("Тест1")
		.Свойство("Тест1")
			.ИмеетТип("Структура")
			.ИмеетСвойство("Поле1_1")
		.Свойство("Тест1.Поле1_1").Равно(3)
	;
	
КонецПроцедуры

Процедура УстановитьJsonДляПути_ГлубокаяВложенностьОтсутствует_Число() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйJson";
	Путь = "Тест1.Поле1_1";
	
	ТестоваяСтруктура = Новый Структура("Тест1, Тест2",
		"ТестовоеЗначение1", "ТестовоеЗначение2");
	
	ЗаписьJson = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку(
		Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет));
	ЗаписатьJSON(ЗаписьJson, ТестоваяСтруктура);
	
	РезультатУстановки = OPI_Redis.УстановитьJson(АдресСервераRedis, Ключ, ЗаписьJson);
	
	// Часть пути "Тест1" есть, но "Поле1_1" отсутствует
	
	ЮТест.ОжидаетЧто(OPI_Redis)
		.Метод("УстановитьJsonДляПути")
			.Параметр(АдресСервераRedis)
			.Параметр(Ключ)
			.Параметр(3)
			.Параметр(Путь)
		.ВыбрасываетИсключение(СтрШаблон("Для значения %1 не определен путь %2", Ключ, Путь))
	;
	
КонецПроцедуры

Процедура ПолучитьJsonДляПути() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйJson";
	
	ТестоваяСтруктура = Новый Структура("Тест1, Тест2",
		"ТестовоеЗначение1", "ТестовоеЗначение2");
	
	ЗаписьJson = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку(
		Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет));
	ЗаписатьJSON(ЗаписьJson, ТестоваяСтруктура);
	
	РезультатУстановкиКорня = OPI_Redis.УстановитьJson(АдресСервераRedis, Ключ, ЗаписьJson);
	
	РезультатПолучения = OPI_Redis.ПолучитьJsonДляПути(АдресСервераRedis, Ключ, "Тест2");
	
	ЗначениеИзRedis = ПрочитатьJSON(РезультатПолучения);
	
	ЮТест.ОжидаетЧто(РезультатУстановкиКорня)
		.ЭтоИстина()
	;
	
	ЮТест.ОжидаетЧто(ЗначениеИзRedis)
		.ИмеетТип("Строка")
		.Равно(ТестоваяСтруктура.Тест2)
	;
	
КонецПроцедуры

Процедура ПолучитьJsonДляПути_КореньОтсутствует() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйJson";
	Путь = "Поле1";
	
	// Путь "Поле1" отсутствует
	
	ЮТест.ОжидаетЧто(OPI_Redis)
		.Метод("ПолучитьJsonДляПути")
			.Параметр(АдресСервераRedis)
			.Параметр(Ключ)
			.Параметр(Путь)
		.ВыбрасываетИсключение(СтрШаблон("Для значения %1 не определен путь %2", Ключ, Путь))
	;
	
КонецПроцедуры

Процедура ПолучитьJsonДляПути_ГлубокаяВложенностьОтсутствует() Экспорт
	
	АдресСервераRedis = АдресСервераRedis();
	Ключ = "ТестовыйJson";
	Путь = "Тест1.Поле1_1";
	
	ТестоваяСтруктура = Новый Структура("Тест1, Тест2",
		"ТестовоеЗначение1", "ТестовоеЗначение2");
	
	ЗаписьJson = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку(
		Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет));
	ЗаписатьJSON(ЗаписьJson, ТестоваяСтруктура);
	
	РезультатУстановки = OPI_Redis.УстановитьJson(АдресСервераRedis, Ключ, ЗаписьJson);
	
	// Часть пути "Тест1" есть, но "Поле1_1" отсутствует
	
	ЮТест.ОжидаетЧто(OPI_Redis)
		.Метод("ПолучитьJsonДляПути")
			.Параметр(АдресСервераRedis)
			.Параметр(Ключ)
			.Параметр(Путь)
		.ВыбрасываетИсключение(СтрШаблон("Для значения %1 не определен путь %2", Ключ, Путь))
	;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция АдресСервераRedis()
	
	Возврат "localhost:6379";
	
КонецФункции

#Область РаботаСоСтруктурой

Функция ПолучитьМассивПолейСтруктуры(Структура)
	
	Результат = Новый Массив;
	
	Для каждого ЭлементСтруктуры Из Структура Цикл
		Результат.Добавить(ЭлементСтруктуры.Ключ);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьЗначениеПоКлючу(Структура, Ключ)
	
	Перем Значение;
	Если Структура.Свойство(Ключ, Значение) Тогда
		Возврат Значение;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция СтруктурыРавны(Структура1, Структура2)
	
    Если Структура1.Количество() <> Структура2.Количество() Тогда
        Возврат Ложь;
	КонецЕсли;
	
    Для Каждого Элемент Из Структура1 Цикл
		Если Не Структура2.Свойство(Элемент.Ключ)
			Или Структура1[Элемент.Ключ] <> Структура2[Элемент.Ключ] Тогда
			
            Возврат Ложь;
			
        КонецЕсли;
	КонецЦикла;
	
    Возврат Истина;
	
КонецФункции

#КонецОбласти

#КонецОбласти
