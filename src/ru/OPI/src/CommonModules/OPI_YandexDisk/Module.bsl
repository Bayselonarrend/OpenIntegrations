// OneScript: ./OInt/core/Modules/OPI_YandexDisk.os
// Lib: Yandex Disk
// CLI: yadisk
// Keywords: yandexdisk, yandex.disk, yandex disk, yandex drive, yadisk, ya disk
// Depends: OPI_YandexID

// MIT License

// Copyright (c) 2023-2025 Anton Tsitavets

// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:

// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.

// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

// https://github.com/Bayselonarrend/OpenIntegrations

// BSLLS:LatinAndCyrillicSymbolInWord-off
// BSLLS:IncorrectLineBreak-off
// BSLLS:NumberOfOptionalParams-off
// BSLLS:UsingServiceTag-off
// BSLLS:UsingSynchronousCalls-off
// BSLLS:MagicNumber-off

//@skip-check method-too-many-params
//@skip-check module-structure-top-region
//@skip-check module-structure-method-in-regions
//@skip-check wrong-string-literal-content

#Область ПрограммныйИнтерфейс

#Область Авторизация

// Получить код подтверждения
// Получает код подтверждения и адрес страницы, на которой его необходимо ввести
//
// Параметры:
//  ClientId - Строка - Client id - id
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция ПолучитьКодПодтверждения(Знач ClientId) Экспорт

    Результат = OPI_YandexID.ПолучитьКодПодтверждения(ClientId);
    Возврат Результат;

КонецФункции

// Преобразовать код в токен
// Преобразовывает код в токен после ввода кода при выполнении ПолучитьКодПодтверждения
//
// Параметры:
//  ClientId      - Строка - Client id                               - id
//  ClientSecret  - Строка - Client secret                           - secret
//  КодУстройства - Строка - device_code из ПолучитьКодПодтверждения - device
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция ПреобразоватьКодВТокен(Знач ClientId, Знач ClientSecret, Знач КодУстройства) Экспорт

    Результат = OPI_YandexID.ПреобразоватьКодВТокен(ClientId, ClientSecret, КодУстройства);
    Возврат Результат;

КонецФункции

// Обновить токен
// Обновляет токен по Refresh token
//
// Параметры:
//  ClientId     - Строка - Client id      - id
//  ClientSecret - Строка - Client secret  - secret
//  RefreshToken - Строка - Refresh token  - refresh
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция ОбновитьТокен(Знач ClientId, Знач ClientSecret, Знач RefreshToken) Экспорт

    Результат = OPI_YandexID.ОбновитьТокен(ClientId, ClientSecret, RefreshToken);
    Возврат Результат;

КонецФункции

#КонецОбласти

#Область РаботаСФайламиИПапками

// Получить информацию о диске
// Получает информацию о текущем диске
//
// Параметры:
//  Токен - Строка - Токен - token
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция ПолучитьИнформациюОДиске(Знач Токен) Экспорт

    Заголовки = OPI_YandexID.ПолучитьЗаголовокАвторизации(Токен);
    Ответ     = OPI_ЗапросыHTTP.Get("https://cloud-api.yandex.net/v1/disk", , Заголовки);

    Возврат Ответ;

КонецФункции

// Создать папку
// Создает каталог на диске
//
// Параметры:
//  Токен - Строка - Токен                     - token
//  Путь  - Строка - Путь к созаваемой папке   - path
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция СоздатьПапку(Знач Токен, Знач Путь) Экспорт

    Заголовки = OPI_YandexID.ПолучитьЗаголовокАвторизации(Токен);
    URL       = "https://cloud-api.yandex.net/v1/disk/resources";
    Href      = "href";

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("path", Путь, "Строка", Параметры);

    Параметры = OPI_Инструменты.ПараметрыЗапросаВСтроку(Параметры);
    Ответ     = OPI_ЗапросыHTTP.PutСТелом(URL + Параметры, , Заголовки, Ложь);

    URLОтвета = Ответ[Href];

    Если Не ЗначениеЗаполнено(URLОтвета) Тогда
        Возврат Ответ;
    КонецЕсли;

    Ответ = OPI_ЗапросыHTTP.Get(URLОтвета, , Заголовки);

    Возврат Ответ;

КонецФункции

// Получить объект
// Получает информацию об объекте диска по заданному пути
//
// Параметры:
//  Токен - Строка - Токен                  - token
//  Путь  - Строка - Путь к папке или файлу - path
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция ПолучитьОбъект(Знач Токен, Знач Путь) Экспорт

    Заголовки = OPI_YandexID.ПолучитьЗаголовокАвторизации(Токен);

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("path", Путь, "Строка", Параметры);

    Ответ = OPI_ЗапросыHTTP.Get("https://cloud-api.yandex.net/v1/disk/resources", Параметры, Заголовки);

    Возврат Ответ;

КонецФункции

// Удалить объект
// Удаляет объект по заданному пути
//
// Параметры:
//  Токен    - Строка - Токен                              - token
//  Путь     - Строка - Путь к удаляемой папке или файлу   - path
//  ВКорзину - Булево - В корзину                          - can
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция УдалитьОбъект(Знач Токен, Знач Путь, Знач ВКорзину = Истина) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьБулево(ВКорзину);

    Заголовки = OPI_YandexID.ПолучитьЗаголовокАвторизации(Токен);

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("path"       , Путь       , "Строка", Параметры);
    OPI_Инструменты.ДобавитьПоле("permanently", Не ВКорзину, "Булево", Параметры);

    Ответ = OPI_ЗапросыHTTP.Delete("https://cloud-api.yandex.net/v1/disk/resources", Параметры, Заголовки);

    Возврат Ответ;

КонецФункции

// Создать копию объекта
// Создает копию объекта по заданному пути и пути к оригиналу
//
// Параметры:
//  Токен          - Строка - Токен                                                   - token
//  Оригинал       - Строка - Путь к оригинальному файлу или каталогу                 - from
//  Путь           - Строка - Путь назначения для копии                               - to
//  Перезаписывать - Булево - Перезаписывать если файл с таким именем уже существует  - rewrite
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция СоздатьКопиюОбъекта(Знач Токен, Знач Оригинал, Знач Путь, Знач Перезаписывать = Ложь) Экспорт

    Заголовки = OPI_YandexID.ПолучитьЗаголовокАвторизации(Токен);
    URL       = "https://cloud-api.yandex.net/v1/disk/resources/copy";
    Href      = "href";

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("from"     , Оригинал      , "Строка", Параметры);
    OPI_Инструменты.ДобавитьПоле("path"     , Путь          , "Строка", Параметры);
    OPI_Инструменты.ДобавитьПоле("overwrite", Перезаписывать, "Булево", Параметры);

    Параметры = OPI_Инструменты.ПараметрыЗапросаВСтроку(Параметры);
    Ответ     = OPI_ЗапросыHTTP.PostСТелом(URL + Параметры, , Заголовки, Ложь);

    URLОтвета = Ответ[Href];

    Если Не ЗначениеЗаполнено(URLОтвета) Тогда
        Возврат Ответ;
    КонецЕсли;

    Ответ = OPI_ЗапросыHTTP.Get(URLОтвета, , Заголовки);

    Возврат Ответ;

КонецФункции

// Получить ссылку для скачивания
// Получает ссылку для скачивания файла
//
// Параметры:
//  Токен - Строка - Токен                         - token
//  Путь  - Строка  - Путь к файлу для скачивания  - path
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция ПолучитьСсылкуДляСкачивания(Знач Токен, Знач Путь) Экспорт

    Заголовки = OPI_YandexID.ПолучитьЗаголовокАвторизации(Токен);

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("path", Путь, "Строка", Параметры);

    Ответ = OPI_ЗапросыHTTP.Get("https://cloud-api.yandex.net/v1/disk/resources/download", Параметры, Заголовки);

    Возврат Ответ;

КонецФункции

// Получить ссылку загрузки файла
// Получает ссылку для ручной загрузки файла на Диск
//
// Параметры:
//  Токен          - Строка - Токен                                                   - token
//  Путь           - Строка - Путь для сохранение файла на Диске                      - path
//  Перезаписывать - Булево - Перезаписывать если файл с таким именем уже существует  - rewrite
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция ПолучитьСсылкуЗагрузкиФайла(Знач Токен, Знач Путь, Знач Перезаписывать = Ложь) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Путь);
    OPI_ПреобразованиеТипов.ПолучитьБулево(Перезаписывать);

    Заголовки = OPI_YandexID.ПолучитьЗаголовокАвторизации(Токен);

    Параметры = Новый Структура;
    Параметры.Вставить("path"       , Путь);
    Параметры.Вставить("overwrite"  , Перезаписывать);

    Ответ = OPI_ЗапросыHTTP.Get("https://cloud-api.yandex.net/v1/disk/resources/upload", Параметры, Заголовки);

    Возврат Ответ;

КонецФункции

// Скачать файл
// Скачивает файл по указанному пути
//
// Параметры:
//  Токен          - Строка - Токен                        - token
//  Путь           - Строка - Путь к файлу для скачивания  - path
//  ПутьСохранения - Строка - Путь сохранения файла        - out
//
// Возвращаемое значение:
//  ДвоичныеДанные,Строка - Двоичные данные или путь к файлу при указании параметра ПутьСохранения
Функция СкачатьФайл(Знач Токен, Знач Путь, Знач ПутьСохранения = "") Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(ПутьСохранения);

    Ответ = ПолучитьСсылкуДляСкачивания(Токен, Путь);
    URL   = Ответ["href"];

    Если Не ЗначениеЗаполнено(URL) Тогда
        Возврат Ответ;
    КонецЕсли;

    Ответ = OPI_ЗапросыHTTP.Get(URL, , , ПутьСохранения);

    Возврат Ответ;

КонецФункции

// Получить список файлов
// Получает список файлов с или без отбора по типу
// Список доступных типов: audio, backup, book, compressed, data, development,
//                         diskimage, document, encoded, executable, flash, font,
//                         mage, settings, spreadsheet, text, unknown, video, web
//
// Параметры:
//  Токен             - Строка       - Токен                                                - token
//  Количество        - Число,Строка - Количество возвращаемых объектов                     - amount
//  СмещениеОтНачала  - Число        - Смещение для получение объектов не из начала списка  - offset
//  ОтборПоТипу       - Строка       - Отбор по типу файла                                  - type
//  СортироватьПоДате - Булево       - Истина > сортировать по дате, Ложь > по алфавиту     - datesort
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция ПолучитьСписокФайлов(Знач Токен
    , Знач Количество = 0
    , Знач СмещениеОтНачала = 0
    , Знач ОтборПоТипу = ""
    , Знач СортироватьПоДате = Ложь) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Количество);
    OPI_ПреобразованиеТипов.ПолучитьСтроку(СмещениеОтНачала);
    OPI_ПреобразованиеТипов.ПолучитьСтроку(ОтборПоТипу);
    OPI_ПреобразованиеТипов.ПолучитьБулево(СортироватьПоДате);

    Заголовки = OPI_YandexID.ПолучитьЗаголовокАвторизации(Токен);

    Параметры = Новый Структура;

    Если ЗначениеЗаполнено(Количество) Тогда
        Параметры.Вставить("limit", OPI_Инструменты.ЧислоВСтроку(Количество));
    КонецЕсли;

    Если ЗначениеЗаполнено(СмещениеОтНачала) Тогда
        Параметры.Вставить("offset", OPI_Инструменты.ЧислоВСтроку(СмещениеОтНачала));
    КонецЕсли;

    Если ЗначениеЗаполнено(ОтборПоТипу) Тогда
        Параметры.Вставить("media_type", ОтборПоТипу);
    КонецЕсли;

    Если СортироватьПоДате Тогда
        Назначение = "last-uploaded";
    Иначе
        Назначение = "files";
    КонецЕсли;

    Ответ = OPI_ЗапросыHTTP.Get("https://cloud-api.yandex.net/v1/disk/resources/" + Назначение, Параметры, Заголовки);

    Возврат Ответ;

КонецФункции

// Переместить объект
// Перемещает объект по заданному пути и пути к оригиналу
//
// Параметры:
//  Токен          - Строка - Токен                                                   - token
//  Оригинал       - Строка - Путь к оригинальному файлу или папке                    - from
//  Путь           - Строка - Путь назначение для перемещения                         - to
//  Перезаписывать - Булево - Перезаписывать если файл с таким именем уже существует  - rewrite
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция ПереместитьОбъект(Знач Токен, Знач Оригинал, Знач Путь, Знач Перезаписывать = Ложь) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Оригинал);
    OPI_ПреобразованиеТипов.ПолучитьСтроку(Путь);
    OPI_ПреобразованиеТипов.ПолучитьБулево(Перезаписывать);

    Заголовки = OPI_YandexID.ПолучитьЗаголовокАвторизации(Токен);
    URL       = "https://cloud-api.yandex.net/v1/disk/resources/move";
    Href      = "href";

    Параметры = Новый Структура;
    Параметры.Вставить("from"       , Оригинал);
    Параметры.Вставить("path"       , Путь);
    Параметры.Вставить("overwrite"  , Перезаписывать);

    Параметры = OPI_Инструменты.ПараметрыЗапросаВСтроку(Параметры);
    Ответ     = OPI_ЗапросыHTTP.PostСТелом(URL + Параметры, , Заголовки, Ложь);
    URLОтвета = Ответ[Href];

    Если Не ЗначениеЗаполнено(URLОтвета) Тогда
        Возврат Ответ;
    КонецЕсли;

    Ответ = OPI_ЗапросыHTTP.Get(URLОтвета, , Заголовки);

    Возврат Ответ;

КонецФункции

// Загрузить файл
// Загружает файл на диск по заданному пути
//
// Параметры:
//  Токен          - Строка                - Токен                                                      - token
//  Путь           - Строка                - Путь для сохранение файла на Диске                         - path
//  Файл           - Строка,ДвоичныеДанные - Файл для загрузки                                          - file
//  Перезаписывать - Булево                - Перезаписывать, если файл с таким именем уже существует    - rewrite
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция ЗагрузитьФайл(Знач Токен, Знач Путь, Знач Файл, Знач Перезаписывать = Ложь) Экспорт

    Заголовки = OPI_YandexID.ПолучитьЗаголовокАвторизации(Токен);

    OPI_ПреобразованиеТипов.ПолучитьДвоичныеДанные(Файл);
    Файл = Новый Структура("file", Файл);

    Ответ = ПолучитьСсылкуЗагрузкиФайла(Токен, Путь, Перезаписывать);
    URL   = Ответ.Получить("href");

    Если Не ЗначениеЗаполнено(URL) Тогда
        Возврат Ответ;
    КонецЕсли;

    Ответ = OPI_ЗапросыHTTP.PutMultipart(URL, Новый Структура(), Файл, "multipart", Заголовки);

    Возврат Ответ;

КонецФункции

// Загрузить файл частями
// Загружает файл на диск по заданному пути частями
//
// Параметры:
//  Токен          - Строка                - Токен                                                      - token
//  Путь           - Строка                - Путь для сохранение файла на Диске                         - path
//  Файл           - Строка,ДвоичныеДанные - Файл для загрузки                                          - file
//  РазмерЧасти    - Число                 - Размер одной части при загрузке                            - psize
//  Перезаписывать - Булево                - Перезаписывать, если файл с таким именем уже существует    - rewrite
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция ЗагрузитьФайлЧастями(Знач Токен
    , Знач Путь
    , Знач Файл
    , Знач РазмерЧасти = 33554432
    , Знач Перезаписывать = Ложь) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьДвоичныеДанные(Файл);
    OPI_ПреобразованиеТипов.ПолучитьЧисло(РазмерЧасти);

    РазмерЧасти = ?(ЗначениеЗаполнено(РазмерЧасти), РазмерЧасти, 33554432);

    Ответ = ПолучитьСсылкуЗагрузкиФайла(Токен, Путь, Перезаписывать);
    URL   = Ответ.Получить("href");

    Если Не ЗначениеЗаполнено(URL) Тогда
        Возврат Ответ;
    КонецЕсли;

    РезультатЗапроса = OPI_ЗапросыHTTP.НовыйЗапрос()
        .Инициализировать(URL)
        .УстановитьДвоичноеТело(Файл)
        .ОтправитьДанныеЧастями(РазмерЧасти);

    ОтветОбъект = РезультатЗапроса.ВернутьОтвет(Истина, Истина);
    ОтветJSON   = РезультатЗапроса.ВернутьОтветКакJSONКоллекцию(Истина, Истина);
    ОтветСтрока = РезультатЗапроса.ВернутьОтветКакСтроку(Истина, Истина);

    Если ЗначениеЗаполнено(ОтветJSON) Тогда
        Возврат ОтветJSON;
    Иначе

       Ответ = Новый Соответствие;
       Ответ.Вставить("status", ОтветОбъект.КодСостояния);

       Если ЗначениеЗаполнено(ОтветСтрока) Тогда
           Ответ.Вставить("data", ОтветСтрока);
       КонецЕсли;

    КонецЕсли;

    Возврат Ответ;

КонецФункции

// Загрузить файл по URL
// Загружает файл на диск, забирая его по заданному URL
//
// Параметры:
//  Токен - Строка - Токен                               - token
//  Путь  - Строка - Путь помещения загруженного файла   - path
//  Адрес - Строка - URL файла                           - url
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция ЗагрузитьФайлПоURL(Знач Токен, Знач Путь, Знач Адрес) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Путь);
    OPI_ПреобразованиеТипов.ПолучитьСтроку(Адрес);

    Заголовки = OPI_YandexID.ПолучитьЗаголовокАвторизации(Токен);
    URL       = "https://cloud-api.yandex.net/v1/disk/resources/upload";

    Параметры = Новый Структура;
    Параметры.Вставить("url" , КодироватьСтроку(Адрес, СпособКодированияСтроки.КодировкаURL));
    Параметры.Вставить("path", Путь);

    Параметры = OPI_Инструменты.ПараметрыЗапросаВСтроку(Параметры);
    Ответ     = OPI_ЗапросыHTTP.PostСТелом(URL + Параметры, , Заголовки, Ложь);

    Возврат Ответ;

КонецФункции

#КонецОбласти

#Область УправлениеПубличнымДоступом

// Опубликовать объект
// Публикует объект диска в публичный доступ
//
// Параметры:
//  Токен - Строка - Токен                        - token
//  Путь  - Строка - Путь к публикуемому объекту  - path
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция ОпубликоватьОбъект(Знач Токен, Знач Путь) Экспорт

    Возврат ПереключениеОбщегоДоступа(Токен, Путь, Истина);

КонецФункции

// Отменить публикацию объекта
// Отменяет публикацию ранее опубликованного объекта
//
// Параметры:
//  Токен - Строка - Токен                                  - token
//  Путь  - Строка - Путь к опубликованному ранее объекту   - path
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция ОтменитьПубликациюОбъекта(Знач Токен, Знач Путь) Экспорт

    Возврат ПереключениеОбщегоДоступа(Токен, Путь, Ложь);

КонецФункции

// Получить список опубликованных объектов.
// Получает список опубликованных объектов
//
// Параметры:
//  Токен            - Строка - Токен                                               - token
//  Количество       - Число  - Количество возвращаемых объектов                    - amount
//  СмещениеОтНачала - Число  - Смещение для получение объектов не из начала списка - offset
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция ПолучитьСписокОпубликованныхОбъектов(Знач Токен, Знач Количество = 0, Знач СмещениеОтНачала = 0) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Количество);
    OPI_ПреобразованиеТипов.ПолучитьСтроку(СмещениеОтНачала);

    Заголовки = OPI_YandexID.ПолучитьЗаголовокАвторизации(Токен);

    Параметры = Новый Структура;

    Если ЗначениеЗаполнено(Количество) Тогда
        Параметры.Вставить("limit", Количество);
    КонецЕсли;

    Если ЗначениеЗаполнено(СмещениеОтНачала) Тогда
        Параметры.Вставить("offset", СмещениеОтНачала);
    КонецЕсли;

    Ответ = OPI_ЗапросыHTTP.Get("https://cloud-api.yandex.net/v1/disk/resources/public", Параметры, Заголовки);

    Возврат Ответ;

КонецФункции

// Получить публичный объект
// Получает информацию об опубликованном объекте по его URL
//
// Параметры:
//  Токен            - Строка - Токен                                                           - token
//  URL              - Строка - Адрес объекта                                                   - url
//  Количество       - Число  - Количество возвращаемых вложенных объектов (для каталога)       - amount
//  СмещениеОтНачала - Число  - Смещение для получение вложенных объектов не из начала списка   - offset
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция ПолучитьПубличныйОбъект(Знач Токен, Знач URL, Знач Количество = 0, Знач СмещениеОтНачала = 0) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(URL);
    OPI_ПреобразованиеТипов.ПолучитьСтроку(Количество);
    OPI_ПреобразованиеТипов.ПолучитьСтроку(СмещениеОтНачала);

    Заголовки = OPI_YandexID.ПолучитьЗаголовокАвторизации(Токен);

    Параметры = Новый Структура;

    Если ЗначениеЗаполнено(Количество) Тогда
        Параметры.Вставить("limit", OPI_Инструменты.ЧислоВСтроку(Количество));
    КонецЕсли;

    Если ЗначениеЗаполнено(СмещениеОтНачала) Тогда
        Параметры.Вставить("offset", OPI_Инструменты.ЧислоВСтроку(СмещениеОтНачала));
    КонецЕсли;

    Параметры.Вставить("public_key", URL);

    Ответ = OPI_ЗапросыHTTP.Get("https://cloud-api.yandex.net/v1/disk/public/resources", Параметры, Заголовки);

    Возврат Ответ;

КонецФункции

// Получить ссылку скачивания публичного объекта
// Получает прямую ссылку для скачивания публичного объекта
//
// Параметры:
//  Токен - Строка - Токен               - token
//  URL   - Строка - Адрес объекта       - url
//  Путь  - Строка - Путь внутри объекта - path
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция ПолучитьСсылкуСкачиванияПубличногоОбъекта(Знач Токен, Знач URL, Знач Путь = "") Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(URL);
    OPI_ПреобразованиеТипов.ПолучитьСтроку(Путь);

    Заголовки = OPI_YandexID.ПолучитьЗаголовокАвторизации(Токен);

    Параметры = Новый Структура;

    Если ЗначениеЗаполнено(Путь) Тогда
        Параметры.Вставить("path", Путь);
    КонецЕсли;

    Параметры.Вставить("public_key", URL);

    Ответ = OPI_ЗапросыHTTP.Get("https://cloud-api.yandex.net/v1/disk/public/resources/download", Параметры, Заголовки);

    Возврат Ответ;

КонецФункции

// Сохранить публичный объект на диск
// Сохраняет публичный объект на ваш диск
//
// Параметры:
//  Токен  - Строка - Токен                                               - token
//  URL    - Строка - Адрес объекта                                       - url
//  Откуда - Строка - Путь внутри публичного каталога (только для папок)  - from
//  Куда   - Строка - Путь сохранения файла                               - to
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Yandex
Функция СохранитьПубличныйОбъектНаДиск(Знач Токен, Знач URL, Откуда = "", Куда = "") Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(URL);
    OPI_ПреобразованиеТипов.ПолучитьСтроку(Откуда);
    OPI_ПреобразованиеТипов.ПолучитьСтроку(Куда);

    Заголовки = OPI_YandexID.ПолучитьЗаголовокАвторизации(Токен);
    Адрес     = "https://cloud-api.yandex.net/v1/disk/public/resources/save-to-disk";
    Href      = "href";

    Параметры = Новый Структура;
    Параметры.Вставить("public_key", КодироватьСтроку(URL, СпособКодированияСтроки.КодировкаURL));

    Если ЗначениеЗаполнено(Откуда) Тогда
        Параметры.Вставить("path", Откуда);
    КонецЕсли;

    Если ЗначениеЗаполнено(Куда) Тогда
        Параметры.Вставить("save_path", Куда);
    КонецЕсли;

    Параметры = OPI_Инструменты.ПараметрыЗапросаВСтроку(Параметры);
    Ответ     = OPI_ЗапросыHTTP.PostСТелом(Адрес + Параметры, , Заголовки, Ложь);

    URLОтвета = Ответ[Href];

    Если Не ЗначениеЗаполнено(URLОтвета) Тогда
        Возврат Ответ;
    КонецЕсли;

    Ответ = OPI_ЗапросыHTTP.Get(URLОтвета, , Заголовки);

    Возврат Ответ;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПереключениеОбщегоДоступа(Знач Токен, Знач Путь, Знач ОбщийДоступ)

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Путь);
    OPI_ПреобразованиеТипов.ПолучитьБулево(ОбщийДоступ);

    Заголовки  = OPI_YandexID.ПолучитьЗаголовокАвторизации(Токен);
    Назначение = ?(ОбщийДоступ, "publish", "unpublish");
    Href       = "href";

    URL = "https://cloud-api.yandex.net/v1/disk/resources/" + Назначение;

    Параметры = Новый Структура;
    Параметры.Вставить("path", Путь);

    Параметры = OPI_Инструменты.ПараметрыЗапросаВСтроку(Параметры);
    Ответ     = OPI_ЗапросыHTTP.PutСТелом(URL + Параметры, , Заголовки, Ложь);

    URLОтвета = Ответ[Href];

    Если Не ЗначениеЗаполнено(URLОтвета) Тогда
        Возврат Ответ;
    КонецЕсли;

    Ответ = OPI_ЗапросыHTTP.Get(URLОтвета, , Заголовки);

    Возврат Ответ;

КонецФункции

#КонецОбласти
