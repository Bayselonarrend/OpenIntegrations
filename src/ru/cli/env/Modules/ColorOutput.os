#Область ПрограммныйИнтерфейс

Процедура Сообщить(Знач ТекстСообщения = "", Знач пСтатусСообщения = Неопределено) Экспорт
	
	Если пСтатусСообщения = СтатусСообщения.Важное Тогда
		ЦветТекста = "Красный";
	ИначеЕсли пСтатусСообщения = СтатусСообщения.Внимание Тогда
		ЦветТекста = "Желтый";
	ИначеЕсли пСтатусСообщения = СтатусСообщения.Информация Тогда
		ЦветТекста = "Зеленый";
	ИначеЕсли пСтатусСообщения = СтатусСообщения.ОченьВажное Тогда
		ЦветТекста = "Красный";
	Иначе
		ЦветТекста = Неопределено;
	КонецЕсли;

	ВывестиСтроку(ТекстСообщения, ЦветТекста);

КонецПроцедуры

Процедура ВывестиСтроку(Знач ТекстСообщения = "", Знач ЦветТекста = Неопределено) Экспорт

	Вывести(ТекстСообщения, ЦветТекста);

	Консоль.Вывести(Символы.ПС);

КонецПроцедуры

Процедура Вывести(Знач ТекстСообщения, Знач ЦветТекста = Неопределено) Экспорт
	
	Если Врег(ЦветТекста) = "МОНОХРОМ" Тогда
		ЦветТекста = Неопределено;
		ЦветнойВыводРазрешен = Ложь;
	Иначе
		ЦветнойВыводРазрешен = Истина;
	КонецЕсли;

	ТаблицаПолей = РазобратьСтроку(ТекстСообщения, ЦветТекста);

	Если ЦветнойВыводРазрешен Тогда
		
		ПредыдущееЗначениеЦветаТекта = Консоль.ЦветТекста;
		Если НЕ ЗначениеЗаполнено(ПредыдущееЗначениеЦветаТекта) Тогда
			ПредыдущееЗначениеЦветаТекта = ЦветКонсоли.Белый;
		КонецЕсли;
		
		Если ЦветТекста = Неопределено Тогда
			ЦветТекста = ПредыдущееЗначениеЦветаТекта;
		КонецЕсли;
		Для Каждого Поле Из ТаблицаПолей Цикл
			Если ЗначениеЗаполнено(Поле.Цвет) Тогда
				Консоль.ЦветТекста = ЦветКонсоли[Поле.Цвет];
			Иначе
				Консоль.ЦветТекста = ПредыдущееЗначениеЦветаТекта;
			КонецЕсли;
			Консоль.Вывести(Поле.Текст);
		КонецЦикла;
		
		Консоль.ЦветТекста = ПредыдущееЗначениеЦветаТекта;
		
	Иначе
		
		МассивПолей = ТаблицаПолей.ВыгрузитьКолонку("Текст");
		ТекстСообщенияБезЦветныхПолей = СтрСоединить(МассивПолей);
		
		Консоль.Вывести(ТекстСообщенияБезЦветныхПолей);

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РазобратьСтроку(ТекстСообщения, ЦветТекста)

	Длина = СтрДлина(ТекстСообщения);
	
	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("Текст");
	Таблица.Колонки.Добавить("Цвет");

	тзЦветныеПоля = ЦветныеПоля(ТекстСообщения);

	Поз = 1;
	Для Каждого ЦветПоле Из тзЦветныеПоля Цикл
		Если Поз < ЦветПоле.Позиция Тогда
			Текст = Сред(ТекстСообщения, Поз, ЦветПоле.Позиция - Поз);
			Стр = Таблица.Добавить();
			Стр.Текст = Текст;
			Стр.Цвет = ЦветТекста;
		КонецЕсли;
		
		Стр = Таблица.Добавить();
		Стр.Текст = ЦветПоле.Текст;
		Стр.Цвет = ЦветПоле.Цвет;
		
		Поз = ЦветПоле.Позиция + ЦветПоле.Длина;
	КонецЦикла;

	Если Поз-1 <= Длина Тогда
		Текст = Сред(ТекстСообщения, Поз);
		Стр = Таблица.Добавить();
		Стр.Текст = Текст;
		Стр.Цвет = ЦветТекста;
	КонецЕсли;
	
	Возврат Таблица;
	
КонецФункции

Функция ЦветныеПоля(ТекстСообщения)
	
	РВ = Новый РегулярноеВыражение(ШаблонЦветнойПодстроки());
	Совпадения = РВ.НайтиСовпадения(ТекстСообщения);

	ЦветныеПоля = Новый ТаблицаЗначений;
	ЦветныеПоля.Колонки.Добавить("Позиция");
	ЦветныеПоля.Колонки.Добавить("Текст");
	ЦветныеПоля.Колонки.Добавить("Длина");
	ЦветныеПоля.Колонки.Добавить("Цвет");
	
	Для Каждого Совпадение Из Совпадения Цикл
		Группы = Совпадение.Группы;
		стр = ЦветныеПоля.Добавить();
		стр.Позиция = Совпадение.Индекс+1;
		стр.Текст = Группы[1].Значение;
		стр.Длина = Совпадение.Длина;
		ЦветИмя = Группы[2].Значение;
		стр.Цвет = ЦветИмя;
	КонецЦикла;
	
	ЦветныеПоля.Сортировать("Позиция");
	
	Возврат ЦветныеПоля;

КонецФункции

Функция ШаблонЦветнойПодстроки()
	
	Возврат "\((.+?)\|#color=(.+?)\)"; // (Текст|#color=Цвет)

КонецФункции

#КонецОбласти
#Region Alternate

Procedure Message(Val MessageText = "", Val pMessageStatus = Undefined) Export
    Сообщить(MessageText, pMessageStatus);
EndProcedure

Procedure WriteLine(Val MessageText = "", Val TextColor = Undefined) Export
    ВывестиСтроку(MessageText, TextColor);
EndProcedure

Procedure Write(Val MessageText, Val TextColor = Undefined) Export
    Вывести(MessageText, TextColor);
EndProcedure

#EndRegion