Перем ЦветнойВывод;
Перем ШиринаКонсоли;
Перем ИспользоватьАдаптивныйВывод;
Перем ОбъектОПИ;

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриСозданииОбъекта(ШаблонДоступа_, ОбъектОПИ_)

	ЦветнойВывод  = ЗагрузитьСценарий(СтрШаблон(ШаблонДоступа_, "env/Modules/ColorOutput.os"));
	ОбъектОПИ     = ОбъектОПИ_;

	Попытка
		ШиринаКонсоли = Консоль.Ширина;
		ИспользоватьАдаптивныйВывод = Истина;
	Исключение
		ИспользоватьАдаптивныйВывод = Ложь;
	КонецПопытки;

КонецПроцедуры

Процедура ВывестиНачальнуюСтраницу() Экспорт

	Версия                    = ОбъектОПИ.ПолучитьВерсию();
	СоответствиеКомандМодулей = ОбъектОПИ.ПолучитьСоответствиеКомандМодулей();
	МассивКоманд              = Новый Массив;
	
	Для Каждого Команда Из СоответствиеКомандМодулей Цикл
		МассивКоманд.Добавить(Команда.Ключ)
	КонецЦикла;

	СписокКоманд  = СтрСоединить(МассивКоманд, ", ");

	ДлинаОтступа       = 11;
	ОтступНовойСтроки  = "           ";

	Консоль.ЦветТекста = ЦветКонсоли.Желтый;
	ЦветнойВывод.Вывести(СтрШаблон("
		|
		|    _______ _____________  ___  _______
		|    __  __ ___/__  _/_ /  |  / /___  __/
		|    _  / / / __  /  __      / __  /   
		|    / /_/ / __/ /   _  /|  /  _  /    
		|    \____/  /___/   /_/ |_/   /_/     
		|                          
		|
		| Добро пожаловать в (OInt|#color=Белый) v (" + Версия + "|#color=Зеленый)!
		|
		| Структура вызова:
	    | 
		| (oint|#color=Белый) (<библиотека>|#color=Бирюза) (<метод>|#color=Бирюза) (--опция1|#color=Серый) (""|#color=Зеленый)(Значение|#color=Белый)(""|#color=Зеленый) (...|#color=Белый) (--опцияN|#color=Серый) (""|#color=Зеленый)(Значение|#color=Белый)(""|#color=Зеленый) 
		|
		| Вызов библиотеки без метода или метода без параметров возвращает справку
		| Список доступных библиотек: (%1|#color=Белый) 
		|
		| (Стандартные опции:|#color=Желтый)
		|
		|  (--help|#color=Зеленый)  (-%2|#color=Белый)
		|  (--debug|#color=Зеленый) (-%3|#color=Белый)
		|  (--out|#color=Зеленый)   (-%4|#color=Белый)
		|
		|  (Полную документацию можно найти по адресу:|#color=Желтый) (https://openintegrations.dev|#color=Бирюза)
		|"
		, СписокКоманд
		, ПолучитьОписаниеСПереносамиПоШирине("выводит справку по текущей команде или методу. Аналогично вызову команды без опций", ОтступНовойСтроки, ДлинаОтступа)
		, ПолучитьОписаниеСПереносамиПоШирине("флаг, отвечающий за предоставление более подробной информации при работе программы", ОтступНовойСтроки, ДлинаОтступа)
		, ПолучитьОписаниеСПереносамиПоШирине("путь к файлу сохранения результата (двоичных данных в частности)", ОтступНовойСтроки, ДлинаОтступа)));

	Консоль.ЦветТекста = ЦветКонсоли.Белый;

	ЗавершитьРаботу(0);
	
КонецПроцедуры

Процедура ВывестиСправкуПоМетодам(Знач Команда) Экспорт

	ДанныеКоманды  = ОбъектОПИ.ПолучитьИнформациюИндекса(Команда);
	ДанныеОбластей = ДанныеКоманды["regions"];

	Консоль.ЦветТекста = ЦветКонсоли.Белый;

	ШаблонОбласти = "   (■|#color=Желтый) (%1|#color=Бирюза)";
	ШаблонМетода  = "   (%1|#color=Желтый) %2";

	ЦветнойВывод.ВывестиСтроку(СтрШаблон("
	| (■|#color=Зеленый) Библиотека - (%1|#color=Бирюза)
	| (■|#color=Зеленый) Доступные методы: 
	|", Команда));

	Для каждого СтрокаОбласти Из ДанныеОбластей Цикл

		Первый         = Истина;
		ТекущаяОбласть = СтрокаОбласти["name"];
		МетодыОбласти  = СтрокаОбласти["methods"];

		ЦветнойВывод.ВывестиСтроку(СтрШаблон(ШаблонОбласти, ТекущаяОбласть));
		
		Счетчик = 0;
		Для Каждого МетодОбласти Из МетодыОбласти Цикл

			Последний = Счетчик = МетодыОбласти.ВГраница();

			Если Первый И Последний Тогда
				Метка = "└───";
			ИначеЕсли Первый Тогда
				Метка = "└─┬─";
			ИначеЕсли Последний Тогда
				Метка = "  └─";
			Иначе
				Метка = "  ├─";
			КонецЕсли;
			
			ЦветнойВывод.ВывестиСтроку(СтрШаблон(ШаблонМетода, Метка, МетодОбласти));

			Счетчик = Счетчик + 1;
			Первый  = Ложь;

		КонецЦикла;

	КонецЦикла;

	ЗавершитьРаботу(0);

КонецПроцедуры

Процедура ВывестиСправкуПоПараметрам(Знач Команда, Знач Метод) Экспорт 

	ДанныеМетода = ОбъектОПИ.ПолучитьИнформациюОМетоде(Команда, Метод);

	Если Не ЗначениеЗаполнено(ДанныеМетода) Тогда
		ВывестиСообщениеИсключения("Метод");
	КонецЕсли;

	ИмяМетода      = ДанныеМетода["name"];
	ОписаниеМетода = ДанныеМетода["description"];
	
	ПолноеОписаниеПараметров = ПолучитьПолноеОписаниеПараметров(ДанныеМетода);

	ТекстСправки = СтрШаблон("
	| (■|#color=Зеленый) Метод (%1|#color=Бирюза)
	| (■|#color=Зеленый) %2
	|
	|%3", ИмяМетода, ОписаниеМетода, ПолноеОписаниеПараметров); 

	ЦветнойВывод.ВывестиСтроку(ТекстСправки);

	ЗавершитьРаботу(0);
	
КонецПроцедуры

Процедура ВывестиСообщениеИсключения(Знач Причина, Знач ФайлВывода = "") Экспорт

	ФайлВывода = Строка(ФайлВывода);

	Если Причина = "Команда" Тогда
		Текст = "Некорректная команда! Проверьте правильность ввода";
		Код   = 1;

	ИначеЕсли Причина = "Метод" Тогда
		Текст = "Некорректный метод! Проверьте правильность ввода";
		Код   = 2;
		
	Иначе
		Текст = "Непредвиденная ошибка!: " + Причина;
		Код   = 99;
	КонецЕсли;

	Текст = СтрШаблон("
	|%1
	|", Текст);
	
	Сообщить(Текст, СтатусСообщения.ОченьВажное);

	Если ЗначениеЗаполнено(ФайлВывода) Тогда

		ТекстДД = ПолучитьДвоичныеДанныеИзСтроки(Текст);

		Попытка
			ТекстДД.Записать(ФайлВывода);
			Сообщить("Сообщение об ошибке сохранено в файл: " + ФайлВывода, СтатусСообщения.Внимание);
		Исключение
			Сообщить("Не удалось сохранить ошибку в файл вывода: " + ОписаниеОшибки(), СтатусСообщения.Внимание);
		КонецПопытки;

	КонецЕсли;

	ЗавершитьРаботу(Код);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьПолноеОписаниеПараметров(ДанныеМетода)

	МаксимальнаяДлина 	      = 0;
	СоответствиеСписковОпций  = Новый Соответствие();
	МассивПолныхОписаний      = Новый Массив;
	ШаблонОписанияПараметра   = "   (%1|#color=Желтый) -%2";
	ШаблонЗначенияПоУмолчанию = " (необяз. по ум. - %1)";
	ПараметрыМетода           = ДанныеМетода["params"];

	Для Каждого ПараметрМетода Из ПараметрыМетода Цикл

		ОпцияПолная = ПараметрМетода["name"];

		ОпцияСокращенная    = ПараметрМетода["short"];
		ЗначениеПоУмолчанию = ПараметрМетода["default"];

		ТекущийСписокОпций = ?(ЗначениеЗаполнено(ОпцияСокращенная)
			, СтрШаблон("%1, %2", ОпцияСокращенная, ОпцияПолная)
			, СтрШаблон("    %1", ОпцияПолная));

		ТекущаяДлина = СтрДлина(ТекущийСписокОпций);

		Если МаксимальнаяДлина < ТекущаяДлина Тогда
			МаксимальнаяДлина = ТекущаяДлина;
		КонецЕсли;

		СоответствиеСписковОпций.Вставить(ОпцияПолная, ТекущийСписокОпций);
		
	КонецЦикла;

	ДлинаОтступаНовойСтроки = МаксимальнаяДлина + 4;
	ОтступНовойСтроки       = "";
	
	Для Н = 0 По ДлинаОтступаНовойСтроки Цикл
		ОтступНовойСтроки = ОтступНовойСтроки + " ";
	КонецЦикла;

	Для Каждого ПараметрМетода Из ПараметрыМетода Цикл
			
		ТекущийСписокОпций          = СоответствиеСписковОпций.Получить(ПараметрМетода["name"]);
		ТекущееОписание             = ПараметрМетода["description"];
		ТекущееЗначениеПоУмолчанию  = ПараметрМетода["default"];

		Если ТекущееЗначениеПоУмолчанию <> Неопределено Тогда
			ОписаниеЗначенияПоУмолчанию = СтрШаблон(ШаблонЗначенияПоУмолчанию, ТекущееЗначениеПоУмолчанию);
		Иначе
			ОписаниеЗначенияПоУмолчанию = "";
		КонецЕсли;

		Пока СтрДлина(ТекущийСписокОпций) < МаксимальнаяДлина Цикл
			ТекущийСписокОпций = ТекущийСписокОпций + " ";
		КонецЦикла;

		ТекстОписания   = СтрШаблон("%1%2", ПараметрМетода["description"], ОписаниеЗначенияПоУмолчанию);
		МассивОписания  = СтрРазделить(ТекстОписания, Символы.ПС);
		ТекущееОписание = Неопределено;
		
		Если ИспользоватьАдаптивныйВывод Тогда
			ТекущееОписание = ПолучитьОписаниеСПереносамиПоШирине(МассивОписания, ОтступНовойСтроки, ДлинаОтступаНовойСтроки);
		КонецЕсли;

		Если ТекущееОписание = Неопределено Тогда

			Если МассивОписания.Количество() = 1 Тогда
				ТекущееОписание = МассивОписания[0];
			Иначе
				ТекущееОписание = СтрСоединить(МассивОписания, Символы.ПС + ОтступНовойСтроки);
			КонецЕсли;

		КонецЕсли;

		ТекущееПолноеОписание = СтрШаблон(ШаблонОписанияПараметра, ТекущийСписокОпций, ТекущееОписание);
		МассивПолныхОписаний.Добавить(ТекущееПолноеОписание);

	КонецЦикла;

	ПолноеОписаниеПараметров = СтрСоединить(МассивПолныхОписаний, Символы.ПС);

	Возврат ПолноеОписаниеПараметров;

КонецФункции

Функция ПолучитьОписаниеСПереносамиПоШирине(МассивСтрокОписания, ОтступНовойСтроки, ДлинаОтступа)

	Если Не ТипЗнч(МассивСтрокОписания) = Тип("Массив") Тогда
		МассивСтрокОписания_ = Новый Массив;
		МассивСтрокОписания_.Добавить(МассивСтрокОписания);
		МассивСтрокОписания = МассивСтрокОписания_;
	КонецЕсли;

	ДопустимаяДлинаСтроки = ШиринаКонсоли - ДлинаОтступа - 4;
	ТретьДопустимой       = Окр(ДопустимаяДлинаСтроки / 3);

	Если ДопустимаяДлинаСтроки < 0 Тогда
		Возврат Неопределено;
	КонецЕсли;

	МассивРазделенный = Новый Массив;

	Для Каждого СтрокаОписания Из МассивСтрокОписания Цикл

		МассивСлов    = СтрРазделить(СокрЛП(СтрокаОписания), " ");
		ТекущаяСтрока = "";

		Н = 0;

		Пока Н <= МассивСлов.ВГраница() Цикл

			Слово       = МассивСлов[Н];		
			НоваяСтрока = СтрШаблон("%1 %2", ТекущаяСтрока, Слово);

			Если ДопустимаяДлинаСтроки >= СтрДлина(НоваяСтрока) Тогда
				ТекущаяСтрока = НоваяСтрока;
			Иначе

				Если ТекущаяСтрока = "" Или СтрДлина(Слово) > ТретьДопустимой Тогда

					ЧислоСимволов = ДопустимаяДлинаСтроки - СтрДлина(ТекущаяСтрока) - 1;
					ЛеваяЧасть    = Лев(Слово, ЧислоСимволов);
					ТекущаяСтрока = СтрШаблон("%1%2-", ТекущаяСтрока, ЛеваяЧасть);
					МассивСлов[Н] = Прав(Слово, СтрДлина(Слово) - СтрДлина(ЛеваяЧасть));
				
				КонецЕсли;

				МассивРазделенный.Добавить(ТекущаяСтрока);
				ТекущаяСтрока = "";
				Продолжить;

			КонецЕсли;

			Н = Н + 1;

		КонецЦикла;		

		Если ТекущаяСтрока <> "" Тогда
			МассивРазделенный.Добавить(ТекущаяСтрока);
		КонецЕсли;
		
	КонецЦикла;

	ПолноеОписание = СтрСоединить(МассивРазделенный, Символы.ПС + ОтступНовойСтроки);

	Возврат ПолноеОписание;

КонецФункции

#КонецОбласти
