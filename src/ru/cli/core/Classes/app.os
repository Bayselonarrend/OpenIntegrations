Перем Отладка;           // Флаг вывода отладочной информации
Перем Тестирование;      // Флаг отключения отправки данных после обработки

Перем Парсер;            // Объект парсера входящих данных 
Перем ОбъектОПИ;         // Объект работы с методами ОПИ
Перем Справка;           // Объект вывода справочной информации

Перем ФайлВывода;        // Путь перенаправления вывода в файл
Перем ТекущаяКоманда;    // Имя текущей команды
Перем ТекущийМетод;
Перем ТекущийИндекс;

Перем ШаблонOint;     
Перем КаталогПакетов;
Перем ШаблонДоступа;
Перем Исполнитель;       

#Область СлужебныеПроцедурыИФункции

#Область Основные

Процедура ОсновнойОбработчик()
	
	Отладка        = Истина;
	Тестирование   = Ложь;

	УстановитьПеременнуюСреды("OINT_CLI", "YES");

	ОпределитьШаблоныПутей();
	
	Парсер    = ЗагрузитьСценарий(СтрШаблон(ШаблонДоступа, "env/Classes/CommandLineArgumentParser.os"));
	ОбъектОПИ = ЗагрузитьСценарий(СтрШаблон(ШаблонДоступа, "data/Classes/LibraryComposition.os"));
	ОбъектОПИ.УстановитьКаталогПакетов(КаталогПакетов);

	ПодключитьСценарий(СтрШаблон(ШаблонДоступа, "help/Classes/Help.os"), "Справка");
	Справка = Новый Справка(ШаблонДоступа, ОбъектОПИ);
	
	ОпределитьТекущуюКомандуМетод();
	СформироватьНаборПараметров();

	Результат = Парсер.Разобрать(АргументыКоманднойСтроки);
	ВыполнитьОбработкуКоманды(Результат);

КонецПроцедуры

Процедура ОпределитьТекущуюКомандуМетод()

	ЧислоАргументов = АргументыКоманднойСтроки.Количество();

	Если ЧислоАргументов > 0 Тогда
		ТекущаяКоманда = АргументыКоманднойСтроки[0];
	Иначе
		ТекущаяКоманда = Неопределено;
	КонецЕсли;

	Если ЧислоАргументов > 1 Тогда
		ТекущийМетод = АргументыКоманднойСтроки[1];
	Иначе
		ТекущийМетод = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьНаборПараметров()

	Если ТекущаяКоманда = Неопределено Тогда
		Справка.ВывестиНачальнуюСтраницу();
		Возврат;
	КонецЕсли;
	
	Команда = Парсер.ОписаниеКоманды(ТекущаяКоманда);

	Парсер.ДобавитьПозиционныйПараметрКоманды(Команда, "Метод");
	
	ДобавитьПараметрыМетода(Команда, Парсер);

	Парсер.ДобавитьПараметрФлагКоманды(Команда, "--help");
	Парсер.ДобавитьПараметрФлагКоманды(Команда, "--debug");
	Парсер.ДобавитьПараметрФлагКоманды(Команда, "--test");
	Парсер.ДобавитьИменованныйПараметрКоманды(Команда, "--out");

	Парсер.ДобавитьКоманду(Команда);

КонецПроцедуры

Процедура ВыполнитьОбработкуКоманды(Знач Данные)
	
	ТекущаяКоманда = Данные["Команда"];
	Параметры      = Данные["ЗначенияПараметров"];
	Вывод		   = "";

	УстановитьРежимОтладки(Параметры);
	УстановитьРежимТеста(Параметры);
	УстановитьФайлВывода(Параметры);
	ВывестиДопИнформацию(Параметры);

	Попытка
			
		Вывод = ПолучитьРезультатОбработки(ТекущаяКоманда, Параметры);

		ОбработатьВыводJSON(Вывод);
		СообщитьРезультат(Вывод, СтатусСообщения.Внимание);

	Исключение
		ОбработатьОшибочныйВывод(Вывод, ИнформацияОбОшибке());
	КонецПопытки;
	
КонецПроцедуры

Функция ПолучитьРезультатОбработки(Знач Команда, Знач Параметры)

	Если ТекущаяКоманда = "hashsum" Тогда

		ПодключитьИсполнитель();
		Возврат Исполнитель.ПолучитьХешСуммуПоследнейСборки();

	КонецЕсли;

	Метод     = СокрЛП(Параметры["Метод"]);
	Ответ     = "Функция вернула пустое значение";

	ЧислоСтандартныхПараметров = 4;

	Если Не ЗначениеЗаполнено(Метод) Или Метод = "--help" Тогда
		Справка.ВывестиСправкуПоМетодам(Команда);
	КонецЕсли;

	Если Параметры.Количество() = ЧислоСтандартныхПараметров Или Параметры["--help"] Тогда
		Справка.ВывестиСправкуПоПараметрам(Команда, Метод);
	КонецЕсли;

	СтруктураВыполнения = ОбъектОПИ.СформироватьСтрокуВызоваМетода(Параметры, Команда, Метод);

	Если СтруктураВыполнения["Ошибка"] Тогда
		Справка.ВывестиСообщениеИсключения(СтруктураВыполнения["Результат"], ФайлВывода);
	Иначе
		ТекстВыполнения = СтруктураВыполнения["Результат"];
	КонецЕсли;

	Если Отладка Или Тестирование Тогда
		Сообщить(ТекстВыполнения, СтатусСообщения.Внимание);
	КонецЕсли;

	Если Не Тестирование Тогда
		ПодключитьИсполнитель();
		Исполнитель.ВыполнитьСкрипт(ТекстВыполнения, Ответ);
	КонецЕсли;

	Возврат Ответ;
	
КонецФункции

#КонецОбласти

#Область Вспомогательные

Процедура ДобавитьПараметрыМетода(Команда, Парсер)
	
	Если Не ЗначениеЗаполнено(ТекущийМетод) Тогда
		Возврат	
	КонецЕсли;

	ДанныеМетода     = ОбъектОПИ.ПолучитьИнформациюОМетоде(ТекущаяКоманда, ТекущийМетод);
	
	Если Не ЗначениеЗаполнено(ДанныеМетода) Тогда
		Справка.ВывестиСообщениеИсключения("Метод", ФайлВывода);
	КонецЕсли;

	ПараметрыМетода = ДанныеМетода["params"];

	Для Каждого Параметр Из ПараметрыМетода Цикл

		ИмяПолное = Параметр["name"];

		Если ИмяПолное = "--out" Тогда
			Продолжить;
		КонецЕсли;

		ИмяСокращенное = Параметр["short"];

		Парсер.ДобавитьИменованныйПараметрКоманды(Команда, ИмяПолное);
		Парсер.ДобавитьИменованныйПараметрКоманды(Команда, ИмяСокращенное);

	КонецЦикла;

КонецПроцедуры

Процедура ОпределитьШаблоныПутей()

	ТекущийКаталог = ТекущийСценарий().Каталог;
	ТекущийКаталог = СтрЗаменить(ТекущийКаталог, "\", "/");

	ЧастиПути      = СтрРазделить(ТекущийКаталог, "/");
	ЧастиПути.Удалить(ЧастиПути.ВГраница());
	ЧастиПути.Удалить(ЧастиПути.ВГраница());

	ШаблонДоступа = СтрСоединить(ЧастиПути, "/") + "/%1";

	ЧастиПути.Удалить(ЧастиПути.ВГраница());

	КаталогПакетов = СтрСоединить(ЧастиПути, "/");

КонецПроцедуры

Процедура ОбработатьВыводJSON(Вывод)
	
	Если ПустойВывод(Вывод) Тогда
		Вывод = Новый Структура;
	КонецЕсли;

	Если ТипЗнч(Вывод) = Тип("Структура")
		Или ТипЗнч(Вывод) = Тип("Соответствие")
		Или ТипЗнч(Вывод) = Тип("Массив") Тогда
	
		Вывод = JSONСтрокой(Вывод);

	КонецЕсли;

КонецПроцедуры

Процедура УстановитьРежимОтладки(Знач Параметры)

	Если Параметры["--debug"] Тогда
		Отладка         = Истина;
		ПеременнаяСреды = "YES";
	Иначе
		Отладка         = Ложь;
		ПеременнаяСреды = "NO"
	КонецЕсли;

	УстановитьПеременнуюСреды("OINT_DEBUG", ПеременнаяСреды);

КонецПроцедуры

Процедура УстановитьРежимТеста(Знач Параметры)

	Если Параметры["--test"] Тогда
		Тестирование = Истина;
	Иначе
		Тестирование = Ложь;
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьФайлВывода(Знач Параметры)

	Вывод = Параметры["--out"];

	Если ЗначениеЗаполнено(Вывод) Тогда
		ФайлВывода = Вывод;
	КонецЕсли;

КонецПроцедуры

Процедура ВывестиДопИнформацию(Параметры)

	Если Отладка Или Тестирование Тогда

		Для каждого ВводныйПараметр Из Параметры Цикл
			Сообщить(ВводныйПараметр.Ключ + " : " + ВводныйПараметр.Значение);	
		КонецЦикла;

    КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОшибочныйВывод(Вывод, ИнформацияОбОшибке)

	Информация = "";
	Если ЗначениеЗаполнено(Вывод) Тогда

		Если Отладка Или Тестирование Тогда
			Информация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		КонецЕсли;

		СообщитьРезультат(Вывод);
	Иначе

		Если Отладка Или Тестирование Тогда
			Информация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		Иначе
			Информация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		КонецЕсли;
	
	КонецЕсли;
	
	Справка.ВывестиСообщениеИсключения(Информация, ФайлВывода);
	
КонецПроцедуры

Процедура СообщитьРезультат(Знач Текст, Знач Статус = "")

	Если Не ЗначениеЗаполнено(Статус) Тогда
		Статус = СтатусСообщения.БезСтатуса;
	КонецЕсли;

	Если ЗначениеЗаполнено(ФайлВывода) Тогда
		Текст = ЗаписатьЗначениеВФайл(Текст, ФайлВывода);
	ИначеЕсли ТипЗнч(Текст) = Тип("ДвоичныеДанные") Тогда
		Текст = "Похоже, что в ответе пришли двоичные данные! "
		    + "В следующий раз используйте опцию --out для указания пути их сохранения";
		Статус = СтатусСообщения.Информация;
	Иначе 
		Текст = Строка(Текст);
	КонецЕсли;

    Сообщить(Текст, Статус);
	
КонецПроцедуры

Процедура ПодключитьИсполнитель()

	Если Исполнитель = Неопределено Тогда

		ТекущийКаталогСкрипта = СтрЗаменить(ТекущийСценарий().Каталог, "\", "/");

		ПутьИсполнителя  = СтрШаблон("%1/%2"
			, ТекущийКаталогСкрипта
			, "internal/Modules/Executor.os");

		Исполнитель  = ЗагрузитьСценарий(ПутьИсполнителя);

	КонецЕсли;

КонецПроцедуры

Функция ЗаписатьЗначениеВФайл(Знач Значение, Знач Путь)
	
	СтандартнаяЕдиница = 1024;
	ЕдиницаДанных      = СтандартнаяЕдиница * СтандартнаяЕдиница;
	Значение           = ?(ТипЗнч(Значение) = Тип("ДвоичныеДанные"), Значение, Строка(Значение));

	Если ТипЗнч(Значение) = Тип("Строка") Тогда 

		ВозможныйФайл = Новый Файл(Значение);

		Если ВозможныйФайл.Существует() Тогда
			Путь = Значение;
		Иначе
			Значение = ПолучитьДвоичныеДанныеИзСтроки(Значение);
	    КонецЕсли;

	КонецЕсли;

	Если ТипЗнч(Значение) = Тип("ДвоичныеДанные") Тогда
        Значение.Записать(Путь);
	КонецЕсли;

	ЗаписанныйФайл = Новый Файл(Путь);

	Если ЗаписанныйФайл.Существует() Тогда
		Возврат "Файл размером " 
		    + Строка(Окр(ЗаписанныйФайл.Размер() / ЕдиницаДанных, 3)) 
			+ " МБ был записан в " 
			+ ЗаписанныйФайл.ПолноеИмя;
	Иначе
		ВызватьИсключение "Файл не был записан! Используйте флаг --debug для получения дополнительной информации";
	КонецЕсли;

КонецФункции

Функция ПустойВывод(Вывод)

	Если ТипЗнч(Вывод) = Тип("ДвоичныеДанные") Тогда
		Возврат Вывод.Размер() = 0;
	Иначе
		Возврат Не ЗначениеЗаполнено(Вывод);
	КонецЕсли;
	
КонецФункции

Функция JSONСтрокой(Знач Данные)

    Перенос = ПереносСтрокJSON.Windows;

    ПараметрыJSON = Новый ПараметрыЗаписиJSON(Перенос
        , " "
        , Ложь
        , ЭкранированиеСимволовJSON["Нет"]
        , Ложь
        , Ложь
        , Ложь
        , Ложь);

    Попытка

        ЗаписьJSON = Новый ЗаписьJSON;
        ЗаписьJSON.УстановитьСтроку(ПараметрыJSON);

        ЗаписатьJSON(ЗаписьJSON, Данные);
        Возврат ЗаписьJSON.Закрыть();

    Исключение
        Возврат "НЕ JSON: " + Строка(Данные);
    КонецПопытки;

КонецФункции

#КонецОбласти

#КонецОбласти

Попытка
	ОсновнойОбработчик();	
Исключение
	
	Если Отладка Тогда
		Информация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	Иначе
		Информация = ИнформацияОбОшибке().Описание;
	КонецЕсли;

	Попытка
		Справка.ВывестиСообщениеИсключения(Информация, ФайлВывода);	
	Исключение
		ВызватьИсключение Информация
	КонецПопытки;

КонецПопытки;
