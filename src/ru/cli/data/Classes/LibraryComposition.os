Перем СоответствиеКомандМодулей;
Перем Версия;
Перем КэшированиеИндексов;
Перем ТекущийКаталог;
Перем КаталогПакетов;


Процедура ПриСозданииОбъекта()
    
    ТекущийКаталог = ТекущийСценарий().Каталог;
    ИнициализироватьОсновныеСписки();  
    
КонецПроцедуры

Процедура ИнициализироватьОсновныеСписки() Экспорт
    
    ФайлДанных = ОбъединитьПути(ТекущийКаталог, "index", "lib.json");  
    Данные     = ПрочитатьJSONФайл(ФайлДанных);
    
    КэшированиеИндексов       = Новый Соответствие();
    СоответствиеКомандМодулей = Данные["modules"];
    Версия                    = Данные["version"];
    
КонецПроцедуры

Функция ПолучитьВерсию() Экспорт
    Возврат Версия;
КонецФункции

Функция ПолучитьСоответствиеКомандМодулей() Экспорт
    Возврат СоответствиеКомандМодулей;
КонецФункции

Функция ПолучитьИнформациюИндекса(Знач Команда) Экспорт
    
    ИнформацияИндекса = КэшированиеИндексов.Получить(Команда);
    
    Если ИнформацияИндекса = Неопределено Тогда
        
        Попытка
            
            ПутьИндекса       = ОбъединитьПути(ТекущийКаталог, "index", Команда + ".json");
            ИнформацияИндекса = ПрочитатьJSONФайл(ПутьИндекса);        
            
            КэшированиеИндексов.Вставить(Команда, ИнформацияИндекса);
            
        Исключение
            ВызватьИсключение СтрШаблон("Некорректное имя команды: %1", Команда)
        КонецПопытки;
        
    КонецЕсли;
    
    Возврат ИнформацияИндекса;
    
КонецФункции

Функция ПолучитьИнформациюОМетоде(Знач Команда, Знач Метод) Экспорт
    
    ИнформацияМетода = Неопределено;
    ТекущийИндекс    = ПолучитьИнформациюИндекса(Команда);
    МетодПоиска      = вРег(Метод);
    
    Для Каждого МетодИндекса Из ТекущийИндекс["methods"] Цикл
        Если МетодИндекса["id"] = МетодПоиска Тогда
            ИнформацияМетода = МетодИндекса;
            Прервать;
        КонецЕсли;
    КонецЦикла;
    
    Возврат ИнформацияМетода;
    
КонецФункции

Функция ПолучитьПолныйСостав() Экспорт
    
    Для Каждого Команда Из СоответствиеКомандМодулей Цикл
        ПолучитьИнформациюИндекса(Команда.Ключ);
    КонецЦикла;
    
    Возврат КэшированиеИндексов;
    
КонецФункции

Функция СформироватьСтрокуВызоваМетода(Знач ПереданныеПараметры, Знач Команда, Знач Метод, Знач Динамически = Истина) Экспорт
    
    Если КаталогПакетов = Неопределено Тогда
        ОпределитьКаталогПакетов();
    КонецЕсли;
    
    Модуль = СоответствиеКомандМодулей.Получить(Команда);
    
    Если Не ЗначениеЗаполнено(Модуль) Тогда
        Возврат Новый Структура("Ошибка,Результат", Истина, "Команда");
    КонецЕсли;
    
    ДанныеКоманды = ПолучитьИнформациюИндекса(Команда);
    ДанныеМетода  = ПолучитьИнформациюОМетоде(Команда, Метод);
    
    Если ДанныеМетода = Неопределено Тогда
        Возврат Новый Структура("Ошибка,Результат", Истина, "Метод");
    КонецЕсли;
    
    МассивСтрок      = Новый Массив;
    МассивВызова     = Новый Массив;
    ТекстВыполнения  = "";
    
    ПараметрыМетода  = ДанныеМетода["params"];
    ШаблонПеременной = "%1 = ""%2"";";
    ШаблонУП         = "OPI_Инструменты.ЗаменитьУправляющиеПоследовательности(%1);";
    
    Если Динамически Тогда
        СтрокаИнициализации = СформироватьСтрокуИнициализацииМодуля(ДанныеКоманды, Модуль);
        МассивСтрок.Добавить(СтрокаИнициализации);
    КонецЕсли;
      
    Для Каждого НеобходимыйПараметр Из ПараметрыМетода Цикл
        
        ИмяПараметра = НеобходимыйПараметр["name"];

        ИмяПараметраСокр  = НеобходимыйПараметр["short"];
        ЗначениеПараметра = ПереданныеПараметры.Получить(ИмяПараметра);
        ЗначениеПараметра = ?(ЗначениеПараметра = Неопределено
            , ПереданныеПараметры.Получить(ИмяПараметраСокр)
            , ЗначениеПараметра);
        
        Если ЗначениеЗаполнено(ЗначениеПараметра) Тогда
            
            ИмяПараметра      = СтрШаблон("Параметр%1", СтрЗаменить(ИмяПараметра, "--", "_"));
            ОбернутоеЗначение = СтрЗаменить(ЗначениеПараметра, """", """""");
            
            МассивСтрок.Добавить(СтрШаблон(ШаблонПеременной, ИмяПараметра, ОбернутоеЗначение));
            
            Если ТребуетсяОбработкаУправляющихПоследовательностей(ИмяПараметра, ЗначениеПараметра) Тогда
                МассивСтрок.Добавить(СтрШаблон(ШаблонУП, ИмяПараметра));
            КонецЕсли;
            
            МассивВызова.Добавить(ИмяПараметра);
            
        Иначе
            МассивВызова.Добавить("");
        КонецЕсли;
        
    КонецЦикла;
    
    СтрокаВызова     = СтрШаблон("Ответ = %1.%2(%3)", Модуль, Метод, СтрСоединить(МассивВызова, ","));
    МассивСтрок.Добавить(СтрокаВызова);
    
    ТекстВыполнения = СтрСоединить(МассивСтрок, Символы.ПС);
    
    СтруктураВозврата = Новый Структура("Ошибка,Результат", Ложь, ТекстВыполнения);
    
    Возврат СтруктураВозврата;
    
КонецФункции

Процедура ДополнитьКэшСостава(Знач Библиотека, Знач ТаблицаПараметров, Команда = "") Экспорт
    
    Команда           = ?(ЗначениеЗаполнено(Команда), Команда, Библиотека);
    
    КэшированиеИндексов.Вставить(Команда, ТаблицаПараметров);
    СоответствиеКомандМодулей.Вставить(Команда, Библиотека);
    
КонецПроцедуры

Процедура УстановитьКаталогПакетов(Знач Путь) Экспорт
    КаталогПакетов = Путь;
КонецПроцедуры

Процедура ОпределитьКаталогПакетов()
    
    ТекущийКаталог = ТекущийСценарий().Каталог;
    ТекущийКаталог = СтрЗаменить(ТекущийКаталог, "\", "/");
    
    ЧастиПути      = СтрРазделить(ТекущийКаталог, "/");
    ЧастиПути.Удалить(ЧастиПути.ВГраница());
    ЧастиПути.Удалить(ЧастиПути.ВГраница());
    ЧастиПути.Удалить(ЧастиПути.ВГраница());
    
    КаталогПакетов = СтрСоединить(ЧастиПути, "/");
    
КонецПроцедуры

Функция ТребуетсяОбработкаУправляющихПоследовательностей(Знач ИмяПараметра, Знач ЗначениеПараметра)
    
    ФайлПараметра         = Новый Файл(ЗначениеПараметра);
    ЗначениеПараметраСокр = СокрЛП(ЗначениеПараметра);
    
    Возврат Не СтрНачинаетсяС(ЗначениеПараметраСокр, "{")
    И Не СтрНачинаетсяС(ЗначениеПараметраСокр, "[") 
    И Не ФайлПараметра.Существует() 
    И Не ИмяПараметра = "Параметр_out";
    
КонецФункции

Функция СформироватьСтрокуИнициализацииМодуля(Знач ДанныеКоманды, Знач Модуль)
    
    ШаблонЗагрузки  = СтрШаблон("%%1 = ЗагрузитьСценарий(""%1/oint/api/Modules/%%1.os"", Контекст);", КаталогПакетов);
    ШаблонКонтекста = "Контекст.Вставить(""%1"", %2);";
    МассивВызова    = Новый Массив;

    МассивВызова.Добавить("Контекст = Новый Структура;");

    Зависимости = ДанныеКоманды["dependencies"];

    Если Зависимости <> Неопределено Тогда

        Для Каждого Зависимость Из Зависимости Цикл

            МассивВызова.Добавить(СтрШаблон(ШаблонЗагрузки, Зависимость));
            МассивВызова.Добавить(СтрШаблон(ШаблонКонтекста, Зависимость, Зависимость));

        КонецЦикла;

    КонецЕсли;
    
    МассивВызова.Добавить(СтрШаблон(ШаблонКонтекста, Модуль, "Неопределено"));
    МассивВызова.Добавить(СтрШаблон(ШаблонЗагрузки , Модуль));
    
    Если ДанныеКоманды["self_depend"] Тогда
        
        МассивВызова.Добавить(СтрШаблон(ШаблонКонтекста, Модуль, Модуль));
        МассивВызова.Добавить(СтрШаблон(ШаблонЗагрузки , Модуль));
        
    КонецЕсли;
    
    ТекстВыполнения = СтрСоединить(МассивВызова, Символы.ПС);

    Возврат ТекстВыполнения;
    
КонецФункции

Функция ПрочитатьJSONФайл(Знач Путь)
    
    ЧтениеJSON = Новый ЧтениеJSON();
    ЧтениеJSON.ОткрытьФайл(Путь);
    Данные = ПрочитатьJSON(ЧтениеJSON, Истина);
    ЧтениеJSON.Закрыть();
    
    Возврат Данные;
    
КонецФункции
#Region Alternate

Procedure InitializeCommonLists() Export
    ИнициализироватьОсновныеСписки();
EndProcedure

Function GetVersion() Export
    Return ПолучитьВерсию();
EndFunction

Function GetCommandModuleMapping() Export
    Return ПолучитьСоответствиеКомандМодулей();
EndFunction

Function GetIndexData(Val Command) Export
    Return ПолучитьИнформациюИндекса(Command);
EndFunction

Function GetMethodData(Val Command, Val Method) Export
    Return ПолучитьИнформациюОМетоде(Command, Method);
EndFunction

Function GetFullComposition() Export
    Return ПолучитьПолныйСостав();
EndFunction

Function FormMethodCallString(Val PassedParameters, Val Command, Val Method, Val Dynamically = True) Export
    Return СформироватьСтрокуВызоваМетода(PassedParameters, Command, Method, Dynamically);
EndFunction

Procedure CompleteCompositionCache(Val Library, Val ParametersTable, Command = "") Export
    ДополнитьКэшСостава(Library, ParametersTable, Command);
EndProcedure

Procedure SetPackagesDirectory(Val Path) Export
    УстановитьКаталогПакетов(Path);
EndProcedure

#EndRegion