// OneScript: ./OInt/core/Modules/OPI_MongoDB.os
// Lib: MongoDB
// CLI: mongodb
// Keywords: mongodb, mongo, nosql

// MIT License

// Copyright (c) 2023-2025 Anton Tsitavets

// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:

// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.

// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

// https://github.com/Bayselonarrend/OpenIntegrations

// BSLLS:Typo-off
// BSLLS:LatinAndCyrillicSymbolInWord-off
// BSLLS:IncorrectLineBreak-off
// BSLLS:NumberOfOptionalParams-off
// BSLLS:UsingServiceTag-off
// BSLLS:LineLength-off
// BSLLS:UsingSynchronousCalls-off

//@skip-check module-structure-top-region
//@skip-check module-structure-method-in-regions
//@skip-check wrong-string-literal-content
//@skip-check method-too-many-params
//@skip-check constructor-function-return-section
//@skip-check doc-comment-collection-item-type

#Область ПрограммныйИнтерфейс

#Область ОсновныеМетоды

// Открыть соединение !NOCLI
// Создает подключение к указанной базе
//
// Параметры:
//  СтрокаПодключения - Строка - Строка подключения - string
//
// Возвращаемое значение:
//  Произвольный - Объект коннектора или структура с информацией об ошибке
Функция ОткрытьСоединение(Знач СтрокаПодключения) Экспорт

    Если ЭтоКоннектор(СтрокаПодключения) Тогда
        Возврат СтрокаПодключения;
    КонецЕсли;

    OPI_ПреобразованиеТипов.ПолучитьСтроку(СтрокаПодключения);
    OPI_Инструменты.ВернутьУправляющиеПоследовательности(СтрокаПодключения);

    Коннектор = OPI_Компоненты.ПолучитьКомпоненту("MongoDB");

    Коннектор.ConnectionString = СтрокаПодключения;

    Результат = Коннектор.Connect();
    Результат = OPI_Инструменты.JsonВСтруктуру(Результат, Ложь);

    Возврат ?(Результат["result"], Коннектор, Результат);

КонецФункции

// Закрыть соединение !NOCLI
// Явно закрывает переданное соединение
//
// Параметры:
//  Соединение - Произвольный - Объект компоненты с открытым соединением - dbc
//
// Возвращаемое значение:
//  Структура Из КлючИЗначение - Результат закрытия соединения
Функция ЗакрытьСоединение(Знач Соединение) Экспорт

    Если ЭтоКоннектор(Соединение) Тогда

        Результат = Соединение.Disconnect();
        Результат = OPI_Инструменты.JsonВСтруктуру(Результат, Ложь);

    Иначе

        Результат = Новый Структура("result,error", Ложь, "It's not a connection");

    КонецЕсли;

    Возврат Результат;

КонецФункции

// Это коннектор !NOCLI
// Проверяет, что значение является объектом внешней компоненты MSSQL
//
// Параметры:
//  Значение - Произвольный - Значение для проверки - value
//
// Возвращаемое значение:
//  Булево -  Это коннектор
Функция ЭтоКоннектор(Знач Значение) Экспорт

    Возврат Строка(ТипЗнч(Значение)) = "AddIn.OPI_MongoDB.Main";

КонецФункции

// Сформировать строку подключения
// Формирует строку для подключения к серверу MongoDB
//
// Параметры:
//  Адрес     - Строка                                   - IP адрес с портом или доменное имя для подключения - addr
//  База      - Строка                                   - База для подключения, если необходимо              - db
//  Логин     - Строка                                   - Имя пользователя авторизации                       - usr
//  Пароль    - Строка                                   - Пароль пользователя для авторизации                - pwd
//  Параметры - Структура Из КлючИЗначение, Неопределено - Дополнительные параметры подключения               - params
//
// Возвращаемое значение:
//  Строка - Строка подключения MongoDB
Функция СформироватьСтрокуПодключения(Знач Адрес
    , Знач База = ""
    , Знач Логин = ""
    , Знач Пароль = ""
    , Знач Параметры = Неопределено) Экспорт


    OPI_ПреобразованиеТипов.ПолучитьСтроку(Адрес);
    OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    OPI_ПреобразованиеТипов.ПолучитьСтроку(Логин);
    OPI_ПреобразованиеТипов.ПолучитьСтроку(Пароль);

    Если ЗначениеЗаполнено(Параметры) Тогда
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Параметры);
    КонецЕсли;

    ШаблонПодключения = "mongodb://%1";

    Если ЗначениеЗаполнено(Логин) Тогда
        Авторизация = СтрШаблон("%1:%2", Логин, Пароль);
    Иначе
        Авторизация = "";
    КонецЕсли;

    Если ЗначениеЗаполнено(Авторизация) Тогда
        ОсновнаяЧасть = СтрШаблон("%1@%2", Авторизация, Адрес);
    Иначе
        ОсновнаяЧасть = Адрес;
    КонецЕсли;

    СтрокаПодключения = СтрШаблон(ШаблонПодключения, ОсновнаяЧасть);

    Если ЗначениеЗаполнено(База) Или ЗначениеЗаполнено(Параметры) Тогда

        СтрокаПодключения = СтрокаПодключения + "/";

        Если ЗначениеЗаполнено(База) Тогда
            СтрокаПодключения = СтрокаПодключения + База;
        КонецЕсли;

        Если ЗначениеЗаполнено(Параметры) Тогда

            СтрокаПодключения = СтрокаПодключения + "?";
            МассивПараметров  = Новый Массив;

            Для Каждого Параметр Из Параметры Цикл
                МассивПараметров.Добавить(СтрШаблон("%1=%2", Параметр.Ключ, Параметр.Значение));
            КонецЦикла;

            СтрокаПодключения = СтрокаПодключения + СтрСоединить(МассивПараметров, "&");

        КонецЕсли;

    КонецЕсли;

    Возврат СтрокаПодключения;

КонецФункции

// Выполнить команду
// Выполняет команду по ее описанию
//
// Параметры:
//  Соединение    - Строка, Произвольный       - Соединение или строка подключения                    - dbc
//  Комманда      - Строка                     - Имя команды для выполнения                           - comm
//  Аргумент      - Произвольный               - Аргумент команды                                     - arg
//  База          - Строка                     - База данных, в которой необходимо провести операцию  - db
//  Данные        - Структура Из КлючИЗначение - Основные поля данных для выполнения операции         - data
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция ВыполнитьКоманду(Знач Соединение
    , Знач Комманда
    , Знач Аргумент = 1
    , Знач База = Неопределено
    , Знач Данные = Неопределено) Экспорт

    Если ЭтоКоннектор(Соединение) Тогда
        ЗакрыватьСоединение = Ложь;
        Коннектор           = Соединение;
    Иначе
        ЗакрыватьСоединение = Истина;
        Коннектор           = ОткрытьСоединение(Соединение);
    КонецЕсли;

    Если Не ЭтоКоннектор(Коннектор) Тогда
        Возврат Коннектор;
    КонецЕсли;

    СтруктураОперации = Новый Структура();

    OPI_Инструменты.ДобавитьПоле("operation", Комманда, "Строка", СтруктураОперации);

    Если База <> Неопределено Тогда
        OPI_Инструменты.ДобавитьПоле("database", База, "Строка", СтруктураОперации);
    КонецЕсли;

    Если Аргумент <> Неопределено Тогда
        ОбработанныйАргумент = ОбработатьДанныеДляОперации(Аргумент);
        СтруктураОперации.Вставить("argument", ОбработанныйАргумент);
    КонецЕсли;

    Если Данные <> Неопределено Тогда
        ОбработанныеДанные = ОбработатьДанныеДляОперации(Данные);
        СтруктураОперации.Вставить("data", ОбработанныеДанные);
    КонецЕсли;

    ОперацияJSON = OPI_Инструменты.JSONСтрокой(СтруктураОперации);

    Результат = Коннектор.Execute(ОперацияJSON);
    Результат = OPI_Инструменты.JsonВСтруктуру(Результат);

    Если ЗакрыватьСоединение Тогда
        ЗакрытьСоединение(Коннектор);
    КонецЕсли;

    Возврат Результат;

КонецФункции

#КонецОбласти

#Область РаботаСБазами

// Получить список баз
// Получает список баз данных
//
// Параметры:
//  Соединение - Строка, Произвольный - Соединение или строка подключения - dbc
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция ПолучитьСписокБаз(Знач Соединение) Экспорт

    Результат = ВыполнитьКоманду(Соединение, "listDatabases");
    Возврат Результат;

КонецФункции

// Получить базу данных
// Получает информацию о базе данных
//
// Параметры:
//  Соединение - Строка, Произвольный - Соединение или строка подключения       - dbc
//  База       - Строка               - Имя базы. Текущая база, если не указано - db
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция ПолучитьБазуДанных(Знач Соединение, Знач База = Неопределено) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(База);

    Результат = ВыполнитьКоманду(Соединение, "dbStats", , База);
    Возврат Результат;

КонецФункции

// Удалить базу данных
// Удаляет базу данных
//
// Параметры:
//  Соединение - Строка, Произвольный - Соединение или строка подключения       - dbc
//  База       - Строка               - Имя базы. Текущая база, если не указано - db
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция УдалитьБазуДанных(Знач Соединение, Знач База = Неопределено) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(База);

    Результат = ВыполнитьКоманду(Соединение, "dropDatabase", , База);
    Возврат Результат;

КонецФункции

#КонецОбласти

#Область РаботаСКоллекциями

// Создать коллекцию
// Создает новую коллекцию с указанными параметрами
//
// Параметры:
//  Соединение   - Строка, Произвольный       - Соединение или строка подключения       - dbc
//  Наименование - Строка                     - Имя новой коллекции                     - name
//  База         - Строка                     - Имя базы. Текущая база, если не указано - db
//  Параметры    - Структура Из КлючИЗначение - Дополнительные параметры создания       - params
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция СоздатьКоллекцию(Знач Соединение
    , Знач Наименование
    , Знач База = Неопределено
    , Знач Параметры = Неопределено) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Наименование);

    Если Параметры <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Параметры);
    КонецЕсли;

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Результат = ВыполнитьКоманду(Соединение, "createCollection", Наименование, База, Параметры);
    Возврат Результат;

КонецФункции

// Удалить коллекцию
// Удаляет выбранную коллекцию базы
//
// Параметры:
//  Соединение   - Строка, Произвольный - Соединение или строка подключения       - dbc
//  Коллекция    - Строка               - Имя коллекции                           - coll
//  База         - Строка               - Имя базы. Текущая база, если не указано - db
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Результат выполнения операции
Функция УдалитьКоллекцию(Знач Соединение, Знач Коллекция, Знач База = Неопределено) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(Коллекция);

    Если База <> Неопределено Тогда
        OPI_ПреобразованиеТипов.ПолучитьСтроку(База);
    КонецЕсли;

    Результат = ВыполнитьКоманду(Соединение, "drop", Коллекция, База);
    Возврат Результат;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбработатьДанныеДляОперации(Знач Данные)

    Попытка

        ОбработанныеДанные = Данные;
        OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(ОбработанныеДанные);
        ОбработанныеДанные = ОбработатьКоллекциюДляОперации(ОбработанныеДанные);

    Исключение

        ОбработанныеДанные = Данные;

        Если OPI_Инструменты.ЭтоКоллекция(ОбработанныеДанные) Тогда

            ОбработанныеДанные = ОбработатьКоллекциюДляОперации(ОбработанныеДанные);

        ИначеЕсли ТипЗнч(ОбработанныеДанные) = Тип("ДвоичныеДанные") Тогда

            ОбработанныеДанные = Новый Структура("__OPI_BINARY__", ПолучитьBase64СтрокуИзДвоичныхДанных(ОбработанныеДанные));

        ИначеЕсли ТипЗнч(ОбработанныеДанные) = Тип("Дата") Тогда

            ОбработанныеДанные = Новый Структура("__OPI_DATE__", OPI_Инструменты.ДатаRFC3339(ОбработанныеДанные));

        ИначеЕсли Не ТипЗнч(ОбработанныеДанные) = Тип("Число") И Не ТипЗнч(ОбработанныеДанные) = Тип("Булево") Тогда

            OPI_ПреобразованиеТипов.ПолучитьСтроку(ОбработанныеДанные);

        КонецЕсли;

    КонецПопытки;

    Возврат ОбработанныеДанные;

КонецФункции

Функция ОбработатьКоллекциюДляОперации(Знач Данные)

    ОбработанныеДанные = Тип(ТипЗнч(Данные));

    Если OPI_Инструменты.ЭтоКоллекция(ОбработанныеДанные) Тогда

        Для Каждого ЧастьДанных Из Данные Цикл

            ТекущийКлюч     = Строка(ЧастьДанных.Ключ);
            ТекущееЗначение = ОбработатьДанныеДляОперации(ЧастьДанных.Значение);

            ОбработанныеДанные.Вставить(ТекущийКлюч, ТекущееЗначение);

        КонецЦикла;

    Иначе

        Для Каждого Элемент Из Данные Цикл
            ОбработанныеДанные.Добавить(ОбработатьДанныеДляОперации(Элемент));
        КонецЦикла;

    КонецЕсли;

    Возврат ОбработанныеДанные;

КонецФункции

#КонецОбласти

#Region Alternate

Function CreateConnection(Val ConnectionString) Export
	Return ОткрытьСоединение(ConnectionString);
EndFunction

Function CloseConnection(Val Connection) Export
	Return ЗакрытьСоединение(Connection);
EndFunction

Function IsConnector(Val Value) Export
	Return ЭтоКоннектор(Value);
EndFunction

Function GenerateConnectionString(Val Address, Val Base = "", Val Login = "", Val Password = "", Val Parameters = Undefined) Export
	Return СформироватьСтрокуПодключения(Address, Base, Login, Password, Parameters);
EndFunction

Function ExecuteCommand(Val Connection, Val Command, Val Argument = 1, Val Base = Undefined, Val Data = Undefined) Export
	Return ВыполнитьКоманду(Connection, Command, Argument, Base, Data);
EndFunction

Function GetListOfBases(Val Connection) Export
	Return ПолучитьСписокБаз(Connection);
EndFunction

Function GetDatabase(Val Connection, Val Base = Undefined) Export
	Return ПолучитьБазуДанных(Connection, Base);
EndFunction

Function DeleteDatabase(Val Connection, Val Base = Undefined) Export
	Return УдалитьБазуДанных(Connection, Base);
EndFunction

Function CreateCollection(Val Connection, Val Name, Val Base = Undefined, Val Parameters = Undefined) Export
	Return СоздатьКоллекцию(Connection, Name, Base, Parameters);
EndFunction

Function DeleteCollection(Val Connection, Val Collection, Val Base = Undefined) Export
	Return УдалитьКоллекцию(Connection, Collection, Base);
EndFunction

#EndRegion