// OneScript: ./OInt/core/Modules/OPI_GreenMax.os
// Lib: GreenMax
// CLI: greenmax
// Keywords: greenapi, max

// MIT License

// Copyright (c) 2023-2025 Anton Tsitavets

// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:

// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.

// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

// https://github.com/Bayselonarrend/OpenIntegrations

// BSLLS:Typo-off
// BSLLS:LatinAndCyrillicSymbolInWord-off
// BSLLS:IncorrectLineBreak-off
// BSLLS:NumberOfOptionalParams-off
// BSLLS:UsingServiceTag-off
// BSLLS:LineLength-off
// BSLLS:UsingSynchronousCalls-off
// BSLLS:MagicNumber-off

//@skip-check module-structure-top-region
//@skip-check module-structure-method-in-regions
//@skip-check wrong-string-literal-content
//@skip-check method-too-many-params
//@skip-check bsl-legacy-check-string-literal
//@skip-check doc-comment-collection-item-type

#Область ПрограммныйИнтерфейс

#Область Аккаунт

// Сформировать параметры доступа
// Формирует структуру основных авторизационных данных
//
// Примечание:
// Подробнее в документации API: [Получить параметры доступа к инстансу](@green-api.com/v3/docs/before-start/#parameters)
//
// Параметры:
//  ApiUrl           - Строка - Ссылка на хост API                     - api
//  MediaUrl         - Строка - Ссылка на хост API для отправки файлов - media
//  IdInstance       - Строка - Уникальный номер инстанса              - id
//  ApiTokenInstance - Строка - Ключ доступа инстанса                  - token
//
// Возвращаемое значение:
//  Структура -  Структура параметров доступа
Функция СформироватьПараметрыДоступа(Знач ApiUrl, Знач MediaUrl, Знач IdInstance, Знач ApiTokenInstance) Экспорт

    Строка_ = "Строка";

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("apiUrl"          , ApiUrl          , Строка_, Параметры);
    OPI_Инструменты.ДобавитьПоле("mediaUrl"        , MediaUrl        , Строка_, Параметры);
    OPI_Инструменты.ДобавитьПоле("idInstance"      , IdInstance      , Строка_, Параметры);
    OPI_Инструменты.ДобавитьПоле("apiTokenInstance", ApiTokenInstance, Строка_, Параметры);

    Возврат Параметры;

КонецФункции

// Получить информацию об аккаунте
// Получает информацию об аккаунте
//
// Примечание:
// Метод в документации API: [GetWaSettings](@green-api.com/v3/docs/api/account/GetWaSettings/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ПолучитьИнформациюОбАккаунте(Знач ПараметрыДоступа) Экспорт

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "getAccountSettings");
    Ответ = OPI_ЗапросыHTTP.Get(URL);

    Возврат Ответ;

КонецФункции

// Получить настройки инстанса
// Получает текущие настройки инстанса
//
// Примечание:
// Метод в документации API: [GetSettings](@green-api.com/v3/docs/api/account/GetSettings/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ПолучитьНастройкиИнстанса(Знач ПараметрыДоступа) Экспорт

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "getSettings");
    Ответ = OPI_ЗапросыHTTP.Get(URL);

    Возврат Ответ;

КонецФункции

// Установить настройки инстанса
// Устанавливает настройки инстанса
//
// Примечание
// Метод в документации API: [SetSettings](@green-api.com/v3/docs/api/account/SetSettings/)
//
// Параметры:
//  Настройки        - Структура Из КлючИЗначение - Настройки инстанса. См. ПолучитьСтруктуруНастроекИнстанса - settings
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа       - access
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция УстановитьНастройкиИнстанса(Знач Настройки, Знач ПараметрыДоступа) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Настройки);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "setSettings");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Настройки);

    Возврат Ответ;

КонецФункции

// Получить код авторизации
// Отправляет SMS по указанному номеру для получения кода подтверждения
//
// Примечание:
// Метод в документации API: [StartAuthorization](@green-api.com/v3/docs/api/account/StartAuthorization/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа  - access
//  НомерТелефона    - Строка, Число              - Номер телефона без знака +                           - phone
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ПолучитьКодАвторизации(Знач ПараметрыДоступа, Знач НомерТелефона) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(НомерТелефона);

    НомерТелефона = СтрЗаменить(НомерТелефона, "+", "");

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("phoneNumber", НомерТелефона, "Число", Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "startAuthorization");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Отправить код авторизации
// Авторизует инстанс по коду доступа из SMS
//
// Примечание:
// Метод в документации API: [SendAuthorizationCode](@green-api.com/v3/docs/api/account/SendAuthorizationCode/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  КодАвторизации   - Строка                     - Код авторизации                                     - code
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ОтправитьКодАвторизации(Знач ПараметрыДоступа, Знач КодАвторизации) Экспорт

    Параметры = Новый Структура;

    OPI_Инструменты.ДобавитьПоле("code", КодАвторизации, "Строка", Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "sendAuthorizationCode");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Получить состояние инстанса
// Получает состояние инстанса
//
// Примечание:
// Метод в документации API: [GetStateInstance](@green-api.com/v3/docs/api/account/GetStateInstance/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ПолучитьСостояниеИнстанса(Знач ПараметрыДоступа) Экспорт

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "getStateInstance");
    Ответ = OPI_ЗапросыHTTP.Get(URL);

    Возврат Ответ;

КонецФункции

// Установить картинку профиля
// Устанавливает новую картинку профиля
//
// Примечание:
// Метод в документации API: [SetProfilePicture](@green-api.com/v3/docs/api/account/SetProfilePicture/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  Картинка         - ДвоичныеДанные, Строка     - Картинка профиля                                    - picture
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция УстановитьКартинкуПрофиля(Знач ПараметрыДоступа, Знач Картинка) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьДвоичныеДанные(Картинка);

    СоответствиеКартинки = Новый Соответствие();
    СоответствиеКартинки.Вставить("file|file.jpg", Картинка);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "setProfilePicture");
    Ответ = OPI_ЗапросыHTTP.PostMultipart(URL, , СоответствиеКартинки);

    Возврат Ответ;

КонецФункции

// Разлогинить инстанс
// Разлогинивает инстанс
//
// Примечание:
// Метод в документации API: [Logout](@green-api.com/v3/docs/api/account/Logout/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция РазлогинитьИнстанс(Знач ПараметрыДоступа) Экспорт

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "logout");
    Ответ = OPI_ЗапросыHTTP.Get(URL);

    Возврат Ответ;

КонецФункции

// Перезапустить инстанс
// Перезапускает инстанс
//
// Примечание:
// Метод в документации API: [Reboot](@green-api.com/v3/docs/api/account/Reboot/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ПерезапуститьИнстанс(Знач ПараметрыДоступа) Экспорт

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "reboot");
    Ответ = OPI_ЗапросыHTTP.Get(URL);

    Возврат Ответ;

КонецФункции

// Проверить аккаунт
// Проверяет существование аккаунта Max по номеру телефона
//
// Примечание:
// Метод в документации API: [CheckAccount](@green-api.com/v3/docs/api/service/CheckAccount/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  НомерТелефона    - Строка, Число              - Номер телефона для проверки без знака +             - phone
//  ИгнорироватьКэш  - Булево                     - Игнорировать кэш при повторном запросе проверки     - force
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ПроверитьАккаунт(Знач ПараметрыДоступа, Знач НомерТелефона, Знач ИгнорироватьКэш = Ложь) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(НомерТелефона);

    НомерТелефона = СтрЗаменить(НомерТелефона, "+", "");

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("phoneNumber", НомерТелефона  , "Число" , Параметры);
    OPI_Инструменты.ДобавитьПоле("force"      , ИгнорироватьКэш, "Булево", Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "checkAccount");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Получить список контактов
// Получает список контактов аккаунта
//
// Примечание:
// Метод в документации API: [GetContacts](@green-api.com/v3/docs/api/service/GetContacts/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа      - access
//  Количество       - Число                      - Количество контактов для получения. Все, если не указано - count
//
// Возвращаемое значение:
//  Массив Из Соответствие - сериализованный JSON ответа от Green API
Функция ПолучитьСписокКонтактов(Знач ПараметрыДоступа, Знач Количество = Неопределено) Экспорт

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("count", Количество, "Число" , Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "getContacts");
    Ответ = OPI_ЗапросыHTTP.Get(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Получить информацию о контакте
// Получает информацию о выбранном контакте
//
// Примечание:
// Метод в документации API: [GetContactInfo](@green-api.com/v3/docs/api/service/GetContactInfo/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  IDЧата           - Строка                     - Идентификатор чата                                  - chat
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ПолучитьИнформациюОКонтакте(Знач ПараметрыДоступа, Знач IDЧата) Экспорт

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("chatId", IDЧата  , "Строка" , Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "getContactInfo");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Получить список чатов
// Получает список чатов аккаунта
//
// Примечание:
// Метод в документации API: [GetChats](@green-api.com/v3/docs/api/service/GetChats/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//
// Возвращаемое значение:
//  Массив Из Соответствие - сериализованный JSON ответа от Green API
Функция ПолучитьСписокЧатов(Знач ПараметрыДоступа) Экспорт

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "getChats");
    Ответ = OPI_ЗапросыHTTP.Get(URL);

    Возврат Ответ;

КонецФункции

// Получить аватар чата
// Получает URL к картинке чата
//
// Примечание:
// Метод в документации API: [GetAvatar](@green-api.com/v3/docs/api/service/GetAvatar/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  IDЧата           - Строка                     - Идентификатор чата                                  - chat
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ПолучитьАватарЧата(Знач ПараметрыДоступа, Знач IDЧата) Экспорт

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("chatId", IDЧата  , "Строка" , Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "getAvatar");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Получить структуру настроек инстанса
// Получает шаблон структуры для установки настроек инстанса
//
// Параметры:
//  Пустая - Булево - Истина > структура с пустыми значениями, Ложь > в значениях будут описания полей - empty
//
// Возвращаемое значение:
//  Структура Из КлючИЗначение -  Структура настроек инстанса
Функция ПолучитьСтруктуруНастроекИнстанса(Знач Пустая = Ложь) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьБулево(Пустая);

    СтруктураНастроек = Новый Структура;

    СтруктураНастроек.Вставить("webhookUrl"                       , "<URL для отправки уведомлений>");
    СтруктураНастроек.Вставить("webhookUrlToken"                  , "<токен для доступа к вашему серверу уведомлений, если требуется>");
    СтруктураНастроек.Вставить("delaySendMessagesMilliseconds"    , "<интервал отправки сообщений в миллисекундах>");
    СтруктураНастроек.Вставить("markIncomingMessagesReaded"       , "<отмечать входящие сообщения прочитанными: yes, no>");
    СтруктураНастроек.Вставить("markIncomingMessagesReadedOnReply", "<отмечать входящие сообщения прочитанными при отправке сообщения в чат: yes, no>");
    СтруктураНастроек.Вставить("outgoingWebhook"                  , "<получать уведомления о статусах отправки/доставки/прочтении исходящих сообщений: yes, no>");
    СтруктураНастроек.Вставить("outgoingMessageWebhook"           , "<получать уведомления о сообщениях, отправленных с телефона: yes, no>");
    СтруктураНастроек.Вставить("outgoingAPIMessageWebhook"        , "<получать уведомления о сообщениях, отправленных через API: yes, no>");
    СтруктураНастроек.Вставить("stateWebhook"                     , "<получать уведомления об изменении состояния авторизации инстанса: yes, no>");
    СтруктураНастроек.Вставить("incomingWebhook"                  , "<получать уведомления о входящих сообщениях и файлах: yes, no>");

    Если Пустая Тогда
        СтруктураНастроек = OPI_Инструменты.ОчиститьКоллекциюРекурсивно(СтруктураНастроек);
    КонецЕсли;

    //@skip-check constructor-function-return-section
    Возврат СтруктураНастроек;

КонецФункции

#КонецОбласти

#Область РаботаСГруппами

// Создать группу
// Создает новый групповой чат с указанным именем
//
// Примечание:
// Метод в документации API: [CreateGroup](@green-api.com/v3/docs/api/groups/CreateGroup/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  Наименование     - Строка                     - Наименование группы                                 - name
//  Участники        - Массив Из Строка, Строка   - Массив ID участников группы или один ID             - members
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция СоздатьГруппу(Знач ПараметрыДоступа, Знач Наименование, Знач Участники) Экспорт

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("groupName", Наименование, "Строка", Параметры);

    Если ЗначениеЗаполнено(Участники) Тогда
        OPI_Инструменты.ДобавитьПоле("chatIds", Участники, "Массив", Параметры);
    Иначе
        Параметры.Вставить("chatIds", Новый Массив);
    КонецЕсли;

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "createGroup");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Получить информацию о группе
// Получает информацию о групповом чате
//
// Примечание:
// Метод в документации API: [GetGroupData](@green-api.com/v3/docs/api/groups/GetGroupData/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  IDЧата           - Строка                     - ID группового чата                                  - chat
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ПолучитьИнформациюОГруппе(Знач ПараметрыДоступа, Знач IDЧата) Экспорт

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("chatId" , IDЧата , "Строка", Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "getGroupData");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Изменить имя группы
// Изменяет имя группового чата
//
// Примечание:
// Метод в документации API: [UpdateGroupName](@green-api.com/v3/docs/api/groups/UpdateGroupName/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  IDЧата           - Строка                     - ID группового чата                                  - chat
//  Наименование     - Строка                     - Новое наименование чата                             - name
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ИзменитьИмяГруппы(Знач ПараметрыДоступа, Знач IDЧата, Знач Наименование) Экспорт

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("chatId"   , IDЧата      , "Строка", Параметры);
    OPI_Инструменты.ДобавитьПоле("groupName", Наименование, "Строка", Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "updateGroupName");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Установить картинку группы
// Устанавливает картинку в качестве аватара группового чата
//
// Примечание:
// Метод в документации API: [SetGroupPicture](@green-api.com/v3/docs/api/groups/SetGroupPicture/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  IDЧата           - Строка                     - ID группового чата                                  - chat
//  Картинка         - Строка, ДвоичныеДанные     - Картинка в формате jpg                              - picture
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция УстановитьКартинкуГруппы(Знач ПараметрыДоступа, Знач IDЧата, Знач Картинка) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьДвоичныеДанные(Картинка);

    СоответствиеКартинки = Новый Соответствие();
    СоответствиеКартинки.Вставить("file|file.jpg", Картинка);

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("chatId", IDЧата, "Строка", Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "setGroupPicture");
    Ответ = OPI_ЗапросыHTTP.PostMultipart(URL, Параметры, СоответствиеКартинки);

    Возврат Ответ;

КонецФункции

// Изменить настройки группы
// Изменяет выбранные настройки группового
//
// Примечание:
// Метод в документации API: [UpdateGroupSettings](@green-api.com/v3/docs/api/groups/UpdateGroupSettings/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа            - access
//  IDЧата           - Строка                     - ID группового чата                                             - chat
//  Настройки        - Структура Из КлючИЗначение - Настройки группового чата. См. ПолучитьСтруктуруНастроекГруппы - set
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ИзменитьНастройкиГруппы(Знач ПараметрыДоступа, Знач IDЧата, Знач Настройки) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(Настройки);

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("chatId", IDЧата, "Строка", Параметры);

    Для Каждого КлючЗначение Из Настройки Цикл
        OPI_Инструменты.ДобавитьПоле(КлючЗначение.Ключ, КлючЗначение.Значение, "Текущий", Параметры);
    КонецЦикла;

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "updateGroupSettings");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Добавить участника группы
// Добавляет нового участника в выбранный групповой чат
//
// Примечание:
// Метод в документации API: [AddGroupParticipant](@green-api.com/v3/docs/api/groups/AddGroupParticipant/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  IDЧата           - Строка                     - ID группового чата                                  - chat
//  IDУчастника      - Строка                     - ID участника для добавления                         - member
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ДобавитьУчастникаГруппы(Знач ПараметрыДоступа, Знач IDЧата, Знач IDУчастника) Экспорт

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("chatId"           , IDЧата     , "Строка", Параметры);
    OPI_Инструменты.ДобавитьПоле("participantChatId", IDУчастника, "Строка", Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "addGroupParticipant");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Удалить участника группы
// Удаляет участника из выбранного группового чата
//
// Примечание:
// Метод в документации API: [RemoveGroupParticipant](@green-api.com/v3/docs/api/groups/RemoveGroupParticipant/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  IDЧата           - Строка                     - ID группового чата                                  - chat
//  IDУчастника      - Строка                     - ID участника для удаления                           - member
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция УдалитьУчастникаГруппы(Знач ПараметрыДоступа, Знач IDЧата, Знач IDУчастника) Экспорт

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("chatId"           , IDЧата     , "Строка", Параметры);
    OPI_Инструменты.ДобавитьПоле("participantChatId", IDУчастника, "Строка", Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "removeGroupParticipant");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Назначить права администратора
// Назначает права даминистратора указанному участнику группового чата
//
// Примечание:
// Метод в документации API: [SetGroupAdmin](@green-api.com/v3/docs/api/groups/SetGroupAdmin/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  IDЧата           - Строка                     - ID группового чата                                  - chat
//  IDУчастника      - Строка                     - ID участника                                        - member
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция НазначитьПраваАдминистратора(Знач ПараметрыДоступа, Знач IDЧата, Знач IDУчастника) Экспорт

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("chatId"           , IDЧата     , "Строка", Параметры);
    OPI_Инструменты.ДобавитьПоле("participantChatId", IDУчастника, "Строка", Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "setGroupAdmin");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Отозвать права администратора
// Отзывает права даминистратора у указанного участника группового чата
//
// Примечание:
// Метод в документации API: [RemoveAdmin](@green-api.com/v3/docs/api/groups/RemoveAdmin/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  IDЧата           - Строка                     - ID группового чата                                  - chat
//  IDУчастника      - Строка                     - ID участника                                        - member
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ОтозватьПраваАдминистратора(Знач ПараметрыДоступа, Знач IDЧата, Знач IDУчастника) Экспорт

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("chatId"           , IDЧата     , "Строка", Параметры);
    OPI_Инструменты.ДобавитьПоле("participantChatId", IDУчастника, "Строка", Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "removeAdmin");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Выйти из группы
// Исключает текущий аккаунт из указанной группы
//
// Примечание:
// Метод в документации API: [LeaveGroup](@green-api.com/v3/docs/api/groups/LeaveGroup/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  IDЧата           - Строка                     - ID группового чата                                  - chat
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ВыйтиИзГруппы(Знач ПараметрыДоступа, Знач IDЧата) Экспорт

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("chatId", IDЧата, "Строка", Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "leaveGroup");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Получить структуру настроек группы
// Возвращает структуру полей настроек группового чата
//
// Параметры:
//  Пустая          - Булево - Истина > структура с пустыми значениями, Ложь > в значениях будут описания полей - empty
//  КакСоответствие - Булево - Истина > возвращает поля настроек как соответствие                               - map
//
// Возвращаемое значение:
//  Структура Из КлючИЗначение - Структура полей
Функция ПолучитьСтруктуруНастроекГруппы(Знач Пустая = Ложь, Знач КакСоответствие = Ложь) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьБулево(Пустая);
    OPI_ПреобразованиеТипов.ПолучитьБулево(КакСоответствие);

    Если КакСоответствие Тогда
        Настройки = Новый Соответствие;
    Иначе
        Настройки = Новый Структура;
    КонецЕсли;

    Настройки.Вставить("allowParticipantsEditGroupSettings"  , "<разрешить участникам изменять настройки группы>");
    Настройки.Вставить("allowParticipantsPinMessages"        , "<разрешить участникам закреплять сообщения>");
    Настройки.Вставить("allowParticipantsAddMembers"         , "<разрешить участникам добавлять новых участников>");

    Если Пустая Тогда
        Настройки = OPI_Инструменты.ОчиститьКоллекциюРекурсивно(Настройки);
    КонецЕсли;

    //@skip-check constructor-function-return-section
    Возврат Настройки;

КонецФункции

#КонецОбласти

#Область ОтправкаСообщений

// Отправить текстовое сообщение
// Отправляет текстовое сообщение в выбранный чат
//
// Примечание:
// Метод в документации API: [SendMessage](@green-api.com/v3/docs/api/sending/SendMessage/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа         - access
//  IDЧата           - Строка                     - Идентификатор чата                                          - chat
//  Текст            - Строка                     - Текст сообщения                                             - text
//  ВремяНабора      - Число                      - Время показа признака набора текста перед отправкой (в мс.) - typing
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ОтправитьТекстовоеСообщение(Знач ПараметрыДоступа, Знач IDЧата, Знач Текст, Знач ВремяНабора = 0) Экспорт

    Строка_   = "Строка";
    Параметры = Новый Структура;

    OPI_Инструменты.ДобавитьПоле("chatId"    , IDЧата     , Строка_, Параметры);
    OPI_Инструменты.ДобавитьПоле("message"   , Текст      , Строка_, Параметры);
    OPI_Инструменты.ДобавитьПоле("typingTime", ВремяНабора, "Число", Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "sendMessage");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Отправить файл
// Отправляет файл в выбранный чат
//
// Примечание:
// Метод в документации API: [SendFileByUpload](@green-api.com/v3/docs/api/sending/SendFileByUpload/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа  - access
//  IDЧата           - Строка                     - Идентификатор чата                                   - chat
//  Файл             - Строка, ДвоичныеДанные     - Данные или путь к файлу                              - file
//  ИмяФайла         - Строка                     - Имя загружаемого файла с раширением                  - filename
//  Описание         - Строка                     - Текст сообщения под файлом                           - caption
//  ВремяНабора      - Число                      - Время показа признака набора перед отправкой (в мс.) - typing
//  ТипНабора        - Строка                     - Тип набора: text, recording, video, image, file      - ttype
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ОтправитьФайл(Знач ПараметрыДоступа
    , Знач IDЧата
    , Знач Файл
    , Знач ИмяФайла
    , Знач Описание = ""
    , Знач ВремяНабора = 0
    , Знач ТипНабора = "file") Экспорт

    OPI_ПреобразованиеТипов.ПолучитьДвоичныеДанные(Файл);

    Строка_   = "Строка";
    Параметры = Новый Структура;

    OPI_Инструменты.ДобавитьПоле("chatId"    , IDЧата     , Строка_, Параметры);
    OPI_Инструменты.ДобавитьПоле("fileName"  , ИмяФайла   , Строка_, Параметры);
    OPI_Инструменты.ДобавитьПоле("caption"   , Описание   , Строка_, Параметры);
    OPI_Инструменты.ДобавитьПоле("typingTime", ВремяНабора, "Число", Параметры);

    Если ЗначениеЗаполнено(ВремяНабора) Тогда
        OPI_Инструменты.ДобавитьПоле("typingType", ТипНабора   , Строка_, Параметры);
    КонецЕсли;

    СоответствиеФайла = Новый Соответствие();
    СоответствиеФайла.Вставить(СтрШаблон("file|%1", ИмяФайла), Файл);

    URL   = СформироватьМедиаURL(ПараметрыДоступа, "SendFileByUpload");
    Ответ = OPI_ЗапросыHTTP.PostMultipart(URL, Параметры, СоответствиеФайла, "application/octet-stream");

    Возврат Ответ;

КонецФункции

// Отправить файл по URL
// Отправляет файл по URL в выбранный чат
//
// Примечание:
// Метод в документации API: [SendFileByUrl](@green-api.com/v3/docs/api/sending/SendFileByUrl/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа  - access
//  IDЧата           - Строка                     - Идентификатор чата                                   - chat
//  URLФайла         - Строка                     - URL файла для отправки                               - url
//  ИмяФайла         - Строка                     - Имя загружаемого файла с раширением                  - filename
//  Описание         - Строка                     - Текст сообщения под файлом                           - caption
//  ВремяНабора      - Число                      - Время показа признака набора перед отправкой (в мс.) - typing
//  ТипНабора        - Строка                     - Тип набора: text, recording, video, image, file      - ttype
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ОтправитьФайлПоURL(Знач ПараметрыДоступа
    , Знач IDЧата
    , Знач URLФайла
    , Знач ИмяФайла
    , Знач Описание = ""
    , Знач ВремяНабора = 0
    , Знач ТипНабора = "file") Экспорт

    Строка_   = "Строка";
    Параметры = Новый Структура;

    OPI_Инструменты.ДобавитьПоле("chatId"         , IDЧата       , Строка_, Параметры);
    OPI_Инструменты.ДобавитьПоле("urlFile"        , URLФайла     , Строка_, Параметры);
    OPI_Инструменты.ДобавитьПоле("fileName"       , ИмяФайла     , Строка_, Параметры);
    OPI_Инструменты.ДобавитьПоле("caption"        , Описание     , Строка_, Параметры);
    OPI_Инструменты.ДобавитьПоле("typingTime"     , ВремяНабора  , "Число", Параметры);

    Если ЗначениеЗаполнено(ВремяНабора) Тогда
        OPI_Инструменты.ДобавитьПоле("typingType", ТипНабора   , Строка_, Параметры);
    КонецЕсли;

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "sendFileByUrl");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

#КонецОбласти

#Область Уведомления

// Получить уведомление
// Получает последнее уведомление из очереди
//
// Примечание:
// Для получения следующего уведомления, текущее уведомление должно быть удалено
// Метод в документации API: [ReceiveNotification](@green-api.com/v3/docs/api/receiving/technology-http-api/ReceiveNotification/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  Таймаут          - Число                      - Таймаут ожидания уведомления, если очередь пуста    - timeout
//  Удалять          - Булево                     - Удалять уведомление из очереди после получения      - del
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ПолучитьУведомление(Знач ПараметрыДоступа, Знач Таймаут = 5, Знач Удалять = Ложь) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьБулево(Удалять);

    URL = СформироватьОсновнойURL(ПараметрыДоступа, "receiveNotification");

    Параметры = Новый Структура;
    OPI_Инструменты.ДобавитьПоле("receiveTimeout", Таймаут, "Число", Параметры);

    Ответ = OPI_ЗапросыHTTP.Get(URL, Параметры);

    Если Удалять Тогда

        IDУведомления = Ответ["receiptId"];

        Если ЗначениеЗаполнено(IDУведомления)  Тогда
            Ответ.Вставить("deleting", УдалитьУведомление(ПараметрыДоступа, IDУведомления));
        КонецЕсли;

    КонецЕсли;

    Возврат Ответ;

КонецФункции

// Удалить уведомление
// Удаляет указанное уведомление из очереди
//
// Примечание:
// Метод в документации API: [DeleteNotification](@green-api.com/v3/docs/api/receiving/technology-http-api/DeleteNotification/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  IDУведомления    - Число                      - ID уведомления для удаления                         - id
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция УдалитьУведомление(Знач ПараметрыДоступа, Знач IDУведомления) Экспорт

    OPI_ПреобразованиеТипов.ПолучитьСтроку(IDУведомления);

    URL = СформироватьОсновнойURL(ПараметрыДоступа, "deleteNotification");
    URL = СтрШаблон("%1/%2", URL, IDУведомления);

    Ответ = OPI_ЗапросыHTTP.Delete(URL);

    Возврат Ответ;

КонецФункции

#КонецОбласти

#Область ИсторияСообщений

// Получить историю сообщений чата
// Получает последние сообщения из истории чата
//
// Примечание:
// Метод в документации API: [GetChatHistory](@green-api.com/v3/docs/api/journals/GetChatHistory/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  IDЧата           - Строка                     - ID чата                                             - chat
//  Количество       - Число                      - Количество сообщений для получения                  - count
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ПолучитьИсториюСообщенийЧата(Знач ПараметрыДоступа, Знач IDЧата, Знач Количество = 100) Экспорт

    Параметры = Новый Структура;

    OPI_Инструменты.ДобавитьПоле("chatId", IDЧата    , "Строка", Параметры);
    OPI_Инструменты.ДобавитьПоле("count" , Количество, "Число" , Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "getChatHistory");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Получить сообщение чата
// Получает информацию о сообщении чата по ID
//
// Примечание:
// Метод в документации API: [GetMessage](@green-api.com/v3/docs/api/journals/GetMessage/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  IDЧата           - Строка                     - ID чата                                             - chat
//  IDСообщения      - Строка                     - ID сообщения                                        - message
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ПолучитьСообщениеЧата(Знач ПараметрыДоступа, Знач IDЧата, Знач IDСообщения) Экспорт

    Параметры = Новый Структура;

    OPI_Инструменты.ДобавитьПоле("chatId"   , IDЧата     , "Строка" , Параметры);
    OPI_Инструменты.ДобавитьПоле("idMessage", IDСообщения, "Строка" , Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "getMessage");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Получить журнал входящих сообщений
// Получает последние входящие сообщения инстанса
//
// Примечание:
// Метод в документации API: [LastIncomingMessages](@green-api.com/v3/docs/api/journals/LastIncomingMessages/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа      - access
//  Период           - Число                      - Время в минутах, за которое требуется показать сообщения - span
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ПолучитьЖурналВходящихСообщений(Знач ПараметрыДоступа, Знач Период = 1440) Экспорт

    Параметры = Новый Структура;

    OPI_Инструменты.ДобавитьПоле("minutes", Период, "Число", Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "lastIncomingMessages");
    Ответ = OPI_ЗапросыHTTP.Get(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Получить журнал исходящих сообщений
// Получает последние исходящие сообщения инстанса
//
// Примечание:
// Метод в документации API: [LastOutgoingMessages](@green-api.com/v3/docs/api/journals/LastOutgoingMessages/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа      - access
//  Период           - Число                      - Время в минутах, за которое требуется показать сообщения - span
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ПолучитьЖурналИсходящихСообщений(Знач ПараметрыДоступа, Знач Период = 1440) Экспорт

    Параметры = Новый Структура;

    OPI_Инструменты.ДобавитьПоле("minutes", Период, "Число", Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "lastOutgoingMessages");
    Ответ = OPI_ЗапросыHTTP.Get(URL, Параметры);

    Возврат Ответ;

КонецФункции

// Отметить сообщения как прочитанные
// Отмечает сообщения в чате как прочитанные
//
// Примечание:
// При указании ID сообщения все сообщения после указанного будут помечены как прочитанные
// Метод в документации API: [ReadChat](@green-api.com/v3/docs/api/marks/ReadChat/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа - access
//  IDЧата           - Строка                     - ID чата                                             - chat
//  IDСообщения      - Строка                     - ID сообщения. Все сообщения, если не указано        - message
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ОтметитьСообщенияКакПрочитанные(Знач ПараметрыДоступа, Знач IDЧата, Знач IDСообщения = "") Экспорт

    Строка_   = "Строка";
    Параметры = Новый Структура;

    OPI_Инструменты.ДобавитьПоле("chatId"   , IDЧата     , Строка_, Параметры);
    OPI_Инструменты.ДобавитьПоле("idMessage", IDСообщения, Строка_, Параметры);

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "readChat");
    Ответ = OPI_ЗапросыHTTP.PostСТелом(URL, Параметры);

    Возврат Ответ;

КонецФункции

#КонецОбласти

#Область Очереди

// Получить количество сообщений к отправке
// Получает количество сообщений в очереди на отправку
//
// Примечание:
// Метод в документации API: [GetMessagesCount](@green-api.com/v3/docs/api/queues/GetMessagesCount/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа  - access
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ПолучитьКоличествоСообщенийКОтправке(Знач ПараметрыДоступа) Экспорт

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "getMessagesCount");
    Ответ = OPI_ЗапросыHTTP.Get(URL);

    Возврат Ответ;

КонецФункции

// Получить очередь сообщений к отправке
// Получает список сообщений к отправке
//
// Примечание:
// Метод в документации API: [ShowMessagesQueue](@green-api.com/v3/docs/api/queues/ShowMessagesQueue/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа  - access
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ПолучитьОчередьСообщенийКОтправке(Знач ПараметрыДоступа) Экспорт

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "showMessagesQueue");
    Ответ = OPI_ЗапросыHTTP.Get(URL);

    Возврат Ответ;

КонецФункции

// Очистить очередь сообщений к отправке
// Очищает список сообщений к отправке
//
// Примечание:
// Метод в документации API: [ClearMessagesQueue](@green-api.com/v3/docs/api/queues/ClearMessagesQueue/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа  - access
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ОчиститьОчередьСообщенийКОтправке(Знач ПараметрыДоступа) Экспорт

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "clearMessagesQueue");
    Ответ = OPI_ЗапросыHTTP.Get(URL);

    Возврат Ответ;

КонецФункции

// Получить количество входящих уведомлений
// Получает количество уведомлений во входящей очереди
//
// Примечание:
// Метод в документации API: [GetWebhooksCount](@green-api.com/v3/docs/api/queues/GetWebhooksCount/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа  - access
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ПолучитьКоличествоВходящихУведомлений(Знач ПараметрыДоступа) Экспорт

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "getWebhooksCount");
    Ответ = OPI_ЗапросыHTTP.Get(URL);

    Возврат Ответ;

КонецФункции

// Очистить очередь входящих уведомлений
// Очищает очередь входящих уведомлений
//
// Примечание:
// Метод в документации API: [ClearWebhooksQueue](@green-api.com/v3/docs/api/queues/ClearWebhooksQueue/)
//
// Параметры:
//  ПараметрыДоступа - Структура Из КлючИЗначение - Параметры доступа. См. СформироватьПараметрыДоступа  - access
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - сериализованный JSON ответа от Green API
Функция ОчиститьОчередьВходящихУведомлений(Знач ПараметрыДоступа) Экспорт

    URL   = СформироватьОсновнойURL(ПараметрыДоступа, "clearWebhooksQueue");
    Ответ = OPI_ЗапросыHTTP.Delete(URL);

    Возврат Ответ;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьОсновнойURL(Знач ПараметрыДоступа, Знач Метод)

    Возврат СформироватьURL(ПараметрыДоступа, Метод, "apiUrl");

КонецФункции

Функция СформироватьМедиаURL(Знач ПараметрыДоступа, Знач Метод)

    Возврат СформироватьURL(ПараметрыДоступа, Метод, "mediaUrl");

КонецФункции

Функция СформироватьURL(ПараметрыДоступа, Метод, ПолеURL)

    OPI_ПреобразованиеТипов.ПолучитьКоллекциюКлючИЗначение(ПараметрыДоступа);
    OPI_ПреобразованиеТипов.ПолучитьСтроку(Метод);

    ОбязательныеПоля  = СтрРазделить(ПолеURL + ",idInstance,apiTokenInstance", ",");
    ОтсутствующиеПоля = OPI_Инструменты.НайтиОтсутствующиеПоляКоллекции(ПараметрыДоступа, ОбязательныеПоля);

    Если ОтсутствующиеПоля.Количество() > 0 Тогда
        ВызватьИсключение "Отсутствуют обязательные поля в параметрах доступа!";
    КонецЕсли;

    Url              = ПараметрыДоступа[ПолеURL];
    IdInstance       = ПараметрыДоступа["idInstance"];
    ApiTokenInstance = ПараметрыДоступа["apiTokenInstance"];

    URL = СтрШаблон("%1/waInstance%2/%3/%4", Url, IdInstance, Метод, ApiTokenInstance);

    Возврат URL;

КонецФункции

#КонецОбласти

#Region Alternate

Function FormAccessParameters(Val ApiUrl, Val MediaUrl, Val IdInstance, Val ApiTokenInstance) Export
	Return СформироватьПараметрыДоступа(ApiUrl, MediaUrl, IdInstance, ApiTokenInstance);
EndFunction

Function GetAccountInformation(Val AccessParameters) Export
	Return ПолучитьИнформациюОбАккаунте(AccessParameters);
EndFunction

Function GetInstanceSettings(Val AccessParameters) Export
	Return ПолучитьНастройкиИнстанса(AccessParameters);
EndFunction

Function SetInstanceSettings(Val Settings, Val AccessParameters) Export
	Return УстановитьНастройкиИнстанса(Settings, AccessParameters);
EndFunction

Function GetAuthorizationCode(Val AccessParameters, Val PhoneNumber) Export
	Return ПолучитьКодАвторизации(AccessParameters, PhoneNumber);
EndFunction

Function SendAuthorizationCode(Val AccessParameters, Val AuthCode) Export
	Return ОтправитьКодАвторизации(AccessParameters, AuthCode);
EndFunction

Function GetInstanceStatus(Val AccessParameters) Export
	Return ПолучитьСостояниеИнстанса(AccessParameters);
EndFunction

Function SetProfilePicture(Val AccessParameters, Val Image) Export
	Return УстановитьКартинкуПрофиля(AccessParameters, Image);
EndFunction

Function LogoutInstance(Val AccessParameters) Export
	Return РазлогинитьИнстанс(AccessParameters);
EndFunction

Function RebootInstance(Val AccessParameters) Export
	Return ПерезапуститьИнстанс(AccessParameters);
EndFunction

Function CheckAccount(Val AccessParameters, Val PhoneNumber, Val IgnoreCache = False) Export
	Return ПроверитьАккаунт(AccessParameters, PhoneNumber, IgnoreCache);
EndFunction

Function GetContactList(Val AccessParameters, Val Count = Undefined) Export
	Return ПолучитьСписокКонтактов(AccessParameters, Count);
EndFunction

Function GetContactInformation(Val AccessParameters, Val ChatID) Export
	Return ПолучитьИнформациюОКонтакте(AccessParameters, ChatID);
EndFunction

Function GetChatList(Val AccessParameters) Export
	Return ПолучитьСписокЧатов(AccessParameters);
EndFunction

Function GetChatAvatar(Val AccessParameters, Val ChatID) Export
	Return ПолучитьАватарЧата(AccessParameters, ChatID);
EndFunction

Function GetInstanceSettingsStructure(Val Clear = False) Export
	Return ПолучитьСтруктуруНастроекИнстанса(Clear);
EndFunction

Function CreateGroup(Val AccessParameters, Val Name, Val Members) Export
	Return СоздатьГруппу(AccessParameters, Name, Members);
EndFunction

Function GetGroupInformation(Val AccessParameters, Val ChatID) Export
	Return ПолучитьИнформациюОГруппе(AccessParameters, ChatID);
EndFunction

Function UpdateGroupName(Val AccessParameters, Val ChatID, Val Name) Export
	Return ИзменитьИмяГруппы(AccessParameters, ChatID, Name);
EndFunction

Function SetGroupPicture(Val AccessParameters, Val ChatID, Val Image) Export
	Return УстановитьКартинкуГруппы(AccessParameters, ChatID, Image);
EndFunction

Function ChangeGroupSettings(Val AccessParameters, Val ChatID, Val Settings) Export
	Return ИзменитьНастройкиГруппы(AccessParameters, ChatID, Settings);
EndFunction

Function AddGroupMember(Val AccessParameters, Val ChatID, Val MemberID) Export
	Return ДобавитьУчастникаГруппы(AccessParameters, ChatID, MemberID);
EndFunction

Function RemoveGroupMember(Val AccessParameters, Val ChatID, Val MemberID) Export
	Return УдалитьУчастникаГруппы(AccessParameters, ChatID, MemberID);
EndFunction

Function SetAdminRights(Val AccessParameters, Val ChatID, Val MemberID) Export
	Return НазначитьПраваАдминистратора(AccessParameters, ChatID, MemberID);
EndFunction

Function RevokeAdminRights(Val AccessParameters, Val ChatID, Val MemberID) Export
	Return ОтозватьПраваАдминистратора(AccessParameters, ChatID, MemberID);
EndFunction

Function LeaveGroup(Val AccessParameters, Val ChatID) Export
	Return ВыйтиИзГруппы(AccessParameters, ChatID);
EndFunction

Function GetGroupSettingsStructure(Val Clear = False, Val AsMap = False) Export
	Return ПолучитьСтруктуруНастроекГруппы(Clear, AsMap);
EndFunction

Function SendTextMessage(Val AccessParameters, Val ChatID, Val Text, Val TypingTime = 0) Export
	Return ОтправитьТекстовоеСообщение(AccessParameters, ChatID, Text, TypingTime);
EndFunction

Function SendFile(Val AccessParameters, Val ChatID, Val File, Val FileName, Val Description = "", Val TypingTime = 0, Val TypingType = "file") Export
	Return ОтправитьФайл(AccessParameters, ChatID, File, FileName, Description, TypingTime, TypingType);
EndFunction

Function SendFileByURL(Val AccessParameters, Val ChatID, Val FileURL, Val FileName, Val Description = "", Val TypingTime = 0, Val TypingType = "file") Export
	Return ОтправитьФайлПоURL(AccessParameters, ChatID, FileURL, FileName, Description, TypingTime, TypingType);
EndFunction

Function GetNotification(Val AccessParameters, Val Timeout = 5, Val Delete = False) Export
	Return ПолучитьУведомление(AccessParameters, Timeout, Delete);
EndFunction

Function DeleteNotification(Val AccessParameters, Val NotificationID) Export
	Return УдалитьУведомление(AccessParameters, NotificationID);
EndFunction

Function GetChatMessageHistory(Val AccessParameters, Val ChatID, Val Count = 100) Export
	Return ПолучитьИсториюСообщенийЧата(AccessParameters, ChatID, Count);
EndFunction

Function GetChatMessage(Val AccessParameters, Val ChatID, Val MessageID) Export
	Return ПолучитьСообщениеЧата(AccessParameters, ChatID, MessageID);
EndFunction

Function GetIncomingMessageLog(Val AccessParameters, Val Period = 1440) Export
	Return ПолучитьЖурналВходящихСообщений(AccessParameters, Period);
EndFunction

Function GetOutgoingMessageLog(Val AccessParameters, Val Period = 1440) Export
	Return ПолучитьЖурналИсходящихСообщений(AccessParameters, Period);
EndFunction

Function MarkMessagesAsRead(Val AccessParameters, Val ChatID, Val MessageID = "") Export
	Return ОтметитьСообщенияКакПрочитанные(AccessParameters, ChatID, MessageID);
EndFunction

Function GetOutgoingMessageCount(Val AccessParameters) Export
	Return ПолучитьКоличествоСообщенийКОтправке(AccessParameters);
EndFunction

Function GetOutgoingMessageQueue(Val AccessParameters) Export
	Return ПолучитьОчередьСообщенийКОтправке(AccessParameters);
EndFunction

Function ClearOutgoingMessageQueue(Val AccessParameters) Export
	Return ОчиститьОчередьСообщенийКОтправке(AccessParameters);
EndFunction

Function GetIncomingNotificationCount(Val AccessParameters) Export
	Return ПолучитьКоличествоВходящихУведомлений(AccessParameters);
EndFunction

Function ClearIncomingNotificationQueue(Val AccessParameters) Export
	Return ОчиститьОчередьВходящихУведомлений(AccessParameters);
EndFunction

#EndRegion