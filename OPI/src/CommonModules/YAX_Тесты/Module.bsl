// Набор тестов для YAxUnit
// @skip-check undefined-variable
// BSLLS:Typo-off

#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
   
    ЮТТесты 
 		.ДобавитьТестовыйНабор("Телеграм")               								
            .ДобавитьСерверныйТест("Телеграм_ПолучитьИнформациюБота"      , "Получить информацию бота") 	
			.ДобавитьСерверныйТест("Телеграм_ПолучитьОбновления"          , "Получить обновления")
			.ДобавитьСерверныйТест("Телеграм_УстановитьWebhook"           , "Установить Webhook")
			.ДобавитьСерверныйТест("Телеграм_УдалитьWebhook"              , "Удалить Webhook")
			.ДобавитьСерверныйТест("Телеграм_ОтправитьТекстовоеСообщение" , "Отправить текстовое сообщение")
			.ДобавитьСерверныйТест("Телеграм_ОтправитьКартинку"           , "Отправить картинку")
			.ДобавитьСерверныйТест("Телеграм_ОтправитьВидео"              , "Отправить видео")
            .ДобавитьСерверныйТест("Телеграм_ОтправитьАудио"              , "Отправить аудио")
            .ДобавитьСерверныйТест("Телеграм_ОтправитьДокумент"           , "Отправить документ")
            .ДобавитьСерверныйТест("Телеграм_ОтправитьГифку"              , "Отправить гифку")
            .ДобавитьСерверныйТест("Телеграм_ОтправитьМедиагруппу"        , "Отправить медиагруппу")
            .ДобавитьСерверныйТест("Телеграм_ОтправитьМестоположение"     , "Отправить местоположение")
            .ДобавитьСерверныйТест("Телеграм_ОтправитьКонтакт"            , "Отправить контакт")
            .ДобавитьСерверныйТест("Телеграм_ОтправитьОпрос"              , "Отправить опрос")
            .ДобавитьСерверныйТест("Телеграм_ПереслатьСообщение"          , "Переслать сообщение")
            .ДобавитьСерверныйТест("Телеграм_БанРазбан"                   , "Бан/Разбан")
            .ДобавитьСерверныйТест("Телеграм_СоздатьСсылкуПриглашение"    , "Создать ссылку-приглашение")
            .ДобавитьСерверныйТест("Телеграм_ЗакрепитьОткрепитьСообщение" , "Закрепить/открепить сообщение")
            .ДобавитьСерверныйТест("Телеграм_ПолучитьЧислоУчастников"     , "Получить число участников")
            
        .ДобавитьТестовыйНабор("ВКонтакте")
            .ДобавитьСерверныйТест("ВК_СоздатьСсылкуТокена"               , "Создать ссылку получения токена")
            .ДобавитьСерверныйТест("ВК_СоздатьУдалитьПост"                , "Создать/Удалить пост")
            .ДобавитьСерверныйТест("ВК_СоздатьОпрос"                      , "Создать опрос")
            .ДобавитьСерверныйТест("ВК_СохранитьУдалитьКартинку"          , "Добавить/удалить картинку и альбом")
            .ДобавитьСерверныйТест("ВК_СоздатьИсторию"                    , "Создать историю")
            .ДобавитьСерверныйТест("ВК_МетодыОбсуждений"                  , "Действия с обсуждениями")
            .ДобавитьСерверныйТест("ВК_ЛайкРепостКоммент"                 , "Лайк/Репост/Комментарий")
            .ДобавитьСерверныйТест("ВК_ПолучитьСтатистику"                , "Получить статистику")
            .ДобавитьСерверныйТест("ВК_ПолучитьСтатистикуПостов"          , "Получить статистику постов")
            .ДобавитьСерверныйТест("ВК_СоздатьРекламнуюКампанию"          , "Создать рекламную кампанию")
            .ДобавитьСерверныйТест("ВК_ОтправитьСообщение"                , "Отправить сообщение")
            .ДобавитьСерверныйТест("ВК_ПолучитьКатегорииТоваров"          , "Получить категории товаров")
            .ДобавитьСерверныйТест("ВК_СоздатьТоварПодборку"              , "Создать товар и подборку")
            .ДобавитьСерверныйТест("ВК_СоздатьТоварСоСвойствами"          , "Создать товар со свойствами")
            .ДобавитьСерверныйТест("ВК_ПолучитьСписокТоваров"             , "Получить список товаров")
            .ДобавитьСерверныйТест("ВК_ПолучитьСписокПодборок"            , "Получить список подборок")
            .ДобавитьСерверныйТест("ВК_ПолучитьСписокСвойств"             , "Получить список свойств")
                       
        .ДобавитьТестовыйНабор("Яндекс.Диск")
            .ДобавитьСерверныйТест("ЯДиск_ПолучитьИнформациюОДиске"       , "Получить информацию о диске")
            .ДобавитьСерверныйТест("ЯДиск_СоздатьПапку"                   , "Создать папку")
            .ДобавитьСерверныйТест("ЯДиск_ЗагрузитьПоАдресуПолучитьОбъект", "Загрузить по URL/Получить объект")
            .ДобавитьСерверныйТест("ЯДиск_ЗагрузитьУдалитьФайл"           , "Загрузить/Удалить файл")
            .ДобавитьСерверныйТест("ЯДиск_СоздатьКопиюОбъекта"            , "Создать копию объекта")
            .ДобавитьСерверныйТест("ЯДиск_ПолучитьСсылкуНаСкачивание"     , "Получить ссылку на скачивание")
            .ДобавитьСерверныйТест("ЯДиск_ПолучитьСписокФайлов"           , "Получить список файлов")
            .ДобавитьСерверныйТест("ЯДиск_ПереместитьОбъект"              , "Переместить объект")
            .ДобавитьСерверныйТест("ЯДиск_ДействияПубличныхОбъектов"      , "Действия с публичными объектами")
            .ДобавитьСерверныйТест("ЯДиск_ПолучитьСписокОпубликованных"   , "Получить список опубликованных");
            
КонецПроцедуры

#Область Тесты

#Область Telegram

Процедура Телеграм_ПолучитьИнформациюБота() Экспорт

    Токен 		= ПолучитьПараметр("Телеграм_Токен");
	Результат 	= OPI_Telegram.ПолучитьИнформациюБота(Токен);
	
	ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип("Соответствие") 
        .Заполнено()
		.Свойство("ok").Равно(Истина)
		.Свойство("result.username").Заполнено();
		
КонецПроцедуры

Процедура Телеграм_ПолучитьОбновления() Экспорт
	
	Токен 		= ПолучитьПараметр("Телеграм_Токен");
	Результат 	= OPI_Telegram.ПолучитьОбновления(Токен);

	OPI_Telegram.УдалитьWebhook(Токен);
	
	ЮТест.ОжидаетЧто(Результат) 
		.ИмеетТип("Соответствие") 
		.Заполнено()
		.Свойство("ok").Равно(Истина)
		.Свойство("result").ИмеетТип(Тип("Массив"));

КонецПроцедуры

Процедура Телеграм_УстановитьWebhook() Экспорт
	
	Токен 		= ПолучитьПараметр("Телеграм_Токен");
	URL 		= ПолучитьПараметр("Телеграм_URL");

	Результат 	= OPI_Telegram.УстановитьWebhook(Токен, URL);

	ЮТест.ОжидаетЧто(Результат) 
		.ИмеетТип("Соответствие") 
		.Заполнено()
		.Свойство("ok").Равно(Истина)
		.Свойство("result").Равно(Истина)
		.Свойство("description").Равно("Webhook was set");
		
КонецПроцедуры
	
Процедура Телеграм_УдалитьWebhook() Экспорт
	
	Токен 		= ПолучитьПараметр("Телеграм_Токен");
	
	Результат 	= OPI_Telegram.УдалитьWebhook(Токен);
	
	ЮТест.ОжидаетЧто(Результат) 
		.ИмеетТип("Соответствие") 
		.Заполнено()
		.Свойство("ok").Равно(Истина)
		.Свойство("result").Равно(Истина)
		.Свойство("description").Равно("Webhook was deleted");
	                   
КонецПроцедуры

Процедура Телеграм_ОтправитьТекстовоеСообщение() Экспорт
	
	Токен  	 = ПолучитьПараметр("Телеграм_Токен");
	IDЧата 	 = ПолучитьПараметр("Телеграм_Чат");
	IDКанала = ПолучитьПараметр("Телеграм_Канал");
	Текст    = "Сообщение из автоматического теста";
	
	МассивРезультатов = Новый Массив;
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, IDЧата, Текст));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, IDКанала, Текст));
    
    IDСообщения = OPI_Инструменты.ЧислоВСтроку(МассивРезультатов[1]["result"]["message_id"]);
    ЗаписатьПараметр("Телеграм_IDСообщенияКанала", IDСообщения);
    
	Для Каждого Результат Из МассивРезультатов Цикл
		
		ЮТест.ОжидаетЧто(Результат) 
			.ИмеетТип("Соответствие") 
			.Заполнено()
			.Свойство("ok").Равно(Истина)
			.Свойство("result.text").Равно(Текст);
		
	КонецЦикла;
		
КонецПроцедуры

Процедура Телеграм_ОтправитьКартинку() Экспорт
	
	Токен  	 = ПолучитьПараметр("Телеграм_Токен");
	IDЧата 	 = ПолучитьПараметр("Телеграм_Чат");
	IDКанала = ПолучитьПараметр("Телеграм_Канал");
	Текст    = "Сообщение из автоматического теста";
	Картинка = ПолучитьДвоичные("Картинка");
	ИВФ      = ПолучитьИмяВременногоФайла("png");
	Картинка.Записать(ИВФ);
	
	МассивРезультатов = Новый Массив;
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьКартинку(Токен, IDЧата  , Текст, Картинка));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьКартинку(Токен, IDКанала, Текст, Картинка));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьКартинку(Токен, IDЧата  , Текст, ИВФ));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьКартинку(Токен, IDКанала, Текст, ИВФ));
	
	Для Каждого Результат Из МассивРезультатов Цикл

		ЮТест.ОжидаетЧто(Результат) 
			.ИмеетТип("Соответствие") 
			.Заполнено()
			.Свойство("ok").Равно(Истина)
			.Свойство("result.caption").Равно(Текст)
			.Свойство("result.photo").ИмеетТип(Тип("Массив"));
		
	КонецЦикла;
	
	УдалитьФайлы(ИВФ);
	
КонецПроцедуры

Процедура Телеграм_ОтправитьВидео() Экспорт
	
	Токен  	 = ПолучитьПараметр("Телеграм_Токен");
	IDЧата 	 = ПолучитьПараметр("Телеграм_Чат");
	IDКанала = ПолучитьПараметр("Телеграм_Канал");
	Текст    = "Сообщение из автоматического теста";
	Видео    = ПолучитьДвоичные("Видео");
	ИВФ      = ПолучитьИмяВременногоФайла("mp4");
	Видео.Записать(ИВФ);
	
	МассивРезультатов = Новый Массив;
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьВидео(Токен, IDЧата  , Текст, Видео));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьВидео(Токен, IDКанала, Текст, Видео));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьВидео(Токен, IDЧата  , Текст, ИВФ));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьВидео(Токен, IDКанала, Текст, ИВФ));
	
	Для Каждого Результат Из МассивРезультатов Цикл

		ЮТест.ОжидаетЧто(Результат) 
			.ИмеетТип("Соответствие") 
			.Заполнено()
			.Свойство("ok").Равно(Истина)
			.Свойство("result.caption").Равно(Текст)
			.Свойство("result.video.mime_type").Равно("video/mp4");
		
	КонецЦикла;
	
	УдалитьФайлы(ИВФ);
	
КонецПроцедуры

Процедура Телеграм_ОтправитьАудио() Экспорт
	
	Токен  	 = ПолучитьПараметр("Телеграм_Токен");
	IDЧата 	 = ПолучитьПараметр("Телеграм_Чат");
	IDКанала = ПолучитьПараметр("Телеграм_Канал");
	Текст    = "Сообщение из автоматического теста";
	Аудио    = ПолучитьДвоичные("Аудио");
	ИВФ      = ПолучитьИмяВременногоФайла("mp3");
	Аудио.Записать(ИВФ);
	
	МассивРезультатов = Новый Массив;
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьАудио(Токен, IDЧата  , Текст, Аудио));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьАудио(Токен, IDКанала, Текст, Аудио));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьАудио(Токен, IDЧата  , Текст, ИВФ));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьАудио(Токен, IDКанала, Текст, ИВФ));
	
	Для Каждого Результат Из МассивРезультатов Цикл

		ЮТест.ОжидаетЧто(Результат) 
			.ИмеетТип("Соответствие") 
			.Заполнено()
			.Свойство("ok").Равно(Истина)
			.Свойство("result.caption").Равно(Текст)
			.Свойство("result.audio.mime_type").Равно("audio/mpeg");
		
	КонецЦикла;
	
	УдалитьФайлы(ИВФ);
	
КонецПроцедуры

Процедура Телеграм_ОтправитьДокумент() Экспорт
	
	Токен  	 = ПолучитьПараметр("Телеграм_Токен");
	IDЧата 	 = ПолучитьПараметр("Телеграм_Чат");
	IDКанала = ПолучитьПараметр("Телеграм_Канал");
	Текст    = "Сообщение из автоматического теста";
	Документ = ПолучитьДвоичные("Документ");
	ИВФ      = ПолучитьИмяВременногоФайла("docx");
	Документ.Записать(ИВФ);
	
	МассивРезультатов = Новый Массив;
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьДокумент(Токен, IDЧата  , Текст, Документ));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьДокумент(Токен, IDКанала, Текст, Документ));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьДокумент(Токен, IDЧата  , Текст, ИВФ));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьДокумент(Токен, IDКанала, Текст, ИВФ));
	
	Для Каждого Результат Из МассивРезультатов Цикл

		ЮТест.ОжидаетЧто(Результат) 
			.ИмеетТип("Соответствие") 
			.Заполнено()
			.Свойство("ok").Равно(Истина)
			.Свойство("result.caption").Равно(Текст)
			.Свойство("result.document").ИмеетТип("Соответствие").Заполнено();
		
	КонецЦикла;
	
	УдалитьФайлы(ИВФ);
	
КонецПроцедуры

Процедура Телеграм_ОтправитьГифку() Экспорт
	
	Токен  	 = ПолучитьПараметр("Телеграм_Токен");
	IDЧата 	 = ПолучитьПараметр("Телеграм_Чат");
	IDКанала = ПолучитьПараметр("Телеграм_Канал");
	Текст    = "Сообщение из автоматического теста";
	Гифка    = ПолучитьДвоичные("Гифка");
	ИВФ      = ПолучитьИмяВременногоФайла("gif");
	Гифка.Записать(ИВФ);
	
	МассивРезультатов = Новый Массив;
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьГифку(Токен, IDЧата  , Текст, Гифка));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьГифку(Токен, IDКанала, Текст, Гифка));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьГифку(Токен, IDЧата  , Текст, ИВФ));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьГифку(Токен, IDКанала, Текст, ИВФ));
	
	Для Каждого Результат Из МассивРезультатов Цикл

		ЮТест.ОжидаетЧто(Результат) 
			.ИмеетТип("Соответствие") 
			.Заполнено()
			.Свойство("ok").Равно(Истина)
			.Свойство("result.caption").Равно(Текст)
            .Свойство("result.document").ИмеетТип("Соответствие").Заполнено()
			.Свойство("result.animation.mime_type").Равно("video/mp4");
		
	КонецЦикла;
	
	УдалитьФайлы(ИВФ);
	
КонецПроцедуры

Процедура Телеграм_ОтправитьМедиагруппу() Экспорт
    
    Токен    = ПолучитьПараметр("Телеграм_Токен");
	IDЧата   = ПолучитьПараметр("Телеграм_Чат");
	IDКанала = ПолучитьПараметр("Телеграм_Канал");
	Текст    = "Сообщение из автоматического теста";
	
	Картинка     = ПолучитьДвоичные("Картинка");
    ИВФ_Картинка = ПолучитьИмяВременногоФайла("png");
    Картинка.Записать(ИВФ_Картинка);
    
	Видео        = ПолучитьДвоичные("Видео");
	ИВФ_Видео    = ПолучитьИмяВременногоФайла("mp4");
	Видео.Записать(ИВФ_Видео);
	    
    МедиагруппаЧат    = Новый Соответствие;
    МедиагруппаЧат.Вставить(Картинка , "photo");
    МедиагруппаЧат.Вставить(ИВФ_Видео, "video");
    
    МедиагруппаКанал = Новый Соответствие;
    МедиагруппаКанал.Вставить(ИВФ_Картинка, "photo");   
    МедиагруппаКанал.Вставить(Видео       , "video");
    
    МассивРезультатов = Новый Массив;
    МассивРезультатов.Добавить(OPI_Telegram.ОтправитьМедиагруппу(Токен, IDЧата, Текст, МедиагруппаЧат));
    МассивРезультатов.Добавить(OPI_Telegram.ОтправитьМедиагруппу(Токен, IDКанала, Текст, МедиагруппаКанал));
    
    Для Каждого Результат Из МассивРезультатов Цикл

		ЮТест.ОжидаетЧто(Результат) 
			.ИмеетТип("Соответствие") 
			.Заполнено()
			.Свойство("ok").Равно(Истина)
            .Свойство("result").ИмеетТип("Массив").ИмеетДлину(2);
		
	КонецЦикла;

    УдалитьФайлы(ИВФ_Картинка);
    УдалитьФайлы(ИВФ_Видео);
		                  
КонецПроцедуры

Процедура Телеграм_ОтправитьМестоположение() Экспорт
    
    Токен    = ПолучитьПараметр("Телеграм_Токен");
	IDЧата   = ПолучитьПараметр("Телеграм_Чат");
	IDКанала = ПолучитьПараметр("Телеграм_Канал");
    Ширина   = "48.87373649724122";
    Долгота  = "48.87373649724122";

    МассивРезультатов = Новый Массив;
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьМестоположение(Токен, IDЧата  , Ширина, Долгота));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьМестоположение(Токен, IDКанала, Ширина, Долгота));
	
	Для Каждого Результат Из МассивРезультатов Цикл
		
		ЮТест.ОжидаетЧто(Результат) 
			.ИмеетТип("Соответствие") 
			.Заполнено()
			.Свойство("ok").Равно(Истина)
            .Свойство("result.location").ИмеетТип("Соответствие").Заполнено();
		
	КонецЦикла;

КонецПроцедуры

Процедура Телеграм_ОтправитьКонтакт() Экспорт
    
    Токен    = ПолучитьПараметр("Телеграм_Токен");
	IDЧата   = ПолучитьПараметр("Телеграм_Чат");
	IDКанала = ПолучитьПараметр("Телеграм_Канал");
    Имя      = "Петр";
    Фамилия  = "Петров";
    Телефон  = "88005553535";

    МассивРезультатов = Новый Массив;
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьКонтакт(Токен, IDЧата  , Имя, Фамилия, Телефон));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьКонтакт(Токен, IDКанала, Имя, Фамилия, Телефон));
	
	Для Каждого Результат Из МассивРезультатов Цикл
		
		ЮТест.ОжидаетЧто(Результат) 
			.ИмеетТип("Соответствие") 
			.Заполнено()
			.Свойство("ok").Равно(Истина)
            .Свойство("result.contact").ИмеетТип("Соответствие").Заполнено()
            .Свойство("result.contact.first_name").Равно(Имя);
		
	КонецЦикла;

КонецПроцедуры

Процедура Телеграм_ОтправитьОпрос() Экспорт
    
    Токен    = ПолучитьПараметр("Телеграм_Токен");
	IDЧата   = ПолучитьПараметр("Телеграм_Чат");
	IDКанала = ПолучитьПараметр("Телеграм_Канал");
    
    Вопрос   = "Какой ваш любимый цвет?";
    
    МассивОтветов     = Новый Массив;
    МассивОтветов.Добавить("Красный");
    МассивОтветов.Добавить("Желтый");
    МассивОтветов.Добавить("Зеленый");
    
    МассивРезультатов = Новый Массив;
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьОпрос(Токен, IDЧата  , Вопрос, МассивОтветов, Ложь));
	МассивРезультатов.Добавить(OPI_Telegram.ОтправитьОпрос(Токен, IDКанала, Вопрос, МассивОтветов, Истина));
	
	Для Каждого Результат Из МассивРезультатов Цикл
		
		ЮТест.ОжидаетЧто(Результат) 
			.ИмеетТип("Соответствие") 
			.Заполнено()
			.Свойство("ok").Равно(Истина)
            .Свойство("result.poll").ИмеетТип("Соответствие").Заполнено()
            .Свойство("result.poll.question").Равно(Вопрос);
		
	КонецЦикла;

КонецПроцедуры

Процедура Телеграм_ПереслатьСообщение() Экспорт
    
    Токен       = ПолучитьПараметр("Телеграм_Токен");
	IDЧата      = ПолучитьПараметр("Телеграм_Чат");
	IDКанала    = ПолучитьПараметр("Телеграм_Канал");
    IDСообщения = ПолучитьПараметр("Телеграм_IDСообщенияКанала");

    ЮТест.ОжидаетЧто(OPI_Telegram.ПереслатьСообщение(Токен, IDСообщения, IDКанала, IDЧата))
        .ИмеетТип("Соответствие") 
        .Заполнено()
        .Свойство("ok").Равно(Истина)
        .Свойство("result.forward_origin.message_id").Равно(Число(IDСообщения));
        
КонецПроцедуры

Процедура Телеграм_БанРазбан() Экспорт
    
    Токен       = ПолучитьПараметр("Телеграм_Токен");
    IDЧата      = ПолучитьПараметр("Телеграм_Чат");
    IDКанала    = ПолучитьПараметр("Телеграм_Канал");
    
    МассивРезультатов = Новый Массив;
    МассивРезультатов.Добавить(OPI_Telegram.Бан(Токен, IDКанала, IDЧата));
    МассивРезультатов.Добавить(OPI_Telegram.Разбан(Токен, IDКанала, IDЧата));
    
    Для Каждого Результат Из МассивРезультатов Цикл
        
        ЮТест.ОжидаетЧто(Результат)
            .ИмеетТип("Соответствие") 
            .Заполнено()
            .Свойство("description").Равно("Bad Request: can't remove chat owner");
        
    КонецЦикла;
    
КонецПроцедуры

Процедура Телеграм_СоздатьСсылкуПриглашение() Экспорт
    
    Час           = 3600;
    ТекущаяДата   = ТекущаяДатаСеанса();
    Токен         = ПолучитьПараметр("Телеграм_Токен");
    IDКанала      = ПолучитьПараметр("Телеграм_Канал");
    Заголовок     = "Ссылка " + Строка(ТекущаяДата); 
    Истечение     = ТекущаяДата + Час;
    UnixИстечение = Число(OPI_Инструменты.UNIXTime(Истечение));
                                              
    Результат = OPI_Telegram.СоздатьСсылкуПриглашение(Токен, IDКанала, Заголовок, Истечение, 200);
    
    ЮТест.ОжидаетЧто(Результат)
        .ИмеетТип("Соответствие") 
        .Заполнено()
        .Свойство("ok").Равно(Истина)
        .Свойство("result.member_limit").Равно(200)
        .Свойство("result.name").Равно(Заголовок)
        .Свойство("result.expire_date").Равно(UnixИстечение);

КонецПроцедуры

Процедура Телеграм_ЗакрепитьОткрепитьСообщение() Экспорт
    
    Токен         = ПолучитьПараметр("Телеграм_Токен");
    IDКанала      = ПолучитьПараметр("Телеграм_Канал");
    IDСообщения   = ПолучитьПараметр("Телеграм_IDСообщенияКанала");
    
    МассивРезультатов = Новый Массив;
    МассивРезультатов.Добавить(OPI_Telegram.ЗакрепитьСообщение(Токен, IDКанала, IDСообщения));
    МассивРезультатов.Добавить(OPI_Telegram.ОткрепитьСообщение(Токен, IDКанала, IDСообщения));
    
    Для Каждого Результат Из МассивРезультатов Цикл
        
        ЮТест.ОжидаетЧто(Результат)
        .ИмеетТип("Соответствие") 
        .Заполнено()
        .Свойство("ok").Равно(Истина)
        .Свойство("result").Равно(Истина);
        
    КонецЦикла;

КонецПроцедуры

Процедура Телеграм_ПолучитьЧислоУчастников() Экспорт
    
    Токен         = ПолучитьПараметр("Телеграм_Токен");
    IDКанала      = ПолучитьПараметр("Телеграм_Канал");

    Результат  = OPI_Telegram.ПолучитьЧислоУчастников(Токен, IDКанала);
    
    ЮТест.ОжидаетЧто(Результат)
        .ИмеетТип("Соответствие") 
        .Заполнено()
        .Свойство("ok").Равно(Истина)
        .Свойство("result").ИмеетТип(Тип("Число"));

КонецПроцедуры

#КонецОбласти

#Область ВК

Процедура ВК_СоздатьСсылкуТокена() Экспорт
    
    Приложение = ПолучитьПараметр("ВК_Приложение");
    Результат  = OPI_VK.СоздатьСсылкуПолученияТокена(Приложение);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип("Строка")
        .Заполнено();
    
КонецПроцедуры

Процедура ВК_СоздатьУдалитьПост() Экспорт
    
    Параметры = ПолучитьПараметрыВК();
    Текст     = "Пост из автотеста";
    URL       = "https://github.com/Bayselonarrend/OpenIntegrations";
	Картинка  = ПолучитьДвоичные("Картинка");
	ИВФ       = ПолучитьИмяВременногоФайла("png");   
	Картинка.Записать(ИВФ);
	
	МассивРезультатов = Новый Массив;
    
	МассивРезультатов.Добавить(OPI_VK.СоздатьПост(Текст, Картинка, Ложь  ,    , Параметры));
    ЮТОбщий.Пауза(10);
    
    МассивРезультатов.Добавить(OPI_VK.СоздатьПост(Текст, ИВФ     , Истина, URL, Параметры));
    ЮТОбщий.Пауза(10);
    
    Для Каждого Результат Из МассивРезультатов Цикл

		ЮТест.ОжидаетЧто(Результат) 
			.ИмеетТип("Соответствие") 
			.Заполнено()
            .Свойство("response.post_id").ИмеетТип("Число").Заполнено();
            
        ИДПоста  = Результат["response"]["post_id"];
        Удаление = OPI_VK.УдалитьПост(ИДПоста, Параметры);
        
        ЮТест.ОжидаетЧто(Удаление) 
            .ИмеетТип("Соответствие") 
            .Заполнено()
            .Свойство("response").ИмеетТип("Число").Равно(1);
            
	КонецЦикла;
	
	УдалитьФайлы(ИВФ);

КонецПроцедуры

Процедура ВК_СоздатьОпрос() Экспорт
    
    Параметры = ПолучитьПараметрыВК();
    Вопрос    = "Вопрос из автотеста";
    
    МассивВариантов = Новый Массив;
    МассивВариантов.Добавить("Вариант 1");
    МассивВариантов.Добавить("Вариант 2");
    МассивВариантов.Добавить("Вариант 3");
    
    Результат = OPI_VK.СоздатьОпрос(Вопрос, МассивВариантов, , Параметры);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип("Соответствие") 
        .Заполнено()
        .Свойство("response.post_id").ИмеетТип("Число").Заполнено();
        
    ИДПоста  = Результат["response"]["post_id"];
    OPI_VK.УдалитьПост(ИДПоста, Параметры);
       
КонецПроцедуры

Процедура ВК_СохранитьУдалитьКартинку() Экспорт
    
    Параметры        = ПолучитьПараметрыВК();
    Имя              = "Альбом из автотеста";
    Описание         = "Новый альбом из автотеста";
    ОписаниеКартинки = "Картинка автотест";
    Картинка         = ПолучитьДвоичные("Картинка");
    ИВФ              = ПолучитьИмяВременногоФайла("png"); 
    ТипСоответствие  = Тип("Соответствие");  
    Response         = "response";
    Картинка.Записать(ИВФ);
      
    Альбом    = OPI_VK.СоздатьАльбом(Имя, Описание, Параметры);
    
    ЮТест.ОжидаетЧто(Альбом) 
        .ИмеетТип(ТипСоответствие) 
        .Заполнено()
        .Свойство("response.description").Равно(Описание);
        
    ИДАльбома  = Альбом[Response]["id"];
    
    МассивРезультатов = Новый Массив;
    
    МассивРезультатов.Добавить(OPI_VK.СохранитьКартинкуВАльбом(ИДАльбома, Картинка, ОписаниеКартинки, Параметры));
    ЮТОбщий.Пауза(10);
    
    МассивРезультатов.Добавить(OPI_VK.СохранитьКартинкуВАльбом(ИДАльбома, ИВФ     , ОписаниеКартинки, Параметры));
    ЮТОбщий.Пауза(10);
    
    Для Каждого Результат Из МассивРезультатов Цикл
        
        ЮТест.ОжидаетЧто(Результат) 
            .ИмеетТип(ТипСоответствие) 
            .Заполнено()
            .Свойство("response[0].text").Равно(ОписаниеКартинки)
            .Свойство("response[0].album_id").Равно(ИДАльбома);
        
        ИДКартинки  = Результат[Response][0]["id"];
        Удаление    = OPI_VK.УдалитьКартинку(ИДКартинки, Параметры);
        
        ЮТест.ОжидаетЧто(Удаление) 
            .ИмеетТип(ТипСоответствие) 
            .Заполнено()
            .Свойство(Response).ИмеетТип("Число").Равно(1);
        
    КонецЦикла;
    
    Удаление = OPI_VK.УдалитьАльбом(ИДАльбома, Параметры);
    
    ЮТест.ОжидаетЧто(Удаление) 
        .ИмеетТип(ТипСоответствие) 
        .Заполнено()
        .Свойство(Response).ИмеетТип("Число").Равно(1);
        
    УдалитьФайлы(ИВФ);
    
КонецПроцедуры

Процедура ВК_СоздатьИсторию() Экспорт
    
    Параметры = ПолучитьПараметрыВК();
    URL       = "https://github.com/Bayselonarrend/OpenIntegrations";
    Картинка  = ПолучитьДвоичные("Картинка");
    ИВФ       = ПолучитьИмяВременногоФайла("png");   
    Картинка.Записать(ИВФ);
    
    МассивРезультатов = Новый Массив;
    
    МассивРезультатов.Добавить(OPI_VK.СоздатьИсторию(ИВФ          , URL, Параметры));
    ЮТОбщий.Пауза(10);
    
    МассивРезультатов.Добавить(OPI_VK.СоздатьИсторию(Картинка,    , Параметры));
    ЮТОбщий.Пауза(10);
    
    Для Каждого Результат Из МассивРезультатов Цикл
        
        ЮТест.ОжидаетЧто(Результат) 
            .ИмеетТип("Соответствие") 
            .Заполнено()
            .Свойство("response.count").ИмеетТип("Число").Равно(1)
            .Свойство("response.items").ИмеетТип("Массив").Заполнено();
            
    КонецЦикла;
    
    УдалитьФайлы(ИВФ);
    
КонецПроцедуры

Процедура ВК_МетодыОбсуждений() Экспорт
    
    Параметры       = ПолучитьПараметрыВК();
    Название        = "Обсуждение из автотеста";
    Сообщение       = "Сообщение из автотеста";
    ТипСоответствие = Тип("Соответствие");
    ТипЧисло        = Тип("Число");
    Response        = "response";
    
    Результат = OPI_VK.СоздатьОбсуждение(Название, Сообщение, Параметры);
    ЮТОбщий.Пауза(5);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип(ТипСоответствие) 
        .Заполнено()
        .Свойство(Response).ИмеетТип(ТипЧисло).Заполнено();
    
    ИДОбсуждения = Результат[Response];    
    Закрытие     = OPI_VK.ЗакрытьОбсуждение(ИДОбсуждения, Ложь, Параметры);
    
    ЮТест.ОжидаетЧто(Закрытие) 
        .ИмеетТип(ТипСоответствие) 
        .Заполнено()
        .Свойство(Response).ИмеетТип(ТипЧисло).Равно(1);
        
    Открытие = OPI_VK.ОткрытьОбсуждение(ИДОбсуждения, Параметры);
    ЮТОбщий.Пауза(5);
    
    ЮТест.ОжидаетЧто(Открытие) 
        .ИмеетТип(ТипСоответствие) 
        .Заполнено()
        .Свойство(Response).ИмеетТип(ТипЧисло).Равно(1);
        
    Отправка = OPI_VK.НаписатьВОбсуждение(ИДОбсуждения, Сообщение, Параметры);
    
    ЮТест.ОжидаетЧто(Отправка) 
        .ИмеетТип(ТипСоответствие) 
        .Заполнено()
        .Свойство(Response).ИмеетТип(ТипЧисло).Заполнено();
       
    Удаление = OPI_VK.ЗакрытьОбсуждение(ИДОбсуждения, Истина, Параметры);
    ЮТОбщий.Пауза(5);

    ЮТест.ОжидаетЧто(Удаление) 
        .ИмеетТип(ТипСоответствие) 
        .Заполнено()
        .Свойство(Response).ИмеетТип(ТипЧисло).Равно(1);
        
КонецПроцедуры

Процедура ВК_ЛайкРепостКоммент() Экспорт
    
    Параметры       = ПолучитьПараметрыВК();
    Текст           = "Пост из автотеста";
    Сообщение       = "Сообщение из автотеста";
    ТипСоответствие = Тип("Соответствие");
    ТипЧисло        = Тип("Число");
    Response        = "response";
    
    Результат = OPI_VK.СоздатьПост(Текст, Новый Массив, , ,Параметры);
    
    ИДПоста  = Результат[Response]["post_id"];    
    Лайк     = OPI_VK.ПоставитьЛайк(ИДПоста, , Параметры);
    ЮТОбщий.Пауза(5);
      
    ЮТест.ОжидаетЧто(Лайк) 
        .ИмеетТип(ТипСоответствие) 
        .Заполнено()
        .Свойство("response.likes").ИмеетТип(ТипЧисло).Заполнено();
        
    ВнешнийПост  = 2571;
    ВнешняяСтена = -218704372;
        
    Репост       = OPI_VK.СделатьРепост(ВнешнийПост, ВнешняяСтена, , , Параметры);
    ЮТОбщий.Пауза(5);
    
    ЮТест.ОжидаетЧто(Репост) 
        .ИмеетТип(ТипСоответствие) 
        .Заполнено()
        .Свойство("response.success").ИмеетТип(ТипЧисло).Равно(1)
        .Свойство("response.wall_repost_count").ИмеетТип(ТипЧисло).Равно(1); 
        
    Комментарий  = OPI_VK.НаписатьКомментарий(ИДПоста, Параметры["owner_id"], Сообщение, Параметры);
    ЮТОбщий.Пауза(5);
    
    ЮТест.ОжидаетЧто(Комментарий) 
        .ИмеетТип(ТипСоответствие) 
        .Заполнено()
        .Свойство("response.comment_id").ИмеетТип(ТипЧисло).Заполнено();
   
    OPI_VK.УдалитьПост(ИДПоста, Параметры);
    OPI_VK.УдалитьПост(Репост[Response]["post_id"], Параметры);

КонецПроцедуры

Процедура ВК_ПолучитьСтатистику() Экспорт
    
    Параметры       = ПолучитьПараметрыВК();
    Дата0           = НачалоДня(ТекущаяДатаСеанса());
    Дата1           = КонецДня(Дата0);
    ТипСоответствие = Тип("Соответствие");
     
    Результат = OPI_VK.ПолучитьСтатистику(Дата0, Дата1, Параметры);
    ЮТОбщий.Пауза(10);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип(ТипСоответствие) 
        .Заполнено()
        .Свойство("response[0].visitors").ИмеетТип(ТипСоответствие).Заполнено()
        .Свойство("response[0].reach").ИмеетТип(ТипСоответствие).Заполнено();

КонецПроцедуры

Процедура ВК_ПолучитьСтатистикуПостов() Экспорт
    
    Параметры    = ПолучитьПараметрыВК();
    
    МассивПостов = Новый Массив;
    МассивПостов.Добавить(214);
    МассивПостов.Добавить(215);
    
    Результат = OPI_VK.ПолучитьСтатистикуПостов(МассивПостов, Параметры);
    ЮТОбщий.Пауза(10);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип("Массив") 
        .ИмеетДлину(2);

КонецПроцедуры

Процедура ВК_СоздатьРекламнуюКампанию() Экспорт
    
    Параметры       = ПолучитьПараметрыВК();
    ИДКабинета      = ПолучитьПараметр("ВК_Кабинет");
    Наименование    = "Тестовая кампания";
    ТипСоответствие = Тип("Соответствие");
    ТипЧисло        = Тип("Число");
    Response        = "response";
    UID             = "id";
        
    Результат    = OPI_VK.СоздатьРекламнуюКампанию(ИДКабинета, Наименование, Параметры);
    Результат    = Результат[Response][0];
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип(ТипСоответствие) 
        .Свойство("error_code").ИмеетТип(ТипЧисло).Равно(602)
        .Свойство(UID).ИмеетТип(ТипЧисло).Заполнено();
        
    ИДКампании  = Результат[UID];
    ИДКатегории = 126;
    Лимит       = 150;
    
    Результат = OPI_VK.СоздатьПост(Наименование, Новый Массив, , ,Параметры); 
    ИДПоста   = Результат[Response]["post_id"]; 
        
    Объявление   = OPI_VK.СоздатьРекламноеОбъявление(ИДКампании
        , Лимит
        , ИДКатегории
        , ИДПоста
        , ИДКабинета
        , Параметры);
    Объявление   = Объявление[Response][0];
    
    ЮТест.ОжидаетЧто(Объявление) 
        .ИмеетТип(ТипСоответствие) 
        .Свойство("error_code").ИмеетТип(ТипЧисло).Равно(602)
        .Свойство(UID).ИмеетТип(ТипЧисло).Заполнено();
        
    ИДОбъявления = Объявление[UID];
    Остановка    = OPI_VK.ПриостановитьРекламноеОбъявление(ИДКабинета, ИДОбъявления, Параметры);
    Остановка    = Остановка[Response][0];
    
    ЮТест.ОжидаетЧто(Остановка) 
        .ИмеетТип(ТипСоответствие) 
        .Свойство(UID).ИмеетТип(ТипЧисло).Заполнено();
 
    OPI_VK.УдалитьПост(ИДПоста, Параметры);
    
КонецПроцедуры

Процедура ВК_ОтправитьСообщение() Экспорт
    
    Параметры    = ПолучитьПараметрыВК();
    Пользователь = ПолучитьПараметр("ВК_Пользователь");
    Токен        = ПолучитьПараметр("ВК_КТ");
    Текст        = "Сообщение из автотеста";
    
    МассивКнопок = Новый Массив;
    МассивКнопок.Добавить("Кнопка 1");
    МассивКнопок.Добавить("Кнопка 2"); 
    
    Клавиатура = OPI_VK.СформироватьКлавиатуру(МассивКнопок);
    Результат  = OPI_VK.НаписатьСообщение(Текст, Пользователь, Токен, Клавиатура, Параметры);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип("Соответствие") 
        .Свойство("response").ИмеетТип("Число").Заполнено();
  
КонецПроцедуры

Процедура ВК_ПолучитьКатегорииТоваров() Экспорт
    
    Параметры = ПолучитьПараметрыВК();
    Результат = OPI_VK.ПолучитьСписокКатегорийТоваров(Параметры);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип("Соответствие")
        .Заполнено();
    
КонецПроцедуры

Процедура ВК_СоздатьТоварПодборку() Экспорт
    
    Параметры       = ПолучитьПараметрыВК();
    ТипСоответствие = Тип("Соответствие");
    ТипЧисло        = Тип("Число");
    Response        = "response";
    Картинка        = ПолучитьДвоичные("Картинка");
    ИВФ             = ПолучитьИмяВременногоФайла("png");   
    Картинка.Записать(ИВФ);
    
    Результат = OPI_VK.СоздатьПодборкуТоваров("Тестовая подборка"
        , Картинка
        , Истина
        , Ложь
        , Параметры);  
    ЮТОбщий.Пауза(5);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип(ТипСоответствие)
        .Свойство("response.albums_count").ИмеетТип(ТипЧисло).Заполнено()
        .Свойство("response.market_album_id").ИмеетТип(ТипЧисло).Заполнено();
             
    ИДПодборки = Результат[Response]["market_album_id"];
    
    Результат  = OPI_VK.ИзменитьПодборкуТоваров("Измененная подборка", ИДПодборки, , , , Параметры);
    ЮТОбщий.Пауза(5);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип(ТипСоответствие)
        .Свойство(Response).ИмеетТип(ТипЧисло).Равно(1);
 
    МассивКартинок = Новый Массив;
    МассивКартинок.Добавить(ИВФ);
    МассивКартинок.Добавить(Картинка);
    
    Товар = Новый Соответствие();
    Товар.Вставить("Имя"                , "Тестовый товар 2");    
    Товар.Вставить("Описание"           , "Описание товара");
    Товар.Вставить("Категория"          , "20173");           
    Товар.Вставить("Цена"               , 1);                
    Товар.Вставить("СтараяЦена"         , 15);     
    Товар.Вставить("ОсновноеФото"       , Картинка);                   
    Товар.Вставить("URL"                , "https://github.com/Bayselonarrend/OpenIntegrations");     
    Товар.Вставить("ДополнительныеФото" , МассивКартинок);     
    Товар.Вставить("ГлавныйВГруппе"     , Истина);             
    Товар.Вставить("НомерГруппы"        , Неопределено);      
    Товар.Вставить("Ширина"             , 20);     
    Товар.Вставить("Высота"             , 30);     
    Товар.Вставить("Глубина"            , 40);     
    Товар.Вставить("Вес"                , 100);
    Товар.Вставить("SKU"                , 12345);
    Товар.Вставить("ДоступныйОстаток"   , "10");
    
    Результат = OPI_VK.ДобавитьТовар(Товар, ИДПодборки, Параметры);                // Добавление товара
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип(ТипСоответствие)
        .Свойство("response.market_item_id").ИмеетТип(ТипЧисло).Заполнено();
        
    ИДТовара  = Результат[Response]["market_item_id"];
    
    Товар = Новый Соответствие;
    Товар.Вставить("Имя", "Тестовый товар измененный");
    
    Результат = OPI_VK.ИзменитьТовар(ИДТовара, Товар, , Параметры);                // Изменение товара
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип(ТипСоответствие)
        .Свойство(Response).ИмеетТип(ТипЧисло).Равно(1);
    
    Результат = OPI_VK.ДобавитьТоварВПодборку(ИДТовара, ИДПодборки, Параметры);    // Добавление в подборку
    ЮТОбщий.Пауза(5);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип(ТипСоответствие)
        .Свойство(Response).ИмеетТип(ТипЧисло).Заполнено();
        
    Результат = OPI_VK.УдалитьТоварИзПодборки(ИДТовара, ИДПодборки, Параметры);    // Удаляет из подборки
    ЮТОбщий.Пауза(5);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип(ТипСоответствие)
        .Свойство(Response).ИмеетТип(ТипЧисло).Равно(1);
        
    Результат = OPI_VK.УдалитьТовар(ИДТовара, Параметры);                          // Удаление товара
    ЮТОбщий.Пауза(5);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип(ТипСоответствие)
        .Свойство(Response).ИмеетТип(ТипЧисло).Равно(1);
        
    Результат = OPI_VK.УдалитьПодборку(ИДПодборки, Параметры);                     // Уадление подборки
    ЮТОбщий.Пауза(5);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип(ТипСоответствие)
        .Свойство(Response).ИмеетТип(ТипЧисло).Равно(1);

    УдалитьФайлы(ИВФ);
    
КонецПроцедуры

Процедура ВК_СоздатьТоварСоСвойствами() Экспорт
    
    Параметры       = ПолучитьПараметрыВК();
    ТипСоответствие = Тип("Соответствие");
    ТипЧисло        = Тип("Число");
    Response        = "response";
    Желтый          = "Желтый";
    Красный         = "Красный";
    Картинка        = ПолучитьДвоичные("Картинка");
    ИВФ             = ПолучитьИмяВременногоФайла("png");   
    Картинка.Записать(ИВФ);
    
    МассивВариантов = Новый Массив;
    МассивВариантов.Добавить(Желтый);
    МассивВариантов.Добавить("Синий");
    МассивВариантов.Добавить(Красный);
    
    Результат = OPI_VK.СоздатьСвойствоТовара("Цвет", Параметры);
    ЮТОбщий.Пауза(10);
    
    Свойство  = Результат[Response]["property_id"];
    Свойство  = OPI_Инструменты.ЧислоВСтроку(Свойство);
     
    СоответствиеСвойств = Новый Соответствие;
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип(ТипСоответствие)
        .Свойство("response.property_id").ИмеетТип(ТипЧисло).Заполнено();
        
    Результат = OPI_VK.ИзменитьСвойствоТовара("Цвет (изм.)", Свойство, Параметры);
    ЮТОбщий.Пауза(10);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип(ТипСоответствие) 
        .Заполнено()
        .Свойство(Response).ИмеетТип(ТипЧисло).Равно(1);
   
    Для Каждого Вариант Из МассивВариантов Цикл
        
        Результат = OPI_VK.ДобавитьВариантСвойстваТовара(Вариант, Свойство, Параметры);
        ЮТОбщий.Пауза(5);
        
        ЮТест.ОжидаетЧто(Результат) 
            .ИмеетТип(ТипСоответствие)
            .Свойство("response.variant_id").ИмеетТип(ТипЧисло).Заполнено();
            
        ИДВарианта = Результат[Response]["variant_id"];
        СоответствиеСвойств.Вставить(Вариант, ИДВарианта);
        
        Результат = OPI_VK.ИзменитьВариантСвойстваТовара(Вариант + " (изм.)"
            , Свойство
            , ИДВарианта
            , Параметры);
        
        ЮТест.ОжидаетЧто(Результат) 
            .ИмеетТип(ТипСоответствие) 
            .Заполнено()
            .Свойство(Response).ИмеетТип(ТипЧисло).Равно(1);
              
    КонецЦикла;
  
    МассивКартинок = Новый Массив;
    МассивКартинок.Добавить(ИВФ);
    МассивКартинок.Добавить(Картинка);
    
    Товар = Новый Соответствие();
    Товар.Вставить("Имя"                , "Тестовый товар (" + Желтый + ")");    
    Товар.Вставить("Описание"           , "Описание товара");
    Товар.Вставить("Категория"          , "20173");           
    Товар.Вставить("Цена"               , 1);                
    Товар.Вставить("СтараяЦена"         , 15);     
    Товар.Вставить("ОсновноеФото"       , Картинка);                   
    Товар.Вставить("URL"                , "https://github.com/Bayselonarrend/OpenIntegrations");     
    Товар.Вставить("ДополнительныеФото" , МассивКартинок);     
    Товар.Вставить("ГлавныйВГруппе"     , Истина);             
    Товар.Вставить("НомерГруппы"        , Неопределено);      
    Товар.Вставить("Ширина"             , 20);     
    Товар.Вставить("Высота"             , 30);     
    Товар.Вставить("Глубина"            , 40);     
    Товар.Вставить("Вес"                , 100);
    Товар.Вставить("SKU"                , 12345);
    Товар.Вставить("ДоступныйОстаток"   , "10");
    Товар.Вставить("ЗначенияСвойств"    , СоответствиеСвойств[Желтый]);
    
    Результат = OPI_VK.ДобавитьТовар(Товар, , Параметры);                // Добавление товара  
    ЮТОбщий.Пауза(10);
    
    ИДЖелтого = Результат[Response]["market_item_id"];               
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип(ТипСоответствие)
        .Свойство("response.market_item_id").ИмеетТип(ТипЧисло).Заполнено();
        
    Товар.Вставить("Имя"            , "Тестовый товар (" + Красный + ")");
    Товар.Вставить("ЗначенияСвойств", СоответствиеСвойств[Красный]);

    Результат  = OPI_VK.ДобавитьТовар(Товар, , Параметры);                // Добавление товара  
    ЮТОбщий.Пауза(10);
    
    ИДКрасного = Результат[Response]["market_item_id"];               
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип(ТипСоответствие)
        .Свойство("response.market_item_id").ИмеетТип(ТипЧисло).Заполнено();
        
    МассивТоваров = Новый Массив;
    МассивТоваров.Добавить(ИДЖелтого);
    МассивТоваров.Добавить(ИДКрасного);
    
    Результат = OPI_VK.ПолучитьТоварыПоИД(МассивТоваров, Параметры);
    ЮТОбщий.Пауза(10);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип(ТипСоответствие)
        .Свойство("response.items").ИмеетТип("Массив").ИмеетДлину(2);
        
    Результат = OPI_VK.СгруппироватьТовары(МассивТоваров, , Параметры);
    ЮТОбщий.Пауза(10);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип(ТипСоответствие)
        .Свойство("response.item_group_id").ИмеетТип(ТипЧисло).Заполнено();
       
    OPI_VK.УдалитьТовар(ИДЖелтого , Параметры);
    OPI_VK.УдалитьТовар(ИДКрасного, Параметры);
    
    Для Каждого Вариант Из СоответствиеСвойств Цикл
        
        Удаление = OPI_VK.УдалитьВариантСвойстваТовара(Вариант.Значение, Параметры);
        ЮТОбщий.Пауза(5);
        
        ЮТест.ОжидаетЧто(Удаление) 
            .ИмеетТип(ТипСоответствие) 
            .Заполнено()
            .Свойство(Response).ИмеетТип(ТипЧисло).Равно(1);
        
    КонецЦикла;
        
    Удаление = OPI_VK.УдалитьСвойствоТовара(Свойство, Параметры);
    ЮТОбщий.Пауза(10);
    
    ЮТест.ОжидаетЧто(Удаление) 
        .ИмеетТип(ТипСоответствие) 
        .Заполнено()
        .Свойство(Response).ИмеетТип(ТипЧисло).Равно(1);

    УдалитьФайлы(ИВФ);
    
Конецпроцедуры

Процедура ВК_ПолучитьСписокТоваров() Экспорт
    
    Параметры = ПолучитьПараметрыВК();
    Картинка  = ПолучитьДвоичные("Картинка"); 
    
    МассивКартинок = Новый Массив;
    МассивКартинок.Добавить(Картинка);
    
    Товар = Новый Соответствие();
    Товар.Вставить("Имя"                , "Тестовый товар 2");    
    Товар.Вставить("Описание"           , "Описание товара");
    Товар.Вставить("Категория"          , "20173");           
    Товар.Вставить("Цена"               , 1);                
    Товар.Вставить("СтараяЦена"         , 15);     
    Товар.Вставить("ОсновноеФото"       , Картинка);                   
    Товар.Вставить("URL"                , "https://github.com/Bayselonarrend/OpenIntegrations");     
    Товар.Вставить("ДополнительныеФото" , МассивКартинок);     
    Товар.Вставить("ГлавныйВГруппе"     , Истина);             
    Товар.Вставить("НомерГруппы"        , Неопределено);      
    Товар.Вставить("Ширина"             , 20);     
    Товар.Вставить("Высота"             , 30);     
    Товар.Вставить("Глубина"            , 40);     
    Товар.Вставить("Вес"                , 100);
    Товар.Вставить("SKU"                , 12345);
    Товар.Вставить("ДоступныйОстаток"   , "10");
    
    Результат = OPI_VK.ДобавитьТовар(Товар, , Параметры);
    ИДТовара  = Результат["response"]["market_item_id"];
    ЮТОбщий.Пауза(5);
    
    Результат = OPI_VK.ПолучитьСписокТоваров(, Параметры);
    ЮТОбщий.Пауза(5);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип("Массив").Заполнено();
              
    OPI_VK.УдалитьТовар(ИДТовара, Параметры);
    
КонецПроцедуры

Процедура ВК_ПолучитьСписокПодборок() Экспорт
    
    Параметры = ПолучитьПараметрыВК();
    Картинка  = ПолучитьДвоичные("Картинка");    
    Результат = OPI_VK.СоздатьПодборкуТоваров("Тестовая подборка"
        , Картинка
        , Истина
        , Ложь
        , Параметры); 
        
    ИДПодборки = Результат["response"]["market_album_id"];      
    Результат  = OPI_VK.ПолучитьСписокПодборок(Параметры);
    ЮТОбщий.Пауза(5);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип("Массив").Заполнено();
        
    OPI_VK.УдалитьПодборку(ИДПодборки, Параметры); 
    
КонецПроцедуры

Процедура ВК_ПолучитьСписокСвойств() Экспорт
    
    Параметры  = ПолучитьПараметрыВК();
    Результат  = OPI_VK.ПолучитьСписокСвойств(Параметры);
    ЮТОбщий.Пауза(5);
    
    ЮТест.ОжидаетЧто(Результат) 
        .ИмеетТип("Массив").Заполнено();

КонецПроцедуры

#КонецОбласти

#Область YandexDisk

Процедура ЯДиск_ПолучитьИнформациюОДиске() Экспорт
    
    Токен        = ПолучитьПараметр("ЯДиск_Токен");
    Соответствие = "Соответствие";
    
    Результат = OPI_YandexDisk.ПолучитьИнформациюОДиске(Токен);
    
    ЮТест.ОжидаетЧто(Результат)
        .ИмеетТип(Соответствие) 
        .Заполнено()
        .Свойство("system_folders").ИмеетТип(Соответствие)
        .Свойство("user").ИмеетТип(Соответствие);
    
КонецПроцедуры

Процедура ЯДиск_СоздатьПапку() Экспорт
    
   Токен = ПолучитьПараметр("ЯДиск_Токен");
   Путь  = "/" + Строка(Новый УникальныйИдентификатор);
   
   Результат = OPI_YandexDisk.СоздатьПапку(Токен, Путь);
   
   ЮТест.ОжидаетЧто(Результат)
        .ИмеетТип("Соответствие") 
        .Заполнено()
        .Свойство("type").Равно("dir")
        .Свойство("path").Равно("disk:" + Путь);
        
  OPI_YandexDisk.УдалитьОбъект(Токен, Путь, Ложь);
  
КонецПроцедуры

Процедура ЯДиск_ЗагрузитьПоАдресуПолучитьОбъект() Экспорт
    
    Токен = ПолучитьПараметр("ЯДиск_Токен");
    Путь  = "/" + Строка(Новый УникальныйИдентификатор) + ".png";
    URL   = "https://raw.githubusercontent.com/Bayselonarrend/OpenIntegrations/main/Media/logo.png";

    OPI_YandexDisk.ЗагрузитьФайлПоURL(Токен, Путь, URL);
    ЮТОбщий.Пауза(10);
        
    Результат = OPI_YandexDisk.ПолучитьОбъект(Токен, Путь);
    
    ЮТест.ОжидаетЧто(Результат)
        .ИмеетТип("Соответствие") 
        .Заполнено()
        .Свойство("type").Равно("file")
        .Свойство("path").Равно("disk:" + Путь);

     OPI_YandexDisk.УдалитьОбъект(Токен, Путь, Ложь);
     
КонецПроцедуры

Процедура ЯДиск_ЗагрузитьУдалитьФайл() Экспорт
    
    Токен    = ПолучитьПараметр("ЯДиск_Токен");
    Путь     = "/" + Строка(Новый УникальныйИдентификатор) + ".png";
    Картинка = ПолучитьДвоичные("Картинка");
    ИВФ      = ПолучитьИмяВременногоФайла("png");
    Картинка.Записать(ИВФ);

    МассивРезультатов = Новый Массив;
    
    МассивРезультатов.Добавить(OPI_YandexDisk.ЗагрузитьФайл(Токен, Путь, Картинка, Истина));
    ЮТОбщий.Пауза(10);
    
    МассивРезультатов.Добавить(OPI_YandexDisk.УдалитьОбъект(Токен, Путь, Ложь));
    
    МассивРезультатов.Добавить(OPI_YandexDisk.ЗагрузитьФайл(Токен, Путь, ИВФ, Истина));
    ЮТОбщий.Пауза(10);
    
    МассивРезультатов.Добавить(OPI_YandexDisk.УдалитьОбъект(Токен, Путь, Ложь));
    
    Для Каждого Результат Из МассивРезультатов Цикл
        
        ЮТест.ОжидаетЧто(Результат)
            . ИмеетТип("Строка")
            . НеЗаполнено();
            
    КонецЦикла;     
              
    УдалитьФайлы(ИВФ);
    
КонецПроцедуры

Процедура ЯДиск_СоздатьКопиюОбъекта() Экспорт
    
    Токен           = ПолучитьПараметр("ЯДиск_Токен");
    ПутьОригинала   = "/" + Строка(Новый УникальныйИдентификатор) + ".png";
    ПутьКопии       = "/" + Строка(Новый УникальныйИдентификатор) + ".png";
    URL             = "https://raw.githubusercontent.com/Bayselonarrend/"
        + "OpenIntegrations/main/Media/logo.png";

    OPI_YandexDisk.ЗагрузитьФайлПоURL(Токен, ПутьОригинала, URL);
    ЮТОбщий.Пауза(10);
      
    Результат = OPI_YandexDisk.СоздатьКопиюОбъекта(Токен, ПутьОригинала, ПутьКопии, Истина);
    ЮТОбщий.Пауза(10);
    
    ЮТест.ОжидаетЧто(Результат)
        .ИмеетТип("Соответствие") 
        .Заполнено()
        .Свойство("type").Равно("file")
        .Свойство("path").Равно("disk:" + ПутьКопии);

     OPI_YandexDisk.УдалитьОбъект(Токен, ПутьОригинала, Ложь);
     OPI_YandexDisk.УдалитьОбъект(Токен, ПутьКопии, Ложь);
     
КонецПроцедуры

Процедура ЯДиск_ПолучитьСсылкуНаСкачивание() Экспорт
    
    Токен = ПолучитьПараметр("ЯДиск_Токен");
    Путь  = "/" + Строка(Новый УникальныйИдентификатор) + ".png";
    URL   = "https://raw.githubusercontent.com/Bayselonarrend/OpenIntegrations/main/Media/logo.png";

    OPI_YandexDisk.ЗагрузитьФайлПоURL(Токен, Путь, URL);
    ЮТОбщий.Пауза(10);
       
    Результат = OPI_YandexDisk.ПолучитьСсылкуДляСкачивания(Токен, Путь);
    
    ЮТест.ОжидаетЧто(Результат)
        .ИмеетТип("Соответствие") 
        .Заполнено()
        .Свойство("method").Равно("GET")
        .Свойство("href").ИмеетТип("Строка").Заполнено();

     OPI_YandexDisk.УдалитьОбъект(Токен, Путь, Ложь);

КонецПроцедуры

Процедура ЯДиск_ПолучитьСписокФайлов() Экспорт
    
    Токен = ПолучитьПараметр("ЯДиск_Токен");
    Количество = 2;
    Отступ     = 1;
    
    Результат = OPI_YandexDisk.ПолучитьСписокФайлов(Токен, Количество, Отступ, "image");
    
    ЮТест.ОжидаетЧто(Результат)
        .ИмеетТип("Соответствие") 
        .Заполнено()
        .Свойство("limit").Равно(Количество)
        .Свойство("offset").Равно(Отступ)
        .Свойство("items").ИмеетТип("Массив");
    
КонецПроцедуры

Процедура ЯДиск_ПереместитьОбъект() Экспорт
    
    Токен           = ПолучитьПараметр("ЯДиск_Токен");
    ПутьОригинала   = "/" + Строка(Новый УникальныйИдентификатор) + ".png";
    ПутьКопии       = "/" + Строка(Новый УникальныйИдентификатор) + ".png";
    URL             = "https://raw.githubusercontent.com/Bayselonarrend/"
        + "OpenIntegrations/main/Media/logo.png";

    OPI_YandexDisk.ЗагрузитьФайлПоURL(Токен, ПутьОригинала, URL);
    ЮТОбщий.Пауза(15);
      
    Результат = OPI_YandexDisk.ПереместитьОбъект(Токен, ПутьОригинала, ПутьКопии, Истина);
    
    ЮТест.ОжидаетЧто(Результат)
        .ИмеетТип("Соответствие") 
        .Заполнено()
        .Свойство("type").Равно("file")
        .Свойство("path").Равно("disk:" + ПутьКопии);

     OPI_YandexDisk.УдалитьОбъект(Токен, ПутьОригинала, Ложь);
     OPI_YandexDisk.УдалитьОбъект(Токен, ПутьКопии, Ложь);
     
КонецПроцедуры

Процедура ЯДиск_ДействияПубличныхОбъектов() Экспорт
    
    PUrl         = "public_url";
    Соответствие = "Соответствие";
    Токен        = ПолучитьПараметр("ЯДиск_Токен");
    Путь         = "/" + Строка(Новый УникальныйИдентификатор) + ".png";
    URL          = "https://raw.githubusercontent.com/Bayselonarrend/OpenIntegrations/main/Media/logo.png";
    
    OPI_YandexDisk.ЗагрузитьФайлПоURL(Токен, Путь, URL);
    ЮТОбщий.Пауза(10);
    
    МассивРезультатов = Новый Массив;
    
    МассивРезультатов.Добавить(OPI_YandexDisk.ОпубликоватьОбъект(Токен, Путь)); 
    ПубличныйURL      = МассивРезультатов[0][PUrl];
    
    МассивРезультатов.Добавить(OPI_YandexDisk.СохранитьПубличныйОбъектНаДиск(Токен, ПубличныйURL));
         
    ПубличныйОбъект   = OPI_YandexDisk.ПолучитьПубличныйОбъект(Токен, ПубличныйURL);
    СсылкаСкачивания  = OPI_YandexDisk.ПолучитьСсылкуСкачиванияПубличногоОбъекта(Токен, ПубличныйURL);    
       
    МассивРезультатов.Добавить(OPI_YandexDisk.ОтменитьПубликациюОбъекта(Токен, Путь));
    
    Счетчик = 0;
    Для Каждого Результат Из МассивРезультатов Цикл
        
        ЮТест.ОжидаетЧто(Результат)
            .ИмеетТип(Соответствие) 
            .Заполнено()
            .Свойство("type").Равно("file")
            .Свойство("path").Заполнено();
            
        Если Счетчик = 0 Тогда   
            ЮТест.ОжидаетЧто(Результат).Свойство(PUrl).ИмеетТип("Строка").Заполнено();
        Иначе
            ЮТест.ОжидаетЧто(Результат).НетСвойства(PUrl);
        КонецЕсли;
        
        Счетчик = Счетчик + 1;
        
    КонецЦикла;
    
    ЮТест.ОжидаетЧто(СсылкаСкачивания)
        .ИмеетТип(Соответствие) 
        .Заполнено()
        .Свойство("method").Равно("GET")
        .Свойство("href").ИмеетТип("Строка").Заполнено();
    
    ЮТест.ОжидаетЧто(ПубличныйОбъект)
        .ИмеетТип(Соответствие) 
        .Заполнено()
        .Свойство("type").Равно("file")
        .Свойство("path").Равно("/");
    
    OPI_YandexDisk.УдалитьОбъект(Токен, Путь, Ложь);

КонецПроцедуры

Процедура ЯДиск_ПолучитьСписокОпубликованных() Экспорт
    
    Токен = ПолучитьПараметр("ЯДиск_Токен");
    Количество = 2;
    Отступ     = 1;
    
    Результат = OPI_YandexDisk.ПолучитьСписокОпубликованныхОбъектов(Токен, Количество, Отступ);
    
    ЮТест.ОжидаетЧто(Результат)
        .ИмеетТип("Соответствие") 
        .Заполнено()
        .Свойство("limit").Равно(Количество)
        .Свойство("offset").Равно(Отступ)
        .Свойство("items").ИмеетТип("Массив");
    
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьПараметр(Параметр) 

	ИмяСправочника  = "ЮТПараметры";
	ИмяРеквизита    = "Значение";
	
	Возврат ПолучитьЗначение(Параметр, ИмяСправочника, ИмяРеквизита);

КонецФункции

Функция ПолучитьДвоичные(Имя)
	
	ИмяСправочника  = "ЮТФайлы";
	ИмяРеквизита    = "Значение";

	Возврат ПолучитьЗначение(Имя, ИмяСправочника, ИмяРеквизита);
	
КонецФункции

Функция ПолучитьЗначение(Параметр, ИмяСправочника, ИмяРеквизита) 
		
	ЭлементСпр = Справочники[ИмяСправочника].НайтиПоНаименованию(Параметр);
	Значение   = ЭлементСпр[ИмяРеквизита];
	
	Если ТипЗнч(Значение) = Тип("ХранилищеЗначения") Тогда
		Значение = Значение.Получить();
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

Функция ПолучитьПараметрыВК()
    
    Параметры   = Новый Структура;
    НомерГруппы = ПолучитьПараметр("ВК_НомерГруппы");
    
    Параметры.Вставить("access_token"  , ПолучитьПараметр("ВК_Токен"));
    Параметры.Вставить("owner_id"      , "-" + НомерГруппы);
    Параметры.Вставить("app_id"        , ПолучитьПараметр("ВК_Приложение"));
    Параметры.Вставить("group_id"      , НомерГруппы);
    
    Возврат Параметры;
    
КонецФункции

Процедура ЗаписатьПараметр(Параметр, Значение)
	                
	ИмяСправочника  = "ЮТПараметры";
	ИмяРеквизита    = "Значение";
	
	ЭлементСпр = Справочники[ИмяСправочника].НайтиПоНаименованию(Параметр);
    
    Если ЗначениеЗаполнено(ЭлементСпр) Тогда
	    ОбъектСпр = ЭлементСпр.ПолучитьОбъект();
    Иначе
        ОбъектСпр = Справочники[ИмяСправочника].СоздатьЭлемент();
        ОбъектСпр.Наименование = Параметр;
    КонецЕсли;
    
	ОбъектСпр[ИмяРеквизита] = Значение; 
	ОбъектСпр.Записать();
	
КонецПроцедуры

#КонецОбласти

// BSLLS:Typo-on