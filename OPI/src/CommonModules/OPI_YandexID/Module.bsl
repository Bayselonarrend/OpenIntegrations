#Область ПрограммныйИнтерфейс

// Получить код подтверждения.
// 
// Параметры:
//  ClientId - Строка - Client id
// 
// Возвращаемое значение:
//  Строка, Произвольный, Неопределено, ДвоичныеДанные, HTTPОтвет - Ответ сервера Yandex
Функция ПолучитьКодПодтверждения(Знач ClientId) Экспорт

    Параметры = Новый Структура("client_id", ClientId);
    Ответ     = OPI_Инструменты.Post("https://oauth.yandex.ru/device/code", Параметры, , Ложь);
    
    Возврат Ответ;
	
КонецФункции

// Преобразовать код в токен.
// 
// Параметры:
//  ClientId - Строка - Client id
//  ClientSecret - Строка - Client secret
//  КодУстройства - Строка - device_code из ПолучитьКодПодтверждения()
// 
// Возвращаемое значение:
//  Строка, Произвольный, Неопределено, ДвоичныеДанные, HTTPОтвет - Преобразовать код в токен
Функция ПреобразоватьКодВТокен(Знач ClientId, Знач ClientSecret, Знач КодУстройства) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("grant_type"    , "device_code");
	Параметры.Вставить("code"          , КодУстройства);
	Параметры.Вставить("client_id"     , ClientId);
	Параметры.Вставить("client_secret" , ClientSecret);
	
	Ответ = OPI_Инструменты.Post("https://oauth.yandex.ru/token", Параметры, , Ложь);
	
	Возврат Ответ;
	
КонецФункции

// Обновить токен.
// 
// Параметры:
//  ClientId - Строка - Client id
//  ClientSecret - Строка - Client secret
//  RefreshToken - Строка - Refresh token
// 
// Возвращаемое значение:
//  Строка, Произвольный, Неопределено, ДвоичныеДанные, HTTPОтвет - Обновить токен
Функция ОбновитьТокен(Знач ClientId, Знач ClientSecret, Знач RefreshToken) Экспорт
    
    Параметры = Новый Структура;
    Параметры.Вставить("grant_type"    , "refresh_token");
    Параметры.Вставить("refresh_token" , RefreshToken);
    Параметры.Вставить("client_id"     , ClientId);
    Параметры.Вставить("client_secret" , ClientSecret);
    
    Ответ = OPI_Инструменты.Post("https://oauth.yandex.ru/token", Параметры, , Ложь);
    
    Возврат Ответ;
    
КонецФункции

#КонецОбласти

