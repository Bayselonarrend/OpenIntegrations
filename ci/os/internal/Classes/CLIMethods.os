#Использовать osparser
#Использовать "./internal"

Перем ФайлСоставаОПИ;
Перем МодульСоставаОПИ;
Перем СоответствиеМодулейКоманд;
Перем ТекущийМодуль;
Перем ТаблицаОписанийПараметров;
Перем ОбщийМассивМодулей;
Перем ТекущийФайлСостава;
Перем КаталогДополненийОпций;
Перем СоответствиеСлужебныхКлючей;
Перем СписокСогласных;
Перем СписокГласных;

Процедура ПриСозданииОбъекта(Знач ДанныеПроекта_ = Неопределено)
    
    Если ДанныеПроекта_ = Неопределено Тогда
        ДанныеПроекта = Новый ProjectData;
    Иначе
        ДанныеПроекта = ДанныеПроекта_;
    КонецЕсли;
    
    Корень            = ДанныеПроекта.ПолучитьЗначениеНастройки("paths.root");
    Языки             = ДанныеПроекта.ПолучитьЗначениеНастройки("localization.langs");
    КаталогДополнений = ДанныеПроекта.ПолучитьЗначениеНастройки("paths.paramsExamplesSrc");
    
    CommonTools.СообщитьПроцесс("Generating CLI version from Oint");
    
    ЗаполнитьСписокСогласных();
    ЗаполнитьСписокГласных();
    
    Для Каждого Язык Из Языки Цикл
        
        ОпределитьСоответствиеМодулейКоманд(Язык);

        МодульСоставаОПИ       = Новый ТекстовыйДокумент();
        КаталогИндекса         = Корень + Язык + "/cli/data/Classes/index";
        КаталогДополненийОпций = КаталогДополнений;
        
        ОбъектИндекса = Новый Файл(КаталогИндекса);
        
        Если Не ОбъектИндекса.Существует() Тогда
            СоздатьКаталог(КаталогИндекса);
        КонецЕсли;
        
        СоздатьФайлОсновныхДанных(Язык, КаталогИндекса);
        ЗаполнитьТаблицыСостава(КаталогИндекса);
        
    КонецЦикла;
    
КонецПроцедуры

Процедура СоздатьФайлОсновныхДанных(Язык, КаталогИндекса)
    
    ОсновныеДанные = Новый Соответствие();
    ОсновныеДанные.Вставить("version", ПолучитьВерсиюПакета());
        
    СоответствиеМодулей = Новый Соответствие();
    
    Для Каждого КомандаМодуля Из СоответствиеМодулейКоманд Цикл
        СоответствиеМодулей.Вставить(КомандаМодуля.Значение, КомандаМодуля.Ключ);
    КонецЦикла;
    
    ОсновныеДанные.Вставить("modules", СоответствиеМодулей);
    
    Путь = ОбъединитьПути(КаталогИндекса, "lib.json");
    
    CommonTools.ЗаписатьФайлJSON(Путь, ОсновныеДанные);
    
КонецПроцедуры

Процедура ЗаполнитьТаблицыСостава(КаталогИндекса)
    
    Для Каждого Модуль Из ОбщийМассивМодулей Цикл
        
        ИмяКоманды = СоответствиеМодулейКоманд[Модуль.ИмяБезРасширения];

        Если Не ИмяКоманды = Неопределено Тогда    
            
            ТекущийМодуль = РазобратьМодуль(Модуль);
            ПутьФайла     = ОбъединитьПути(КаталогИндекса, ИмяКоманды + ".json");

            CommonTools.ЗаписатьФайлJSON(ПутьФайла, ТекущийМодуль);

        КонецЕсли;
        
    КонецЦикла;
    
КонецПроцедуры

Функция РазобратьМодуль(Модуль)
    
    ТекущийМодуль     = Модуль.ИмяБезРасширения;
    ТекущаяОбласть    = "Основные методы";
    ТекущаяБиблиотека = СоответствиеМодулейКоманд.Получить(ТекущийМодуль);
    
    Парсер         = Новый ПарсерВстроенногоЯзыка;
    ДокументМодуля = Новый ТекстовыйДокумент;
    ДокументМодуля.Прочитать(Модуль.ПолноеИмя);
    
    ТекстМодуля     = ДокументМодуля.ПолучитьТекст();  
    СтруктураМодуля = Парсер.Разобрать(ТекстМодуля);
    
    
    ТекущийИндекс = Новый Соответствие;
    ТекущийИндекс.Вставить("module" , ТекущаяБиблиотека);
    ТекущийИндекс.Вставить("library", ТекущаяБиблиотека);
    
    МассивМетодов = Новый Массив;
    
    Для Каждого Метод Из СтруктураМодуля.Объявления Цикл
        
        Если Метод.Тип = "ИнструкцияПрепроцессораОбласть" Тогда
            ТекущаяОбласть = Синонимайзер(Метод.Имя);
        КонецЕсли;
        
        Если Метод.Тип = "ОбъявлениеМетода" И Метод.Сигнатура.Экспорт = Истина Тогда
            
            ТекущийМетод = РазобратьКомментарийМетода(ДокументМодуля, Метод, Модуль);	 
            ТекущийМетод.Вставить("region", ТекущаяОбласть);
            
            МассивМетодов.Добавить(ТекущийМетод);
            
        КонецЕсли;
        
    КонецЦикла;

    СписокЗависимостей = Новый Структура;
    ОпределитьСписокЗависимостей(ТекстМодуля, СписокЗависимостей);
    ТекущийИндекс.Вставить("depends", СписокЗависимостей);
    ТекущийИндекс.Вставить("methods", МассивМетодов);

    Возврат ТекущийИндекс;
    
КонецФункции

Функция РазобратьКомментарийМетода(ТекстовыйДокумент, Метод, Модуль)
    
    СписокЗанятыхБукв    = Новый СписокЗначений();
    
    ТекущийМетод = Новый Соответствие();
    
    НомерСтроки         = Метод.Начало.НомерСтроки;
    ИмяМетода           = Метод.Сигнатура.Имя;
    
    МассивКомментария = ПарсингКомментария(ТекстовыйДокумент, НомерСтроки);
    МассивПараметров  = Новый Массив;
    
    ОписаниеМетода = СформироватьСтруктуруМетода(МассивКомментария, МассивПараметров);
    МассивОписаний = Новый Массив;
    
    Для Каждого СтрокаПараметра Из МассивПараметров Цикл
        ТекущееОписание = СформироватьСтруктуруОписанияПараметра(Метод, СтрокаПараметра, СписокЗанятыхБукв);
        МассивОписаний.Добавить(ТекущееОписание);
    КонецЦикла;

    ДопОписание    = ОпределитьДопОписание(Модуль, МассивОписаний);
    ОписаниеМетода = СокрЛП(ОписаниеМетода) + ДопОписание;
    
    ТекущийМетод.Вставить("name"       , ИмяМетода);
    ТекущийМетод.Вставить("description", ОписаниеМетода);
    ТекущийМетод.Вставить("params"     , МассивОписаний);
    
    Возврат ТекущийМетод;
    
КонецФункции

Функция СформироватьСтруктуруМетода(Знач МассивКомментария, МассивПараметров)
    
    ОписаниеМетода = "";
    
    ЗаписыватьПараметры = Ложь;
    ЗаписыватьОписание  = Истина;
    
    Счетчик = 0;
    Для Каждого СтрокаКомментария Из МассивКомментария Цикл
        
        Счетчик = Счетчик + 1;
        
        Если Не ЗначениеЗаполнено(СокрЛП(СтрокаКомментария)) Тогда
            ЗаписыватьОписание = Ложь;
        КонецЕсли;
        
        Если ЗаписыватьОписание = Истина И Счетчик > 1 Тогда
            ОписаниеМетода = ?(ЗначениеЗаполнено(ОписаниеМетода), ОписаниеМетода + "    |   ", ОписаниеМетода) 
            + СтрокаКомментария;
        КонецЕсли;
        
        Если СтрНайти(СтрокаКомментария, "Параметры:") > 0 Или СтрНайти(СтрокаКомментария, "Parameters:") > 0 Тогда
            ЗаписыватьПараметры = Истина;
            ЗаписыватьОписание  = Ложь;
            
        ИначеЕсли СтрНайти(СтрокаКомментария, "Возвращаемое значение:") > 0 Или СтрНайти(СтрокаКомментария, "Returns:") > 0 Тогда
            Прервать;
            
        ИначеЕсли ЗаписыватьПараметры = Истина 
            И ЗначениеЗаполнено(СокрЛП(СтрокаКомментария)) 
            И Не СтрНачинаетсяС(СокрЛП(СтрокаКомментария), "*") Тогда
            
            МассивПараметров.Добавить(СтрокаКомментария);
            
        Иначе
            Продолжить;
        КонецЕсли;
        
    КонецЦикла;
    
    Возврат ОписаниеМетода;
    
КонецФункции

Процедура ОпределитьСписокЗависимостей(ТекстМодуля, СтруктураЗависимостей)
    
    Для Каждого Модуль Из ОбщийМассивМодулей Цикл
        
        ИскомаяЗависимость = Модуль.ИмяБезРасширения;
        
        АбсолютныйПуть = Модуль.ПолноеИмя;
        АбсолютныйПуть = СтрЗаменить(АбсолютныйПуть, "\", "/");
        ЧастиПути      = СтрРазделить(АбсолютныйПуть, "/");
        ЧислоЧастей    = ЧастиПути.ВГраница();
        
        НовыйПуть = Новый Массив;
        
        Для Н = 0 По ЧислоЧастей Цикл
            
            ОбратныйИндекс = ЧислоЧастей - Н;
            ТекущийЭлемент = ЧастиПути[ОбратныйИндекс];
            
            Если нРег(ТекущийЭлемент) = "oint" Тогда
                НовыйПуть.Вставить(0, "oint");
                НовыйПуть.Вставить(0, "%1");
                Прервать;
            Иначе
                НовыйПуть.Вставить(0, ТекущийЭлемент);
            КонецЕсли;
            
        КонецЦикла;
        
        НовыйПуть = СтрСоединить(НовыйПуть, "/");
                
        Если СтруктураЗависимостей.Свойство(ИскомаяЗависимость) Тогда
            Продолжить;
        КонецЕсли;
        
        СтруктураЗависимостей.Вставить(ИскомаяЗависимость, НовыйПуть);
        
    КонецЦикла;

КонецПроцедуры

Функция ПарсингКомментария(Знач ТекстовыйДокумент, Знач НомерСтроки)
    
    ТекущаяСтрока       = ТекстовыйДокумент.ПолучитьСтроку(НомерСтроки - 1);
    ТекстКомментария    = ТекущаяСтрока;
    
    Счетчик	= 1;
    Пока СтрНайти(ТекущаяСтрока, "//") > 0 Цикл
        
        Счетчик = Счетчик + 1;
        
        ТекущаяСтрока    = ТекстовыйДокумент.ПолучитьСтроку(НомерСтроки - Счетчик);
        ТекстКомментария = ТекущаяСтрока + Символы.ПС + ТекстКомментария;
        
    КонецЦикла;
    
    Если СтрНайти(ТекстКомментария, "!NOCLI") > 0 Тогда
        Возврат Новый Массив;
    КонецЕсли;
    
    МассивКомментария = СтрРазделить(ТекстКомментария, "//", Ложь);
    
    Если МассивКомментария.Количество() = 0 Тогда
        Возврат Новый Массив;
    Иначе
        МассивКомментария.Удалить(0);
    КонецЕсли;
    
    Возврат МассивКомментария;
    
КонецФункции

Функция СформироватьСтруктуруОписанияПараметра(Знач Метод, Знач ПараметрМетода, СписокЗанятыхБукв)
    
    ТекущееОписание = Новый Соответствие();

    Разделитель = "-";
    
    МассивЭлементовПараметра = СтрРазделить(ПараметрМетода, Разделитель, Ложь);
    КоличествоЭлементов      = МассивЭлементовПараметра.Количество();
    
    Для Н = 0 По МассивЭлементовПараметра.ВГраница() Цикл
        МассивЭлементовПараметра[Н] = СокрЛП(МассивЭлементовПараметра[Н]);
    КонецЦикла;
    
    Если КоличествоЭлементов < 4 Тогда
        ВызватьИсключение("Недостаточный набор данных в док. комментарии: " + Метод.Сигнатура.Имя);
    КонецЕсли;
     
    Имя1С      = МассивЭлементовПараметра[0];
    Имя        = "--" + МассивЭлементовПараметра[3];
    Буква      = Лев(МассивЭлементовПараметра[3], 1);
    Типы       = МассивЭлементовПараметра[1];
    Описание   = ?(КоличествоЭлементов >= 5, МассивЭлементовПараметра[4], МассивЭлементовПараметра[2]);
    ЗначениеУМ = ПолучитьЗначениеПараметраПоУмолчанию(Имя1С, Метод);
    
    Если КоличествоЭлементов > 5 Или СтрНайти(Имя, " ") > 0 Тогда
        ВызватьИсключение("Некорректный документирующий комментарий в методе: " + Метод.Сигнатура.Имя);
    КонецЕсли;
    
    ТекущееОписание.Вставить("name"       , Имя);
    ТекущееОписание.Вставить("types"      , Типы);
    ТекущееОписание.Вставить("description", Описание);
    ТекущееОписание.Вставить("default"    , ЗначениеУМ);
    
    Если СписокЗанятыхБукв.НайтиПоЗначению(Буква) = Неопределено Тогда
        
        СписокЗанятыхБукв.Добавить(Буква);
        ТекущееОписание.Вставить("short", "-" + Буква);

    Иначе

        Для Н = 3 По СтрДлина(Имя) Цикл
            
            ТекущаяБуква = Сред(Имя, Н, 1);
            
            ЭтоСогласная = СписокСогласных.НайтиПоЗначению(ТекущаяБуква) <> Неопределено;
            ЭтоНезанятая = СписокЗанятыхБукв.НайтиПоЗначению(ТекущаяБуква) = Неопределено;
            
            Если ЭтоСогласная И ЭтоНезанятая Тогда   
                ТекущееОписание.Вставить("short", "-" + Буква);
                СписокЗанятыхБукв.Добавить(ТекущаяБуква);
                Прервать;
            КонецЕсли;
            
        КонецЦикла;
        
        Если ЗначениеЗаполнено(ТекущееОписание["short"]) Тогда
            Возврат ТекущееОписание;
        КонецЕсли;
        
        Для Н = 3 По СтрДлина(Имя) Цикл
            
            ТекущаяБуква = Сред(Имя, Н, 1);
            
            ЭтоГласная   = СписокГласных.НайтиПоЗначению(ТекущаяБуква) <> Неопределено;
            ЭтоНезанятая = СписокЗанятыхБукв.НайтиПоЗначению(ТекущаяБуква) = Неопределено;
            
            Если ЭтоГласная И ЭтоНезанятая Тогда
                ТекущееОписание.Вставить("short", "-" + Буква);
                СписокЗанятыхБукв.Добавить(ТекущаяБуква);
                Прервать;    
            КонецЕсли;
            
        КонецЦикла;
        
        Если ЗначениеЗаполнено(ТекущееОписание["short"]) Тогда
            Возврат ТекущееОписание;
        КонецЕсли;
        
        Для Каждого СогласнаяБуква Из СписокСогласных Цикл
            
            ТекущаяБуква = СогласнаяБуква.Значение;
            ЭтоНезанятая = СписокЗанятыхБукв.НайтиПоЗначению(ТекущаяБуква) = Неопределено;
            
            Если ЭтоНезанятая Тогда
                ТекущееОписание.Вставить("short", "-" + Буква);
                СписокЗанятыхБукв.Добавить(ТекущаяБуква);
                Прервать;
            КонецЕсли;
            
        КонецЦикла;
        
    КонецЕсли;
    
    Возврат ТекущееОписание;

КонецФункции

Функция ПолучитьЗначениеПараметраПоУмолчанию(Знач Имя, Знач Метод)
    
    Значение = "";
    
    Для Каждого ПараметрМетода Из Метод.Сигнатура.Параметры Цикл
        
        Если ПараметрМетода.Имя = Имя Тогда
            
            ЗначениеПараметра = ПараметрМетода.Значение;
            Если ЗначениеЗаполнено(ЗначениеПараметра) Тогда
                Попытка
                    Значение = ЗначениеПараметра["Элементы"][0]["Значение"];
                Исключение 
                    Значение = ЗначениеПараметра.Значение;
                КонецПопытки;
                Значение = ?(ЗначениеЗаполнено(Значение), Значение, "Пустое значение");
            КонецЕсли;
            
        КонецЕсли;
        
    КонецЦикла;
    
    Возврат Значение;
    
КонецФункции

Функция ОпределитьДопОписание(Модуль, МассивОписаний)
    
    ДопОписание      = "";
    ЕстьМассив       = Ложь;
    ЕстьДата         = Ложь;
    ТекстДополнения  = "";
    ИмяМодуля        = Модуль.ИмяБезРасширения;
    
    ДЛя Каждого СтрокаПараметра Из МассивОписаний Цикл
        
        Типы = СтрокаПараметра["types"];
        Имя  = СтрокаПараметра["name"];
        
        Если СтрНайти(Типы, "Массив") > 0 Тогда
            ЕстьМассив = Истина;
        КонецЕсли;
        
        Если СтрНайти(Типы, "Дата") > 0 Тогда
            ЕстьДата = Истина;
        КонецЕсли;
        
    КонецЦикла;
    
    Если ЕстьМассив Тогда
        ТекстДополнения =  
        "
        |
        |    Пример указания параметра типа массив:
        |    --param ""['Val1','Val2','Val3']""
        |" + ТекстДополнения;
        
    КонецЕсли;
    
    Если ЕстьДата Тогда
        ТекстДополнения =  
        "
        |
        |    Дата указывается в формате ISO 8601:
        |    ""2024-04-07""
        |    ""2024-04-07T13:34:42+00:00"" 
        |    ""2024-04-07T13:34:42Z""
        |" + ТекстДополнения;
    КонецЕсли;
    
    ДопОписание = ДопОписание +  ТекстДополнения;
    
    Возврат ДопОписание;
    
КонецФункции


Процедура ОпределитьСоответствиеМодулейКоманд(ТекущийЯзык)
    
    СоответствиеМодулейКоманд  = Новый Соответствие();
    
    ОбщийМассивМодулей  = Новый Массив;
    
    ФайлыМодулей = НайтиФайлы("./src/" + ТекущийЯзык + "/", "*.os", Истина);
    
    Для Каждого Модуль Из ФайлыМодулей Цикл
        
        КомандаCLI     = ОпределитьКомандуCLI(Модуль.ПолноеИмя);
        НазваниеМодуля = Модуль.ИмяБезРасширения;
        
        Если Не ЗначениеЗаполнено(КомандаCLI) Или СокрЛП(Строка(КомандаCLI)) = "none" Тогда
            Продолжить;
        КонецЕсли;
        
        СоответствиеМодулейКоманд.Вставить(Модуль.ИмяБезРасширения, КомандаCLI);
        ОбщийМассивМодулей.Добавить(Модуль);
        
    КонецЦикла;
    
КонецПроцедуры

Функция ОпределитьКомандуCLI(Знач ПутьКМодулю)
    
    КомандаCLI     = "";
    ДокументМодуля = Новый ТекстовыйДокумент();
    Признак        = "// CLI: ";
    ДокументМодуля.Прочитать(ПутьКМодулю);
    
    Для Н = 1 По ДокументМодуля.КоличествоСтрок() Цикл
        
        ТекущаяСтрока = СокрЛП(ДокументМодуля.ПолучитьСтроку(Н));
        
        Если Не ЗначениеЗаполнено(ТекущаяСтрока) Тогда
            Прервать;
        КонецЕсли;
        
        Если СтрНачинаетсяС(ТекущаяСтрока, Признак) Тогда
            КомандаCLI = СтрЗаменить(ТекущаяСтрока, Признак, "");
            КомандаCLI = СокрЛП(КомандаCLI);
            Прервать;
        КонецЕсли;
        
    КонецЦикла;
    
    Возврат КомандаCLI;
    
КонецФункции

Функция ПолучитьВерсиюПакета()
    
    Версия     = "";
    Packagedef = "./src/ru/OInt/packagedef";
    Признак    = ".Версия(""";
    
    ТекстФайла = Новый ТекстовыйДокумент();
    ТекстФайла.Прочитать(Packagedef);
    
    Для Н = 1 По ТекстФайла.КоличествоСтрок() Цикл
        
        ТекущаяСтрока = СокрЛП(ТекстФайла.ПолучитьСтроку(Н));
        Если СтрНайти(ТекущаяСтрока, Признак) Тогда
            Версия = СтрЗаменить(ТекущаяСтрока, Признак, "");
            Версия = Лев(Версия, СтрДлина(Версия) - 2);
            Прервать;
        КонецЕсли;    
    КонецЦикла;
    
    Возврат Версия;
    
КонецФункции

Функция Синонимайзер(ИмяРеквизита)
    
    Перем Синоним, ъ, Символ, ПредСимвол, СледСимвол, Прописная, ПредПрописная, СледПрописная, ДлинаСтроки;
    
    Синоним = ВРег(Сред(ИмяРеквизита, 1, 1));
    ДлинаСтроки = СтрДлина(ИмяРеквизита);
    Для ъ=2 По ДлинаСтроки Цикл
        Символ = Сред(ИмяРеквизита, ъ, 1);
        ПредСимвол = Сред(ИмяРеквизита, ъ-1, 1);
        СледСимвол = Сред(ИмяРеквизита, ъ+1, 1);
        Прописная = Символ = ВРег(Символ);
        ПредПрописная = ПредСимвол = ВРег(ПредСимвол);
        СледПрописная = СледСимвол = ВРег(СледСимвол);
        
        // Варианты:
        Если НЕ ПредПрописная И Прописная Тогда
            Синоним = Синоним + " " + Символ;
        ИначеЕсли Прописная И НЕ СледПрописная Тогда
            Синоним = Синоним + " " + Символ;
        Иначе
            Синоним = Синоним + Символ;
        Конецесли;
    КонецЦикла;
    
    Синоним = ВРег(Лев(Синоним,1)) + нРег(Сред(Синоним,2));
    
    Возврат Синоним;
    
КонецФункции

Процедура СкопироватьФайлы(Знач КаталогИсточник, Знач КаталогПриемник)
    
    СоздатьКаталог(КаталогПриемник);
    
    МассивФайлов = НайтиФайлы(КаталогИсточник, "*.*", Истина);
    
    Для Каждого Файл Из МассивФайлов Цикл
        
        ПолноеИмяИсточник = Файл.ПолноеИмя;
        ПолноеИмяПриемник = КаталогПриемник + СтрЗаменить(Файл.ПолноеИмя, КаталогИсточник, "");
        
        Если Файл.ЭтоКаталог() Тогда
            СоздатьКаталог(ПолноеИмяПриемник);	
        Иначе
            КопироватьФайл(ПолноеИмяИсточник, ПолноеИмяПриемник);
        КонецЕсли;
    КонецЦикла;	
    
КонецПроцедуры

Процедура ЗаполнитьСписокСогласных()
    
    СписокСогласных = Новый СписокЗначений();
    СписокСогласных.Добавить("b");
    СписокСогласных.Добавить("c");
    СписокСогласных.Добавить("d");
    СписокСогласных.Добавить("f");
    СписокСогласных.Добавить("g");
    СписокСогласных.Добавить("h");
    СписокСогласных.Добавить("j");
    СписокСогласных.Добавить("k");
    СписокСогласных.Добавить("l");
    СписокСогласных.Добавить("m");
    СписокСогласных.Добавить("n");
    СписокСогласных.Добавить("p");
    СписокСогласных.Добавить("q");
    СписокСогласных.Добавить("r");
    СписокСогласных.Добавить("s");
    СписокСогласных.Добавить("t");
    СписокСогласных.Добавить("v");
    СписокСогласных.Добавить("w");
    СписокСогласных.Добавить("x");
    СписокСогласных.Добавить("z");
    
КонецПроцедуры

Процедура ЗаполнитьСписокГласных()
    
    СписокГласных = Новый СписокЗначений();
    СписокГласных.Добавить("a");
    СписокГласных.Добавить("e");
    СписокГласных.Добавить("i");
    СписокГласных.Добавить("o");
    СписокГласных.Добавить("u");
    СписокГласных.Добавить("y");
    
КонецПроцедуры
