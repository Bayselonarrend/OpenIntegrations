#Использовать "./internal"
#Использовать fs

Перем Репозиторий;
Перем Версия;
Перем Режим;
Перем Файл1С;
Перем Сервер;
Перем ПутьВыгрузки;
Перем Оскрипт;
Перем ПутьGHCLI;
Перем ПутьInnoSetup;
Перем ХешСумма;
Перем НеобходимоеЧислоАртефактов;
Перем ДанныеПроекта;


Процедура ПриСозданииОбъекта(Знач ДанныеПроекта_ = Неопределено)
	
	Если ДанныеПроекта_ = Неопределено Тогда
		ДанныеПроекта = Новый ProjectData;
	Иначе
		ДанныеПроекта = ДанныеПроекта_;
	КонецЕсли;
	
	CommonTools.СообщитьПроцесс("Creating release artifacts");
	
	НеобходимоеЧислоАртефактов = ДанныеПроекта.ПолучитьЗначениеНастройки("release.artifactsCount");
	Репозиторий                = ДанныеПроекта.ПолучитьЗначениеНастройки("primary.repoUrl");
	Версия                     = ДанныеПроекта.ПолучитьЗначениеНастройки("primary.version");
	Языки                      = ДанныеПроекта.ПолучитьЗначениеНастройки("localization.langs");
	
	Режим = "CONFIG";
	
	Файл1С            = СтрШаблон("""%1""",  ДанныеПроекта.ПолучитьЗначениеНастройки("local.onecPath"));
	Сервер            = ДанныеПроекта.ПолучитьЗначениеНастройки("local.serverName");
	ОСкрипт           = ДанныеПроекта.ПолучитьЗначениеНастройки("local.osPath");
	ПутьGHCLI         = ДанныеПроекта.ПолучитьЗначениеНастройки("local.ghcliPath");
	ПутьInnoSetup     = ДанныеПроекта.ПолучитьЗначениеНастройки("local.innoPath");
	
	ПутьВыгрузки      = СтрШаблон("./%1/", Версия);
	ХешСумма          = ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные("./service/last_build_hash.txt"));
	
	ОбъектПутьВыгрузки = Новый Файл(ПутьВыгрузки);
	
	Если Не ОбъектПутьВыгрузки.Существует() Тогда
		СоздатьКаталог(ПутьВыгрузки);
	КонецЕсли;
	
	ЗапуститьПриложение("git clone https://github.com/bayselonarrend/Melezh");
	
	КаталогВыгрузки = Новый Файл(ПутьВыгрузки);
	Если КаталогВыгрузки.Существует() Тогда 
		УдалитьФайлы(ПутьВыгрузки);
	КонецЕсли;
	
	ПутьВыгрузки = КаталогВыгрузки.ПолноеИмя;
	
	СоздатьКаталог(ПутьВыгрузки);
	Приостановить(2000);
	
	Для Каждого Язык Из Языки Цикл
		
		База     = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("ib_name", Язык);
		Описание = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("packageDescriptionSrc", Язык);
		URL      = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("docsUrl", Язык);
		
		СтруктураДанных = Новый Структура();
		СтруктураДанных.Вставить("База"    , База);
		СтруктураДанных.Вставить("ПутьКEDT", СтрШаблон("./src/%1/OPI", Язык));
		СтруктураДанных.Вставить("ПутьOS"  , СтрШаблон("./src/%1/OInt", Язык));
		СтруктураДанных.Вставить("ПутьCLI" , СтрШаблон("./src/%1/cli/core/Classes/app.os", Язык));
		СтруктураДанных.Вставить("ПутьCLIP", СтрШаблон("./src/%1/cli", Язык));
		СтруктураДанных.Вставить("ПутьISS" , СтрШаблон("./service/iss/%1.iss", Язык));
		СтруктураДанных.Вставить("URL"     , URL);
		СтруктураДанных.Вставить("Описание", ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(Описание)));
		СтруктураДанных.Вставить("Префикс" , Язык);
		
		СоздатьXML(СтруктураДанных);
		СоздатьCFE(СтруктураДанных);
		СоздатьEDT(СтруктураДанных);   
		СоздатьOSPX(СтруктураДанных);
		СоздатьПакеты(СтруктураДанных);
		СоздатьУстановщик(СтруктураДанных);
		
		УдалитьФайлы("./ci/installer_set/share/oint/lib/oint");
		УдалитьФайлы("./ci/installer_set/share/oint/lib/oint-cli");
		
	КонецЦикла;
	
	ФайлыРелиза          = НайтиФайлы(ПутьВыгрузки, "*", Истина);
	КоличествоАртефактов = ФайлыРелиза.Количество();
	
	Если КоличествоАртефактов <> НеобходимоеЧислоАртефактов Тогда
		
		CommonTools.СообщитьПроцесс(СтрШаблон("Incorrect number of artifacts for export: required %1, found %2"
		, НеобходимоеЧислоАртефактов
		, КоличествоАртефактов));
		
	КонецЕсли;
	
	CommonTools.СообщитьПроцесс("Start GHCLI");
	
	Для Каждого ФайлРелиза Из ФайлыРелиза Цикл
		
		СтрокаЗапуска = СтрШаблон("""%1"" release delete-asset draft --yes --repo %2 ""%3""", ПутьGHCLI, Репозиторий, ФайлРелиза.Имя);
		CommonTools.ЗапуститьВнешнееПриложение(СтрокаЗапуска);
		
		СтрокаЗапуска = СтрШаблон("""%1"" release upload draft --repo %2 ""%3""", ПутьGHCLI, Репозиторий, ФайлРелиза.ПолноеИмя);
		CommonTools.ЗапуститьВнешнееПриложение(СтрокаЗапуска);
		
	КонецЦикла;
	
	CommonTools.СообщитьПроцесс("Release creating complete!");
	
КонецПроцедуры


Процедура СоздатьCFE(Данные)
	
	CommonTools.СообщитьПроцесс("Start CFE");
	
	База          = Данные["База"];
	Префикс       = Данные["Префикс"];
	ВыгрузкаВФайл = СтрШаблон("%1 %2 /S %3\%4 /DumpCfg ""%5OpenIntegrations_%6_%7.cfe"" -Extension OpenIntegrations"
	, Файл1С
	, Режим
	, Сервер
	, База
	, ПутьВыгрузки
	, Версия
	, Префикс);
	
	CommonTools.ЗапуститьВнешнееПриложение(ВыгрузкаВФайл);
	
КонецПроцедуры

Процедура СоздатьXML(Данные)
	
	CommonTools.СообщитьПроцесс("Start XML");
	
	Префикс = вРег(Данные["Префикс"]); 
	База    = Данные["База"];  
	
	ПапкаXML   = СтрШаблон("%1XML_%2", ПутьВыгрузки, Префикс);
	ПутьZIP    = СтрШаблон("%1.zip", ПапкаXML);
	КаталогXML = Новый Файл(ПапкаXML);
	
	Если Не КаталогXML.Существует() Тогда
		СоздатьКаталог(ПапкаXML);
	КонецЕсли;
	
	ВыгрузкаВXML = СтрШаблон("%1 %2 /S %3\%4 /DumpConfigToFiles ""%5"" -Extension OpenIntegrations"
	, Файл1С
	, Режим
	, Сервер
	, База
	, ПапкаXML);
	
	CommonTools.ЗапуститьВнешнееПриложение(ВыгрузкаВXML);
	
	ZipXML = Новый ЗаписьZipФайла(ПутьZIP);
	
	ZipXML.Добавить(ПапкаXML + "\*.*" 
	, РежимСохраненияПутейZIP.СохранятьОтносительныеПути
	, РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
	
	ZipXML.Записать();
	
	УдалитьФайлы(ПапкаXML);
	
КонецПроцедуры

Процедура СоздатьEDT(Данные)
	
	CommonTools.СообщитьПроцесс("Start EDT");
	
	Префикс  = вРег(Данные["Префикс"]);
	ПутьКEDT = Данные["ПутьКEDT"];
	
	ПутьZIP = СтрШаблон("%1EDT_%2.zip", ПутьВыгрузки, Префикс);
	ZipEDT  = Новый ЗаписьZipФайла(ПутьZIP);
	
	ZipEDT.Добавить(ПутьКEDT + "\*.*" 
	, РежимСохраненияПутейZIP.СохранятьОтносительныеПути
	, РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
	
	ZipEDT.Записать();
	
КонецПроцедуры


Процедура СоздатьOSPX(Данные)
	
	CommonTools.СообщитьПроцесс("Start OSPX");
	
	ПутьOS   = Данные["ПутьOS"];
	ПутьCLIP = Данные["ПутьCLIP"];
	Префикс  = Данные["Префикс"];
	
	ВременныКаталог  = КаталогВременныхФайлов();
	
	СоответСтвиеОписанийOSPX = Новый Соответствие();
	СоответСтвиеОписанийOSPX.Вставить("oint"    , ПутьOS);
	СоответСтвиеОписанийOSPX.Вставить("oint-cli", ПутьCLIP);
	
	Для Каждого ОписаниеOSPX Из СоответСтвиеОписанийOSPX Цикл
		
		ИдентификаторOSPX  = ОписаниеOSPX.Ключ;
		ПутьСборки         = ОписаниеOSPX.Значение;

		СтандартноеИмяOSPX = СтрШаблон("%1-%2.ospx"   , ИдентификаторOSPX, Версия);
		ИмяOSPX            = СтрШаблон("%1-%2_%3.ospx", ИдентификаторOSPX, Версия, Префикс);
		КонечныйПутьOSPX   = ПутьВыгрузки + ИмяOSPX;		
		
		СборкаOS  = СтрШаблон("""%2bin/opm.bat"" b -o %1", ВременныКаталог, ОСкрипт);
		
		CommonTools.ЗапуститьВнешнееПриложение(СборкаOS, ПутьСборки);
		ПереместитьФайл(ОбъединитьПути(ВременныКаталог, СтандартноеИмяOSPX), КонечныйПутьOSPX);
		
		УдалитьФайлы(СтрШаблон("%1lib\%2", Оскрипт, ИдентификаторOSPX));
		
		Приостановить(1000);
		СтрокаВызова = СтрШаблон("""%2bin/opm.bat"" install -f ""%1""", КонечныйПутьOSPX, ОСкрипт);
		CommonTools.ЗапуститьВнешнееПриложение(СтрокаВызова);
		Приостановить(1000);
		
		НачальныйПуть = СтрШаблон("%1lib\%2", ОСкрипт, ИдентификаторOSPX);
		КонечныйПуть  = СтрШаблон(".\ci\installer_set\share\oint\lib\%1", ИдентификаторOSPX);
		
		ФС.КопироватьСодержимоеКаталога(НачальныйПуть, КонечныйПуть);
		
		ТекущийХеш = ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(КонечныйПуть + "\.versionhash"));
		
		Если Не ТекущийХеш = ХешСумма Тогда
			ВызватьИсключение СтрШаблон("Хеш сумма %1 не совпадает с основной!", ИдентификаторOSPX);
		КонецЕсли;

	КонецЦикла;
	
	УдалитьФайлы(".\ci\installer_set\share\oint\lib\oint\tests");

КонецПроцедуры

Процедура СоздатьПакеты(Данные)
	
	CommonTools.СообщитьПроцесс("Start linux packages");
	
	ХешУстановщика = ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(".\ci\installer_set\share\oint\.versionhash"));
	
	Если Не ХешУстановщика = ХешСумма Тогда
		ВызватьИсключение "Хеш сумма набора установщика не совпадает с основной!";
	КонецЕсли;
	
	ПутьДвижка  = ДанныеПроекта.ПолучитьЗначениеНастройки("local.engineLinuxWsl");
	Мейнтейнер  = ДанныеПроекта.ПолучитьЗначениеНастройки("packaging.maintainer");
	ВидыПакетов = ДанныеПроекта.ПолучитьЗначениеНастройки("packaging.packageTypes");
	Постинсталл = ДанныеПроекта.ПолучитьЗначениеНастройки("packaging.postinstScript");
	
	КонечныйПутьПостинсталл = ОбъединитьПути(ПутьВыгрузки, "postinst.sh");

	КопироватьФайл(Постинсталл, КонечныйПутьПостинсталл);

	Описание = Данные["Описание"];
	Префикс  = Данные["Префикс"];
	URL      = Данные["URL"];
	
	Для Каждого Пакет Из ВидыПакетов Цикл
		
		ВидПакета = Пакет.Ключ;
		
		MakeSh  = СтрШаблон("%1make%2.sh" , ПутьВыгрузки, ВидПакета);
		MakeBat = СтрШаблон("%1make%2.bat", ПутьВыгрузки, ВидПакета);
		
		ПутьШаблонаСкрипта = Пакет.Значение["value"]["buildScript"]["value"];
		Дистрибутив        = Пакет.Значение["value"]["wslDistro"]["value"];
		
		ТекстСкрипта = ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(ПутьШаблонаСкрипта));
		ТекстСкрипта = СтрШаблон(ТекстСкрипта
		, Версия
		, Префикс
		, СтрЗаменить(Описание, Символы.ПС, "\n")
		, URL
		, Мейнтейнер
		, ПутьДвижка);
		
		ПолучитьДвоичныеДанныеИзСтроки(ТекстСкрипта).Записать(MakeSh);
		
		ТекстBat = СтрШаблон("wsl -d %1 ""./make%2.sh""", Дистрибутив, ВидПакета);
		ТекстBat = ПолучитьДвоичныеДанныеИзСтроки(ТекстBat);
		ТекстBat.Записать(MakeBat);
		
		CommonTools.ЗапуститьВнешнееПриложение(СтрШаблон("%2make%1.bat", ВидПакета, ПутьВыгрузки), ПутьВыгрузки, Истина);
		
		УдалитьФайлы(MakeBat);
		УдалитьФайлы(MakeSh);
		
	КонецЦикла;

	УдалитьФайлы(КонечныйПутьПостинсталл);
	
КонецПроцедуры

Процедура СоздатьУстановщик(Данные)
	
	CommonTools.СообщитьПроцесс("Start ISS");
	
	ПутьISS = Данные["ПутьISS"];
	ПутьCLI = Данные["ПутьCLI"];
	Префикс = Данные["Префикс"];
	
	ТекстISS = Новый ТекстовыйДокумент();
	ТекстISS.Прочитать(ПутьISS);
	
	Для Н = 1 По ТекстISS.КоличествоСтрок() Цикл
		
		ТекущаяСтрока = СокрЛП(ТекстISS.ПолучитьСтроку(Н));
		
		Если СтрНайти(ТекущаяСтрока, "#define MyAppVersion") Тогда
			ТекстISS.ЗаменитьСтроку(Н, "#define MyAppVersion """ + Версия + """");
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	ТекстISS.Записать(ПутьISS);
	
	СборкаSetup = СтрШаблон("""%1"" /cc ""%2""", ПутьInnoSetup, ПутьISS);
	CommonTools.ЗапуститьВнешнееПриложение(СборкаSetup);
	
КонецПроцедуры