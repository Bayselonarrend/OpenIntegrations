#Использовать "./internal"

Перем Путь1С;
Перем ИмяСервера;
Перем ДанныеПроекта;
Перем КаталогEDT;
Перем Корень;
Перем МассивПризнаковИсключения;

Процедура ПриСозданииОбъекта(Знач ДанныеПроекта_ = Неопределено)

	Если ДанныеПроекта_ = Неопределено Тогда
		ДанныеПроекта = Новый ProjectData;
	Иначе
		ДанныеПроекта = ДанныеПроекта_;
	КонецЕсли;

	Путь1С     = ДанныеПроекта.ПолучитьЗначениеНастройки("local.onecConfig");
	ИмяСервера = ДанныеПроекта.ПолучитьЗначениеНастройки("local.serverName");
	КаталогEDT = ДанныеПроекта.ПолучитьЗначениеНастройки("local.edtProjectsPath");
	Языки      = ДанныеПроекта.ПолучитьЗначениеНастройки("localization.langs"); 
	Корень     = ДанныеПроекта.ПолучитьЗначениеНастройки("paths.root"); 

	МассивПризнаковИсключения = Новый Массив;
	МассивПризнаковИсключения.Добавить("error");
	МассивПризнаковИсключения.Добавить("exception");
	МассивПризнаковИсключения.Добавить("failed");

	Для Каждого Язык Из Языки Цикл
		ОбновитьБазуИзИсходников(Язык);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьБазуИзИсходников(Знач Язык)

	ИмяИБ  = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("ib_name", Язык);
	ИмяEDT = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("edt_project", Язык);

	КаталогСборки = СтрШаблон("./build/OPI_%1", Язык);

	ПутьEDT = ОбъединитьПути(КаталогEDT, ИмяEDT);

	КомандаЭкспорта = СтрШаблон("1cedtcli.exe -data ""%1"" -command export --project ""${projectPath}"" --configuration-files ""${configPath}"""
		, ПутьEDT
		, ОбъединитьПути(Корень, Язык, "OPI")
		, КаталогСборки);

	ВыводЭкспорт = CommonTools.ЗапуститьВнешнееПриложение(КомандаЭкспорта);
	ВыводЭкспорт = нРег(ВыводЭкспорт);

	Для Каждого Признак Из МассивПризнаковИсключения Цикл
		Если СтрНайти(ВыводЭкспорт, Признак) > 0 Тогда
			ВызватьИсключение "EDT build failed!";
		КонецЕсли;
	КонецЦикла;

	ЗаменитьCRLF(КаталогСборки);

	ТекущаяКоманда = СтрШаблон("""%1"" DESIGNER /IBName ""%2"" /LoadConfigFromFiles ""%3"" -Extension OpenIntegrations", Путь1С, ИмяИБ, КаталогСборки);
	CommonTools.ЗапуститьВнешнееПриложение(ТекущаяКоманда);

	ТекущаяКоманда = СтрШаблон("""%1"" DESIGNER /IBName ""%2"" /UpdateDBCfg -Extension OpenIntegrations -SessionTerminate force", Путь1С, ИмяИБ);
	CommonTools.ЗапуститьВнешнееПриложение(ТекущаяКоманда);

	ТекущаяКоманда = СтрШаблон("""%1"" DESIGNER /IBName ""%2"" /UpdateDBCfg -SessionTerminate force", Путь1С, ИмяИБ);
	CommonTools.ЗапуститьВнешнееПриложение(ТекущаяКоманда);
	
КонецПроцедуры

Процедура ЗаменитьCRLF(Знач КаталогСборки)

	МассивФайлов            = Новый Массив;
	МассивРезультатовПоиска = Новый Массив;

	МассивРезультатовПоиска.Добавить(НайтиФайлы(КаталогСборки, "*.bsl", Истина));
	МассивРезультатовПоиска.Добавить(НайтиФайлы(КаталогСборки, "*.xml", Истина));

	Для Каждого Результат Из МассивРезультатовПоиска Цикл
		Для Каждого Файл Из Результат Цикл

			Если Не Файл.ЭтоКаталог() Тогда
				МассивФайлов.Добавить(Файл.ПолноеИмя);
			КонецЕсли;

		КонецЦикла;
	КонецЦикла;

	Для Каждого Файл Из МассивФайлов Цикл
		ПолучитьДвоичныеДанныеИзСтроки(

			СтрЗаменить(
				ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(Файл))
			, Символы.ВК + Символы.ПС
			, Символы.ПС)
		)
		.Записать(Файл);
	КонецЦикла;

КонецПроцедуры