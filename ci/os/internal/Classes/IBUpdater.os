#Использовать "./internal"

Перем Путь1С;
Перем Путь1СК;
Перем ИмяСервера;
Перем ДанныеПроекта;
Перем КаталогEDT;
Перем Корень;
Перем МассивПризнаковИсключения;
Перем YaxUnitConf;

Процедура ПриСозданииОбъекта(Знач ДанныеПроекта_ = Неопределено)

	Если ДанныеПроекта_ = Неопределено Тогда
		ДанныеПроекта = Новый ProjectData;
	Иначе
		ДанныеПроекта = ДанныеПроекта_;
	КонецЕсли;

	CommonTools.СообщитьПроцесс("1C IB Updating");

	Путь1С      = ДанныеПроекта.ПолучитьЗначениеНастройки("local.onecConfig");
	Путь1СК     = ДанныеПроекта.ПолучитьЗначениеНастройки("local.onecClient");
	ИмяСервера  = ДанныеПроекта.ПолучитьЗначениеНастройки("local.serverName");
	КаталогEDT  = ДанныеПроекта.ПолучитьЗначениеНастройки("local.edtProjectsPath");
	Языки       = ДанныеПроекта.ПолучитьЗначениеНастройки("localization.langs"); 
	Корень      = ДанныеПроекта.ПолучитьЗначениеНастройки("paths.root"); 
	YaxUnitConf = ДанныеПроекта.ПолучитьЗначениеНастройки("tests.yaxunitConf");

	МассивПризнаковИсключения = Новый Массив;
	МассивПризнаковИсключения.Добавить("error");
	МассивПризнаковИсключения.Добавить("exception");
	МассивПризнаковИсключения.Добавить("failed");

	Для Каждого Язык Из Языки Цикл
		ОбновитьБазуИзИсходников(Язык);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьБазуИзИсходников(Знач Язык)

	ИмяИБ  = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("ib_name", Язык);
	ИмяEDT = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("edt_project", Язык);

	КаталогСборки = СтрШаблон("./build/OPI_%1", вРег(Язык));

	ПутьEDT = СтрШаблон("%1/%2", КаталогEDT, ИмяEDT);

	КомандаЭкспорта = СтрШаблон("1cedtcli.exe -data ""%1"" -command export --project ""%2"" --configuration-files ""%3"""
		, ПутьEDT
		, СтрШаблон("%1%2/%3", Корень, Язык, "OPI")
		, КаталогСборки);

	CommonTools.СообщитьПроцесс(СтрШаблон("EDT project export for %1...", Язык));

	ВыводЭкспорт = CommonTools.ЗапуститьВнешнееПриложение(КомандаЭкспорта);
	ВыводЭкспорт = нРег(ВыводЭкспорт);

	Для Каждого Признак Из МассивПризнаковИсключения Цикл
		Если СтрНайти(ВыводЭкспорт, Признак) > 0 Тогда
			ВызватьИсключение "EDT build failed!";
		КонецЕсли;
	КонецЦикла;

	ЗаменитьCRLF(КаталогСборки);

	CommonTools.СообщитьПроцесс(СтрШаблон("1С batch updating for %1...", Язык));

	ТекущаяКоманда = СтрШаблон("""%1"" DESIGNER /IBName ""%2"" /LoadConfigFromFiles ""%3"" -Extension OpenIntegrations", Путь1С, ИмяИБ, КаталогСборки);
	CommonTools.ЗапуститьВнешнееПриложение(ТекущаяКоманда);

	ТекущаяКоманда = СтрШаблон("""%1"" DESIGNER /IBName ""%2"" /UpdateDBCfg -Extension OpenIntegrations -SessionTerminate force", Путь1С, ИмяИБ);
	CommonTools.ЗапуститьВнешнееПриложение(ТекущаяКоманда);

	ТекущаяКоманда = СтрШаблон("""%1"" DESIGNER /IBName ""%2"" /UpdateDBCfg -SessionTerminate force", Путь1С, ИмяИБ);
	CommonTools.ЗапуститьВнешнееПриложение(ТекущаяКоманда);

	CommonTools.СообщитьПроцесс(СтрШаблон("Hash sum test for %1", Язык));
	ПроверитьХеш(Язык, ИмяИБ);
	
КонецПроцедуры

Процедура ЗаменитьCRLF(Знач КаталогСборки)

	МассивФайлов            = Новый Массив;
	МассивРезультатовПоиска = Новый Массив;

	МассивРезультатовПоиска.Добавить(НайтиФайлы(КаталогСборки, "*.bsl", Истина));
	МассивРезультатовПоиска.Добавить(НайтиФайлы(КаталогСборки, "*.xml", Истина));

	Для Каждого Результат Из МассивРезультатовПоиска Цикл
		Для Каждого Файл Из Результат Цикл

			Если Не Файл.ЭтоКаталог() Тогда
				МассивФайлов.Добавить(Файл.ПолноеИмя);
			КонецЕсли;

		КонецЦикла;
	КонецЦикла;

	Для Каждого Файл Из МассивФайлов Цикл
		ПолучитьДвоичныеДанныеИзСтроки(

			СтрЗаменить(
				ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(Файл))
			, Символы.ВК + Символы.ПС
			, Символы.ПС)
		)
		.Записать(Файл);
	КонецЦикла;

КонецПроцедуры

Процедура ПроверитьХеш(Знач Язык, Знач ИмяИБ)

	КонфигурацияYaxUnit = СтрШаблон("%1%2/BuildCheck.json", YaxUnitConf, Язык);
	СтрокаПодключения   = СтрШаблон("Srvr=""""%1"""";Ref=""""%2"""";", ИмяСервера, ИмяИБ);

	СтрокаЗапуска = СтрШаблон("""%1"" /IBConnectionString ""%2"" /UsePrivilegedMode /C""RunUnitTests=%3"""
		, Путь1СК
		, СтрокаПодключения
		, КонфигурацияYaxUnit);

	CommonTools.ЗапуститьВнешнееПриложение(СтрокаЗапуска);
	ФайлРезультата = Новый Файл("./test_results/BuildCheck.code");

	ДатаНачала = ТекущаяДата();

	Пока Не ФайлРезультата.Существует() И ДатаНачала + 60 > ТекущаяДата() Цикл
		Приостановить(1000);
	КонецЦикла;

	ФайлОтчета = Новый Файл("./test_results/BuildCheck.report");

	Если ФайлРезультата.Существует() Тогда

		КодРезультата = ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(ФайлРезультата.ПолноеИмя));
		КодРезультата = СокрЛП(КодРезультата);

		Если КодРезультата <> "0" Тогда

			ВызватьИсключение СтрШаблон("Test failed with code %1", КодРезультата);

			Если ФайлОтчета.Существует() Тогда
				CommonTools.СообщитьПроцесс(ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(ФайлОтчета.ПолноеИмя)));
			Иначе
				CommonTools.СообщитьПроцесс("Report file not found!");
			КонецЕсли;
			
		КонецЕсли;

	Иначе
		ВызватьИсключение "Exit code file not found!";
	КонецЕсли;

	CommonTools.СообщитьПроцесс("Hash sum test complete!");

КонецПроцедуры