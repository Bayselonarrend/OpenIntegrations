#Использовать "./internal"
#Использовать "../../../../src/ru/OInt"

Процедура ПриСозданииОбъекта(Знач ДанныеПроекта_ = Неопределено)
	
	Если ДанныеПроекта_ = Неопределено Тогда
		ДанныеПроекта = Новый ProjectData;
	Иначе
		ДанныеПроекта = ДанныеПроекта_;
	КонецЕсли;
	
	CommonTools.СообщитьПроцесс("SonarQube check");
	
	URL = ДанныеПроекта.ПолучитьЗначениеНастройки("sonar.serverUrl");
	
	Результат = OPI_ЗапросыHTTP.НовыйЗапрос()
		.Инициализировать()
		.УстановитьURL(URL)
		.УстановитьТаймаут(10)
		.ОбработатьЗапрос("GET");

	Если Результат.ВернутьОтвет() = Неопределено Тогда
		CommonTools.СообщитьПроцесс("SonarQube server unavailable. Skip...");
		Возврат;
	КонецЕсли;

	КаталогИсходников = ДанныеПроекта.ПолучитьЗначениеНастройки("paths.configurationSrc");

	ИмяПроекта = ДанныеПроекта.ПолучитьЗначениеНастройки("sonar.projectKey");
	ПолеТокена = ДанныеПроекта.ПолучитьЗначениеНастройки("sonar.tokenField");
	Токен      = ДанныеПроекта.ПолучитьЗначениеСекретныхДанных(ПолеТокена);
	
	ШаблонЗапуска = "sonar-scanner -D""sonar.projectKey=%1"" -D""sonar.host.url=%2"" -D""sonar.token=%3"" -D""sonar.sourceEncoding=UTF-8"" -D""sonar.inclusions=**/*.bsl""";
	СтрокаЗапуска = СтрШаблон(ШаблонЗапуска, ИмяПроекта, URL, Токен);

	ЗапуститьПриложение(СтрокаЗапуска, КаталогИсходников, Истина);
	
КонецПроцедуры