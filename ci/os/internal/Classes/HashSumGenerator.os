#Использовать "./internal"

Процедура ПриСозданииОбъекта(Знач ДанныеПроекта_ = Неопределено)

	Если ДанныеПроекта_ = Неопределено Тогда
		ДанныеПроекта = Новый ProjectData;
	Иначе
		ДанныеПроекта = ДанныеПроекта_;
	КонецЕсли;

	CommonTools.СообщитьПроцесс("Build hashsum update");

	Хеширование = Новый ХешированиеДанных(ХешФункция.SHA256);
    
	Для Каждого Файл Из НайтиФайлы("./src/ru", "*", Истина) Цикл
		Если Не Файл.ЭтоКаталог() И Не Файл.ИмяБезРасширения = "OPI_BuildHash" Тогда
			Хеширование.Добавить(Новый ДвоичныеДанные(Файл.ПолноеИмя));
		КонецЕсли;
	КонецЦикла;

	Для Каждого Файл Из НайтиФайлы("./src/en", "*", Истина) Цикл
		Если Не Файл.ЭтоКаталог() И Не Файл.ИмяБезРасширения = "OPI_BuildHash" Тогда
			Хеширование.Добавить(Новый ДвоичныеДанные(Файл.ПолноеИмя));
		КонецЕсли;
	КонецЦикла;

    Сумма = Хеширование.ХешСуммаСтрокой;

	CommonTools.СообщитьПроцесс(СтрШаблон("Hashsum calculation complete: %1", ТекущаяДата()));
	CommonTools.СообщитьПроцесс(СтрШаблон("Output: %1", Сумма));
	
	Для Каждого Файл Из НайтиФайлы("./src", "*", Истина) Цикл

		Если Не Файл.ИмяБезРасширения = "OPI_BuildHash" Тогда
			Продолжить;
		КонецЕсли;

		ПутьМодуляХеша     = Файл.ПолноеИмя;
		ПутьМодуляХеша     = СтрЗаменить(ПутьМодуляХеша, "\", "/");
		ПутьМодуляХеша     = ?(Файл.ЭтоКаталог(), ПутьМодуляХеша + "/Module.bsl", ПутьМодуляХеша);

		МодульИнструментов = Новый ТекстовыйДокумент();
		МодульИнструментов.Прочитать(ПутьМодуляХеша);

		Для Н = 1 По МодульИнструментов.КоличествоСтрок() Цикл

			ТекущаяСтрока = МодульИнструментов.ПолучитьСтроку(Н);

			Если СтрНайти(ТекущаяСтрока, "LastBuildHash") > 0 Тогда
				МодульИнструментов.ЗаменитьСтроку(Н, СтрШаблон("    LastBuildHash  = ""%1"";", Сумма));
				Прервать;
			КонецЕсли;

		КонецЦикла;

		CommonTools.ЗаписатьТекст(МодульИнструментов, ПутьМодуляХеша);

	КонецЦикла;

	СуммаДД = ПолучитьДвоичныеДанныеИзСтроки(Сумма);

	СуммаДД.Записать("./service/last_build_hash.txt");
	СуммаДД.Записать("./src/ru/OInt/.versionhash");
	СуммаДД.Записать("./src/en/OInt/.versionhash");
	СуммаДД.Записать("./src/ru/cli/.versionhash");
	СуммаДД.Записать("./src/en/cli/.versionhash");
	СуммаДД.Записать("./ci/installer_set/share/oint/.versionhash");

КонецПроцедуры