#Использовать "./internal"

Перем ДанныеПроекта;

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриСозданииОбъекта(Знач ДанныеПроекта_ = Неопределено)

	Если ДанныеПроекта_ = Неопределено Тогда
		ДанныеПроекта = Новый ProjectData;
	Иначе
		ДанныеПроекта = ДанныеПроекта_;
	КонецЕсли;

	ОбновитьНомераВерсий();
	ОбновитьLibConfig();

КонецПроцедуры

Процедура ОбновитьНомераВерсий() Экспорт

	CommonTools.СообщитьПроцесс("Version numbers updating");

	ПутьPackagedef = ДанныеПроекта.ПолучитьЗначениеНастройки("paths.packagedefSrc");
	Версия         = ДанныеПроекта.ПолучитьЗначениеНастройки("primary.version");

	Признак    = ".Версия(""";
	Packagedef = Новый ТекстовыйДокумент();
	Packagedef.Прочитать(ПутьPackagedef);
	
	Для Н = 1 По Packagedef.КоличествоСтрок() Цикл
		
		ТекущаяСтрока = СокрЛП(Packagedef.ПолучитьСтроку(Н));
		Если СтрНайти(ТекущаяСтрока, Признак) Тогда
			Packagedef.ЗаменитьСтроку(Н, "    .Версия(""" + Версия + """)");
			CommonTools.ЗаписатьТекст(Packagedef, ПутьPackagedef);
			Возврат;
		КонецЕсли;    
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьLibConfig() Экспорт

	CommonTools.СообщитьПроцесс("libconfig updating");

	ПутьПакета                    = ДанныеПроекта.ПолучитьЗначениеНастройки("paths.packageSrc");
	Языки                         = ДанныеПроекта.ПолучитьЗначениеНастройки("localization.langs");
	ОсновнойЯзык                  = ДанныеПроекта.ПолучитьЗначениеНастройки("localization.mainLang");
	СоответствиеКонвертации       = ДанныеПроекта.ПолучитьСопоставлениеФайлов();
	
	Документ = Новый ТекстовыйДокумент();
	Документ.ДобавитьСтроку("<package-def>");
	
	Макет = "    <module name=""%1"" file=""%2""/>";
	
	Для Каждого Модуль Из СоответствиеКонвертации Цикл
		
		ТекущийМодуль = Модуль.Значение;
		ФайлМодуля    = Новый Файл(ТекущийМодуль);
		ПутьМодуля    = СокрЛП(СтрЗаменить(ТекущийМодуль, ПутьПакета, ""));
		Библиотека    = ФайлМодуля.ИмяБезРасширения;
		
		Если Не СтрНачинаетсяС(ПутьМодуля, "api") Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущаяСтрока = СтрШаблон(Макет, Библиотека, ПутьМодуля);
		Документ.ДобавитьСтроку(ТекущаяСтрока);
		
		
		Для Каждого Язык Из Языки Цикл

			Если Язык = ОсновнойЯзык Тогда
				Продолжить;
			КонецЕсли;

			СоответствиеНесовпадающихИмен = ДанныеПроекта.ПолучитьСоответствиеНесовпадающихИмен(Язык);

			Для Каждого ПараИмен Из СоответствиеНесовпадающихИмен Цикл
				
				Если ПараИмен.Ключ = Библиотека Тогда		
					ТекущаяСтрока = СтрШаблон(Макет, ПараИмен.Значение, ПутьМодуля);
					Документ.ДобавитьСтроку(ТекущаяСтрока);
					Продолжить;
				КонецЕсли;
				
				Если ПараИмен.Значение = Библиотека Тогда		
					ТекущаяСтрока = СтрШаблон(Макет, ПараИмен.Ключ, ПутьМодуля);
					Документ.ДобавитьСтроку(ТекущаяСтрока);
					Продолжить;
				КонецЕсли;

			КонецЦикла;

		КонецЦикла;

		
	КонецЦикла;
	
	Документ.ДобавитьСтроку("</package-def>");
	CommonTools.ЗаписатьТекст(Документ, ПутьПакета + "lib.config");

	ТекстConfig = Документ.ПолучитьТекст();
	ТекстConfig = СтрЗаменить(ТекстConfig, """api/", """");
	CommonTools.ЗаписатьТекст(ТекстConfig, ПутьПакета + "api/lib.config");
	
КонецПроцедуры

#КонецОбласти