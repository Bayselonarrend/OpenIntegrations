
#Использовать "./internal"

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриСозданииОбъекта(Знач ДанныеПроекта_ = Неопределено)

	Если ДанныеПроекта_ = Неопределено Тогда
		ДанныеПроекта = Новый ProjectData;
	Иначе
		ДанныеПроекта = ДанныеПроекта_;
	КонецЕсли;

	Набор             = ДанныеПроекта.ПолучитьСопоставлениеФайлов();
	СоответствиеЗамен = ДанныеПроекта.ПолучитьЗначениеНастройки("replacements.oscriptConvertation");

	CommonTools.СообщитьПроцесс("Converting 1C to OS project");

	Для Каждого Элемент Из Набор Цикл
		ПортироватьФайл(Элемент.Ключ, Элемент.Значение, СоответствиеЗамен);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПортироватьФайл(Знач Файл1С, Знач ФайлОС, Знач СоответствиеЗамен) Экспорт
	
	ФайлМодуля = Новый Файл(Файл1С);

	Если Не ФайлМодуля.Существует() Тогда
		Возврат;
	КонецЕсли;

	Модуль = ПрочитатьМодуль(Файл1С);

	Если СтрДлина(Модуль) = 0 Тогда
		Возврат;
	КонецЕсли;

	ОбработатьЗаменыМодуля(Модуль, СоответствиеЗамен);
	ЗаписатьМодуль(ФайлОС, Модуль);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПрочитатьМодуль(Знач ФайлМодуля)

	ЧтениеТекста = Новый ЧтениеТекста(ФайлМодуля, "UTF-8");
	Модуль       = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	
	Возврат Модуль;

КонецФункции

Процедура ЗаписатьМодуль(Знач ФайлМодуля, Знач Модуль)

	ФайлОбъект = Новый Файл(ФайлМодуля);
	ФайлКаталог = Новый Файл(ФайлОбъект.Путь);

	Если Не ФайлКаталог.Существует() Тогда
		СоздатьКаталог(ФайлКаталог.ПолноеИмя);
	КонецЕсли;

	CommonTools.ЗаписатьТекст(Модуль, ФайлМодуля);

КонецПроцедуры

Процедура ОбработатьЗаменыМодуля(Модуль, СоответствиеЗамен)

	Для Каждого Замена Из СоответствиеЗамен Цикл
		Модуль = СтрЗаменить(Модуль, Замена.Ключ, Замена.Значение);
	КонецЦикла;

	Если СтрНайти(Модуль, "// !OPI") > 0 Тогда
		
		ТекстовыйДокумент = Новый ТекстовыйДокумент();
		ТекстовыйДокумент.УстановитьТекст(Модуль);
		КоличествоСтрок = ТекстовыйДокумент.КоличествоСтрок();

		Для Н = 0 По КоличествоСтрок - 1 Цикл

			Индекс        = КоличествоСтрок - Н;
			ТекущаяСтрока = ТекстовыйДокумент.ПолучитьСтроку(Индекс);

			Если СтрНайти(ТекущаяСтрока, "// !OPI") > 0 Тогда
				ТекстовыйДокумент.УдалитьСтроку(Индекс)
			КонецЕсли;
			
		КонецЦикла;

		Модуль = ТекстовыйДокумент.ПолучитьТекст();

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

