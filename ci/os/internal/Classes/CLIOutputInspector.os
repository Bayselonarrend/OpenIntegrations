#Использовать "./internal"

Процедура ПриСозданииОбъекта(Знач ДанныеПроекта_ = Неопределено)

	Если ДанныеПроекта_ = Неопределено Тогда
		ДанныеПроекта = Новый ProjectData;
	Иначе
		ДанныеПроекта = ДанныеПроекта_;
	КонецЕсли;
	
	CommonTools.СообщитьПроцесс("Checking CLI help output");

	Корень = ДанныеПроекта.ПолучитьЗначениеНастройки("paths.root");
	Языки  = ДанныеПроекта.ПолучитьЗначениеНастройки("localization.langs");

	Для Каждого Язык Из Языки Цикл

		ОбъектCLI = ЗагрузитьСценарий(Корень + Язык + "/cli/data/Classes/LibraryComposition.os");
		Состав    = ?(Язык = "ru", ОбъектCLI.ПолучитьПолныйСостав(), ОбъектCLI.GetFullComposition());

		ШаблонВызоваМетода     = СтрШаблон("oscript %1%2/cli/core/Classes/app.os %%1 %%2", Корень, Язык);
		ШаблонВызоваБиблиотеки = СтрШаблон("oscript %1%2/cli/core/Classes/app.os %%1", Корень, Язык);

		СоответствиеВызововБиблиотек = Новый Соответствие();
		СоответствиеВызововМетодов   = Новый Соответствие();

		Для Каждого СтрокаСостава Из Состав Цикл

			ТекущийСостав = СтрокаСостава.Значение;

			Для Каждого Метод Из ТекущийСостав["methods"] Цикл

				ТекущаяБиблиотека = ТекущийСостав["library"];
				ТекущийМетод      = Метод["name"];
				
				СоответствиеВызововБиблиотек.Вставить(СтрШаблон(ШаблонВызоваБиблиотеки, ТекущаяБиблиотека), ТекущаяБиблиотека);
				СоответствиеВызововМетодов.Вставить(СтрШаблон(ШаблонВызоваМетода, ТекущаяБиблиотека, ТекущийМетод)
					, СтрШаблон("%1 %2", ТекущаяБиблиотека, ТекущийМетод));

			КонецЦикла;

		КонецЦикла;

		Для Каждого СтрокаВызова Из СоответствиеВызововБиблиотек Цикл
			
			Попытка
				CommonTools.ЗапуститьПриложениеВКонсоли(СтрокаВызова.Ключ);
			Исключение
				ВызватьИсключение СтрШаблон("Ошибка при проверке справки CLI: %1 (%2)", СтрокаВызова.Ключ, Язык);
			КонецПопытки;

		КонецЦикла;

		Для Каждого СтрокаВызова Из СоответствиеВызововМетодов Цикл
			
			Попытка
				CommonTools.ЗапуститьПриложениеВКонсоли(СтрокаВызова.Ключ);
			Исключение
				ВызватьИсключение СтрШаблон("Ошибка при проверке справки CLI: %1 (%2)", СтрокаВызова.Ключ, Язык);
			КонецПопытки;

		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

