#Использовать "./internal"

Процедура ПриСозданииОбъекта(Знач ДанныеПроекта_ = Неопределено)

	Если ДанныеПроекта_ = Неопределено Тогда
		ДанныеПроекта = Новый ProjectData;
	Иначе
		ДанныеПроекта = ДанныеПроекта_;
	КонецЕсли;
	
	CommonTools.СообщитьПроцесс("Конвертация тестов в тесты для CLI");

	Корень = ДанныеПроекта.ПолучитьЗначениеНастройки("paths.root");
	Языки  = ДанныеПроекта.ПолучитьЗначениеНастройки("localization.langs");

	Для Каждого Язык Из Языки Цикл

		ОбъектCLI = ЗагрузитьСценарий(Корень + Язык + "/cli/data/Classes/LibraryComposition.os");
		Состав    = ?(Язык = "ru", ОбъектCLI.ПолучитьПолныйСостав(), ОбъектCLI.GetFullComposition());

		ШаблонВызоваМетода     = СтрШаблон("oscript %1%2/cli/core/Classes/app.os %%1 %%2", Корень, Язык);
		ШаблонВызоваБиблиотеки = СтрШаблон("oscript %1%2/cli/core/Classes/app.os %%1", Корень, Язык);

		СоответствиеВызововБиблиотек = Новый Соответствие();
		СоответствиеВызововМетодов   = Новый Соответствие();

		Для Каждого СтрокаСостава Из Состав Цикл

			Библиотека_ = ?(Язык = "ru", "Библиотека", "Library");
			Метод_      = ?(Язык = "ru", "Метод", "Method");
			
			СоответствиеВызововБиблиотек.Вставить(СтрШаблон(ШаблонВызоваБиблиотеки, СтрокаСостава[Библиотека_]), СтрокаСостава[Библиотека_]);
			СоответствиеВызововМетодов.Вставить(СтрШаблон(ШаблонВызоваМетода, СтрокаСостава[Библиотека_], СтрокаСостава[Метод_])
				, СтрШаблон("%1 %2", СтрокаСостава[Библиотека_], СтрокаСостава[Метод_]));
		КонецЦикла;

		Для Каждого СтрокаВызова Из СоответствиеВызововБиблиотек Цикл
			КодВозврата = Неопределено;
			ЗапуститьПриложение(СтрокаВызова.Ключ, , Истина, КодВозврата);

			Если КодВозврата <> 0 Тогда
				ВызватьИсключение СтрШаблон("Ошибка при проверке справки CLI: %1 %2 (%3)", КодВозврата, СтрокаВызова.Ключ, Язык);
			КонецЕсли;

		КонецЦикла;

		Для Каждого СтрокаВызова Из СоответствиеВызововМетодов Цикл
			КодВозврата = Неопределено;
			ЗапуститьПриложение(СтрокаВызова.Ключ, , Истина, КодВозврата);

			Если КодВозврата <> 0 Тогда
				ВызватьИсключение СтрШаблон("Ошибка при проверке справки CLI: %1 %2 (%3)", КодВозврата, СтрокаВызова.Ключ, Язык);
			КонецЕсли;

		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

