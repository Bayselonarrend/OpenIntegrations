#Использовать "./../../../../src/en/OInt/tools/"
#Использовать "./../../../../src/ru/OInt/tools/"
#Использовать "./internal"

Перем Путь1с;
Перем Сервер1с;
Перем База1сRu;
Перем База1сEn;

Перем КаталогКонфигурацииYaxUnit;
Перем ШаблонКонфигурацииYaxUnit;

Перем ФайлКонфигурацииТестов;
Перем ДанныеКонфигурацииТестов;
Перем СписокКритическихТестов;

Перем СписокРабот;
Перем ШаблонCliWindows;
Перем ШаблонOsWindows;
Перем Шаблон1cWindows;
Перем ШаблонOsLinux;
Перем ШаблонCliRpm;
Перем ШаблонCliDeb;

Перем ШаблонШага;
Перем ШаблонКритическогоШага;
Перем ШаблонВыполнения;
Перем ШаблонПайплайна;
Перем ШаблонПапки;
Перем ЛогинJenkins;
Перем ТокенJenkins;
Перем ХостJenkins;
Перем ДанныеПроекта;

Процедура ПриСозданииОбъекта(Знач ДанныеПроекта_ = Неопределено)

	Если ДанныеПроекта_ = Неопределено Тогда
		ДанныеПроекта = Новый ProjectData;
	Иначе
		ДанныеПроекта = ДанныеПроекта_;
	КонецЕсли;

	CommonTools.СообщитьПроцесс("Формирование jenkinsfile тестирования");
	
	КаталогШаблонов   = ДанныеПроекта.ПолучитьЗначениеНастройки("paths.templatesSrc");
	КаталогWorkflow   = ДанныеПроекта.ПолучитьЗначениеНастройки("paths.jenkinsSrc");
	Языки             = ДанныеПроекта.ПолучитьЗначениеНастройки("localization.langs");

	Путь1с      = ДанныеПроекта.ПолучитьЗначениеНастройки("local.onecPath");
	Сервер1с    = ДанныеПроекта.ПолучитьЗначениеНастройки("local.serverName");
	ХостJenkins = ДанныеПроекта.ПолучитьЗначениеНастройки("local.jenkinsHost");
	База1сRu    = ДанныеПроекта.ПолучитьЗначениеНастройки("localization.langsVars.ru.ib_name");
	База1сEn    = ДанныеПроекта.ПолучитьЗначениеНастройки("localization.langsVars.en.ib_name");

	ДанныеКонфигурацииТестов   = ДанныеПроекта.ПолучитьЗначениеНастройки("tests.availability");
	КаталогКонфигурацииYaxUnit = ДанныеПроекта.ПолучитьЗначениеНастройки("tests.yaxunitConf");
	ПутьJenkinsFull            = ДанныеПроекта.ПолучитьЗначениеНастройки("tests.jenkinsFull");
	ПутьJenkinsSplit           = ДанныеПроекта.ПолучитьЗначениеНастройки("tests.jenkinsSplit");

	КаталогWorkflowТестов     = КаталогWorkflow + "jenkins_full_tests";
	ШаблонКонфигурацииYaxUnit = КаталогКонфигурацииYaxUnit + "template.txt";
	ШаблонКонфигурацииYaxUnit = Новый ДвоичныеДанные(ШаблонКонфигурацииYaxUnit);
	ШаблонКонфигурацииYaxUnit = ПолучитьСтрокуИзДвоичныхДанных(ШаблонКонфигурацииYaxUnit);

	СписокКритическихТестов = Новый СписокЗначений();
	СписокКритическихТестов.Добавить("BuildCheck");

	КаталогШаблоновJenkins = КаталогШаблонов + "jenkins/";
	ЛогинJenkins           = ПолучитьПеременнуюСреды("JENKINS_LOGIN", РасположениеПеременнойСреды.Машина);
	ТокенJenkins           = ПолучитьПеременнуюСреды("JENKINS_TOKEN", РасположениеПеременнойСреды.Машина);
	
	ПутьШаблонаCliWindows = КаталогШаблоновJenkins + "cli_test_windows.txt";
	ПутьШаблонаOsWindows  = КаталогШаблоновJenkins + "os_test_windows.txt";
	ПутьШаблонаOsLinux    = КаталогШаблоновJenkins + "os_test_linux.txt";
	ПутьШаблонаCliRpm     = КаталогШаблоновJenkins + "cli_test_rpm.txt";
	ПутьШаблонаCliDeb     = КаталогШаблоновJenkins + "cli_test_deb.txt";
	ПутьШаблона1cWindows  = КаталогШаблоновJenkins + "1c_test_windows.txt";

	ПутьШаблонаШага             = КаталогШаблоновJenkins + "test_stage.txt";
	ПутьШаблонаКритическогоШага = КаталогШаблоновJenkins + "test_stage_fatal.txt";
	ПутьШаблонаВыполнения       = КаталогШаблоновJenkins + "test_exec.txt";
	ПутьШаблонаПайплайна        = КаталогШаблоновJenkins + "conf_pipeline.txt";
	ПутьШаблонаПапки            = КаталогШаблоновJenkins + "conf_folder.txt";

	ШаблонCliWindows = Новый ДвоичныеДанные(ПутьШаблонаCliWindows);
	ШаблонCliWindows = ПолучитьСтрокуИзДвоичныхДанных(ШаблонCliWindows);

	ШаблонOsWindows = Новый ДвоичныеДанные(ПутьШаблонаOsWindows);
	ШаблонOsWindows = ПолучитьСтрокуИзДвоичныхДанных(ШаблонOsWindows);

	ШаблонOsLinux = Новый ДвоичныеДанные(ПутьШаблонаOsLinux);
	ШаблонOsLinux = ПолучитьСтрокуИзДвоичныхДанных(ШаблонOsLinux);

	ШаблонCliRpm = Новый ДвоичныеДанные(ПутьШаблонаCliRpm);
	ШаблонCliRpm = ПолучитьСтрокуИзДвоичныхДанных(ШаблонCliRpm);

	ШаблонCliDeb = Новый ДвоичныеДанные(ПутьШаблонаCliDeb);
	ШаблонCliDeb = ПолучитьСтрокуИзДвоичныхДанных(ШаблонCliDeb);

	Шаблон1cWindows = Новый ДвоичныеДанные(ПутьШаблона1cWindows);
	Шаблон1cWindows = ПолучитьСтрокуИзДвоичныхДанных(Шаблон1cWindows);

	ШаблонШага = Новый ДвоичныеДанные(ПутьШаблонаШага);
	ШаблонШага = ПолучитьСтрокуИзДвоичныхДанных(ШаблонШага);

	ШаблонКритическогоШага = Новый ДвоичныеДанные(ПутьШаблонаКритическогоШага);
	ШаблонКритическогоШага = ПолучитьСтрокуИзДвоичныхДанных(ШаблонКритическогоШага);

	ШаблонВыполнения = Новый ДвоичныеДанные(ПутьШаблонаВыполнения);
	ШаблонВыполнения = ПолучитьСтрокуИзДвоичныхДанных(ШаблонВыполнения);

	ШаблонПайплайна = Новый ДвоичныеДанные(ПутьШаблонаПайплайна);
	ШаблонПайплайна = ПолучитьСтрокуИзДвоичныхДанных(ШаблонПайплайна);

	ШаблонПапки = Новый ДвоичныеДанные(ПутьШаблонаПапки);
	ШаблонПапки = ПолучитьСтрокуИзДвоичныхДанных(ШаблонПапки);

	СоздатьНачальнуюСтруктуруJenkins();

	Для Каждого Язык Из Языки Цикл

		СписокБиблиотек = Неопределено;
		Выполнить("СписокБиблиотек = " + ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("testsSectionsMethod", Язык));

		СформироватьWorflow(КаталогWorkflowТестов, Язык, СписокБиблиотек, ПутьJenkinsFull);

		Для Каждого Библиотека Из СписокБиблиотек Цикл

			ТекущийСписок = Новый Структура;
			ТекущийСписок.Вставить("BuildCheck", 1);
			ТекущийСписок.Вставить(Библиотека.Ключ, Библиотека.Значение);

			ИмяБиблиотеки          = Библиотека.Ключ;
			ПутьJenkinsБиблиотеки  = ПутьJenkinsSplit + "/job/" + ИмяБиблиотеки;

			СоздатьПапкуJenkins(ПутьJenkinsSplit, ИмяБиблиотеки, ИмяБиблиотеки);
			СформироватьWorflow(СтрШаблон("%1%2/%3/", КаталогWorkflow, "jenkins_split_tests", ИмяБиблиотеки)
				, Язык
				, ТекущийСписок
				, ПутьJenkinsБиблиотеки);

		КонецЦикла;

	КонецЦикла;

	OPI_Tools.ЗаписатьJSONФайл(ФайлКонфигурацииТестов, ДанныеКонфигурацииТестов);

КонецПроцедуры

Процедура СформироватьWorflow(Знач КаталогWorkflow
	, Знач Язык
	, Знач СписокБиблиотек
	, Знач ПутьJenkins)
	
	СписокРабот     = "";
	ТаблицаТестов   = Неопределено;
	МассивВлияющих  = Новый Массив;
	ЯзыкВрег        = ВРег(Язык);
	ЯзыкТрег        = ТРег(Язык);
	База            = ?(Язык = "ru", База1сRu, База1сEn);

	МодульТестов    = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("testsModulePath", Язык);
	МодульТестовCLI = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("testsCliModulePath", Язык);
	МетодТаблицы    = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("testsTableMethod", Язык);

	Выполнить("ТаблицаТестов   = " + МетодТаблицы);

	КаталогWorkflowОбъект = Новый Файл(КаталогWorkflow);
	
	Если Не КаталогWorkflowОбъект.Существует() Тогда
		СоздатьКаталог(КаталогWorkflow);
	КонецЕсли;
	
	ФайлJenkinsOS    = КаталогWorkflow + "os_test_" + Язык;
	ФайлJenkinsOSRPM = КаталогWorkflow + "os_rpm_test_" + Язык;
	ФайлJenkinsOSDEB = КаталогWorkflow + "os_deb_test_" + Язык;
	ФайлWindowsCLI   = КаталогWorkflow + "cli_test_" + Язык;
	ФайлRpmCLI       = КаталогWorkflow + "cli_rpm_test_" + Язык;
	ФайлDebCLI       = КаталогWorkflow + "cli_deb_test_" + Язык;
	ФайлWindows1c    = КаталогWorkflow + "1c_test_" + Язык;

	ТекстВыполненияOS  = ПолучитьТекстВыполненияOs(ТаблицаТестов, СписокБиблиотек, МодульТестов, Язык);
	ТекстВыполненияCLI = ПолучитьТекстВыполненияOs(ТаблицаТестов, СписокБиблиотек, МодульТестовCLI, Язык);
	ТекстВыполнения1С  = ПолучитьТекстВыполнения1c(ТаблицаТестов, СписокБиблиотек, МодульТестов, Язык);

	ТекстJFCLI   = СтрШаблон(ШаблонCliWindows, Язык, ТекстВыполненияCLI);
	ТекстJFOS    = СтрШаблон(ШаблонOsWindows, Язык, ТекстВыполненияOS);

	ТекстJFOSRPM = СтрШаблон(ШаблонOsLinux, Язык, ТекстВыполненияOS, "Rpm-Agent");
	ТекстJFOSDEB = СтрШаблон(ШаблонOsLinux, Язык, ТекстВыполненияOS, "Deb-Agent");
	
	ТекстJFCLIRPM = СтрШаблон(ШаблонCliRpm, Язык, ТекстВыполненияCLI, "Rpm-Agent");
	ТекстJFCLIDEB = СтрШаблон(ШаблонCliDeb, Язык, ТекстВыполненияCLI, "Deb-Agent");

	ТекстJF1CWin = СтрШаблон(Шаблон1cWindows, ТекстВыполнения1С, Путь1с, Сервер1с, База);

	ШаблонПредставления = "OPI | Тестирование версии для %1 (%2, %3)";

	ПолучитьДвоичныеДанныеИзСтроки(ТекстJFOS).Записать(ФайлJenkinsOS);
	СоздатьПайплайнJenkins(ПутьJenkins
		, "OpiOsWin" + ЯзыкТрег
		, СтрШаблон(ШаблонПредставления, "OneScript", "Windows", ЯзыкВрег)
		, ФайлJenkinsOS);

	ПолучитьДвоичныеДанныеИзСтроки(ТекстJFOSRPM).Записать(ФайлJenkinsOSRPM);
	СоздатьПайплайнJenkins(ПутьJenkins
		, "OpiOsRpm" + ЯзыкТрег
		, СтрШаблон(ШаблонПредставления, "OneScript", "RPM", ЯзыкВрег)
		, ФайлJenkinsOSRPM);

	ПолучитьДвоичныеДанныеИзСтроки(ТекстJFOSDEB).Записать(ФайлJenkinsOSDEB);
	СоздатьПайплайнJenkins(ПутьJenkins
		, "OpiOsDeb" + ЯзыкТрег
		, СтрШаблон(ШаблонПредставления, "OneScript", "DEB", ЯзыкВрег)
		, ФайлJenkinsOSDEB);

	ПолучитьДвоичныеДанныеИзСтроки(ТекстJFCLI).Записать(ФайлWindowsCLI);
	СоздатьПайплайнJenkins(ПутьJenkins
		, "OpiCliWin" + ЯзыкТрег
		, СтрШаблон(ШаблонПредставления, "CLI", "Windows", ЯзыкВрег)
		, ФайлWindowsCLI);

	ПолучитьДвоичныеДанныеИзСтроки(ТекстJFCLIRPM).Записать(ФайлRpmCLI);
	СоздатьПайплайнJenkins(ПутьJenkins
		, "OpiCliRpm" + ЯзыкТрег
		, СтрШаблон(ШаблонПредставления, "CLI", "RPM", ЯзыкВрег)
		, ФайлRpmCLI);

	ПолучитьДвоичныеДанныеИзСтроки(ТекстJFCLIDEB).Записать(ФайлDebCLI);
	СоздатьПайплайнJenkins(ПутьJenkins
		, "OpiCliDeb" + ЯзыкТрег
		, СтрШаблон(ШаблонПредставления, "CLI", "DEB", ЯзыкВрег)
		, ФайлDebCLI);

	ПолучитьДвоичныеДанныеИзСтроки(ТекстJF1CWin).Записать(ФайлWindows1c);
	СоздатьПайплайнJenkins(ПутьJenkins
		, "Opi1cWin" + ЯзыкТрег
		, СтрШаблон(ШаблонПредставления, "1C:Enterprise", "Windows", ЯзыкВрег)
		, ФайлWindows1c);

КонецПроцедуры

Функция ПолучитьТекстВыполненияOs(Знач ТаблицаТестов, Знач СписокБиблиотек, Знач МодульТестов, Знач Язык)
	
	СтрокаРаздел    = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("sectionsString", Язык);
	СтрокаМетод     = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("methodString", Язык);
	СтрокаСиноним   = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("synonymString", Язык);

	Если Не ТипЗнч(СписокБиблиотек) = Тип("Структура") Тогда
		СписокБиблиотек_ = Новый Структура;
		СписокБиблиотек_.Вставить(СписокБиблиотек.Ключ, СписокБиблиотек.Значение);
		СписокБиблиотек = СписокБиблиотек_;
	КонецЕсли;

	МассивШагов = Новый Массив;
	Для Каждого Библиотека Из СписокБиблиотек Цикл

		Раздел            = Библиотека.Ключ;
		ТекущийШаблонШага = ?(СписокКритическихТестов.НайтиПоЗначению(Раздел) = Неопределено
			, ШаблонШага
			, ШаблонКритическогоШага);	

		Отбор = Новый Структура(СтрокаРаздел, Раздел);
		ТестыТекущегоРаздела = ТаблицаТестов.НайтиСтроки(Отбор);

		МассивВыполнений = Новый Массив;
		Для Каждого Тест Из ТестыТекущегоРаздела Цикл

			Метод = Тест[СтрокаМетод];
			
			МассивВыполнений.Добавить(СтрШаблон(ШаблонВыполнения, МодульТестов, Метод))

		КонецЦикла;

		МассивШагов.Добавить(СтрШаблон(ТекущийШаблонШага, Раздел, СтрСоединить(МассивВыполнений, Символы.ПС)));

		Если ДанныеКонфигурацииТестов.Получить(Раздел) = Неопределено Тогда
			ДанныеКонфигурацииТестов.Вставить(Раздел, Истина);
		КонецЕсли;

	КонецЦикла;

	Возврат СтрСоединить(МассивШагов, Символы.ПС);

КонецФункции

Функция ПолучитьТекстВыполнения1c(Знач ТаблицаТестов, Знач СписокБиблиотек, Знач МодульТестов, Знач Язык)
	
	СтрокаРаздел    = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("sectionsString", Язык);
	СтрокаМетод     = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("methodString", Язык);
	СтрокаСиноним   = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("synonymString", Язык);

	ИмяМодуляТестов = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("testsModule", Язык);

	Если Не ТипЗнч(СписокБиблиотек) = Тип("Структура") Тогда
		СписокБиблиотек_ = Новый Структура;
		СписокБиблиотек_.Вставить(СписокБиблиотек.Ключ, СписокБиблиотек.Значение);
		СписокБиблиотек = СписокБиблиотек_;
	КонецЕсли;

	МассивШагов = Новый Массив;
	Для Каждого Библиотека Из СписокБиблиотек Цикл

		Раздел  = Библиотека.Ключ;

		ТекущийШаблонШага = ?(СписокКритическихТестов.НайтиПоЗначению(Раздел) = Неопределено
			, ШаблонШага
			, ШаблонКритическогоШага);	

		YaxConf = КаталогКонфигурацииYaxUnit + Язык + "/" + Раздел + ".json";
		Выполнение = СтрШаблон(ШаблонВыполнения, Раздел, YaxConf);

		Отбор = Новый Структура(СтрокаРаздел, Раздел);
		ТестыТекущегоРаздела = ТаблицаТестов.НайтиСтроки(Отбор);

		МассивМетодов = Новый Массив;

		Для Каждого Тест Из ТестыТекущегоРаздела Цикл

			Метод = Тест[СтрокаМетод];
			МассивМетодов.Добавить("""" + ИмяМодуляТестов + "." + Метод + """" );
			
		КонецЦикла;

		ПолучитьДвоичныеДанныеИзСтроки(СтрШаблон(ШаблонКонфигурацииYaxUnit, Раздел, СтрСоединить(МассивМетодов, "," + Символы.ПС)))
			.Записать(КаталогКонфигурацииYaxUnit + Язык + "/" + Раздел + ".json");

		МассивШагов.Добавить(СтрШаблон(ТекущийШаблонШага, Раздел, Выполнение));

	КонецЦикла;

	Возврат СтрСоединить(МассивШагов, Символы.ПС);

КонецФункции

Процедура СоздатьНачальнуюСтруктуруJenkins()

	УдалитьЭлементJenkins("/job/OPITest");

	СоздатьПапкуJenkins("", "OPITest", "OPI | Тестирование");
	СоздатьПапкуJenkins("/job/OPITest", "OpiFullTest", "Полные тесты");
	СоздатьПапкуJenkins("/job/OPITest", "OpiSplitTests", "Раздельные тесты");

КонецПроцедуры

Процедура СоздатьПапкуJenkins(Знач Путь, Знач Имя, Знач Представление)

	Данные = СтрШаблон(ШаблонПапки, Представление);

	СоздатьЭлементJenkins(Путь, Имя + "&mode=com.cloudbees.hudson.plugins.folder.Folder", Данные);

КонецПроцедуры

Процедура СоздатьПайплайнJenkins(Знач Путь, Знач Имя, Знач Представление, Знач Jenkinsfile)

	Данные = СтрШаблон(ШаблонПайплайна, Представление, Jenkinsfile);

	СоздатьЭлементJenkins(Путь, Имя, Данные);

КонецПроцедуры

Процедура СоздатьЭлементJenkins(Знач Путь, Знач Имя, Знач Данные)

	Url = СтрШаблон("%1%2/createItem?name=%3", ХостJenkins, Путь, Имя);

	Результат = OPI_ЗапросыHTTP.НовыйЗапрос()
        .Инициализировать()
        .УстановитьURL(URL)
		.УстановитьСтроковоеТело(Данные)
		.УстановитьТипДанных("application/xml")
        .ДобавитьBasicАвторизацию(ЛогинJenkins, ТокенJenkins)
        .ОбработатьЗапрос("POST")
        .ВернутьОтвет()
		.КодСостояния;

	Если Результат >= 400 Тогда
		Сообщить(СтрШаблон("		Ошибка создания %1: %2", Путь, Результат))
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьЭлементJenkins(Знач Путь)

	Url = СтрШаблон("%1%2/doDelete", ХостJenkins, Путь);

    Результат = OPI_ЗапросыHTTP.НовыйЗапрос()
        .Инициализировать()
        .УстановитьURL(URL)
        .ДобавитьBasicАвторизацию(ЛогинJenkins, ТокенJenkins)
        .ОбработатьЗапрос("POST")
        .ВернутьОтвет()
		.КодСостояния;

	Если Результат >= 400 Тогда
		Сообщить(СтрШаблон("		Ошибка удаления %1: %2", Путь, Результат));
	КонецЕсли;

КонецПроцедуры
