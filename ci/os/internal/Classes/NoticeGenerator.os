#Использовать "./internal"

Процедура ПриСозданииОбъекта(Знач ДанныеПроекта_ = Неопределено)

	Если ДанныеПроекта_ = Неопределено Тогда
		ДанныеПроекта = Новый ProjectData;
	Иначе
		ДанныеПроекта = ДанныеПроекта_;
	КонецЕсли;

	CommonTools.СообщитьПроцесс("Checking licenses for Rust projects");

	КаталогКомпонент    = ДанныеПроекта.ПолучитьЗначениеНастройки("paths.addinsSrc");
	КаталогШаблонов     = ДанныеПроекта.ПолучитьЗначениеНастройки("paths.templatesSrc");
	КаталогLicenseCheck = ДанныеПроекта.ПолучитьЗначениеНастройки("paths.licenseCheckSrc");

	ПутьNotice   = КаталогШаблонов + "notice.txt";
	ШаблонNotice = Новый Файл(ПутьNotice);

	Если Не ШаблонNotice.Существует() Тогда
		ВызватьИсключение СтрШаблон("notice.txt not found at %1", КаталогШаблонов);
	Иначе
		ПутьNotice = ШаблонNotice.ПолноеИмя;
	КонецЕсли;

	ПутьAbout = КаталогLicenseCheck + "about.toml";
	ФайлAbout = Новый Файл(ПутьAbout);

	Если Не ФайлAbout.Существует() Тогда
		ВызватьИсключение СтрШаблон("about.toml not found at %1", КаталогLicenseCheck);
	Иначе
		ПутьAbout = ФайлAbout.ПолноеИмя;
	КонецЕсли;

	ПутьDeny = КаталогШаблонов + "deny.toml";
	ФайлDeny = Новый Файл(ПутьDeny);

	Если Не ФайлDeny.Существует() Тогда
		ВызватьИсключение СтрШаблон("deny.toml not found at %1", КаталогШаблонов);
	Иначе
		ПутьDeny = ФайлDeny.ПолноеИмя;
	КонецЕсли;

	ДокументNotice = Новый ТекстовыйДокумент();
	ДокументNotice.Прочитать(ПутьNotice);

	ФайлыCargo = НайтиФайлы(КаталогКомпонент, "Cargo.toml", Истина);

	ШаблонВызова   = СтрШаблон("cargo about generate --config ""%1"" --format json", ПутьAbout);
	ШаблонУказания = "
	|%1
	| * %2
	| * %3
	| * %4";

	ШаблонИспользующих = "================================================================================
	|License: %1
	|Used by: %2
	|================================================================================
	|
	|%3
	|
	|";

	СоответствиеТекстовЛицензий  = Новый Соответствие();
	СоответствиеОписанийПроектов = Новый Соответствие();
	СоответствиеИспользующих     = Новый Соответствие();

	Для Каждого Проект Из ФайлыCargo Цикл
	
		ТекущийКаталог  = Проект.Путь;
		ТекущийПутьDeny = ТекущийКаталог + "deny.toml";

		ОбъектКаталога = Новый Файл(ТекущийКаталог);
		ШаблонЛога     = СтрШаблон("%1: %%1", ОбъектКаталога.Имя);

		ФайлDeny = Новый Файл(ТекущийПутьDeny);

		Если ФайлDeny.Существует() Тогда
			УдалитьФайлы(ТекущийПутьDeny);
		КонецЕсли;
		
		КопироватьФайл(ПутьDeny, ТекущийПутьDeny);

		CommonTools.СообщитьПроцесс(СтрШаблон(ШаблонЛога, "licenses compatibility check"));
		CommonTools.ЗапуститьВнешнееПриложение("cargo deny check licenses", ТекущийКаталог, Ложь);
		CommonTools.СообщитьПроцесс(СтрШаблон(ШаблонЛога, "check complete"));
		

		CommonTools.СообщитьПроцесс(СтрШаблон(ШаблонЛога, "Fetching information for NOTICE and TPL"));
		ТекстРезультата = CommonTools.ЗапуститьВнешнееПриложение(ШаблонВызова, ТекущийКаталог, Ложь);
		ТекущиеДанные   = CommonTools.JsonВСоответствие(ТекстРезультата);

		ТекущийЛицензии = ТекущиеДанные["licenses"];

		Для Каждого Лицензия Из ТекущийЛицензии Цикл

			Использующие = Лицензия["used_by"];

			ПолноеИмяЛицензии = СтрШаблон("%1 (SPDX: %2)", Лицензия["name"], Лицензия["id"]);

			МассивИспользующих = СоответствиеИспользующих.Получить(ПолноеИмяЛицензии);
			МассивИспользующих = ?(МассивИспользующих = Неопределено, Новый Массив, МассивИспользующих);

			Для Каждого Использующий Из Использующие Цикл
				
				ТекущийКрейт = Использующий["crate"];
				ТекстУказания = СтрШаблон(ШаблонУказания
					, ТекущийКрейт["name"]
					, СтрСоединить(ТекущийКрейт["authors"], ", ")
					, ТекущийКрейт["license"]
					, ТекущийКрейт["repository"]);

				СоответствиеОписанийПроектов.Вставить(ТекущийКрейт["name"], ТекстУказания);

				Если МассивИспользующих.Найти(ТекущийКрейт["name"]) = Неопределено Тогда
					МассивИспользующих.Добавить(ТекущийКрейт["name"]);
				КонецЕсли;
				
			КонецЦикла;

			СоответствиеТекстовЛицензий.Вставить(ПолноеИмяЛицензии, Лицензия["text"]);
			СоответствиеИспользующих.Вставить(ПолноеИмяЛицензии, МассивИспользующих);

		КонецЦикла;

	КонецЦикла;

	CommonTools.СообщитьПроцесс("Generating NOTICE file");
	Для Каждого Описание Из СоответствиеОписанийПроектов Цикл
		ДокументNotice.ДобавитьСтроку(Описание.Значение);
	КонецЦикла;

	CommonTools.СообщитьПроцесс("Generating THIRD-PARTY-LICENSES file");
	ДокументTPL = Новый ТекстовыйДокумент();
	ДокументTPL.ДобавитьСтроку("THIRD-PARTY SOFTWARE LICENSES
	|=================================
	|
	|This file contains the full license texts for all third-party dependencies used in this project.
	|
	|
	|");

	Для Каждого Лицензия Из СоответствиеИспользующих Цикл

		ДокументTPL.ДобавитьСтроку(СтрШаблон(ШаблонИспользующих
			, Лицензия.Ключ
			, СтрСоединить(Лицензия.Значение, ", ")
			, СоответствиеТекстовЛицензий.Получить(Лицензия.Ключ)));

	КонецЦикла;

	CommonTools.СообщитьПроцесс("Saving...");

	ДокументNotice.Записать("./NOTICE");
	ДокументTPL.Записать("./THIRD-PARTY-LICENSES");

	CommonTools.СообщитьПроцесс("Generating complete");

КонецПроцедуры