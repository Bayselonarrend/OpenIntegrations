
Функция ПрочитатьФайлJSON(Знач Путь) Экспорт
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.ОткрытьФайл(Путь);
	Данные = ПрочитатьJSON(ЧтениеJSON, Истина);
	ЧтениеJSON.Закрыть();
	
	Возврат Данные;
	
КонецФункции

Процедура ЗаписатьФайлJSON(Знач Путь, Знач Данные) Экспорт
	
	Запись = Новый ЗаписьJSON;
	ПараметрыЗаписиJSON = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Unix, Символы.Таб);
	Запись.ОткрытьФайл(Путь, , , ПараметрыЗаписиJSON);
	ЗаписатьJSON(Запись, Данные);
	Запись.Закрыть();
	
КонецПроцедуры

Функция JsonВСоответствие(Знач Текст) Экспорт

    ЧтениеJSON = Новый ЧтениеJSON;
    ТипТекста  = ТипЗнч(Текст);

    Если ТипТекста = Тип("ДвоичныеДанные") Тогда

        Текст = ПолучитьСтрокуИзДвоичныхДанных(Текст);
        ЧтениеJSON.УстановитьСтроку(Текст);

    ИначеЕсли ТипТекста = Тип("Поток") Или ТипТекста = Тип("ПотокВПамяти") Или ТипТекста = Тип("ФайловыйПоток") Тогда


		ЧтениеДанных    = Новый ЧтениеДанных(Текст);
		РезультатЧтения = ЧтениеДанных.Прочитать();
		JSONДвоичные    = РезультатЧтения.ПолучитьДвоичныеДанные();

		ЧтениеJSON.УстановитьСтроку(ПолучитьСтрокуИзДвоичныхДанных(JSONДвоичные));
		ЧтениеДанных.Закрыть();

    Иначе

        ЧтениеJSON.УстановитьСтроку(Строка(Текст));

    КонецЕсли;

    Данные = ПрочитатьJSON(ЧтениеJSON, Истина, Неопределено, ФорматДатыJSON.ISO);
    ЧтениеJSON.Закрыть();

    Возврат Данные;

КонецФункции

Процедура СообщитьПроцесс(Знач Текст) экспорт
	Сообщить(СтрШаблон("[%1] %2", ТекущаяДата(), Текст));
КонецПроцедуры

Функция ЗапуститьВнешнееПриложение(Знач СтрокаВызова, Знач Каталог = Неопределено, Знач ВыводВКонсоль = Истина) Экспорт
	
	КодВозврата = Неопределено;
	
	Переменные = Новый Соответствие();
	Переменные.Вставить("PATH", ПолучитьПеременнуюСреды("PATH", РасположениеПеременнойСреды.Машина));
	
	Процесс = СоздатьПроцесс(СтрокаВызова, Каталог, Истина, , , Переменные);
	Процесс.Запустить();
	
	ПериодОпросаВМиллисекундах = 10;
	МассивРезультата = Новый Массив;

	Пока Не Процесс.Завершен Или Процесс.ПотокВывода.ЕстьДанные Или Процесс.ПотокОшибок.ЕстьДанные Цикл

		Приостановить(ПериодОпросаВМиллисекундах);

		ОчереднаяСтрокаВывода = Процесс.ПотокВывода.Прочитать();
		ОчереднаяСтрокаОшибок = Процесс.ПотокОшибок.Прочитать();

		Если Не ПустаяСтрока(ОчереднаяСтрокаВывода) Тогда

			Если ВыводВКонсоль Тогда
				Сообщить(ОчереднаяСтрокаВывода, СтатусСообщения.Информация);
			КонецЕсли;

			МассивРезультата.Добавить(ОчереднаяСтрокаВывода);

		КонецЕсли;

		Если Не ПустаяСтрока(ОчереднаяСтрокаОшибок) Тогда
			Сообщить(ОчереднаяСтрокаОшибок, СтатусСообщения.Важное);
		КонецЕсли;

	КонецЦикла;

	
	Если Процесс.КодВозврата <> 0 Тогда
		ВызватьИсключение "Process failed";
	КонецЕсли;

	Возврат СтрСоединить(МассивРезультата, Символы.ПС);
	
КонецФункции

Функция ПолучитьИнформациюОМодуле(Знач Модуль) Экспорт

	Если ТипЗнч(Модуль) = Тип("ТекстовыйДокумент") Тогда

		ТекущийМодуль = Модуль;

	ИначеЕсли ТипЗнч(Модуль) = Тип("Строка") Тогда

		ТекущийМодуль = Новый ТекстовыйДокумент();

		ФайлМодуля = Новый Файл(Модуль);

		Если ФайлМодуля.Существует() Тогда
			ТекущийМодуль.Прочитать(ФайлМодуля.ПолноеИмя);
		Иначе
			ТекущийМодуль.УстановитьТекст(Модуль);
		КонецЕсли;

	Иначе
		ВызватьИсключение "Неподдерживаемый тип модуля для получения информации";
	КонецЕсли;

	СоответствиеПризнаков = Новый Соответствие();
	СоответствиеПризнаков.Вставить("// OneScript: "   , "МодульOS");
	СоответствиеПризнаков.Вставить("// Lib: "         , "Библиотека");
	СоответствиеПризнаков.Вставить("// CLI: "         , "Команда");
	СоответствиеПризнаков.Вставить("// Keywords: "    , "КлючевыеСлова");
	СоответствиеПризнаков.Вставить("// Depends: "     , "Зависимости");
	СоответствиеПризнаков.Вставить("// DocsCategory: ", "Категория");
	СоответствиеПризнаков.Вставить("// DocsNameRU: "  , "ПредставлениеRu");
	СоответствиеПризнаков.Вставить("// DocsNameEN: "  , "ПредставлениеEn");

	СоответствиеДанных = Новый Соответствие();

	Для Н = 1 По 100 Цикл

		ТекущаяСтрока = СокрЛП(ТекущийМодуль.ПолучитьСтроку(Н));

		Для Каждого Признак Из СоответствиеПризнаков Цикл
			Если СтрНачинаетсяС(ТекущаяСтрока, Признак.Ключ) Тогда
				СоответствиеДанных.Вставить(Признак.Значение, СтрЗаменить(ТекущаяСтрока, Признак.Ключ, ""));
				Прервать;
			КонецЕсли;
		КонецЦикла;

	КонецЦикла;

	Каталог = СтрЗаменить(СоответствиеДанных["Библиотека"], " ", "");
	Каталог = СтрЗаменить(Каталог, "клиент", "client");

	СоответствиеДанных.Вставить("Каталог", Каталог);

	Возврат СоответствиеДанных;

КонецФункции

Функция ЭтоWindows() Экспорт

    СистемнаяИнформация = Новый СистемнаяИнформация;
    ОперационнаяСистема = Строка(СистемнаяИнформация.ТипПлатформы);

    Ответ = СтрНайти(нРег(ОперационнаяСистема), "windows") > 0;

    Возврат Ответ;

КонецФункции

Функция ПолучитьАлфавит() Экспорт
	
	Алфавит = Новый Массив;
	
	Алфавит.Добавить("а");
	Алфавит.Добавить("б");
	Алфавит.Добавить("в");
	Алфавит.Добавить("г");
	Алфавит.Добавить("д");
	Алфавит.Добавить("е");
	Алфавит.Добавить("ё");
	Алфавит.Добавить("ж");
	Алфавит.Добавить("з");
	Алфавит.Добавить("и");
	Алфавит.Добавить("й");
	Алфавит.Добавить("к");
	Алфавит.Добавить("л");
	Алфавит.Добавить("м");
	Алфавит.Добавить("н");
	Алфавит.Добавить("о");
	Алфавит.Добавить("п");
	Алфавит.Добавить("р");
	Алфавит.Добавить("с");
	Алфавит.Добавить("т");
	Алфавит.Добавить("у");
	Алфавит.Добавить("ф");
	Алфавит.Добавить("х");
	Алфавит.Добавить("ц");
	Алфавит.Добавить("ч");
	Алфавит.Добавить("ш");
	Алфавит.Добавить("щ");
	Алфавит.Добавить("ъ");
	Алфавит.Добавить("ы");
	Алфавит.Добавить("ь");
	Алфавит.Добавить("э");
	Алфавит.Добавить("ю");
	Алфавит.Добавить("я");
	
	Возврат Алфавит;
	
КонецФункции

Функция ПолучитьМассивСимволов() Экспорт
	
	МассивСимволов = Новый Массив();
	МассивСимволов.Добавить(";");
	МассивСимволов.Добавить(".");
	МассивСимволов.Добавить(",");
	МассивСимволов.Добавить(")");
	МассивСимволов.Добавить("(");
	МассивСимволов.Добавить("/");
	МассивСимволов.Добавить("\");
	МассивСимволов.Добавить("[");
	МассивСимволов.Добавить("]");
	МассивСимволов.Добавить("=");
	МассивСимволов.Добавить("+");
	МассивСимволов.Добавить("-");
	МассивСимволов.Добавить("*");
	МассивСимволов.Добавить("#");
	МассивСимволов.Добавить(" ");
	МассивСимволов.Добавить("_");
	МассивСимволов.Добавить("""");
	
	Возврат МассивСимволов;
	
КонецФункции

Процедура ЗаписатьТекст(Знач Текст, Знач Путь) Экспорт

	Если ТипЗнч(Текст) = Тип("ТекстовыйДокумент") Тогда
		Текст = Текст.ПолучитьТекст();
	КонецЕсли;
	
	Текст = СтрЗаменить(Текст, Символы.ВК + Символы.ПС, Символы.ПС);
	ПолучитьДвоичныеДанныеИзСтроки(Текст).Записать(Путь);

КонецПроцедуры