Перем ДанныеКонфигурации;
Перем НаборФайловФорматирования;
Перем СопоставлениеФайлов;
Перем ФайлКонфигурации;
Перем СекретныеДанные;
Перем КлючРасшифровки;

Процедура ПриСозданииОбъекта(КлючРасшифровки_ = Неопределено)
	
	ФайлКонфигурации = "./ci/config.json";
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.ОткрытьФайл(ФайлКонфигурации);
	ДанныеКонфигурации = ПрочитатьJSON(ЧтениеJSON, Истина);
	ЧтениеJSON.Закрыть();

	КлючРасшифровки = КлючРасшифровки_;
	
КонецПроцедуры

Функция ПолучитьЗначениеНастройки(Знач Имя) Экспорт
	
	ЧастиИмени    = СтрРазделить(Имя, ".");
	ТекущиеДанные = ДанныеКонфигурации;
	
	Для Каждого Часть Из ЧастиИмени Цикл
		Попытка
			ТекущиеДанные = ТекущиеДанные[Часть]["value"];
		Исключение
			ВызватьИсключение СтрШаблон("Ошибка получения настройки: %1", Имя);
		КонецПопытки;
	КонецЦикла;
	
	Возврат ТекущиеДанные;
	
КонецФункции

Функция ПолучитьЗначениеНастройкиЛокализации(Знач Имя, Знач Язык) Экспорт
	Возврат ПолучитьЗначениеНастройки(СтрШаблон("localization.langsVars.%1.%2", Язык, Имя));
КонецФункции

Функция ПолучитьЗначениеСекретныхДанных(Знач Имя) Экспорт

	Если Не ЗначениеЗаполнено(СекретныеДанные) Тогда
		ЗаполнитьСекретныеДанные();
	КонецЕсли;

	Возврат СекретныеДанные[Имя];

КонецФункции

Процедура ЗаписатьЗначениеНастройки(Знач Имя, Знач Значение) Экспорт
	
	ЧастиИмени       = СтрРазделить(Имя, ".");
	ШаблонИндекса    = "[""%1""][""value""]";
	ШаблонВыполнения = "ДанныеКонфигурации%1 = Значение";
	
	МассивИндексов = Новый Массив;
	Для Каждого Часть Из ЧастиИмени Цикл
		МассивИндексов.Добавить(СтрШаблон(ШаблонИндекса, Часть));
	КонецЦикла;
	
	Выполнить(СтрШаблон(ШаблонВыполнения, СтрСоединить(МассивИндексов)));
	
	Запись = Новый ЗаписьJSON;
	ПараметрыJSON = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Авто, "    ");
	Запись.ОткрытьФайл(ФайлКонфигурации, , , ПараметрыJSON);
	ЗаписатьJSON(Запись, ДанныеКонфигурации);
	Запись.Закрыть();
	
КонецПроцедуры

Функция ПолучитьСопоставлениеФайлов() Экспорт
	
	Если СопоставлениеФайлов = Неопределено Тогда
		
		ОсновнойЯзык           = ПолучитьЗначениеНастройки("localization.mainLang");
		Корень                 = ПолучитьЗначениеНастройки("paths.root");
		ОсновнойПутьИсходников = СтрШаблон("%1%2/", Корень, ОсновнойЯзык);
		
		СопоставлениеФайлов = Новый Соответствие();
		ФайлыМодулей  = НайтиФайлы(ОсновнойПутьИсходников, "*.bsl", Истина);
		Признак       = "// OneScript: ";
		
		Для Каждого Файл Из ФайлыМодулей Цикл
			
			ТекущийФайл = Файл.ПолноеИмя;
			ТекстФайла  = Новый ТекстовыйДокумент();
			ТекстФайла.Прочитать(ТекущийФайл, "UTF-8");
			
			Для Н = 1 По ТекстФайла.КоличествоСтрок() Цикл
				
				ТекущаяСтрока = СокрЛП(ТекстФайла.ПолучитьСтроку(Н));
				
				Если Не ЗначениеЗаполнено(ТекущаяСтрока) Тогда
					Прервать;
				КонецЕсли;
				
				Если СтрНайти(ТекущаяСтрока, Признак) > 0 Тогда
					
					ПутьOS = СтрЗаменить(ТекущаяСтрока, Признак, "");
					ПутьOS = СокрЛП(ПутьOS);
					ПутьOS = СтрЗаменить(ПутьOS, "./", ОсновнойПутьИсходников);
					СопоставлениеФайлов.Вставить(ТекущийФайл, ПутьOS);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат СопоставлениеФайлов;
	
КонецФункции

Функция ОпределитьНаборФайловФорматирования() Экспорт
	
	Если НаборФайловФорматирования = Неопределено Тогда
		
		Корень                    = ПолучитьЗначениеНастройки("paths.root");
		ВсеФайлы                  = НайтиФайлы(Корень, "*", Истина);
		НаборФайловФорматирования = Новый Массив;
		
		Для Каждого ФайлПроекта Из ВсеФайлы Цикл
			
			Признак =
			(ФайлПроекта.Расширение = ".os"
			Или ФайлПроекта.Расширение = ".bsl")
			И СтрНайти(ФайлПроекта.ПолноеИмя, "cli/") = 0
			И СтрНайти(ФайлПроекта.ПолноеИмя, "cli\") = 0;
			
			Если Признак Тогда
				НаборФайловФорматирования.Добавить(ФайлПроекта);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат НаборФайловФорматирования;
	
КонецФункции

Функция ПолучитьСоответствиеНесовпадающихИмен(Знач Язык) Экспорт
	
	СоответствиеНесовпадающихИмен = ПолучитьЗначениеНастройкиЛокализации("namesTranslation", Язык);	
	Возврат СоответствиеНесовпадающихИмен;
	
КонецФункции

Процедура ЗаполнитьСекретныеДанные()

	ПутьРасшифрованного = ПолучитьЗначениеНастройки("paths.secretDataSrc");
	ПутьЗашифрованного  = ПутьРасшифрованного + ".gpg";

	ЗашифрованныйФайл = Новый Файл(ПутьЗашифрованного);

	Если Не ЗашифрованныйФайл.Существует() Тогда
		ВызватьИсключение "Credentials file is missing from the repository";
	КонецЕсли;

	РасшифрованныйФайл = Новый Файл(ПутьРасшифрованного);

	Если РасшифрованныйФайл.Существует() Тогда
		УдалитьФайлы(РасшифрованныйФайл.ПолноеИмя);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(КлючРасшифровки) Тогда
		КлючРасшифровки = ПолучитьПеременнуюСреды("GPGKEY", РасположениеПеременнойСреды.Процесс);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(КлючРасшифровки) И АргументыКоманднойСтроки.Количество() > 0 Тогда
		КлючРасшифровки = АргументыКоманднойСтроки[0];
	КонецЕсли;

	Если Не ЗначениеЗаполнено(КлючРасшифровки) Тогда
		ВызватьИсключение "No GPG key passed";
	КонецЕсли;
	
	СтрокаЗапускаРасшифровки = СтрШаблон("gpg --quiet --batch --yes --decrypt --passphrase=""%1"" --output ./data.json ./data.json.gpg", КлючРасшифровки);

	Успех = Ложь;
	Для Н = 1 По 3 Цикл
	
		Попытка
			CommonTools.ЗапуститьВнешнееПриложение(СтрокаЗапускаРасшифровки);
			Успех = Истина;
			Прервать;
		Исключение
			CommonTools.СообщитьПроцесс(СтрШаблон("Decryption attempts: %1/3", Н));
		КонецПопытки;

	КонецЦикла;

	Если Не Успех Тогда
		ВызватьИсключение "Cant decrypt credentials file";
	КонецЕсли;

	РасшифрованныйФайл = Новый Файл(ПутьРасшифрованного);

	Если Не РасшифрованныйФайл.Существует() Тогда
		ВызватьИсключение "Cant find decrypted file";
	КонецЕсли;
	
	СекретныеДанные = CommonTools.ПрочитатьФайлJSON(РасшифрованныйФайл.ПолноеИмя);

КонецПроцедуры