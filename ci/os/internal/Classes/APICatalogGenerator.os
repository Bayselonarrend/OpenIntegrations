
#Использовать "./internal"

#Область СлужебныйПрограммныйИнтерфейс

Перем ТЗКаталога;

Процедура ПриСозданииОбъекта(Знач ДанныеПроекта_ = Неопределено)

	Если ДанныеПроекта_ = Неопределено Тогда
		ДанныеПроекта = Новый ProjectData;
	Иначе
		ДанныеПроекта = ДанныеПроекта_;
	КонецЕсли;

	CommonTools.СообщитьПроцесс("Creating library list");

	МассивФайлов = ДанныеПроекта.ОпределитьНаборФайловФорматирования();

	ТЗКаталога = Новый ТаблицаЗначений();
	ТЗКаталога.Колонки.Добавить("Имя");
	ТЗКаталога.Колонки.Добавить("Представление");
	ТЗКаталога.Колонки.Добавить("Тэги");

	Для Каждого ТекущийФайл Из МассивФайлов Цикл
		ОбработатьМодуль(ТекущийФайл);
	КонецЦикла;

	ТаблицаКаталога =  "|  | Наименование | Ключевые слова |" 
		+ Символы.ПС
		+ "|-|-|-|"
		+ Символы.ПС;

	СтрокаТаблицы      = "|![%1](../%2.png)| `%1`| %3 |";

	МассивТэгов           = Новый Массив;
	МассивИменБезПробелов = Новый Массив;

	Для Каждого ТекущиеДанные Из ТЗКаталога Цикл

		ИмяБиблиотеки = ТекущиеДанные["Имя"];

		ФайлКартинки   = Новый Файл(СтрШаблон("./media/%1.png", ИмяБиблиотеки));

		Если ФайлКартинки.Существует() Тогда

			Представление = ТекущиеДанные["Представление"];
			ИмяФайла      = ИмяБиблиотеки;

		Иначе

			Представление = ТекущиеДанные["Представление"] + " (в разработке)";
			ИмяФайла      = "default";

		КонецЕсли;

		ТаблицаКаталога = ТаблицаКаталога 
			+ СтрШаблон(СтрокаТаблицы, Представление, ИмяФайла, ТекущиеДанные["Тэги"])
			+ Символы.ПС;

		МассивТэгов.Добавить(ТекущиеДанные["Представление"]);
		МассивИменБезПробелов.Добавить(ИмяБиблиотеки);

	КонецЦикла;

	CommonTools.ЗаписатьТекст(ТаблицаКаталога, "./media/catalogs/Catalog.md");

	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.ОткрытьФайл("./media/catalogs/names.json");
	ЗаписатьJSON(ЗаписьJSON, МассивИменБезПробелов);
	ЗаписьJSON.Закрыть();

	ДополнитьReadmeОблакомТэгов(МассивТэгов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьМодуль(Знач ФайлМодуля)
	
	ТекущийФайл = ФайлМодуля.ПолноеИмя;
	ТекстФайла  = Новый ТекстовыйДокумент();
	ТекстФайла.Прочитать(ТекущийФайл, "UTF-8");

	ИнформацияОМодуле = CommonTools.ПолучитьИнформациюОМодуле(ТекстФайла);

	СписокДобавленных = Новый СписокЗначений();

	Если ЗначениеЗаполнено(ИнформацияОМодуле["ПредставлениеEn"]) Тогда

		ТекущаяБиблиотека = ИнформацияОМодуле["Библиотека"];

		Если СписокДобавленных.НайтиПоЗначению(ТекущаяБиблиотека) <> Неопределено Тогда
			Возврат;
		КонецЕсли;

		НоваяСтрока = ТЗКаталога.Добавить();
		НоваяСтрока.Представление = ИнформацияОМодуле["ПредставлениеEn"];
		НоваяСтрока.Имя  = ТекущаяБиблиотека;
		НоваяСтрока.Тэги = ИнформацияОМодуле["КлючевыеСлова"];

		СписокДобавленных.Добавить(ТекущаяБиблиотека);

	КонецЕсли;

КонецПроцедуры

Процедура ДополнитьReadmeОблакомТэгов(МассивТэгов)

	ТекстТэгов  = СтрСоединить(МассивТэгов, ", ");
	ОблакоТэгов = ПолучитьСТрокуИзДвоичныхДанных(Новый ДвоичныеДанные("./service/templates/tags.md"));

	ОблакоТэгов = СтрШаблон(ОблакоТэгов, ТекстТэгов);

	ДокументReadme = Новый ТекстовыйДокумент();
	ДокументReadme.Прочитать("./README.md");

	Признак    = "NOTICE";
	Найдено    = Ложь;
	Счетчик    = 0;
	ВсегоСтрок = ДокументReadme.КоличествоСтрок();

	Пока Не Найдено Цикл

		Индекс        = ВсегоСтрок - Счетчик;
		ТекущаяСтрока = ДокументReadme.ПолучитьСтроку(Индекс); 

		Если СтрНайти(ТекущаяСтрока, Признак) <> 0 Тогда
			Найдено = Истина;
		Иначе
			ДокументReadme.УдалитьСтроку(Индекс);
		КонецЕсли;

		Счетчик = Счетчик + 1;

	КонецЦикла;

	ДокументReadme.ДобавитьСтроку(ОблакоТэгов);

	CommonTools.ЗаписатьТекст(ДокументReadme, "./README.md");

КонецПроцедуры

#КонецОбласти

