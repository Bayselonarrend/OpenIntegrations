#Использовать osparser
#Использовать "./internal"
#Использовать "../../../../src/ru/OInt"

Перем ДанныеПроекта;
Перем ШаблонПромпта;

Перем Токен;
Перем URL;
Перем Модель;

Процедура ПриСозданииОбъекта(Знач ДанныеПроекта_ = Неопределено)
	
	Если ДанныеПроекта_ = Неопределено Тогда
		ДанныеПроекта = Новый ProjectData;
	Иначе
		ДанныеПроекта = ДанныеПроекта_;
	КонецЕсли;
	
	Если Не ДанныеПроекта.ПолучитьЗначениеНастройки("ai.useAi") Тогда
		CommonTools.СообщитьПроцесс("AI localization skipped by useAi=false");
		ЗавершитьРаботу(0);
	КонецЕсли;
	
	CommonTools.СообщитьПроцесс("AI Instructions localization");
	
	
	Языки               = ДанныеПроекта.ПолучитьЗначениеНастройки("localization.langs");
	ОсновнойЯзык        = ДанныеПроекта.ПолучитьЗначениеНастройки("localization.mainLang");
	КаталогШаблонов     = ДанныеПроекта.ПолучитьЗначениеНастройки("paths.templatesSrc");
	URL                 = ДанныеПроекта.ПолучитьЗначениеНастройки("ai.openAiUrl");
	Модель              = ДанныеПроекта.ПолучитьЗначениеНастройки("ai.textModel");
	ПолеТокена			= ДанныеПроекта.ПолучитьЗначениеНастройки("ai.tokenField");
	Токен               = ДанныеПроекта.ПолучитьЗначениеСекретныхДанных(ПолеТокена);
	
	ФайлШаблона = Новый Файл(ОбъединитьПути(КаталогШаблонов, "localization_prompt.txt"));
	
	Если Не ФайлШаблона.Существует() Тогда
		ВызватьИсключение "Prompt file does not exist";
	Иначе
		ШаблонПромпта = ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(ФайлШаблона.ПолноеИмя));
	КонецЕсли;
	
	КаталогДокументации = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("docsMdSrc", ОсновнойЯзык);
	КаталогИнструкций   = ОбъединитьПути(КаталогДокументации, "Instructions");
	
	ФайлыКПереводу  = Новый Соответствие();
	ФайлыИнструкций = НайтиФайлы(КаталогИнструкций, "*.md");
	
	Для Каждого Язык Из Языки Цикл
		
		Если Язык = ОсновнойЯзык Тогда
			Продолжить;
		КонецЕсли;
		
		КаталогДокументацииЯзыка = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("docsMdSrc", Язык);
		КаталогИнструкцийЯзыка   = ОбъединитьПути(КаталогДокументацииЯзыка, "Instructions");
		
		ФайлыПримеров   = Новый Массив;	
		
		Для Каждого ФайлИнструкции Из ФайлыИнструкций Цикл
			
			ФайлИнструкцииЛокализация = ОбъединитьПути(КаталогИнструкцийЯзыка, ФайлИнструкции.Имя);
			ФайлИнструкцииЛокализация = Новый Файл(ФайлИнструкцииЛокализация);
			
			Если ФайлИнструкцииЛокализация.Существует() Тогда
				Если ФайлыПримеров.Количество() < 2 Тогда
					ФайлыПримеров.Добавить(ФайлИнструкции.ПолноеИмя);
				КонецЕсли;
			Иначе
				ФайлыКПереводу.Вставить(ФайлИнструкции.ПолноеИмя, ФайлИнструкцииЛокализация.ПолноеИмя);
			КонецЕсли;

		КонецЦикла;
		
		ДоработатьПереводФайлов(Язык, ФайлыКПереводу, ФайлыПримеров);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДоработатьПереводФайлов(Язык, ФайлыКПереводу, ФайлыПримеров)
	
	ПредставлениеЯзыка = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("title", Язык);
	
	ШаблонФайловПримеров = "
	|Другой, готовый файл локализации для примера номер %1:
	|
	|%2";
	
	Для Каждого ФайлКПереводу Из ФайлыКПереводу Цикл
		
		МассивКонечногоПромпта = Новый Массив;
		ТекстОригинала   = ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(ФайлКПереводу.Ключ));

		ОсновнойПромпт = СтрШаблон(ШаблонПромпта, ПредставлениеЯзыка, ТекстОригинала, Язык);
		МассивКонечногоПромпта.Добавить(ОсновнойПромпт);
		
		Счетчик = 1;
		Для Каждого Пример Из ФайлыПримеров Цикл
			
			ТекстПримера  = ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(Пример));
			ПромптПримера = СтрШаблон(ШаблонФайловПримеров, Счетчик, ТекстПримера);
			МассивКонечногоПромпта.Добавить(ПромптПримера);
			
			Счетчик = Счетчик + 1;
			
		КонецЦикла;
		
		КонечныйПромпт = СтрСоединить(МассивКонечногоПромпта, Символы.ПС);
		
		Сообщения = Новый Массив;
		Сообщения.Добавить(OPI_OpenAI.ПолучитьСтруктуруСообщения("user", КонечныйПромпт));
		
		Результат = OPI_OpenAI.ПолучитьОтвет(URL, Токен, Модель, Сообщения);
		
		Попытка
			ТекстПеревода = Результат["choices"][0]["message"]["content"];
		Исключение
			ВызватьИсключение OPI_Инструменты.JSONСтрокой(Результат);
		КонецПопытки;

		ПолучитьДвоичныеДанныеИзСтроки(ТекстПеревода).Записать(ФайлКПереводу.Значение);
		
	КонецЦикла;
		
КонецПроцедуры