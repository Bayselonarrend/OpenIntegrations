
#Использовать osparser
#Использовать "./internal"

Процедура СформироватьИндексТестов(Знач Корень, Знач Языки) Экспорт

	Для Каждого Язык Из Языки Цикл
		СформироватьИндексТестовЯзыка(Корень, Язык);
	КонецЦикла;

КонецПроцедуры

Процедура СформироватьИндексТестовЯзыка(Знач Корень, Знач Язык)

	Парсер          = Новый ПарсерВстроенногоЯзыка;
	ФайлТестов      = ?(Язык = "ru", "OPI_Тесты.os", "OPI_Tests.os");
	ДанныеМодуля    = Новый ДвоичныеДанные(Корень + Язык + "/OInt/tests/Modules/internal/" + ФайлТестов);
	ОбластьТестов   = ?(Язык = "ru", "АтомарныеТесты", "AtomicTests");
	ТекстМодуля     = ПолучитьСтрокуИзДвоичныхДанных(ДанныеМодуля);

	СтруктураМодуля = Парсер.Разобрать(ТекстМодуля);
	НачалоАтомарных = Ложь;

	СписокВсехТестов = Новый Массив;

	Для Каждого Объявление Из СтруктураМодуля.Объявления Цикл

		Если Не НачалоАтомарных Тогда

			Если Строка(Объявление.Тип) = "ИнструкцияПрепроцессораОбласть" И Объявление.Имя = ОбластьТестов Тогда
				НачалоАтомарных = Истина;
			КонецЕсли;

		Иначе

			Если Строка(Объявление.Тип) = "ОбъявлениеМетода" Тогда

				ЧастиИмени = СтрРазделить(Объявление.Сигнатура.Имя, "_");
				СтруктураИмени = Новый Структура("lib,name", ЧастиИмени[0], ЧастиИмени[1]);
				СписокВсехТестов.Добавить(СтруктураИмени);

			КонецЕсли;

		КонецЕсли;

	КонецЦикла;

	Инструменты.ЗаписатьФайлJSON(СтрШаблон("./service/tests_%1.json", Язык), СписокВсехТестов);

КонецПроцедуры
