#Использовать "./../../../../src/en/OInt/tools/"
#Использовать "./../../../../src/ru/OInt/tools/"
#Использовать "./internal"

Перем СписокРабот;
Перем ШаблонCliWindows;
Перем ШаблонOsWindows;
Перем ШаблонШага;
Перем ШаблонВыполнения;

Процедура СоздатьНаборWorkflow(Знач Языки, Знач КаталогПараметров, Знач КаталогWorkflow) Экспорт

	КаталогШаблонов = "./service/templates/jenkins/";

	ПутьШаблонаCliWindows = КаталогШаблонов + "cli_test_windows.txt";
	ПутьШаблонаOsWindows  = КаталогШаблонов + "os_test_windows.txt";

	ПутьШаблонаШага       = КаталогШаблонов + "test_stage.txt";
	ПутьШаблонаВыполнения = КаталогШаблонов + "test_exec.txt";

	ШаблонCliWindows = Новый ДвоичныеДанные(ПутьШаблонаCliWindows);
	ШаблонCliWindows = ПолучитьСтрокуИзДвоичныхДанных(ШаблонCliWindows);

	ШаблонOsWindows = Новый ДвоичныеДанные(ПутьШаблонаOsWindows);
	ШаблонOsWindows = ПолучитьСтрокуИзДвоичныхДанных(ШаблонOsWindows);

	ШаблонШага = Новый ДвоичныеДанные(ПутьШаблонаШага);
	ШаблонШага = ПолучитьСтрокуИзДвоичныхДанных(ШаблонШага);

	ШаблонВыполнения = Новый ДвоичныеДанные(ПутьШаблонаВыполнения);
	ШаблонВыполнения = ПолучитьСтрокуИзДвоичныхДанных(ШаблонВыполнения);

	Для Каждого Язык Из Языки Цикл

		ФайлПараметров = КаталогПараметров + Язык + ".json";
		ЛокальныеПараметры = Инструменты.ПрочитатьФайлJSON(ФайлПараметров);
		СформироватьWorflow(ЛокальныеПараметры, КаталогWorkflow, Язык);

	КонецЦикла;

КонецПроцедуры

Процедура СформироватьWorflow(Знач Параметры, Знач КаталогWorkflow, Знач Язык)
	
	СписокРабот     = "";
	СписокБиблиотек = Неопределено;
	ТаблицаТестов   = Неопределено;
	МассивВлияющих  = Новый Массив;

	МодульТестов    = Параметры["testsModulePath"];
	МодульТестовCLI = Параметры["testsCliModulePath"];

	Выполнить("ТаблицаТестов   = " + Параметры["testsTableMethod"]);
	Выполнить("СписокБиблиотек = " + Параметры["testsSectionsMethod"]);

	ФайлJenkinsOS   = КаталогWorkflow + "os_test_" + Язык;
	ФайлJenkinsCLI  = КаталогWorkflow + "cli_test_" + Язык;

	ТекстВыполненияOS  = ПолучитьТекстВыполнения(ТаблицаТестов, СписокБиблиотек, Параметры, МодульТестов);
	ТекстВыполненияCLI = ПолучитьТекстВыполнения(ТаблицаТестов, СписокБиблиотек, Параметры, МодульТестовCLI);

	ТекстJFCLI = СтрШаблон(ШаблонCliWindows, Язык, ТекстВыполненияCLI);
	ТекстJFOS  = СтрШаблон(ШаблонOsWindows, Язык, ТекстВыполненияOS);

	ПолучитьДвоичныеДанныеИзСтроки(ТекстJFOS).Записать(ФайлJenkinsOS);
	ПолучитьДвоичныеДанныеИзСтроки(ТекстJFCLI).Записать(ФайлJenkinsCLI);

КонецПроцедуры

Функция ПолучитьТекстВыполнения(Знач ТаблицаТестов, Знач СписокБиблиотек, Знач Параметры, Знач МодульТестов)
	
	СтрокаРаздел    = Параметры["sectionsString"];
	СтрокаМетод     = Параметры["methodString"];
	СтрокаСиноним   = Параметры["synonymString"];

	МассивШагов = Новый Массив;
	Для Каждого Библиотека Из СписокБиблиотек Цикл

		Раздел      = Библиотека.Ключ;

		Отбор = Новый Структура(СтрокаРаздел, Раздел);
		ТестыТекущегоРаздела = ТаблицаТестов.НайтиСтроки(Отбор);

		МассивВыполнений = Новый Массив;
		Для Каждого Тест Из ТестыТекущегоРаздела Цикл

			Метод   = Тест[СтрокаМетод];
			Синоним = Тест[СтрокаСиноним];

			МассивВыполнений.Добавить(СтрШаблон(ШаблонВыполнения, МодульТестов, Метод))

		КонецЦикла;

		МассивШагов.Добавить(СтрШаблон(ШаблонШага, Раздел, СтрСоединить(МассивВыполнений, Символы.ПС)));

	КонецЦикла;

	Возврат СтрСоединить(МассивШагов, Символы.ПС);

КонецФункции


