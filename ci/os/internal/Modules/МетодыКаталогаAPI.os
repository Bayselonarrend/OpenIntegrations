
#Область СлужебныйПрограммныйИнтерфейс

Перем СоответствиеКаталога;

Процедура СформироватьКаталог(Знач МассивФайлов) Экспорт

	СоответствиеКаталога = Новый Соответствие();

	Для Каждого ТекущийФайл Из МассивФайлов Цикл
		ОбработатьМодуль(ТекущийФайл);
	КонецЦикла;

	ТаблицаКаталога =  "|  | Наименование | Ключевые слова |" 
		+ Символы.ПС
		+ "|-|-|-|"
		+ Символы.ПС;


	СтрокаТаблицы      = "|![%1](../%2.png)| `%1`| %3 |";

	МассивТэгов           = Новый Массив;
	МассивИменБезПробелов = Новый Массив;

	Для Каждого КлючЗначение Из СоответствиеКаталога Цикл

		ИмяБезПробелов = СтрЗаменить(КлючЗначение.Ключ, " ", "");
		ИмяБезПробелов = СтрЗаменить(ИмяБезПробелов, "-", "");
		ИмяБезПробелов = СтрЗаменить(ИмяБезПробелов, "client", "");
		ИмяБезПробелов = СтрЗаменить(ИмяБезПробелов, "клиент", "");

		ФайлКартинки   = Новый Файл(СтрШаблон("./media/%1.png", ИмяБезПробелов));

		Если ФайлКартинки.Существует() Тогда

			ИмяБиблиотеки = КлючЗначение.Ключ;
			ИмяФайла      = ИмяБезПробелов;

		Иначе

			ИмяБиблиотеки = КлючЗначение.Ключ + " (в разработке)";
			ИмяФайла      = "default";

		КонецЕсли;

		ТаблицаКаталога = ТаблицаКаталога 
			+ СтрШаблон(СтрокаТаблицы, ИмяБиблиотеки, ИмяФайла, КлючЗначение.Значение)
			+ Символы.ПС;

		МассивТэгов.Добавить(КлючЗначение.Ключ);
		МассивИменБезПробелов.Добавить(ИмяБезПробелов);

	КонецЦикла;

	ПолучитьДвоичныеДанныеИзСтроки(ТаблицаКаталога).Записать("./media/catalogs/Catalog.md");

	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.ОткрытьФайл("./media/catalogs/names.json");
	ЗаписатьJSON(ЗаписьJSON, МассивИменБезПробелов);
	ЗаписьJSON.Закрыть();

	ДополнитьReadmeОблакомТэгов(МассивТэгов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьМодуль(Знач ФайлМодуля)
	
	ТекущийФайл = ФайлМодуля.ПолноеИмя;
	ТекстФайла  = Новый ТекстовыйДокумент();
	ТекстФайла.Прочитать(ТекущийФайл, "UTF-8");

	ПризнакКлюч     = "// Lib: ";
	ПризнакЗначение = "// Keywords: ";

	ТекущийКлюч = "";
	ТекущееЗначение = "";

	Для Н = 1 По ТекстФайла.КоличествоСтрок() Цикл

		ТекущаяСтрока = СокрЛП(ТекстФайла.ПолучитьСтроку(Н));

		Если Не ЗначениеЗаполнено(ТекущаяСтрока) Тогда
			Прервать;
		КонецЕсли;

		Если СтрНайти(ТекущаяСтрока, ПризнакКлюч) > 0 Тогда

			ТекущийКлюч = СтрЗаменить(ТекущаяСтрока, ПризнакКлюч, "");
			ТекущийКлюч = СокрЛП(ТекущийКлюч);

		КонецЕсли;

		Если СтрНайти(ТекущаяСтрока, ПризнакЗначение) > 0 Тогда

			ТекущееЗначение = СтрЗаменить(ТекущаяСтрока, ПризнакЗначение, "");
			ТекущееЗначение = СокрЛП(ТекущееЗначение);

		КонецЕсли;

	КонецЦикла;

	Если ЗначениеЗаполнено(ТекущийКлюч) И ЗначениеЗаполнено(ТекущееЗначение) Тогда
		СоответствиеКаталога.Вставить(ТекущийКлюч, ТекущееЗначение);
	КонецЕсли;

КонецПроцедуры

Процедура ДополнитьReadmeОблакомТэгов(МассивТэгов)

	ТекстТэгов  = СтрСоединить(МассивТэгов, ", ");
	ОблакоТэгов = ПолучитьСТрокуИзДвоичныхДанных(Новый ДвоичныеДанные("./service/templates/tags.md"));

	ОблакоТэгов = СтрШаблон(ОблакоТэгов, ТекстТэгов);

	ДокументReadme = Новый ТекстовыйДокумент();
	ДокументReadme.Прочитать("./README.md");

	Признак    = "NOTICE";
	Найдено    = Ложь;
	Счетчик    = 0;
	ВсегоСтрок = ДокументReadme.КоличествоСтрок();

	Пока Не Найдено Цикл

		Индекс        = ВсегоСтрок - Счетчик;
		ТекущаяСтрока = ДокументReadme.ПолучитьСтроку(Индекс); 

		Если СтрНайти(ТекущаяСтрока, Признак) <> 0 Тогда
			Найдено = Истина;
		Иначе
			ДокументReadme.УдалитьСтроку(Индекс);
		КонецЕсли;

		Счетчик = Счетчик + 1;

	КонецЦикла;

	ДокументReadme.ДобавитьСтроку(ОблакоТэгов);
	ДокументReadme.Записать("./README.md");

КонецПроцедуры

#КонецОбласти

