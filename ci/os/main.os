#Использовать "./internal"
#Использовать "./internal/Modules/internal"

Перем ПутьPackagedef;
Перем ОсновнойПутьИсходников;
Перем СоответствиеЗамен;
Перем ПутьПакета;
Перем Версия;
Перем Языки;
Перем КаталогСловарей;
Перем ФайлыПеревода;
Перем ФайлыРазбораСловаря;
Перем ФайлыФорматирования;
Перем Корень;
Перем ОсновнойЯзык;
Перем КаталогWorkflow;
Перем КаталогJenkins;
Перем КаталогПараметровЛок;
Перем КаталогДополнений;
Перем ФайлПроверкиНабораТестов;

Процедура ПриСозданииОбъекта()

	ДанныеПроекта = ОбщиеМетоды.ПолучитьДанныеПроекта();

	// Основные
	Корень                     = ДанныеПроекта["root"];
	КаталогПакета              = ДанныеПроекта["packageSrc"];
	ОсновнойЯзык               = ДанныеПроекта["mainLang"];
	Версия                     = ДанныеПроекта["version"];
	Языки                      = ДанныеПроекта["additionalLangs"];
	КаталогСловарей            = ДанныеПроекта["dictionariesSrc"];
	КаталогWorkflow            = ДанныеПроекта["workflowsSrc"];
	КаталогJenkins             = ДанныеПроекта["jenkinsSrc"];
	КаталогПараметровЛок       = ДанныеПроекта["locVarsSrc"];
	КаталогДополнений          = ДанныеПроекта["paramsExamplesSrc"];

	ОсновнойПутьИсходников = Корень + ОсновнойЯзык + "/";
	ПутьПакета             = ОсновнойПутьИсходников + КаталогПакета;
	ПутьPackagedef         = ПутьПакета + "packagedef";

	// Замены для OneScript
	СоответствиеЗамен = Новый Соответствие();
	СоответствиеЗамен.Вставить("// #Использовать"					, "#Использовать");
	СоответствиеЗамен.Вставить("//#Использовать" 					, "#Использовать");
	СоответствиеЗамен.Вставить("УстановитьБезопасныйРежим(Истина);"	, "");
	СоответствиеЗамен.Вставить("УстановитьБезопасныйРежим(Ложь);"	, "");
	СоответствиеЗамен.Вставить("// !OInt "						    , "");

	СоответствиеЗамен.Вставить("#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда", "");
	СоответствиеЗамен.Вставить("#Иначе"						                                                , "");
	СоответствиеЗамен.Вставить("#КонецЕсли"						                                            , "");
	
	// Файлы перевода
	ФайлыРазбораСловаря = ОпределитьНаборФайловДляСловаря();
	ФайлыПеревода       = ОпределитьНаборФайловПеревода();

КонецПроцедуры

Процедура ОсновнаяОбработка() Экспорт

	Сообщить("Конвертация: сопоставление файлов");
	СоответствиеМодулей = МетодыКонвертации.ПолучитьСопоставлениеФайлов(ОсновнойПутьИсходников);

	Сообщить("Версионирование: обновление номера версии");
	МетодыВерсионирования.ОбновитьНомераВерсий(ПутьPackagedef, Версия);

	Сообщить("Версионирование: обновление libconfig");
	МетодыВерсионирования.ОбновитьLibConfig(СоответствиеМодулей, ПутьПакета);

	Сообщить("Конвертация: портирование набора");
	МетодыКонвертации.ПортироватьНабор(СоответствиеМодулей, СоответствиеЗамен);
	
	ЛокальныеПараметры = НайтиФайлы(КаталогПараметровЛок, "*.json");
	
	Сообщить("Словари: создание набора словарей");
	ЕстьНовыеСлова = МетодыСловарей.СоздатьНаборСловарей(ФайлыРазбораСловаря, Языки, КаталогСловарей);

	Если Не ЕстьНовыеСлова Тогда

		Сообщить("Перевод: перевод проекта");
		МетодыПеревода.ПеревестиПроект(КаталогСловарей, Корень, ОсновнойЯзык, Языки, ФайлыПеревода);

		ФайлыФорматирования = ОпределитьНаборФайловФорматирования();

		Сообщить("Форматирование: форматирование текстов набора");
		МетодыФорматирования.ОтформатироватьНабор(ФайлыФорматирования);

		Для Каждого ФайлПараметров Из ЛокальныеПараметры Цикл

			ТекущиеПараметры = Инструменты.ПрочитатьФайлJSON(ФайлПараметров.ПолноеИмя);
			МодульТестов          = ТекущиеПараметры["testsModulePath"];
			КаталогПримеров       = ТекущиеПараметры["codeExamplesSrc"];
			КаталогЗначенийТестов = ТекущиеПараметры["testValuesSrc"];
			ИмяОбласти            = ТекущиеПараметры["testsRegionName"];
	
			Сообщить("Примеры кода: получение примеров по " + ФайлПараметров.Имя);
			МетодыПримеровКода.ПолучитьПримерыКодаДокументации(МодульТестов
				, КаталогПримеров
				, КаталогЗначенийТестов
				, ИмяОбласти);
	
		КонецЦикла;

		Языки.Добавить(ОсновнойЯзык);

		Сообщить("CLI: формирование CLI");
		МетодыCLI.СформироватьCLI(Корень, Языки, КаталогДополнений);

		Сообщить("Jenkins: создание набора jenkinsfile");
		МетодыФормированияТестовJenkins.СоздатьНаборWorkflow(Языки, КаталогПараметровЛок, КаталогJenkins);
		МетодыФормированияТестовJenkins.СоздатьНаборWorkflow(Языки, КаталогПараметровЛок, КаталогJenkins);

		Сообщить("Actions: создание набора workflow");
		МетодыФормированияТестовGA.СоздатьНаборWorkflow(Языки, КаталогПараметровЛок, КаталогWorkflow);
		МетодыФормированияТестовGA.СоздатьНаборWorkflow(Языки, КаталогПараметровЛок, КаталогWorkflow);

	Иначе
		Сообщить("В словарях есть новые слова без перевода. Перевод и создание CLI не выполнено!");
		ЗавершитьРаботу(111);
	КонецЕсли;

	Сообщить("Обработка завершена!");

КонецПроцедуры

Функция ОпределитьНаборФайловДляСловаря()

	ВсеФайлы       = НайтиФайлы(ОсновнойПутьИсходников, "*", Истина);
	МассивФайлов   = Новый Массив;
	
	МассивИсключаемыхРасширений = Новый Массив;
	МассивИсключаемыхРасширений.Добавить(".bin");
	МассивИсключаемыхРасширений.Добавить(".addin");
	МассивИсключаемыхРасширений.Добавить(".dll");
	МассивИсключаемыхРасширений.Добавить(".so");
	МассивИсключаемыхРасширений.Добавить(".exe");
	МассивИсключаемыхРасширений.Добавить(".zip");

	Для Каждого ФайлПроекта Из ВсеФайлы Цикл

		ИмяФайла = вРег(ФайлПроекта.ПолноеИмя);

		Признак = 
			СтрНайти(ИмяФайла, вРег("oint\data")) = 0
			И СтрНайти(ИмяФайла, вРег("oint/data")) = 0
			И Не ФайлПроекта.ЭтоКаталог();

		Для Каждого Расширение Из МассивИсключаемыхРасширений Цикл

			Если ФайлПроекта.Расширение = Расширение Тогда
				Признак = Ложь;
				Прервать;
			КонецЕсли;

		КонецЦикла;

		Если Признак Тогда
			МассивФайлов.Добавить(ФайлПроекта);
		КонецЕсли;

	КонецЦикла;

	Возврат МассивФайлов;

КонецФункции

Функция ОпределитьНаборФайловФорматирования()
	
	// Наборы файлов
	ВсеФайлы     = НайтиФайлы(Корень, "*", Истина);
	МассивФайлов = Новый Массив;
	
	Для Каждого ФайлПроекта Из ВсеФайлы Цикл

		Признак =
			(ФайлПроекта.Расширение = ".os"
			Или ФайлПроекта.Расширение = ".bsl")
			И СтрНайти(ФайлПроекта.ПолноеИмя, "cli/") = 0
			И СтрНайти(ФайлПроекта.ПолноеИмя, "cli\") = 0;

		Если Признак Тогда
			МассивФайлов.Добавить(ФайлПроекта);
		КонецЕсли;

	КонецЦикла;

	Возврат МассивФайлов;

КонецФункции

Функция ОпределитьНаборФайловПеревода()
	
	// Наборы файлов
	ВсеФайлы     = НайтиФайлы(ОсновнойПутьИсходников, "*", Истина);
	МассивФайлов = Новый Массив;
	
	Для Каждого ФайлПроекта Из ВсеФайлы Цикл

		Признак = СтрНайти(ФайлПроекта.ПолноеИмя, "cli") = 0;

		Если Признак Тогда
			МассивФайлов.Добавить(ФайлПроекта);
		КонецЕсли;

	КонецЦикла;

	Возврат МассивФайлов;

КонецФункции

ПриСозданииОбъекта();
ОсновнаяОбработка();

