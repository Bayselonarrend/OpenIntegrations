Перем Репозиторий;
Перем Версия;
Перем Режим;
Перем Файл1С;
Перем ПутьКРепозиторию;
Перем Сервер;
Перем ПутьВыгрузки;
Перем Оскрипт;
Перем ПутьДвижка;
Перем ХешСумма;


Процедура НачалоРаботы()
	
	Репозиторий = "https://github.com/Bayselonarrend/OpenIntegrations";
	Версия   = "1.30.0";
	Режим    = "CONFIG";

	//Локальные данные
	Файл1С            = """C:\Program Files\1cv8\8.3.18.1208\bin\1cv8.exe""";
	Сервер            = "DEVSRV";
	ОСкрипт           = "C:\Program Files\OneScript\";
	ПутьДвижка        = "/mnt/c/engine/linux/";

	ПутьКРепозиторию  = "C:\ProgramData\Jenkins\.jenkins\workspace\OpiBuild\OpiRelease";
	//ПутьКРепозиторию  = "C:\Repos\OPI";
	ПутьВыгрузки      = ПутьКРепозиторию + "\" + Версия + "\";
	ПутьMelezh        = ПутьКРепозиторию + "\Melezh";
	ХешСумма          = ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(ПутьКРепозиторию + "\service\last_build_hash.txt"));

	//----------------

	ОбъектПутьВыгрузки = Новый Файл(ПутьВыгрузки);
	
	Если Не ОбъектПутьВыгрузки.Существует() Тогда
		СоздатьКаталог(ПутьВыгрузки);
	КонецЕсли;

	ЗапуститьПриложение("git clone https://github.com/bayselonarrend/Melezh");
	// ЗапуститьПриложение("git clone --depth=1 https://github.com/bayselonarrend/Melezh --branch 0.1.0");
	
	МассивЛокализаций = Новый Массив();

	СтруктураРус = Новый Структура();
	СтруктураРус.Вставить("База"    , "OpenIntegrations");
	СтруктураРус.Вставить("ПутьКEDT", ПутьКРепозиторию + "\src\ru\OPI");
	СтруктураРус.Вставить("ПутьOS"  , ПутьКРепозиторию + "\src\ru\OInt");
	СтруктураРус.Вставить("ПутьCLI" , ПутьКРепозиторию + "\src\ru\cli\core\Classes\app.os");
	СтруктураРус.Вставить("ПутьCLIP", ПутьКРепозиторию + "\src\ru\cli");
	СтруктураРус.Вставить("ПутьISS" , ПутьКРепозиторию + "\service\iss\ru.iss");
	СтруктураРус.Вставить("Описание", "OInt CLI - приложение для работы с API различных онлайн-сервисов из командной строки");
	СтруктураРус.Вставить("Префикс" , "ru");

	СтруктураАнг = Новый Структура();
	СтруктураАнг.Вставить("База"    , "OpenIntegrationsEng");
	СтруктураАнг.Вставить("ПутьКEDT", ПутьКРепозиторию + "\src\en\OPI");
	СтруктураАнг.Вставить("ПутьOS"  , ПутьКРепозиторию + "\src\en\OInt");
	СтруктураАнг.Вставить("ПутьCLI" , ПутьКРепозиторию + "\src\en\cli\core\Classes\app.os");
	СтруктураАнг.Вставить("ПутьCLIP", ПутьКРепозиторию + "\src\en\cli");
	СтруктураАнг.Вставить("ПутьISS" , ПутьКРепозиторию + "\service\iss\en.iss");
	СтруктураАнг.Вставить("Описание", "OInt CLI - CLI toolkit for integrating with APIs of popular online services");
	СтруктураАнг.Вставить("Префикс" , "en");

	МассивЛокализаций.Добавить(СтруктураАнг); 
	МассивЛокализаций.Добавить(СтруктураРус);

	КаталогВыгрузки = Новый Файл(ПутьВыгрузки);
	Если КаталогВыгрузки.Существует() Тогда 
		УдалитьФайлы(ПутьВыгрузки);
	КонецЕсли;
 
	СоздатьКаталог(ПутьВыгрузки);
	Приостановить(2000);
  
	Для Каждого Локализация Из МассивЛокализаций Цикл
  
		СоздатьXML(Локализация);
		СоздатьCFE(Локализация);
		СоздатьEDT(Локализация);   
		СоздатьOSPX(Локализация);
		//СоздатьEXE(Локализация);
		СоздатьПакеты(Локализация);
		СоздатьУстановщик(Локализация);

		УдалитьФайлы(".\ci\installer_set\share\oint\lib\oint");
		УдалитьФайлы(".\ci\installer_set\share\oint\lib\oint-cli");
		
	КонецЦикла;

	//Draft 

	ФайлыРелиза = НайтиФайлы(ПутьВыгрузки, "*", Истина);

	Сообщить("Start GHCLI");

	Для Каждого ФайлРелиза Из ФайлыРелиза Цикл
		ЗапуститьПриложение("""C:\Program Files\GitHub CLI\gh.exe"" release delete-asset draft --yes --repo " + Репозиторий + " """ + ФайлРелиза.Имя + """", , Истина);
		ЗапуститьПриложение("""C:\Program Files\GitHub CLI\gh.exe"" release upload draft --repo " + Репозиторий + " """ + ФайлРелиза.ПолноеИмя + """", , Истина);
	КонецЦикла;

	Сообщить("End GHCLI");

КонецПроцедуры


Процедура СоздатьCFE(Данные)

	Сообщить("Start CFE");

	База    = Данные["База"];
	Префикс = Данные["Префикс"];
	Основа  = Файл1С + " " + Режим + " /S " + Сервер + "\" + База + " ";
 
	//CFE
	ВыгрузкаВФайл = Основа   
		+ "/DumpCfg """    
		+ ПутьВыгрузки 
		+ "OpenIntegrations_" 
		+ Версия 
		+ "_"  
		+ Префикс      
		+ ".cfe"  
		+ """ -Extension OpenIntegrations";
		
		 
	ЗапуститьПриложение(ВыгрузкаВФайл, , Истина);

	Сообщить("End CFE");
	
КонецПроцедуры

Процедура СоздатьXML(Данные)

	Сообщить("Start XML");

	Префикс = вРег(Данные["Префикс"]); 
	База    = Данные["База"];  
	
	Основа  = Файл1С + " " + Режим + " /S " + Сервер + "\" + База + " ";

	// XML 
	ПапкаXML = ПутьВыгрузки + "XML_" + Префикс;

	КаталогXML = Новый Файл(ПапкаXML);
	Если Не КаталогXML.Существует() Тогда
		СоздатьКаталог(ПапкаXML);
	КонецЕсли;

	ВыгрузкаВXML = Основа + "/DumpConfigToFiles """ + ПапкаXML + """ -Extension OpenIntegrations";
	ЗапуститьПриложение(ВыгрузкаВXML, , Истина);

	ПутьZIP = ПутьВыгрузки + "XML_" + Префикс + ".zip";
	ZipXML = Новый ЗаписьZipФайла(ПутьZIP);

	ZipXML.Добавить(ПапкаXML 
		+ "\*.*" , РежимСохраненияПутейZIP.СохранятьОтносительныеПути, РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
	ZipXML.Записать();

	УдалитьФайлы(ПапкаXML);

	Сообщить("End XML");

КонецПроцедуры


Процедура СоздатьEDT(Данные)

	Сообщить("Start EDT");

	Префикс  = вРег(Данные["Префикс"]);
	ПутьКEDT = Данные["ПутьКEDT"];

	ПутьZIP = ПутьВыгрузки + "EDT_" + Префикс + ".zip";
	ZipEDT  = Новый ЗаписьZipФайла(ПутьZIP);

	//EDT
	ZipEDT.Добавить(ПутьКEDT + "\*.*" 
		, РежимСохраненияПутейZIP.СохранятьОтносительныеПути, РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
	ZipEDT.Записать();

	Сообщить("End EDT");

КонецПроцедуры


Процедура СоздатьOSPX(Данные)
	
	Сообщить("Start OSPX");

	ПутьOS   = Данные["ПутьOS"];
	ПутьCLIP = Данные["ПутьCLIP"];
	Префикс  = Данные["Префикс"];

	СтандартноеИмяOSPX = "oint-" + Версия + ".ospx";
	ИмяOSPX            = "oint-" + Версия + "_" + Префикс + ".ospx";
	КонечныйПутьOSPX   = ПутьВыгрузки + ИмяOSPX;

	СтандартноеИмяCLI = "oint-cli-" + Версия + ".ospx";
	ИмяCLI            = "oint-cli-" + Версия + "_" + Префикс + ".ospx";
	КонечныйПутьCLI   = ПутьВыгрузки + ИмяCLI;

	//OSPX
	
	СборкаOS         = "opm b -o ""C:/Dev/"" """ + ПутьOS + """";

	ЗапуститьПриложение(СборкаOS, , Истина);
	ПереместитьФайл("C:\Dev\" + СтандартноеИмяOSPX, КонечныйПутьOSPX);

	УдалитьФайлы(Оскрипт + "lib\oint");

	Приостановить(1000);
	ЗапуститьПриложение("opm install -f """ + КонечныйПутьOSPX + """", , Истина);
	Приостановить(1000);

	// CLI

	СборкаCLI         = "opm b -o ""C:/Dev/"" """ + ПутьCLIP + """";

	ЗапуститьПриложение(СборкаCLI, , Истина);
	ПереместитьФайл("C:\Dev\" + СтандартноеИмяCLI, КонечныйПутьCLI);

	УдалитьФайлы(Оскрипт + "lib\oint-cli");

	Приостановить(1000);
	ЗапуститьПриложение("opm install -f """ + КонечныйПутьCLI + """", , Истина);
	Приостановить(1000);

	НачальныйПутьOint = ОСкрипт + "lib\oint";
	НачальныйПутьCLI  = ОСкрипт + "lib\oint-cli";
	КонечныйПутьOint  = ".\ci\installer_set\share\oint\lib\oint";
	КонечныйПутьCLI   = ".\ci\installer_set\share\oint\lib\oint-cli";

	ХешOint = ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(НачальныйПутьOint + "\.versionhash"));
	ХешCli  = ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(НачальныйПутьCLI + "\.versionhash"));

	Если Не ХешOint = ХешСумма Тогда
		ВызватьИсключение "Хеш сумма Oint не совпадает с основной!";
	КонецЕсли;

	Если Не ХешCli = ХешСумма Тогда
		ВызватьИсключение "Хеш сумма CLI не совпадает с основной!";
	КонецЕсли;	

	ЗапуститьПриложение("xcopy """ + НачальныйПутьOint + """ """ + КонечныйПутьOint + """ /e /y /i", , Истина);
	ЗапуститьПриложение("xcopy """ + НачальныйПутьCLI + """ """ + КонечныйПутьCLI + """ /e /y /i", , Истина);
	УдалитьФайлы(".\ci\installer_set\share\oint\lib\oint\tests");

	ХешOint = ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(КонечныйПутьOint + "\.versionhash"));
	ХешCli  = ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(КонечныйПутьCLI + "\.versionhash"));

	Если Не ХешOint = ХешСумма Тогда
		ВызватьИсключение "Хеш сумма Oint не совпадает с основной!";
	КонецЕсли;

	Если Не ХешCli = ХешСумма Тогда
		ВызватьИсключение "Хеш сумма CLI не совпадает с основной!";
	КонецЕсли;	

	Сообщить("End OSPX");

КонецПроцедуры


Процедура СоздатьEXE(Данные)

	Сообщить("Start EXE");

	ПутьCLI = Данные["ПутьCLI"];
	Префикс =  Данные["Префикс"];

	//EXE
	СборкаEXE = "oscript -make """ + ПутьCLI + """ """ + ПутьВыгрузки + "oint.exe""";
	ЗапуститьПриложение(СборкаEXE, , Истина);
	
	Сообщить("End EXE");

КонецПроцедуры

Процедура СоздатьПакеты(Данные)
	  
	Сообщить("Start Пакеты");

	ХешУстановщика = ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(".\ci\installer_set\share\oint\.versionhash"));

	Если Не ХешУстановщика = ХешСумма Тогда
		ВызватьИсключение "Хеш сумма набора установщика не совпадает с основной!";
	КонецЕсли;

	ТекстSh = "--name oint"
	+ " -s dir"
	+ " --license mit"
	+ " --version " + Версия
	+ " --architecture all"
	+ " --description """ + Данные["Описание"] + """"
	+ " --url ""https://openintegrations.dev/"""
	+ " --maintainer ""Anton Titovets <bayselonarrend@gmail.com>"""
	+ " --verbose"
	+ " ../ci/installer_set/=/usr " + ПутьДвижка + "=/usr/share/oint/bin";

	СоответствиеПакетов = Новый Соответствие();
	СоответствиеПакетов.Вставить("deb", "oint_" + Версия + "_all_" + Данные["Префикс"] + ".deb");
	СоответствиеПакетов.Вставить("rpm", "oint-" + Версия + "-1.noarch_" + Данные["Префикс"] + ".rpm");

	СоответствиеДополнений = Новый Соответствие();
	СоответствиеДополнений.Вставить("deb", " ");
	СоответствиеДополнений.Вставить("rpm", " --rpm-os linux --depends libicu ");

	Для Каждого Пакет Из СоответствиеПакетов Цикл

		MakeSh  = ПутьВыгрузки + "make" + Пакет.Ключ + ".sh";
		MakeBat = ПутьВыгрузки + "make" + Пакет.Ключ + ".bat";

		Дистрибутив = ?(Пакет.Ключ = "deb", "Ubuntu", "OracleLinux_9_1");	

		FPM = "chmod +x " + ПутьДвижка + "
		|chmod +x ../ci/installer_set/bin/oint
		|fpm -t " + Пакет.Ключ + " -p " + Пакет.Значение + " "  + СоответствиеДополнений[Пакет.Ключ] + ТекстSh;
		FPM = ПолучитьДвоичныеДанныеИзСтроки(FPM);
		FPM.Записать(MakeSh);

		ТекстBat = "wsl -d " + Дистрибутив + " ""./make" + Пакет.Ключ + ".sh" + """";
		ТекстBat = ПолучитьДвоичныеДанныеИзСтроки(ТекстBat);
		ТекстBat.Записать(MakeBat);

		ЗапуститьПриложение("make" + Пакет.Ключ + ".bat", ПутьВыгрузки, Истина);

		УдалитьФайлы(MakeBat);
		УдалитьФайлы(MakeSh);

	КонецЦикла;
	
	Сообщить("End Пакеты");

КонецПроцедуры

Процедура СоздатьУстановщик(Данные)

	Сообщить("Start ISS");

	ПутьISS = Данные["ПутьISS"];
	ПутьCLI = Данные["ПутьCLI"];
	Префикс = Данные["Префикс"];

	//Setup

	ТекстISS = Новый ТекстовыйДокумент();
	ТекстISS.Прочитать(ПутьISS);

	Для Н = 1 По ТекстISS.КоличествоСтрок() Цикл

		ТекущаяСтрока = СокрЛП(ТекстISS.ПолучитьСтроку(Н));

		Если СтрНайти(ТекущаяСтрока, "#define MyAppVersion") Тогда
			ТекстISS.ЗаменитьСтроку(Н, "#define MyAppVersion """ + Версия + """");
			Прервать;
		КонецЕсли;

	КонецЦикла;

	ТекстISS.Записать(ПутьISS);

	СборкаSetup = """C:\Program Files (x86)\Inno Setup 6\Compil32.exe"" /cc """ + ПутьISS + """";
	ЗапуститьПриложение(СборкаSetup, , Истина);
	
	//ПереместитьФайл(ПутьВыгрузки + "oint.exe", ПутьВыгрузки + "oint_" + Префикс + ".exe");
	
	Сообщить("End ISS");

КонецПроцедуры


НачалоРаботы();