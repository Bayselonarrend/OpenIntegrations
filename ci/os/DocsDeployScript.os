#Использовать fs
#Использовать "./internal"

Процедура ПриСозданииОбъекта()

	Вариант = АргументыКоманднойСтроки[0];
	Язык    = АргументыКоманднойСтроки[1];

	ДанныеПроекта = Новый ProjectData;
	
	Обработчик = Новый DocsPageLocalizator(ДанныеПроекта, Язык);

	КаталогОсновной    = ДанныеПроекта.ПолучитьЗначениеНастройки("docs.deployMainSrc");
	КаталогДокументов  = ДанныеПроекта.ПолучитьЗначениеНастройки("docs.deployDocsSrc");
	КаталогСборки      = ДанныеПроекта.ПолучитьЗначениеНастройки("docs.deployBuildSrc");
	КаталогNodeМодулей = ДанныеПроекта.ПолучитьЗначениеНастройки("docs.deployModulesSrc");
	КаталогMDЯзыка     = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("docsMdSrc", Язык);

	ОбъектСборки      = Новый Файл(КаталогСборки);
	ОбъектNodeМодулей = Новый Файл(КаталогNodeМодулей);
	ОбъектДокументов  = Новый Файл(КаталогДокументов);

	Если ОбъектСборки.Существует() Тогда
		УдалитьФайлы(КаталогСборки);
	КонецЕсли;

	Если ОбъектNodeМодулей.Существует() Тогда
		УдалитьФайлы(КаталогСборки);
	КонецЕсли;
	
	Если ОбъектДокументов.Существует() Тогда
		УдалитьФайлы(КаталогДокументов, "*");
	КонецЕсли;

	ФС.КопироватьСодержимоеКаталога(КаталогMDЯзыка, КаталогДокументов);

	Попытка

		CommonTools.ЗапуститьВнешнееПриложение("npm install"  , КаталогОсновной);
		CommonTools.ЗапуститьВнешнееПриложение("npm run build", КаталогОсновной);

		Если Вариант = "release" Тогда

			КонфигурацияRclone = ДанныеПроекта.ПолучитьЗначениеНастройки("local.rcloneConf");
			КаталогХостинга    = ДанныеПроекта.ПолучитьЗначениеНастройкиЛокализации("remoteDocsSrc", Язык);

			ВызовДеплоя = СтрШаблон("rclone sync %1 %2:%3 --progress"
				, КаталогСборки
				, КонфигурацияRclone
				, КаталогХостинга);

			CommonTools.ЗапуститьВнешнееПриложение(ВызовДеплоя    , КаталогОсновной);

		КонецЕсли;

		CommonTools.СообщитьПроцесс("Docs processing finished successfully");

		Успех = Истина;

	Исключение
		
		CommonTools.СообщитьПроцесс("Docs processing terminated");

		Успех = Ложь;

	КонецПопытки;

	Если Вариант = "release" Тогда
		CommonTools.СообщитьПроцесс("Cleaning...");
		УдалитьФайлы(КаталогСборки);
		УдалитьФайлы(КаталогNodeМодулей);
	КонецЕсли;

	Если Не Успех Тогда
		ЗавершитьРаботу(1);
	КонецЕсли;

КонецПроцедуры