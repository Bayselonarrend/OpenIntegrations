#Область ПрограммныйИнтерфейс

// Переопределение глобального метода Сообщить()
//
// Параметры:
//  ТекстСообщения  - Строка - Текст собщения с цветными полями по шаблону (Текст|#color=Цвет)
//  пСтатусСообщения  - СтатусСообщения - системное перечисление СтатусСообщения
//                      соотвествует типу второго параметра глобального метода Сообщить()
// 
Процедура Сообщить(Знач ТекстСообщения = "", Знач пСтатусСообщения = Неопределено) Экспорт
	
	Если пСтатусСообщения = СтатусСообщения.Важное Тогда
		ЦветТекста = "Красный";
	ИначеЕсли пСтатусСообщения = СтатусСообщения.Внимание Тогда
		ЦветТекста = "Желтый";
	ИначеЕсли пСтатусСообщения = СтатусСообщения.Информация Тогда
		ЦветТекста = "Зеленый";
	ИначеЕсли пСтатусСообщения = СтатусСообщения.ОченьВажное Тогда
		ЦветТекста = "Красный";
	Иначе
		ЦветТекста = Неопределено;
	КонецЕсли;

	ВывестиСтроку(ТекстСообщения, ЦветТекста);

КонецПроцедуры

// Вывод строки цветного текста в консоль
// с завершением строки (переводом на новую строку)
//
// Параметры:
//  ТекстСообщения  - Строка - Текст собщения с цветными полями по шаблону
//                             `(Текст|#color=Цвет)`
//  ЦветТекста  - Строка - Строковое значение перечисления ЦветКонсоли
// 
Процедура ВывестиСтроку(Знач ТекстСообщения = "", Знач ЦветТекста = Неопределено) Экспорт

	Вывести(ТекстСообщения, ЦветТекста);

	Консоль.Вывести(Символы.ПС);

КонецПроцедуры

// Вывод строки цветного текста в консоль
// без завершения строки
//
// Параметры:
//  ТекстСообщения  - Строка - Текст собщения с цветными полями по шаблону
//                             `(Текст|#color=Цвет)`
//  ЦветТекста  - Строка - Строковое значение перечисления ЦветКонсоли
// 
Процедура Вывести(Знач ТекстСообщения, Знач ЦветТекста = Неопределено) Экспорт
	
	Если Врег(ЦветТекста) = "МОНОХРОМ" Тогда
		ЦветТекста = Неопределено;
		ЦветнойВыводРазрешен = Ложь;
	Иначе
		ЦветнойВыводРазрешен = Истина;
	КонецЕсли;

	ТаблицаПолей = РазобратьСтроку(ТекстСообщения, ЦветТекста);

	Если ЦветнойВыводРазрешен Тогда
		
		ПредыдущееЗначениеЦветаТекта = Консоль.ЦветТекста;
		Если НЕ ЗначениеЗаполнено(ПредыдущееЗначениеЦветаТекта) Тогда
			ПредыдущееЗначениеЦветаТекта = ЦветКонсоли.Белый;
		КонецЕсли;
		
		Если ЦветТекста = Неопределено Тогда
			ЦветТекста = ПредыдущееЗначениеЦветаТекта;
		КонецЕсли;
		Для Каждого Поле Из ТаблицаПолей Цикл
			Если ЗначениеЗаполнено(Поле.Цвет) Тогда
				Консоль.ЦветТекста = ЦветКонсоли[Поле.Цвет];
			Иначе
				Консоль.ЦветТекста = ПредыдущееЗначениеЦветаТекта;
			КонецЕсли;
			Консоль.Вывести(Поле.Текст);
		КонецЦикла;
		
		Консоль.ЦветТекста = ПредыдущееЗначениеЦветаТекта;
		
	Иначе
		
		МассивПолей = ТаблицаПолей.ВыгрузитьКолонку("Текст");
		ТекстСообщенияБезЦветныхПолей = СтрСоединить(МассивПолей);
		
		Консоль.Вывести(ТекстСообщенияБезЦветныхПолей);

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Функция РазобратьСтроку(ТекстСообщения, ЦветТекста)

	Длина = СтрДлина(ТекстСообщения);
	
	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("Текст");
	Таблица.Колонки.Добавить("Цвет");

	тзЦветныеПоля = ЦветныеПоля(ТекстСообщения);

	Поз = 1;
	Для Каждого ЦветПоле Из тзЦветныеПоля Цикл
		Если Поз < ЦветПоле.Позиция Тогда
			Текст = Сред(ТекстСообщения, Поз, ЦветПоле.Позиция - Поз);
			Стр = Таблица.Добавить();
			Стр.Текст = Текст;
			Стр.Цвет = ЦветТекста;
		КонецЕсли;
		
		Стр = Таблица.Добавить();
		Стр.Текст = ЦветПоле.Текст;
		Стр.Цвет = ЦветПоле.Цвет;
		
		Поз = ЦветПоле.Позиция + ЦветПоле.Длина;
	КонецЦикла;

	Если Поз-1 <= Длина Тогда
		Текст = Сред(ТекстСообщения, Поз);
		Стр = Таблица.Добавить();
		Стр.Текст = Текст;
		Стр.Цвет = ЦветТекста;
	КонецЕсли;
	
	Возврат Таблица;
	
КонецФункции

Функция ЦветныеПоля(ТекстСообщения)
	
	РВ = Новый РегулярноеВыражение(ШаблонЦветнойПодстроки());
	Совпадения = РВ.НайтиСовпадения(ТекстСообщения);

	ЦветныеПоля = Новый ТаблицаЗначений;
	ЦветныеПоля.Колонки.Добавить("Позиция");
	ЦветныеПоля.Колонки.Добавить("Текст");
	ЦветныеПоля.Колонки.Добавить("Длина");
	ЦветныеПоля.Колонки.Добавить("Цвет");
	
	Для Каждого Совпадение Из Совпадения Цикл
		Группы = Совпадение.Группы;
		стр = ЦветныеПоля.Добавить();
		стр.Позиция = Совпадение.Индекс+1;
		стр.Текст = Группы[1].Значение;
		стр.Длина = Совпадение.Длина;
		ЦветИмя = Группы[2].Значение;
		стр.Цвет = ЦветИмя;
	КонецЦикла;
	
	ЦветныеПоля.Сортировать("Позиция");
	
	Возврат ЦветныеПоля;

КонецФункции

Функция ШаблонЦветнойПодстроки()
	
	Возврат "\((.+?)\|#color=(.+?)\)"; // (Текст|#color=Цвет)

КонецФункции

#КонецОбласти
